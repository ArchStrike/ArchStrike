# Maintainer: ArchStrike <archstrikelinux@gmail.com>
pkgname=python2-pysqli-git
pkgver=r21.37418c9
pkgrel=1
groups=('archstrike')
pkgdesc="Python SQL injection framework"
arch=('any')
url='https://github.com/sysdream/pysqli'
license=('GPL')
depends=('python2')
makedepends=('git' 'python2-setuptools')
source=("pysqli::git+https://github.com/sysdream/pysqli.git")
sha512sums=('SKIP')

prepare(){
  grep -iRl 'python' "$srcdir/pysqli" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

pkgver() {
  cd "$srcdir/pysqli"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}



package() {
  cd "$srcdir/pysqli"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
