# Maintainer
pkgname=python2-pytorctl
_pkgname=pytorctl
pkgver=20131016
pkgrel=1
epoch=1
pkgdesc="A Python Tor controller with extensions to support path building and various constraints on node and path selection, as well as statistics gathering."
arch=('any')
license=('BSD')
url="http://fscked.org/projects/torctl"
depends=('python2-geoip')
makedepends=('python2-setuptools' 'git')
optdepends=('python2-elixir: SQL Support')
conflicts=('pytorctl')
provides=('pytorctl')
source=("git+http://git.torproject.org/${_pkgname}.git" "setup.py")
sha512sums=('SKIP' '0f925e62308bc450198e927bb6c07f836b923d83227790012ae43217a365057b4399ec94206ed5246671a104ba9c4acee5052dd088b34f6040c844a703d13255')

pkgver() {
    cd ${_pkgname}
    git describe --always | sed 's|-||g'
}

prepare() {
    cd ${_pkgname}
    sed -i 's|/usr/bin/python|/usr/bin/env python2|g' *.py
}

package() {
    install -Dm644 ${_pkgname}/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 ${_pkgname}/README "${pkgdir}/usr/share/doc/${pkgname}/README"
    python2 setup.py install --root="$pkgdir" --optimize=1
}
