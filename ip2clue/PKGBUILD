# Maintainer: ArchAssault <team projectname org>
# Contributor: Prurigro

pkgname=ip2clue
pkgver=0.0.92
pkgrel=3
pkgdesc="A mall memory/CPU footprint daemon to lookup country (and other info) based on IP (v4 and v6)"
url="http://kernel.embedromix.ro/us/"
license=('GPL3')
groups=('projectname' 'projectname-*networking')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('bash' 'conn')
options=('!emptydirs')
source=("http://kernel.embedromix.ro/us/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}d.service")
sha512sums=('87c247d9be634ef594fa5b2c8c135680877e258672deb36e39e2883ee1493dc29b7f5809ca46e83409938968016bb30b16ab304d2f6b5e0a3c664907839d61df'
            '90e43e1a1482c2ab2234a63156a03bc728132abeccb7330d6326dbd93140b8a9bd109e92006c6fb9f9d4de553b4663452173953ae334ca06bd7864a75e4dc947')

build() {
    cd "${pkgname}-${pkgver}"
    ./configure --prefix=/usr
    make
}

package() {
    install -Dm644 "${pkgname}d.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}d.service"
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    mv "${pkgdir}"/usr/sbin/* "${pkgdir}"/usr/bin
    rm -rf "${pkgdir}/etc/rc.d" # We're using systemd instead
}
