# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=pytbull
pkgver=2.1
pkgrel=3
groups=('archstrike' 'archstrike-scanners')
pkgdesc="A python based flexible IDS/IPS testing framework shipped with more than 300 tests"
arch=('any')
url='http://pytbull.sourceforge.net/'
license=('GPL2')
depends=('scapy' 'python2-feedparser')
source=("https://downloads.sourceforge.net/project/pytbull/pytbull-$pkgver.tar.bz2")
sha512sums=('5ac642ded4f772299966950d7255bc67562f95fa92c7b58321623aae6efc3655abddbc94211f11098a53d5544ef3e172fb73634df3a449e97c1d7f6dd5789430')

prepare(){
  grep -iRl 'python' "$srcdir/pytbull" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
  cd $srcdir/pytbull
  sed -i 's/self.checkNewVersionAvailable()/False/' pytbull
}

package() {
  cd "$srcdir/pytbull"

  # Make base directories.
  install -dm755 "$pkgdir/usr/share/pytbull"
  install -dm755 "$pkgdir/usr/bin"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/pytbull"

cat > "$pkgdir/usr/bin/pytbull" << EOF
#!/bin/sh
cd /usr/share/pytbull
python2 pytbull "\$@"
EOF

chmod +x "$pkgdir/usr/bin/pytbull"

}
