# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

buildarch=212

pkgname=sleuthkit-archstrike
_pkgname=sleuthkit
pkgver=4.1.3
pkgrel=5
pkgdesc='File system and media management forensic analysis tools'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://www.sleuthkit.org/sleuthkit"
license=('GPL2' 'CPL' 'custom:"IBM Public Licence"')
depends=('perl' 'java-environment' 'afflib' 'libewf')
makedepends=('libewf')
conflicts=('sleuthkit')
provides=('sleuthkit')
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/$_pkgname/$_pkgname-$pkgver.tar.gz https://googledrive.com/host/0B3fBvzttpiiScUxsUm54cG02RDA/tsk4.1.3_external_type.patch)
sha512sums=('66f6ac32da5a21b3505390557ed6cf7bbcfcca69f231e20a7086fd48c5de45a3f064a716837d25eb582d0a89bae3b6ba7e89dcf3159e1cacdb4b5d452d0ce9a2'
            'f8f0142c3eb8150df06898256e121743a7b7251c3343f56d24052bfc1b56e6d5c213761544fff2030c8d90dcb975ad0f91e9d4a32290afe6396f0a3ef5a75eb8')

prepare(){
  cd ${_pkgname}-${pkgver}/tsk/img
  patch < $srcdir/tsk4.1.3_external_type.patch
}

build() {
  cd ${_pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${_pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

#licenses
  install -d "${pkgdir}"/usr/share/licenses/${pkgname}
  install -Dm0644 licenses/* "${pkgdir}"/usr/share/licenses/${pkgname}
}
