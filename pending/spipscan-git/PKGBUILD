# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=spipscan-git
pkgver=r69.4ad3235
pkgrel=2
groups=('archstrike' 'archstrike-scanners')
pkgdesc="SPIP (CMS) Scanner for penetration testing purpose written in Python"
arch=('any')
url='https://github.com/PaulSec/SPIPScan'
license=('MIT')
depends=('python2' 'python2-requests' 'python2-beautifulsoup4')
makedepends=('git')
provides=('spipscan')
source=("spipscan::git+https://github.com/PaulSec/SPIPScan.git")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/spipscan"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' "$srcdir/spipscan" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/spipscan"

  # Make base directories.
  install -dm755 "$pkgdir/usr/share/spipscan"
  install -dm755 "$pkgdir/usr/bin"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/spipscan"

cat > "$pkgdir/usr/bin/spipscan" << EOF
#!/bin/sh
cd /usr/share/spipscan
python2 spipscan.py "\$@"
EOF

chmod +x "$pkgdir/usr/bin/spipscan"

}
