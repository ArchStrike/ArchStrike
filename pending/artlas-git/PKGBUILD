# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=artlas-git
pkgver=20160614.r140
pkgrel=4
groups=('archstrike' 'archstrike-analysis' 'archstrike-defense')
arch=('any')
pkgdesc="Apache Real Time Logs Analyzer System"
url="https://github.com/mthbernardes/ARTLAS"
license=('MIT')
depends=('python2' 'screen' 'python2-apache-log-parser' 'python2-py-zabbix' 'python2-pygtail' 'python2-requests' 'python2-six' 'python2-telepot' 'python2-ua-parser' 'python2-urllib3' 'python2-user-agents')
makedepends=('git')
source=("${pkgname}::git+https://github.com/mthbernardes/ARTLAS.git")
install="${pkgname}.install"
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}" || exit
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  grep -iRl 'python' "${pkgname}" | xargs sed -i 's|python$|python2|'
}

package() {
  cd "${pkgname}" || exit
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}"
  cp -a --no-preserve=ownership ./* "${pkgdir}/usr/share/${pkgname}"
  cd "${pkgdir}/usr/share/${pkgname}" || exit
  rm LICENSE requirements.txt

cat > "${pkgdir}/usr/bin/artlas" <<EOF
#!/bin/sh
cd "/usr/share/${pkgname}"
python2 artlas.py "\$@"
EOF
chmod 755 "${pkgdir}/usr/bin/artlas"
}
