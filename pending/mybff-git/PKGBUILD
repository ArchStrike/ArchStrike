# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=mybff-git
pkgver=20170103.r94
pkgrel=3
groups=('archstrike' 'archstrike-bruteforce')
arch=('any')
pkgdesc="A web application brute force framework"
url="https://github.com/rapid7/myBFF"
license=('MIT')
depends=('python2-requests' 'python2-lxml' 'python2-pysmb')
makedepends=('git')
source=("${pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "%s.r%s" "$(git show -s --format=%ci master | sed "s/\ .*//g;s/-//g")" "$(git rev-list --count HEAD)"
}

prepare() {
  grep -iRl 'python' ${pkgname} | xargs sed -i 's|python$|python2|g'
}

package() {
  cd ${pkgname}
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"

  install -Dm755 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}"; rm LICENSE.txt

  cp -a --no-preserve=ownership * "${pkgdir}/usr/share/${pkgname}"

cat > "${pkgdir}/usr/bin/mybff" <<EOF
#!/usr/bin/env bash
python2 /usr/share/${pkgname}/myBFF.py "\$@"
EOF

chmod 755 "${pkgdir}/usr/bin/mybff"

}
