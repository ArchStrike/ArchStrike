# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

pkgname=delta-git
_pkgname=delta
pkgver=20160608.r5
pkgrel=7
groups=('archstrike' 'archstrike-recon' 'archstrike-scanners')
arch=('i686' 'x86_64')
pkgdesc="Track the changes of a website"
url="https://github.com/pangbo-1988/data_diff"
license=('GPL3')
depends=('python2' 'python2-urllib3' 'python2-elasticsearch-py' 'python2-mmh3' 'scrapy' 'elasticsearch')
makedepends=('git')
source=("${pkgname}::git+https://github.com/pangbo-1988/data_diff")
install=${pkgname}.install
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  cd "${srcdir}/${pkgname}"
  grep -iRl 'python' ./ | xargs sed -i 's|python$|python2|'
}

package() {
  cd "${srcdir}/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/${_pkgname}"
  install -dm755 "${pkgdir}/usr/share/doc/${_pkgname}"

  cp -a --no-preserve=ownership ./* "${pkgdir}/usr/share/${_pkgname}"
  install -Dm644 readme.md "${pkgdir}/usr/share/docs/${_pkgname}"

cat > "${pkgdir}/usr/bin/${_pkgname}" <<EOF
#!/bin/sh
cd /usr/share/${_pkgname}
python2 delta.py "\$@"
EOF
chmod 755 "${pkgdir}/usr/bin/${_pkgname}"
}
