# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=python2-pattern
pkgver=2.6
pkgrel=2
pkgdesc="Pattern is a web mining module for the Python programming language."
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://www.clips.ua.ac.be/pattern"
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools')
source=(http://www.clips.ua.ac.be/media/pattern-${pkgver}.zip)
sha512sums=('401e9483ba775cbdcda2a1a4c843abe4ab742a392803eb36d43b4a76ec4cc9832eb20c14429e76ab1b862e2d9774c12a4e680a835cbeea46c4d9e2536cd96585')

prepare(){
  grep -iRl 'python' "$srcdir/pattern-${pkgver}" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd ${srcdir}/pattern-${pkgver}
  python2 setup.py install --root=${pkgdir} --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
  # fixing python2.6 issue
  sed -i 's|python2.6|python2|' $pkgdir/usr/lib/python2.7/site-packages/pattern/web/docx/docx.py
}
