# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=automator-git
pkgver=r88.42548cf
pkgrel=4
groups=('archstrike' 'archstrike-forensics')
pkgdesc="IP URL and MD5 OSINT Analysis"
arch=('any')
url='https://github.com/1aN0rmus/TekDefense-Automater'
license=('MIT')
depends=('python2-requests' 'python2-beautifulsoup4')
makedepends=('git')
source=("${pkgname}::git+https://github.com/1aN0rmus/TekDefense-Automater.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname}" || exit
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' "${pkgname}" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "${pkgname}" || exit

  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/automator"
  install -dm755 "${pkgdir}/usr/share/automator/docs"

  #Bins
  install -Dm755 "Automater.py" "${pkgdir}/usr/share/automator/Automater.py"
  install -Dm755 "inputs.py" "${pkgdir}/usr/share/automator/inputs.py"
  install -Dm755 "outputs.py" "${pkgdir}/usr/share/automator/outputs.py"
  install -Dm755 "siteinfo.py" "${pkgdir}/usr/share/automator/siteinfo.py"
  install -Dm644 "sites.xml" "${pkgdir}/usr/share/automator/sites.xml"
  install -Dm755 "utilities.py" "${pkgdir}/usr/share/automator/utilities.py"
  install -Dm644 "tekdefense.xml" "${pkgdir}/usr/share/automator/tekdefense.xml"

  #Docs
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/automator-git/LICENSE"
  install -Dm644 "README.md" "${pkgdir}/usr/share/automator/README.md"
  cp -a --no-preserve=ownership docs/* "${pkgdir}/usr/share/automator/docs"

cat > "${pkgdir}/usr/bin/automater" <<EOF
#!/bin/sh
cd /usr/share/automator
python2 Automater.py "\$@"
EOF

chmod +x "${pkgdir}/usr/bin/automater"

}
