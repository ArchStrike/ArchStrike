# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname='kippo'
pkgver='0.9'
pkgrel=4
pkgdesc='A medium interaction SSH honeypot designed to log brute force attacks and most importantly, the entire shell interaction by the attacker.'
arch=('any')
url='https://github.com/desaster/kippo'
license=('GPL')
groups=('archstrike' 'archstrike-defense')
depends=('python2-twisted' 'python2-crypto' 'python2-pyasn1')
source=("https://github.com/desaster/kippo/archive/v$pkgver.tar.gz"
        'kippo.desktop')
sha512sums=('0e17c410ecb8e44ba3862f0bd6c2773d6fa768750d7917ba7f5fbfdec8ddfa63fbde0e0190bcd5361a6c5ea0bcb2017e24593c4181b5cb915a605723bce07a26'
            '4a6b4b51ef64814946f7fde9e5541d84a1ebf5edc5e6bf3d7b57ec177fb75cbfe9e5ada8dc50af3f6bea4492cc40b86b5c66f31429e6b51c21829db635b30fe1')
prepare(){
 grep -iRl 'python' "$srcdir/kippo-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/kippo-$pkgver"
  install -dm755  "$pkgdir/usr/share/kippo/"
  install -dm755  "$pkgdir/usr/bin/"
  cp -r --no-preserve=ownership * "$pkgdir/usr/share/kippo/"

  cd "$pkgdir/usr/share/kippo"
  ln -sf kippo.cfg.dist kippo.cfg
  install -Dm644 "$srcdir/kippo.desktop" "$pkgdir/usr/share/applications/kippo.desktop"

cat > "$pkgdir/usr/bin/kippo" <<EOF
#!/bin/sh
cd /usr/share/kippo
./start.sh "\$@"
EOF
chmod +x "$pkgdir/usr/bin/kippo"
}
