# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=arpon
pkgver=3.0
pkgrel=3
pkgdesc='A portable handler daemon that make ARP protocol secure in order to avoid the Man In The Middle (MITM) attack through ARP Spoofing, ARP Cache Poisoning or ARP Poison Routing (APR) attacks.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://arpon.sourceforge.net/'
license=('MIT')
depends=('libdnet' 'libnet' 'libpcap')
makedepends=('cmake' 'libdnet')
backup=('etc/conf.d/arpon')
groups=('archstrike' 'archstrike-defense')
source=("http://downloads.sourceforge.net/sourceforge/arpon/ArpON-${pkgver}-ng.tar.gz"
        "arpon"
        "arpon.conf.d"
        'arpon.desktop'
        'arpon.service')
changelog='CHANGELOG'
sha512sums=('e6338018d65f3f8300958e168a9eb6f6be85cba21ae0aee4b03e9838a29a06afbf2448c2f104367aa18389cc549e4489bcf8dad384ad46eadf2884a0908238af'
            '0cd0114efd2f35fcf06ec91a56c5aca66adf13a7b25fbf82b2483489d040447d1a3a609f223f911ad1c407b321c6fbc22d32c9200f08fef2bbbf9ec391b7bb90'
            '75526a64be9ab7e6cc787dbff5cdb1e3d7c3b708fc3f9cc1712ea7b16181767aca4747a00c99f4bec6b0de0f86f131c76ec20e2ef7d24c2237c314df95dfc39d'
            'c9735e899033012ab6eee386b4801febc9ea8e8077372314340a5999360b69c1062de41e307c2e4447f8bca6909fc06031f147dd73b5702ec3be4775707a6e1c'
            'd902e0bc37c922f73ea8c70b388620d86e43e61676ce46011b5c006bef0c09638dd06493e10f911068f0d436439f1da3c365902c386379ddc2a9dfe34c206039')

build() {
  cd "${srcdir}/ArpON-${pkgver}-ng" || exit
  [ -d build ] || mkdir build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd "${srcdir}/ArpON-${pkgver}-ng/build" || exit
  make DESTDIR="${pkgdir}" install
  install -Dm644 "../LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm755 "${srcdir}/arpon" "${pkgdir}/etc/rc.d/arpon"
  install -Dm644 "${srcdir}/arpon.conf.d" "${pkgdir}/etc/conf.d/arpon"
  install -Dm644 "${srcdir}/arpon.service" "${pkgdir}/usr/lib/systemd/system/arpon.service"
  install -Dm644 "${srcdir}/arpon.desktop" "${pkgdir}/usr/share/applications/arpon.desktop"
  install -dm755 "${pkgdir}"/var/log
  install -dm755 "${pkgdir}"/run
  mv "${pkgdir}"/usr/sbin "${pkgdir}"/usr/bin
  mv "${pkgdir}"/usr/etc/arpon.conf "${pkgdir}"/etc/arpon.conf
  mv "${pkgdir}"/usr/log/arpon.log "${pkgdir}"/var/log/arpon.log
  chmod 644 "${pkgdir}"/var/log/arpon.log
  mv "${pkgdir}"/usr/run/arpon.pid "${pkgdir}"/run/arpon.pid
  rm -rf "${pkgdir}"/usr/etc "${pkgdir}"/usr/log "${pkgdir}"/usr/run
}
