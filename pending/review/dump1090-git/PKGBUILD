# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: qs9rx < that nick at enjoys dot it>
# Contributor: David Manouchehri <d@32t.ca>

buildarch=220

pkgname=dump1090-git
pkgver=20141031.r386
pkgrel=4
pkgdesc="Dump1090 is a simple Mode S decoder for RTLSDR devices. MalcolmRobb fork. Git version."
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/MalcolmRobb/dump1090"
license=('BSD')
groups=('archstrike')
depends=('rtl-sdr')
makedepends=('git')
provides=('dump1090')
source=("${pkgname}::git+https://github.com/MalcolmRobb/dump1090.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}" || exit
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  cd "${pkgname}" || exit
  # Reference public_html to /usr/share
  sed -i -e 's/\.\/public_html/\/usr\/share\/dump1090-git\/public_html/g' dump1090.h
}

build() {
  cd "${pkgname}" || exit
  make
}

package() {
  cd "${pkgname}" || exit

  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -Dm755 dump1090 "${pkgdir}/usr/bin/dump1090"
  install -Dm755 view1090 "${pkgdir}/usr/bin/view1090"
  install -Dm644 README.md "$pkgdir/usr/share/licenses/$pkgname/README.md"

  cp -r "public_html" "${pkgdir}/usr/share/${pkgname}/"
}
