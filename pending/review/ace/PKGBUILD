# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=ace
pkgver=1.10
pkgrel=8
groups=('archstrike' 'archstrike-voip')
pkgdesc='A simple yet powerful VoIP Corporate Directory enumeration tool that mimics the behavior of an IP Phone to download name and extension entries'
url='http://ucsniff.sourceforge.net/ace.html'
license=('GPL')
depends=('libpcap')
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
source=("http://downloads.sourceforge.net/ucsniff/${pkgname}-${pkgver}.tar.gz")
sha512sums=('bb6a57aa0ee4fff309b41b352436793888470924478bfa828771b173d4f697b30e3b9afa144f25ed8bd79c5045af6b5973d47abc2040d932a898f8f3dfe3ddaf')

prepare() {
  cd ${pkgname}-${pkgver} || exit
  sed -i 's#linux/if_tr.h#netinet/if_tr.h#' client.h
  sed -i "s:gcc :gcc $CFLAGS :g" Makefile
}

build() {
  cd ${pkgname}-${pkgver} || exit
  make
}

package() {
  cd ${pkgname}-${pkgver} || exit
  install -Dm755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"
}
