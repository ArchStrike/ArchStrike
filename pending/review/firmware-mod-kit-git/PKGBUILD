# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

_pkgname=firmware-mod-kit
pkgname=$_pkgname-git
pkgver=0.99.r75.g104c821
pkgrel=1
groups=('archstrike' 'archstrike-misc')
pkgdesc=" Modify firmware images without recompiling! (expect to brick your router)"
url="https://github.com/rampageX/firmware-mod-kit"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('MIT')
depends=('git' 'python-magic-git')
replaces=("$_pkgname")
source=("$_pkgname::git+https://github.com/rampageX/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
  cd "$srcdir/$_pkgname/src"
  ./configure
  make
}

package() {
  cd "$srcdir/$_pkgname"
  install -dm0755 "$pkgdir/usr/share/$_pkgname"
  install -dm0755 "$pkgdir/usr/share/doc/$_pkgname"
  install -m0755 *.sh "$pkgdir/usr/share/$_pkgname/"
  install -m0644 *.inc "$pkgdir/usr/share/$_pkgname/"
  install -m0644 *.htm "$pkgdir/usr/share/doc/$_pkgname/"
  install -m0644 firmware_mod_kit_version.txt  "$pkgdir/usr/share/$_pkgname/"
  for i in src ipk_template; do cp -a --no-preserve=ownership $i "$pkgdir/usr/share/$_pkgname/"; done
}

