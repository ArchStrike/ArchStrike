# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=dns2tcp
pkgver=0.5.2
pkgrel=5
groups=('archstrike' 'archstrike-tunnel')
pkgdesc="A tool for relaying TCP connections over DNS"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.hsc.fr/ressources/outils/dns2tcp/index.html.en'
license=('GPL')
depends=('glibc')
source=("https://github.com/alex-sector/${pkgname}/archive/v${pkgver}.tar.gz"
        "config.guess-for-aarch64.patch")
sha512sums=('2b8cb84142ed23b62df4d33c8287e9dae0a8df7483661d4a9c3042f5417d010f0da33bf7b0d36f4b8b2ebb0e51aba99dd9aff936d82324fbcc2a2fb5829d1b62'
            '733648e8dade3c13259ab275dc795bf678c1d5001a7e15f8e1a4cc9e518cdc0b465bc9c7d5bcf4f48e7619366b11fea6a14ca694e5b70c4a5fcdf46353b60688')

prepare() {
 cd "${srcdir}/${pkgname}-${pkgver}"
 patch -Np1 -i ${srcdir}/config.guess-for-aarch64.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
