# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=nomorexor
pkgname=$_pkgname-git
pkgver=20180611.r2.84489f9
pkgrel=1
groups=('archstrike' 'archstrike-crypto')
pkgdesc='Tool to help guess a files 256 byte XOR key by using frequency analyze'
arch=('any')
url="https://github.com/hiddenillusion/NoMoreXOR"
license=('custom')
depends=('python2-yara')
makedepends=('git')
source=("$pkgname::git+https://github.com/hiddenillusion/NoMoreXOR/")
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname" || exit
  printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  cd "$pkgname" || exit
  sed -i 's|env python|env python2|' NoMoreXOR.py
}

package() {
  cd "$pkgname" || exit
  install -Dm644 Readme.md "$pkgdir/usr/share/doc/$_pkgname/README.md"
  install -Dm755 NoMoreXOR.py "$pkgdir/usr/share/$_pkgname/$_pkgname.py"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  grep created NoMoreXOR.py > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -dm755 "$pkgdir/usr/bin/"
  ln -sf "/usr/share/$_pkgname/$_pkgname.py" "$pkgdir/usr/bin/$_pkgname"
}
