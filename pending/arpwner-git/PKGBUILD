# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=arpwner-git
pkgver=20120106.r26.f300fdf
pkgrel=6
epoch=1
groups=('archstrike' 'archstrike-scanners')
pkgdesc="A tool for arp posioning and dns poisoning attacks, written in python."
url="https://github.com/ntrippar/ARPwner"
arch=('any')
license=('GPL3')
depends=('python2' 'python2-pypcap')
makedepends=('git')
replaces=('arpwner')
provides=('arpwner')
conflicts=('arpwner')
source=("${pkgname}::git+https://github.com/ntrippar/ARPwner.git" "arpwner.desktop")
sha512sums=('SKIP' 'e396feea228a34e78b581ce53a0a927b09f0afb88f5ddc9c7579e9e4e3d7371dcf66de4995ae567efffa36432649309a8898902a6a275df1a6e3d4008f59f525')

pkgver() {
    cd ${pkgname} || exit
    printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "${pkgname}" || exit
  install -dm755 "${pkgdir}/usr/bin/"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -Dm644 "${srcdir}/arpwner.desktop" "${pkgdir}/usr/share/applications/arpwner.desktop"
  cp -a --no-preserve=ownership * "${pkgdir}/usr/share/${pkgname}"
cat > "${pkgdir}/usr/bin/arpwner" << EOF
#!/bin/sh
cd /usr/share/${pkgname}
python2 main.py "\$@"
EOF
chmod +x "${pkgdir}/usr/bin/arpwner"
}
