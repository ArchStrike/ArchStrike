# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: N3mes1s <n3mes1s.oxoo@gmail.com>

buildarch=220

pkgname=netzob
pkgver=1.0rc1
pkgrel=2
pkgdesc="An opensource tool which supports the expert in its operations of reverse engineering, evaluation and simulation of communication protocols."
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://www.netzob.org"
license=('GPL3')
depends=('python2' 'strace' 'lsof' 'python2-matplotlib' 'python2-lxml' 'python2-sphinx' 'graphviz' 'python2-ptrace' 'python2-pcapy' 'python2-bitarray' 'impacket' 'python2-gobject' 'python2-babel' 'python2-httplib2' 'python2-bintrees')
makedepends=('python2-setuptools')
source=(http://www.netzob.org/repository/$pkgver/Netzob-$pkgver.tar.gz)
sha512sums=('2bb7a1560edaaa98a7d8ac4e374112b19deef4f16a988c8d21282a9f6f544453e968ca2f351d772c895f13aff26e68962477fc8d531caeee36848290d4d3e9ec')

build() {
  cd "$srcdir/Netzob-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/Netzob-$pkgver"
  python2 setup.py install --root=$pkgdir --optimize=1
  # fixing perms on all of the files below
  chmod 644 $pkgdir/usr/share/applications/netzob.desktop
  chmod 644 $pkgdir/usr/lib/python2.7/site-packages/Netzob-$pkgver-py2.7.egg-info/PKG-INFO
  for f in `find $pkgdir/usr/share/netzob/ -type f -iname "*.png" -or -type f -iname "*.xsd" \
  -or -type f -iname "*.glade" -or -type f -iname "*.ui" \
  -or -type f -iname "*.default"`; do chmod 644 $f; done
  for a in `find $pkgdir/usr/lib/python2.7/site-packages/Netzob-$pkgver-py2.7.egg-info/ -type f -iname "*.txt"`; do chmod 644 $a;done
}

