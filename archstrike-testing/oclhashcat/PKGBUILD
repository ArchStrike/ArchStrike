# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=192

pkgname=oclhashcat
_pkgname=oclHashcat
pkgver=1.33
pkgrel=1
pkgdesc="Worlds fastest WPA cracker with dictionary mutation engine"
url=('http://hashcat.net/oclhashcat/')
arch=('i686' 'x86_64')
license=('custom')
depends=('bash' 'libcl')
optdepends=('nvidia-utils>=319.37: For for gpu support via Nvidia CUDA'
            'catalyst-utils>=13.4: For gpu support via ATI Stream'
            'opencl-catalyst: For gpu support via ATI Stream')
source=("http://hashcat.net/files/$_pkgname-$pkgver.7z")
sha512sums=('0a15b8ad0b1bd670f157d8144c87e53370563f343268ad133a9af508485f5611d4b679bf43c8cc9ed907d0d0b9bcc86e777f140b118d0cad06fe4ca8a2a0ff98')
options=('!strip')

prepare() {
  cd $_pkgname-$pkgver
  sed -i '1,2d;s|${OCLHASHCAT_ROOT}/oclHashcat64.bin "${OCLHASHCAT_ROOT}"/oclHashcat32.bin "${OCLHASHCAT_ROOT}"/cudaHashcat64.bin "${OCLHASHCAT_ROOT}"/cudaHashcat32.bin|oclHashcat|' extra/tab_completion/oclHashcat64.sh
}

package() {
  install -dm755 "$pkgdir/opt/$pkgname"
  cp -a --no-preserve=ownership  $_pkgname-$pkgver/* "$pkgdir/opt/$pkgname/"
  cd "$pkgdir/opt/$pkgname"

  # Install bash completion
  install -Dm644 extra/tab_completion/oclHashcat64.sh "$pkgdir/usr/share/bash-completion/completions/oclHashcat"
  rm -rf extra/tab_completion

  # Install the executable script
  install -dm755 "$pkgdir/usr/bin"
  if [ "$CARCH" = "x86_64" ]; then
    rm oclHashcat32.bin
    echo -e "#!/bin/bash\n/opt/$pkgname/oclHashcat64.bin \$@" > "$pkgdir/usr/bin/$_pkgname"
  else
    rm oclHashcat64.bin
    echo -e "#!/bin/bash\n/opt/$pkgname/oclHashcat32.bin \$@" > "$pkgdir/usr/bin/$_pkgname"
  fi
  chmod a+x "$pkgdir/usr/bin/$_pkgname"

  # Cleanup win-executables
  find . \( -name "*.cmd" -o -name "*.exe" \) -exec rm '{}' \;

  # Cleanup permissions
  find "$pkgdir" -iname '*.txt' -exec chmod 644 '{}' \;
  find "$pkgdir" -iname '*.llvmir' -exec chmod 644 '{}' \;
  find "$pkgdir" -iname '*.hash' -exec chmod 644 '{}' \;
  find "$pkgdir" -iname '*.sh' -exec chmod 644 '{}' \;
  find "$pkgdir" -iname '*.rule' -exec chmod 644 '{}' \;
  find "$pkgdir" -iname '*.hcmask' -exec chmod 644 '{}' \;
  find "$pkgdir" -iname '*.hcstat' -exec chmod 644 '{}' \;
  find "$pkgdir" -iname '*.dict' -exec chmod 644 '{}' \;
  find "$pkgdir" -iname '*.bin' -exec chmod 755 '{}' \;
}

