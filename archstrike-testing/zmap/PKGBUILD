# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=zmap
pkgver=1.2.1
pkgrel=1
groups=('archstrike' 'archstrike-scanners')
pkgdesc='Fast network scanner designed for Internet-wide network surveys'
url='https://zmap.io/'
license=('Apache')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('gmp' 'libpcap' 'json-c')
makedepends=('byacc-noconflict' 'cmake' 'gengetopt')
backup=('etc/zmap/blacklist.conf' 'etc/zmap/zmap.conf')
source=("https://github.com/zmap/zmap/archive/v$pkgver.tar.gz")
sha512sums=('1b3d7fa41bb75c859463f3ec8bcde31778f5d91a6f561aa9638ba93be97dacdba89493574581ce52aebcf093a86b042dcce41cffe6cb0849bb76a6005b714be2')

build() {
  cd "$srcdir/zmap-$pkgver"

  cmake -DWITH_JSON=ON
  make
}

package() {
  cd "$srcdir/zmap-$pkgver/src"

  # Bin.
  install -Dm755 zmap "$pkgdir/usr/bin/zmap"

  # Man.
  install -Dm644 zmap.1 "$pkgdir/usr/share/man/man1/zmap.1"

  # Etc.
  install -dm755 "$pkgdir/etc/zmap"
  install -m644 ../conf/* "$pkgdir/etc/zmap"

  # Doc.
  cd ..
  for f in AUTHORS CHANGELOG INSTALL README; do
    install -Dm644 $f "$pkgdir/usr/share/doc/zmap/$f";
  done
  cp -a examples "$pkgdir/usr/share/doc/zmap"
}
