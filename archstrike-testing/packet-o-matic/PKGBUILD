# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=212

pkgname='packet-o-matic'
_pkgname='pom-ng'
pkgver='0.0.18'
pkgrel=1
pkgdesc='A real time packet processor. Reads the packet from an input module, match the packet using rules and connection tracking information and then send it to a target module.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www.packet-o-matic.org/'
license=('GPL')
groups=('archstrike' 'archstrike-networking' 'archstrike-tunnel')
depends=('libpcap' 'libjpeg-turbo' 'libmicrohttpd' 'postgresql-libs' 'file' 'sqlite' 'lua51' 'libexif' 'xmlrpc-c')
source=("https://github.com/gmsoft-tuxicoman/pom-ng/archive/v$pkgver.tar.gz")
sha512sums=('446471405c59210ac4839fc8f6ec8ab23a07a9176c40fc55a274ebfe8c68f3e905aa67bceecc0ed16895dfd01bca2b9f6e0023e6bc1685fd1c0a98a929479ec9')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  touch config.h.in
  libtoolize --force --copy
  aclocal
  automake --add-missing
  autoconf
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

