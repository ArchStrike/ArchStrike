# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname=pathod
pkgver=0.11.1
pkgrel=2
groups=('archstrike' 'archstrike-proxy')
pkgdesc="Crafted malice for tormenting HTTP clients and servers"
arch=('any')
url="http://pathod.net/"
license=('custom')
depends=('python2-flask' 'python2-netlib' 'python2-pyasn1')
source=("http://mitmproxy.org/download/pathod-${pkgver}.tar.gz")
sha512sums=('e576ae9ef91f4b369af1d87e1cd7f2730296a8c116068beb6c4948cea9b71118bd028e9b3916c718a8196335d1e81467bc4161e7927b6eef0bb87bea4f029301')

build() {
  cd "${srcdir}/pathod-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/pathod-${pkgver}"

  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/pathod/LICENSE"
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
   -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
   -e "s|#![ ]*/bin/python$|#!/usr/bin/python2|" \
   -e "s|#![ ]*/usr/local/bin/python$|#!/usr/bin/python2|" \
   $(find $pkgdir -name '*.py')
  rm -rf "${pkgdir}"/usr/lib/python2.7/site-packages/netlib
}
