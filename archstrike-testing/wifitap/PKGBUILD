# Maintainer: ArchStrike <team@archstrike.org>
_gitname="GDSSecurity"

buildarch=1

pkgname=wifitap
pkgver=2b16088
pkgrel=1
groups=('archstrike' 'archstrike-wireless')
pkgdesc="WiFi injection tool through tun/tap device"
arch=('any')
url='https://github.com/GDSSecurity/wifitap'
license=('GPL2')
depends=('python2' 'scapy')
makedepends=('git')
optdepends=('pysco2')
source=("git+https://github.com/GDSSecurity/wifitap.git")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd "$srcdir/wifitap"

  # Fix python shebangs.
  sed -i 's|env python|env python2|' *.py

  # Fix imports.
  sed -i 's|import IP,ICMP|import *|' wifiping.py
  sed -i 's|import ARP|import *|' wifiarp.py
}

package() {
  cd "$srcdir/wifitap"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/wifitap"

  # Bin.
  for file in *.py ; do
    install -m755 "$file" "$pkgdir/usr/bin/${file%.*}"
  done

  # Docs.
  install -m644 README TODO BUGS "$pkgdir/usr/share/wifitap"
}
