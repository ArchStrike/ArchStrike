# Maintainer: ArchStrike <team@archstrike.org>

_pkgname=ar-drone

buildarch=1

pkgname=node-$_pkgname-git
pkgver=v0.3.3.2.g228bd45
pkgrel=2
pkgdesc='A node.js client for controlling Parrot AR Drone 2.0 quad-copters'
arch=('any')
url='https://github.com/felixge/node-ar-drone'
license=('MIT')
depends=('nodejs')
makedepends=('git')
source=("$_pkgname::git+$url")
sha512sums=('SKIP')
replaces=("node-$_pkgname")
conflicts=("node-$_pkgname")
provides=("node-$_pkgname")

pkgver() {
  cd $_pkgname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  # Install node dependencies
  cd $_pkgname
  npm install &>/dev/null
}

package() {
  cd $_pkgname
  install -dm755 "$pkgdir/usr/lib/node_modules/$_pkgname"
  cp --no-preserve=ownership -r {lib,examples,node_modules,test,index.js,Makefile,package.json} "$pkgdir/usr/lib/node_modules/$_pkgname/"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$_pkgname/README.md"
  install -Dm644 Changes.md "$pkgdir/usr/share/doc/$_pkgname/Changes.md"
  install -Dm644 CONTRIBUTING.md "$pkgdir/usr/share/doc/$_pkgname/CONTRIBUTING.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
