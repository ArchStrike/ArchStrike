# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=pytbull
pkgver=2.0
pkgrel=2
groups=('archstrike' 'archstrike-scanners')
pkgdesc="A python based flexible IDS/IPS testing framework shipped with more than 300 tests"
arch=('any')
url='http://pytbull.sourceforge.net/'
license=('GPL2')
depends=('python2' 'scapy' 'python2-feedparser')
source=("https://downloads.sourceforge.net/project/pytbull/pytbull-$pkgver.tar.bz2")
sha512sums=('2131b8eacf7b0e2d00313ae81bf00f15b83b8de14a4e267afc74b99e65b1704fab9a2bf5a4abeff664f7aa9bb367033fa013cb82a5e1b89915fea0bf09b037bb')

prepare(){
  grep -iRl 'python' "$srcdir/pytbull" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
  cd $srcdir/pytbull
  sed -i 's/self.checkNewVersionAvailable()/False/' pytbull
}

package() {
  cd "$srcdir/pytbull"

  # Make base directories.
  install -dm755 "$pkgdir/usr/share/pytbull"
  install -dm755 "$pkgdir/usr/bin"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/pytbull"

cat > "$pkgdir/usr/bin/pytbull" << EOF
#!/bin/sh
cd /usr/share/pytbull
python2 pytbull "\$@"
EOF

chmod +x "$pkgdir/usr/bin/pytbull"

}
