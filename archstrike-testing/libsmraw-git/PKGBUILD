# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

_pkgname=libsmraw
pkgname=${_pkgname}-git
pkgver=r203.29c8155
pkgrel=2
pkgdesc="Library and tools to access the (split) RAW image format"
url="https://github.com/libyal/${_pkgname}"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('LGPL3')
depends=('fuse' 'libcsystem' 'libbfio' 'python2')
makedepends=('git' 'python2')
provides=('libsmraw')
conflicts=('libsmraw')
replaces=('libsmraw')
source=("${_pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_pkgname}"
  ./synclibs.sh
  ./autogen.sh
  PYTHON_VERSION="2" ./configure --prefix=/usr --disable-rpath --enable-verbose-output --enable-debug-output --enable-python --with-pyprefix=2
  make
}

package() {
  cd "${srcdir}/${_pkgname}"
  make DESTDIR="${pkgdir}" install
}
# vim:se ts=2 sw=2 et:
