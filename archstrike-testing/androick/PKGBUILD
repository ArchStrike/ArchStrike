# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=androick
pkgver=2.1
pkgrel=1
groups=('archstrike' 'archstrike-forensics')
pkgdesc="A python tool to help in forensics analysis on android."
arch=('any')
url='https://github.com/Flo354/Androick'
license=('custom')
depends=('python2-magic')
install=${pkgname}.install
source=("https://github.com/Flo354/Androick/archive/${pkgver}.tar.gz")
sha512sums=('a9a5ac434ba6529cc7ca40c65019d750a69f6761518e832e500ef17d6d6213185ec0ccd5b773184c24f38b4379b2622fdf9fdf0eed860a6c64ce683e6c17348f')

prepare(){
  grep -iRl 'python' "$srcdir/Androick-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/Androick-$pkgver"

  # Make base directories.
  install -dm755 "$pkgdir/usr/share/$pkgname"
  install -dm755 "$pkgdir/usr/bin"

  install -Dm755 *.py "$pkgdir/usr/share/$pkgname/"
  install -Dm644 changelog "$pkgdir/usr/share/$pkgname/"
  install -Dm644 README.md "$pkgdir/usr/share/licenses/$pkgname/README.md"

cat > "$pkgdir/usr/bin/androick" << EOF
#!/bin/sh
cd /usr/share/androick
python2 androick.py "\$@"
EOF

chmod +x "$pkgdir/usr/bin/androick"

}
