# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: James An <james@jamesan.ca>

buildarch=1

_pkgname=subbrute
pkgname=$_pkgname-git
pkgver=20150903.r99
pkgrel=1
pkgdesc='A DNS meta-query spider that enumerates DNS records, and subdomains'
arch=('any')
url='https://github.com/TheRook/subbrute'
license=('GPL3')
depends=('python-dnspython' 'python-dnslib')
makedepends=('git')
provides=("$_pkgname")
conflicts=("$_pkgname")
options=('!emptydirs')
source=("git+https://github.com/TheRook/$_pkgname")

sha512sums=('SKIP')

pkgver() {
  cd $_pkgname
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

package() {
  cd $_pkgname

  # install upstream files
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/$_pkgname"
  install -dm755 "$pkgdir/usr/share/$_pkgname/windows"
  install -Dm644 windows/* "$pkgdir/usr/share/$_pkgname/windows/"
  install -m755 -t "$pkgdir/usr/share/$_pkgname" resolvers.txt subbrute.py names.txt names_small.txt
  install -Dm755 README.md "$pkgdir/usr/share/doc/$_pkgname/README.md"

cat > "$pkgdir/usr/bin/$_pkgname" << EOF
#!/usr/bin/env bash

cd /usr/share/$_pkgname
python $_pkgname.py "\$@"
EOF

  chmod 755 "$pkgdir/usr/bin/$_pkgname"
}
