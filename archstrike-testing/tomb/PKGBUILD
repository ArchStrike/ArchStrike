# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Prurigro

buildarch=212

pkgname=tomb
pkgver=2.2
pkgrel=1
pkgdesc="A system for easy encryption and backup of personal files"
url="http://www.dyne.org/software/${pkgname}"
groups=('archstrike')
license=('GPL3')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('cryptsetup' 'pinentry' 'sudo' 'gnupg' 'zsh' 'gtk2' 'libnotify')
optdepends=('dcfldd: Replacement for dd with hashing support'
            'wipe: Secure file wiping utility'
            'e2fsprogs: Create and manipulate ext3 and ext4 filesystems'
            'coreutils: Create temp image files in shared memory'
            'steghide: Hide data in various kinds of image and audio files'
            'swish-e: Support file content indexing'
            'qrencode: Create paper backups of keys as QR codes')

source=("https://files.dyne.org/tomb/tomb-${pkgver}.tar.gz")
sha512sums=('11723f1cccc75ffe64156b1f26ee7095a0147b48e05c58e98ea0fac933e9f7e5884c705a10dd4416338bab443c56902435709de85e313a053fe61883631e1290')

prepare() {
    sed -i 's/^#!\/bin\/zsh/#!\/usr\/bin\/env\ zsh/g' tomb-${pkgver}/${pkgname}
}

build() {
    cd tomb-${pkgver}/extras/gtk-tray
    make

    cd ../kdf-keys
    make
}

package() {
    cd tomb-${pkgver}

    # Install tomb
    make DESTDIR="${pkgdir}" PREFIX="/usr" install
    chmod 644 "${pkgdir}/usr/share/man/man1/tomb.1"

    # Install the gtk tray icon
    install -Dm755 extras/gtk-tray/tomb-gtk-tray "${pkgdir}/usr/bin/tomb-gtk-tray"

    # Install kdf-keys
    cd extras/kdf-keys
    make DESTDIR="${pkgdir}" PREFIX="/usr" install
}
