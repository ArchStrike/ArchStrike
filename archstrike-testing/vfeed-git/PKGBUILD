# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname=vfeed-git
pkgver=20150811.r1
pkgrel=1
epoch=1
groups=('archstrike' 'archstrike-misc')
pkgdesc="Open Source Cross Linked and Aggregated Local Vulnerability Database main repository"
url="http://www.toolswatch.org/vfeed"
arch=('any')
license=('BSD')
depends=('python2')
makedepends=('git')
provides=('vfeed')
conflicts=('vfeed')
replaces=('vfeed')
source=("${pkgname}::git+https://github.com/toolswatch/vFeed.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare(){
  grep -iRl 'python' "${pkgname}" |
    xargs sed -i -e 's|#!.*/usr/bin/python|#!/usr/bin/python2|' \
                 -e 's|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "${pkgname}"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin/"
  install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE.md
  cp -a --no-preserve=ownership * "${pkgdir}/usr/share/${pkgname}"
  ln -s "/usr/share/${pkgname}/vfeedcli.py" "${pkgdir}/usr/bin/vfeed"
}
