# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: William Robertson <nullptr@gmail.com>

buildarch=220

pkgname=silk
pkgver=3.13.0
pkgrel=1
pkgdesc='CERT netflow tools.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'armv8' 'aarch64')
url='http://tools.netsa.cert.org/silk'
license=('GPL')
groups=('archstrike' 'archstrike-networking')
depends=('libfixbuf' 'lzo2' 'libpcap' 'python2' 'gnutls' 'c-ares' 'adns' 'snappy')
makedepends=('perl' 'flex' 'bison')
install="$pkgname.install"
source=("http://tools.netsa.cert.org/releases/$pkgname-$pkgver.tar.gz")
sha512sums=('52e1740a5b5acaa572026d61be19a6c99f9fc84001e29f3c30179068473117fdc112ff1c03bc9258ed7289853471aa2713e7ee7a286100cfd7a4f2e87837fd6c')

prepare(){
  grep -iRl 'python' "$srcdir/$pkgname-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export LIBS="-lpthread"
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin --enable-ipv6 --with-python=$(which python2) --with-pcap \
  --with-pic --with-libfixbuf --with-gnutls --enable-output-compression --enable-localtime --enable-data-rootdir=/usr/share/$pkgname
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  # Fixing python
  sed -i 's|python$|python2|' $pkgdir/usr/bin/rwidsquery $pkgdir/usr/bin/rwpcut
}
