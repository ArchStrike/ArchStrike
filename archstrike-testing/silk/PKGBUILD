# Maintainer: ArchStrike <archstrikelinux@gmail.com>
# Contributor: William Robertson <nullptr@gmail.com>

buildarch=212

pkgname=silk
pkgver=3.10.1
pkgrel=3
pkgdesc='CERT netflow tools.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://tools.netsa.cert.org/silk'
license=('GPL')
groups=('archstrike' 'archstrike-networking')
depends=('libfixbuf' 'lzo2' 'libpcap' 'python2' 'gnutls' 'c-ares' 'adns')
makedepends=('perl' 'flex' 'bison')
install="$pkgname.install"
source=("http://tools.netsa.cert.org/releases/$pkgname-$pkgver.tar.gz")
sha512sums=('73c1804f39014d6b63b43a1d6689a060f700e78dbaeba9b04a0136c0d35873a8e1b0d2c30279f9cb8aaebd0982aa1c212d3a8d8817ceb721715e2b815fc1556e')

prepare(){
  grep -iRl 'python' "$srcdir/$pkgname-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
        export LIBS="-lpthread"
        ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin --enable-ipv6 --with-python=$(which python2) --with-pcap \
        --with-pic --with-libfixbuf --with-gnutls --enable-output-compression --enable-localtime
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
        # Fixing python
        sed -i 's|python$|python2|' $pkgdir/usr/bin/rwidsquery $pkgdir/usr/bin/rwpcut
}
