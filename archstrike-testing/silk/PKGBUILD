# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: William Robertson <nullptr@gmail.com>

buildarch=220

pkgname=silk
pkgver=3.18.0
pkgrel=1
pkgdesc='CERT netflow tools.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'armv8' 'aarch64')
url='http://tools.netsa.cert.org/silk'
license=('GPL')
groups=('archstrike' 'archstrike-networking')
depends=('libfixbuf' 'lzo' 'libpcap' 'python2' 'gnutls' 'c-ares' 'adns' 'snappy')
makedepends=('perl' 'flex' 'bison')
install="$pkgname.install"
source=("http://tools.netsa.cert.org/releases/$pkgname-$pkgver.tar.gz")
sha512sums=('ca07195531ad3d89d7cfd484d147cafe05b7a93b2cfb6ee32792485a002f13afaae3a2e79b09cbdcad1fa6134b5ec2aefe60ce06f589650f0e3da12df784e90d')

prepare(){
  grep -iRl 'python' "$srcdir/$pkgname-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

build() {
  cd "${pkgname}-${pkgver}"
  export LIBS="-lpthread"
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin --enable-ipv6 --with-python=$(which python2) --with-pcap \
  --with-pic --with-libfixbuf --with-gnutls --enable-output-compression --enable-localtime --enable-data-rootdir=/usr/share/$pkgname
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # Fixing python
  sed -i 's|python$|python2|' "${pkgdir}/usr/bin/rwidsquery" "${pkgdir}/usr/bin/rwpcut"
}
