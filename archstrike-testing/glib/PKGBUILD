# Maintainer: ArchStrike <archstrikelinux@gmail.com>
# Contributor: Connor Behan <connor.behan@gmail.com>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

buildarch=212

pkgname=glib
pkgver=1.2.10
pkgrel=13
pkgdesc="Common C routines used by Gtk+ and other libs"
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL')
depends=('glibc' 'sh')
options=('!makeflags')
install=glib.install
source=(ftp://ftp.gnome.org/pub/gnome/sources/glib/1.2/${pkgname}-${pkgver}.tar.gz
  gcc340.patch aclocal-fixes.patch glib1-autotools.patch)
sha512sums=('4452b741382194d4ca425f084fa60e1a9e30040ac2e839c9136047d0461cf4b1ebcc16aac4e65998d1b9329ead8f617966b3d09317a83634f14d556a7e14b7a8'
            'a850b824b55d89fed7a4730c99487aaf24d75e28ae71c195d5edfca4b0db4676ff8a2809bb3c537588e19c8e7ff736a71541505b9f174192785f458c3edc50fe'
            '24fe03a4b767face55a1ca5f8f99ee44e7852adf5c5749ae11aed34d4453ecbabe22b3254eeced4201370b8df91fa46c7746bb7f5116dfcb53670919d031d97d'
            '0b6710db46ebed7a518791623cc08b31fed914965d2be30e93ee113f1a5333db6e9d21a6e474fa1751dd99395871fc597af14c4c0bc4136a5a222a6d7cd6b703')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i "${srcdir}/gcc340.patch"
  patch -Np0 -i "${srcdir}/aclocal-fixes.patch"
  patch -Np1 -i "${srcdir}/glib1-autotools.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  if [[ $CARCH = "i686" ]]; then
    CONFIGFLAG='--host=i686-pc-linux-gnu --target=i686-pc-linux-gnu'
  elif [[ $CARCH = "x86_64" ]]; then
    CONFIGFLAG='--host=x86_64-unknown-linux-gnu --target=x86_64-unknown-linux-gnu'
  elif [[ $CARCH == "armv6h" ]]; then
    CONFIGFLAG='--host=armv6l-unknown-linux-gnueabihf --target=armv6l-unknown-linux-gnueabihf'
  else [[ $CARCH == "armv7h" ]]
    CONFIGFLAG='--host=armv7l-unknown-linux-gnueabihf --target=armv7l-unknown-linux-gnueabihf'
  fi

  autoreconf --force --install
  ./configure --prefix=/usr --mandir=/usr/share/man \
    --infodir=/usr/share/info $CONFIGFLAG
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
