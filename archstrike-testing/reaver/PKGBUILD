# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=reaver
pkgver=1.4
pkgrel=3
groups=('archstrike' 'archstrike-crackers')
pkgdesc="Implements a brute force attack against wifi protected setup WPS registrar PINs in order to recover WPA/WPA2 passphrases"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://code.google.com/p/reaver-wps'
license=('GPL2')
depends=('libpcap' 'sqlite')
source=("https://reaver-wps.googlecode.com/files/reaver-$pkgver.tar.gz")
sha512sums=('59f2d6f8aa6228988ce5d1c102d9a48b2a23e582ff3d9afe39d7c9717b7e4be059362fdd21a3e5f5d59948df1a7616acdaa6df20f139e9c5aa9bd673f764dfbc')

build() {
  cd "$srcdir/$pkgname-$pkgver/src"

  ./configure --prefix=/usr --sysconfdir=/etc/
  make
}

# We can't install with the Makefile. It tries to clean up the src directory,
#  which makepkg forbids.
package() {
  cd "$srcdir/$pkgname-$pkgver/src"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/etc/reaver/"

  install -m755 reaver "$pkgdir/usr/bin"
  install -m755 wash "$pkgdir/usr/bin"
  install -m644 reaver.db "$pkgdir/etc/reaver"
}
