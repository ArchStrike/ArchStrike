# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=212

pkgname=reaver
pkgver=1.4
pkgrel=2
groups=('archstrike' 'archstrike-crackers')
pkgdesc="Implements a brute force attack against wifi protected setup WPS registrar PINs in order to recover WPA/WPA2 passphrases"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://code.google.com/p/reaver-wps'
license=('GPL2')
makedepends=('subversion')
depends=('libpcap')
source=('svn+http://reaver-wps.googlecode.com/svn/trunk/')
md5sums=('SKIP')

build() {
  cd "$srcdir/trunk/src"

  ./configure --prefix=/usr --sysconfdir=/etc/
  make
}

# We can't install with the Makefile. It tries to clean up the src directory,
#  which makepkg forbids.
package() {
  cd "$srcdir/trunk/src"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/etc/reaver/"

  install -m755 reaver "$pkgdir/usr/bin"
  install -m755 wash "$pkgdir/usr/bin"
  install -m644 reaver.db "$pkgdir/etc/reaver"
}
