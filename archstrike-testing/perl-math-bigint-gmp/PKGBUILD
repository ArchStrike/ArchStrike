# Maintainer: ArchStrike <team@archstrike.org>
_author=PJACKLAM
_perlmod=Math-BigInt-GMP

buildarch=212

pkgname=perl-math-bigint-gmp
pkgver=1.51
pkgrel=2
pkgdesc="Big integer calculations using the GNU Multiple Precision Arithmetic Library."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://search.cpan.org/~PJACKLAM/Math-BigInt-GMP-1.37/'
license=('GPL' 'PerlArtistic')
depends=('perl>=5.16.2' 'gmp')
options=(!emptydirs)
source=(http://cpan.perl.org/modules/by-authors/id/P/PJ/$_author/$_perlmod-$pkgver.tar.gz)
sha512sums=('f2e56ce48e9a387821b409078228d20f7348eef5eea0aac4844a0f29340c7a2677693fda40c2bb85527f2f77cd9519401109b037040fc18266b401cfae134601')

build() {
  cd "$srcdir/$_perlmod-$pkgver"
  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir/$_perlmod-$pkgver"
  make install DESTDIR="$pkgdir/"

  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
}
