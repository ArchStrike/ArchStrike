# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname="libfwsi"
pkgver="20160110"
pkgrel=1
pkgdesc="Library to access the Windows Shell Item format"
arch=("i686" "x86_64" "armv6h" "armv7h")
url='https://github.com/libyal/libfwsi'
license=('GPL3')
depends=('libcsystem' 'libbfio' 'python2')
makedepends=('git')
source=("https://github.com/libyal/libfwsi/archive/$pkgver.tar.gz")
sha512sums=('ab61eef6a49ff61b8497b66a83bb3524d5d3a0eb9f47bee989c995830c39454bf74e12e9e0ade938f94f94236c438db578a10e474bf28df80dcf824bb2b929ae')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./synclibs.sh
  ./autogen.sh
  PYTHON_VERSION="2" ./configure --prefix=/usr --disable-rpath --enable-verbose-output --enable-debug-output --enable-python
  # Fix really weird compile error related to a commented out line libcfile_support.c:742:2: error: #error Missing file remove function
  sed -i '742d' libcfile/libcfile_support.c 
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
