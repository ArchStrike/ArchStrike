# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname='grokevt'
pkgver='0.5.0'
pkgrel=2
pkgdesc='A collection of scripts built for reading WindowsÂ® NT/2K/XP/2K eventlog files.'
url='http://code.google.com/p/grokevt/'
license=('GPL')
depends=('python2' 'reglookup' 'cpio')
arch=('any')
groups=('archstrike' 'archstrike-forensics')
source=("http://grokevt.googlecode.com/files/grokevt-$pkgver.tar.gz")
sha512sums=('67a6034810cdc77d50776bc8e9b203137ae0d1aa399d7618ada409fba23cd46541927ef1d6087c6d68f182b14911a820d3020748b35d13dc741e12defd65b4fa')

build() {
  cd "$srcdir/grokevt-$pkgver"
  sed -i 's|PYTHON_PATH:=`which python`|PYTHON_PATH:=`which python2`|' Makefile
  make
}

package() {
  cd "$srcdir/grokevt-$pkgver"
  #fix python2 bindings
  sed -i 's|'grokevt-distutils.py\ install'|'grokevt-distutils.py\ install\ --root=\"$pkgdir\"\ --optimize=1'|' Makefile

  make PREFIX="$pkgdir/usr" ETC_PREFIX="$pkgdir/etc/grokevt" \
    MAN_PREFIX="$pkgdir/usr/share/man" install

  rm -rf "$pkgdir/usr/share/man"
  mv "$pkgdir/usr/share/doc/grokevt/man" "$pkgdir/usr/share/"
  rm -rf "$pkgdir/usr/share/doc"

  for i in $pkgdir/usr/bin/grokevt-findlogs $pkgdir/usr/bin/grokevt-parselog $pkgdir/usr/bin/grokevt-ripdll $pkgdir/usr/bin/grokevt-dumpmsgs $pkgdir/usr/bin/grokevt-builddb; do sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|' $i;done
}

