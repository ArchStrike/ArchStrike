# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

pkgname=dradis
pkgver=3.1.0
pkgrel=1
pkgdesc="An open source framework to enable effective information sharing."
groups=('archstrike' 'archstrike-misc')
url="http://dradisframework.org/"
license=('GPL')
depends=('ruby' 'redis')
arch=('x86_64' 'i686')
options=(!strip)
source_x86_64=("http://downloads.sourceforge.net/dradis/dradis-$pkgver.rc2-linux-x86_64.tar.gz")
sha512sums_x86_64=('ee0b300395c9c59270890c86cf6f0881af494e128d8e837e899ea09f3356e02a78626ee3791630ac00771db4b060f1e718dbf7153c785c1f082334a943bd16e9')
sha512sums_i686=('1ab6b4f53aba87ba524a56bebcb577f2830f2219e905d1dffb9e26b98bc732bc9bff8552836171d4daa143bcd06717915a9d222dc73de44bbe50eedacde83b7e')
source_i686=("http://downloads.sourceforge.net/dradis/dradis-$pkgver.rc2-linux-x86.tar.gz")

package() {

  # Base directories.
  if [[ "$CARCH" == 'i686' ]]; then
    cd dradis-$pkgver.rc2-linux-x86
  elif [[ "$CARCH" == 'x86_64' ]]; then
    cd dradis-$pkgver.rc2-linux-x86_64
  fi
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/dradis"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/dradis"

  cd "$pkgdir/usr/bin"

  # Joint scripts.
  cat > dradis-webapp << EOF
#!/bin/sh
cd /usr/share/dradis
./dradis-webapp "\$@"
EOF

  cat > dradis-worker << EOF
#!/bin/sh
cd /usr/share/dradis
./dradis-worker "\$@"
EOF

  chmod 755 dradis-webapp dradis-worker
}
