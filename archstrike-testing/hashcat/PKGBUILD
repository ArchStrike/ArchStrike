# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

pkgname=hashcat
pkgver=2.00
pkgrel=2
groups=('archstrike' 'archstrike-crackers')
pkgdesc="A multithreaded cross platform hash cracker."
arch=('i686' 'x86_64')
url='https://github.com/hashcat/hashcat'
license=('MIT')
depends=('gmp')
source=("https://github.com/hashcat/hashcat/archive/$pkgver.tar.gz")
sha512sums=('367215bf4f6a48b5ce1806c5e45091b7640c1d2d275ee49b37204273d7485f5f5dce228b4c898247cb152209d85cff737225a457bf16c18a3f7e9afc57201040')

build() {
  cd "${pkgname}-${pkgver}"
  if [[ $CARCH == "x86_64" ]]; then
        make posix64
  else
        make posix32
  fi
}

package() {
  cd "${pkgname}-${pkgver}"

  install -dm755 "${pkgdir}/usr/share/doc/hashcat"
  install -dm755 "${pkgdir}/etc/hashcat"

  if [[ "$CARCH" == 'x86_64' ]]; then
         install -Dm755 hashcat-cli64.bin "${pkgdir}/usr/bin/hashcat"
  else
         install -Dm755 hashcat-cli32.bin "${pkgdir}}/usr/bin/hashcat"
  fi

  cp -dpr --no-preserve=ownership "${srcdir}/hashcat-${pkgver}"/{charsets,rules,salts,tables} "${pkgdir}"/etc/"hashcat"/
  cp -dpr --no-preserve=ownership "${srcdir}/hashcat-${pkgver}"/examples "${pkgdir}"/usr/share/doc/"hashcat"/
  # License
  install -Dm644 docs/license.txt "$pkgdir/usr/share/licenses/$pkgname/license.txt"

  # Fix permissions.
  cd "${pkgdir}"
  find . -type d -exec chmod 755 {} \+
  find . -type f -exec chmod 644 {} \+
  chmod a+x "${pkgdir}/usr/bin/hashcat"
}
