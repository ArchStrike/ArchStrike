# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=crackle-git
pkgver=71.20215f8
pkgrel=1
groups=('archstrike' 'archstrike-crackers' 'archstrike-bluetooth')
pkgdesc="Crack and decrypt BLE encryption"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/mikeryan/crackle/'
license=('GPL')
depends=('libpcap')
makedepends=('git')
replaces=('crackle')
provides=('crackle')
source=("$pkgname::git+https://github.com/mikeryan/crackle/")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$pkgname"
  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
  make
}

package() {
  cd "$pkgname"
  install -dm755 "$pkgdir/usr/bin"
  make "INSTALL_DIR=$pkgdir/usr/bin" install
}
