# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=rfidiot-git
_gitname=RFIDIOt
pkgver=20160514.r73
pkgrel=1
epoch=1
groups=('archstrike' 'archstrike-wireless')
pkgdesc="An open source python library for exploring RFID devices."
url='http://rfidiot.org'
license=('GPL')
makedepends=('git' 'python2-setuptools' 'python2-crypto')
depends=('python2' 'openssl' 'python2-pyscard')
arch=('any')
replaces=('rfidiot')
provides=('rfidiot')
source=("$pkgname::git+https://github.com/AdamLaurie/RFIDIOt")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  cd "$pkgname"
  # Change shebang lines.
  find . -type f -name '*.py' -exec sed -i '1s/python\r\?$/python2/' '{}' \;
}

package() {
  cd "$pkgname"
  python2 setup.py install --root="$pkgdir/" --optimize=1

  cd "$pkgdir/usr/bin"

  # Remove test scripts.
  rm -f *.sh

  # Strip extensions and add the 'rfidiot-' prefix.
  for file in *.py ; do
    # Remove extension. Add prefix.
    new_file=rfidiot-$(basename "${file%.*}")
    # '_' -> '-'
    new_file=${new_file/_/-}
    install -Dm755 "$file" "$new_file"
    rm -f "$file"
  done
}
