# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=suricata
pkgver=3.1.3
pkgrel=2
pkgdesc="An Open Source Next Generation Intrusion Detection and Prevention Engine"
arch=('armv6h' 'armv7h' 'i686' 'x86_64' 'aarch64')
url="http://openinfosecfoundation.org/index.php/download-suricata"
license=('GPL2')
groups=('archstrike')
depends=('nss' 'jansson' 'libcap-ng' 'libnetfilter_queue' 'pcre' 'libdaq' 'libnet' 'libpcap' 'libyaml' 'python2-magic' 'file' 'luajit' 'geoip' 'libnetfilter_log')
optdepends=('snort: suricata can use rulesets provided by snort')
options=('!emptydirs')
backup=('etc/suricata/suricata.yaml')
source=(http://openinfosecfoundation.org/download/$pkgname-$pkgver.tar.gz)
sha512sums=('d29c2c4344d52ba3d8c5ed4331a35b512e323c9a13a73e3039df6406d8c6389d05e3b311db6b561125c12dfbea67b121afbdecb7f0a5cb0594cf339b492726fb')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  find "${srcdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
  find "${srcdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|'
}

build() {
  cd ${srcdir}/$pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
  --localstatedir=/var \
  HAVE_PYTHON_CONFIG=/usr/bin/python2 \
  --enable-gccprotect \
  --enable-gccmarch-native \
  --enable-hiredis \
  --enable-nfqueue \
  --enable-python \
  --enable-nflog \
  --enable-luajit \
  --enable-geoip \
  --disable-coccinelle \
  --enable-old-barnyard2 \
  --with-pie
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  make DESTDIR=${pkgdir} install
  install -dm755 ${pkgdir}/var/log/$pkgname
  install -Dm644 $pkgname.yaml "${pkgdir}/etc/$pkgname/$pkgname.yaml"
  install -Dm644 classification.config "${pkgdir}/etc/$pkgname/classification.config"
  install -Dm644 reference.config "${pkgdir}/etc/$pkgname/reference.config"
  install -dm755 "${pkgdir}/etc/$pkgname/rules"
  install -Dm644 rules/*.rules "${pkgdir}/etc/$pkgname/rules/"
}
