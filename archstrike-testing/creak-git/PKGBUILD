# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=creak-git
pkgver=20161102.r32
pkgrel=1
groups=('archstrike' 'archstrike-mitm')
arch=('any')
pkgdesc="Poison, reset, spoof, redirect MITM script"
url="https://github.com/codepr/creak"
license=('GPL')
depends=('python2' 'python2-dpkt-git' 'python2-pypcap' 'scapy' 'libdnet')
makedepends=('python2-setuptools' 'git')
source=("${pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  grep -iRl 'python' ${pkgname} | xargs sed -i 's|python$|python2|g'
}

package() {
  cd ${pkgname}
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"

  cp -a --no-preserve=ownership * "${pkgdir}/usr/share/${pkgname}"

cat > "${pkgdir}/usr/bin/creak" << EOF
#!/usr/bin/env bash
cd /usr/share/${pkgname}
python2 creak.py "\$@"
EOF
chmod 755 "${pkgdir}/usr/bin/creak"
}
