# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=tplmap
pkgver=0.3.1
pkgrel=2
groups=('archstrike' 'archstrike-scanners')
arch=('any')
pkgdesc="Automatic Server-Side Template Injection Detection and Exploitation Tool"
url="https://github.com/epinna/tplmap"
license=('GPL3')
depends=('python2' 'python2-yaml' 'python2-requests')
source=("https://github.com/epinna/tplmap/archive/v${pkgver}.tar.gz")
sha512sums=('bd8752f4afdd299c17b896310306cc5ea92fc54ee8f2a25441d646f87a4fcf68e251f7240160fed738449abd894217f00fa26b0160c404b3196d957a80021eb7')

prepare() {
  grep -iRl 'python' ${pkgname}-${pkgver} | xargs sed -i 's|python$|python2|'
}

package() {
  cd ${pkgname}-${pkgver}
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"

  cp -a --no-preserve=ownership * "${pkgdir}/usr/share/${pkgname}"
  install -Dm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}"

cat > "${pkgdir}/usr/bin/${pkgname}" <<EOF
#!/bin/sh
cd /usr/share/${pkgname}
python2 tplmap.py "\$@"
EOF
chmod 755 "${pkgdir}/usr/bin/${pkgname}"

}
