# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=python2-pygraphviz
pkgver=1.3.1
pkgrel=2
pkgdesc='PyGraphviz is a Python interface to the Graphviz graph layout and visualization package.'
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
license=('LGPL')
url="https://networkx.lanl.gov/pygraphviz"
depends=('python2' 'graphviz')
makedepends=('python2-setuptools')
source=(http://pypi.python.org/packages/source/p/pygraphviz/pygraphviz-$pkgver.tar.gz)
sha512sums=('e1957d7daf7ae9e60873cb6f23ac19a3320b4b10579ab24a2307beb558dad11a8e82e1da528799165b761d944efb4860b7296e3218846f7885f715c396a8e3dc')

build() {
  cd "$srcdir/pygraphviz-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/pygraphviz-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  # Patch to use python2
  find "$pkgdir" -type f \( -name '*.py' -or -executable \) -exec \
    sed -i -e 's|#![ ]*/usr/bin/python$|#!/usr/bin/python2|' \
           -e 's|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|' \
    {} \;
}
