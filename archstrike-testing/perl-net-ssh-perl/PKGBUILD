# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname=perl-net-ssh-perl
pkgver=1.42
pkgrel=1
pkgdesc="Perl client interface to SSH"
arch=('any')
license=('GPL' 'custom')
options=('!emptydirs')
depends=('perl-convert-pem>=0.05' 'perl-crypt-dh>=0.01' 'perl-crypt-dsa>=0.11'
         'perl-crypt-idea' 'perl-crypt-rsa>=1.37'
         'perl-digest-bubblebabble>=0.01' 'perl-digest-hmac'
         'perl-digest-sha1>=2.10' 'perl-math-gmp>=1.04'
         'perl-string-crc32>=1.2')
url='http://search.cpan.org/dist/Net-SSH-Perl'
source=("http://search.cpan.org/CPAN/authors/id/S/SC/SCHWIGON/Net-SSH-Perl-$pkgver.tar.gz")
sha512sums=('67275e99890948376c7f6feb5be1979f45b5fda2afe440108b3819db192fd41b43e9ee94e7e643671384b37972842f40ce3519cbeef98b6ee796a68e4bfe20dd')
_distdir="Net-SSH-Perl-$pkgver"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    /usr/bin/perl Makefile.PL
    make )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
