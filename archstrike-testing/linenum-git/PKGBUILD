# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=linenum-git
pkgver=20160410.r22.f7b184b
pkgrel=1
epoch=1
groups=('archstrike' 'archstrike-scanners')
pkgdesc="Scripted Local Linux Enumeration & Privilege Escalation Checks"
url="https://github.com/rebootuser/LinEnum"
arch=('any')
license=('GPL3')
depends=('bash')
makedepends=('git')
provides=('linenum')
replaces=('linenum')
source=("$pkgname::git+https://github.com/rebootuser/LinEnum.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//  g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "$pkgname"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin/"
  install -dm755 "$pkgdir/usr/share/$pkgname"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname"

  #Bin
  ln -s /usr/share/$pkgname/LinEnum.sh $pkgdir/usr/bin/linenum
  ln -s /usr/share/$pkgname/LinEnumv0.2_legacy.sh $pkgdir/usr/bin/linenumv0.2_legacy.sh
}
