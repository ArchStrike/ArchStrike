# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=slowhttptest
pkgver=1.6
pkgrel=2
groups=('archstrike' 'archstrike-dos')
pkgdesc="A highly configurable tool that simulates application layer denial of service attacks"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://code.google.com/p/slowhttptest'
license=('Apache')
if [[ $CARCH == 'x86_64' ]]; then
      depends=('openssl' 'gcc-libs-multilib')
else
      depends=('openssl')
fi
source=("http://slowhttptest.googlecode.com/files/slowhttptest-$pkgver.tar.gz" "gcc6.1.fixes.patch")
sha512sums=('34360be27120f5922f68f25690d10d7c2e352c99cdc0224a5de5ad935d00b5e0b79ab9f80dd772fab098767acc1bdba41f7279470e75cd9dba9cf4f6aed28774'
            '66435c64fe63d3976b949833eee8016f85a38d7d37390710bc799b66297e5f180de1c5d787f8eb1053ef4804a5151fa3b0c678c611206e9083cb8251d7ae57e4')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i $srcdir/gcc6.1.fixes.patch
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make "DESTDIR=$pkgdir" install
}
