# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=conpot
pkgver=0.4.0
pkgrel=2
pkgdesc="ICS honeypot with the goal to collect intelligence about the motives
    and methods of adversaries targeting industrial control systems"
url="http://conpot.org"
depends=('python2-gevent' 'python2-pysnmp' 'python2-lxml' 'python2-bottle' 'python2-jinja' 'python2-beautifulsoup4' 'python2-requests' 'python2-sphinx' 'python2-libtaxii>=1.1.0' 'mysql-python' 'python2-xlrd' 'python2-stix' 'python2-cybox' 'python2-crc16' 'python2-enum' 'hpfeeds' 'python2-modbus-tk-git')
makedepends=('python2-distribute')
license=('GPL2')
arch=('any')
source=("https://pypi.python.org/packages/source/C/Conpot/Conpot-${pkgver}.tar.gz")
sha512sums=('06cd8463db9a2e16200582cfc1a354610ceef928a4d1b36c81ee3f3bc6d43c2ae805f717fe901fb6789d7bebe66de8e796dba7981655eeb70998bd7b889885fa')

build() {
  cd "${srcdir}"/Conpot-"${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}"/Conpot-"${pkgver}"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  find "${pkgdir}" -type f -name 'log_worker.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
  mv "$pkgdir/usr/lib/python2.7/site-packages/bin" "$pkgdir/usr/lib/python2.7/site-packages/$pkgname/"
}
