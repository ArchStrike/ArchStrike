# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=spike-proxy
pkgver=148
pkgrel=2
pkgdesc="A Proxy for detecting vulnerabilities in web applications"
arch=('any')
url="http://www.immunitysec.com/resources-freesoftware.shtml"
license=('GPL')
groups=('archstrike' 'archstrike-webapps')
depends=("python2-pyopenssl" "bash")
options=("!emptydirs")
source=(http://www.immunitysec.com/downloads/SP${pkgver}.tgz
        fix-encoding.patch)
sha512sums=('dd420dbd0287f1f238da1af53289f46acf98be64fdac0f481f27d24b8e0109ee406c6edfd3fa2af2fa90e247c200bffa3de5dc550c6720ff13b39d88ef9dce5a'
            'b9c9b881112987c77fe6253b4608a8ffccd1fb4c760ea056f2dfff20a7ec2844f3bf9ef81e8ec7e520badb0e972864e26e0ac0e98c4f4a797b00f476524a5f57')

prepare(){
  cd "$srcdir/spkproxy"
  patch -Np1 -i $srcdir/fix-encoding.patch
}

package() {
  cd "$srcdir/spkproxy"

  # Base directories
  install -dm755 $pkgdir/usr/share/spike-proxy
  install -dm755 $pkgdir/usr/share/doc/spike-proxy
  install -dm755 $pkgdir/usr/bin

  mv *.txt $pkgdir/usr/share/doc/spike-proxy/
  cp -a --no-preserve=ownership * $pkgdir/usr/share/spike-proxy/
  grep -iRl 'python' "$pkgdir" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
  cat > $pkgdir/usr/bin/spike-proxy << EOF
#!/bin/bash
cd /usr/share/spike-proxy
python2 ./spkproxy.py "\$@"
cd \$OLDPWD
EOF
  chmod 755 $pkgdir/usr/bin/spike-proxy
}

