# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=umap-git
pkgver=r25.3ad8121
pkgrel=5
pkgdesc="The USB host security assessment tool"
url="https://github.com/nccgroup/umap"
arch=('any')
license=('AGPL')
groups=('archstrike')
depends=('python' 'python-pyserial')
makedepends=('git')
provides=('umap')
conflicts=('umap')
replaces=('umap')
source=("${pkgname}::git+https://github.com/nccgroup/umap.git")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir"/umap-git
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "$srcdir"/umap-git
  install -dm755 "$pkgdir"/usr/bin/
  install -dm755 "$pkgdir"/usr/share/umap-git
  cp -a --no-preserve=ownership * "${pkgdir}/usr/share/${pkgname}"
  chmod u+rwX,g+rwX,o+rX -R "$pkgdir/usr/share/${pkgname}/devices"

cat > "$pkgdir"/usr/bin/umap <<EOF
#!/usr/bin/env bash
cd /usr/share/${pkgname}
python umap.py "\$@"
EOF
chmod 755 "$pkgdir"/usr/bin/umap
}
