# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=cuckoo
pkgver=1.2
pkgrel=1
pkgdesc="A malware analysis system"
url="http://cuckoosandbox.org/"
arch=('any')
license=('GPL3')
install='cuckoo.install'
options=('!emptydirs')
groups=('archstrike' 'archstrike-malware')
depends=('python2-sqlalchemy' 'python2-bson' 'python2-dpkt'
   'python2-jinja' 'python2-django' 'volatility' 'python2-maec'
         'python2-magic' 'libvirt' 'python2-bottle'
         'python2-pefile' 'python2-pymongo' 'tcpdump' 'yara' 'python2-yara'
   'python2-chardet')
optdepends=('python2-pydeep')
source=("http://downloads.cuckoosandbox.org/$pkgver/cuckoo_$pkgver.tar.gz"
  'cuckoo.desktop')
sha512sums=('fe523a1aca99e01ce93d1e4e888a9b00bb042c65630f02966dffab09c3e1fcff47dfea55527bfa57efef5a305b6fe93821b26a1f2dd28f4eaa36ebd860c25696'
            'fde1e6d9c4bea273d848b9dbee09027f6eb7e8b48116ee4bcaddef314c7c22f8bca5a3e7e9dabe06d007fe603fe7c1cd518b2c707b41866945b0adf83dcc42a4')

prepare() {
   cd "$srcdir/cuckoo"
   find "${srcdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
   sed -i 's|^version_check =.*|version_check = off|' conf/cuckoo.conf
   sed -i 's|^platform =.*|platform = linux|' conf/{kvm.conf,virtualbox.conf,vmware.conf,esx.conf}

}

package() {
  cd "$srcdir/cuckoo"
  install -dm755 "$pkgdir/usr/bin/"
  install -dm755 "$pkgdir/usr/share/cuckoo"
  install -Dm644 docs/LICENSE "$pkgdir/usr/share/licenses/cuckoo/LICENSE"
  install -Dm644 "$srcdir/cuckoo.desktop" "$pkgdir/usr/share/applications/cuckoo.desktop"
  cp --no-preserve=ownership -a * "$pkgdir/usr/share/cuckoo"
  chmod u+rwX,g+rwX,o+rX -R "$pkgdir/usr/share/cuckoo"
  cat > "$pkgdir/usr/bin/cuckoo" <<EOF
#!/bin/sh
if ! id | grep -q '(cuckoo)' && ! [ \$EUID -eq 0 ] ; then
  echo "==> Add yourself to the cuckoo group to use cuckoo as an unprivledged user."
  exit 1
fi
cd /usr/share/cuckoo
python2 cuckoo.py "\$@"
EOF
 chmod +x "$pkgdir/usr/bin/cuckoo"
}

