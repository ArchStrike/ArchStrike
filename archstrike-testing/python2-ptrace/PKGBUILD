# Maintainer: ArchStrike <team@archstrike.org>
pkgbase=python2-ptrace

buildarch=1

pkgname=(python2-ptrace python-ptrace)
_pkgname=python-ptrace
pkgver=0.9
pkgrel=1
pkgdesc="A debugging using ptrace (Linux, BSD and Darwin system call to trace processes) written in Python."
url="https://pypi.python.org/pypi/python-ptrace/"
arch=('any')
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://pypi.python.org/packages/72/7d/c2bb02f71264eba6fe807983495acee389d251e42ad718cf7e281952d883/python-ptrace-$pkgver.tar.gz")
sha512sums=('a09e20d193f036cbd36af8c139ba35253f23b31d1d512b955a8bce7bca20d77e8a313f48142bc122e1d95c5994dc6ce24db38ce33b0840998d7004a38fc4738f')

prepare() {
  cp -a $_pkgname-$pkgver{,-py2}

  cd $_pkgname-$pkgver-py2
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build

  cd ../$_pkgname-$pkgver-py2
  python2 setup.py build
}

package_python-ptrace() {
  depends=('python')
  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py install --root=$pkgdir/ --optimize=1
  #Fixing for conflicts
  mv $pkgdir/usr/bin/gdb.py $pkgdir/usr/bin/gdb3.py
  mv $pkgdir/usr/bin/strace.py $pkgdir/usr/bin/strace3.py
}

package_python2-ptrace() {
  depends=('python2')
  cd "$srcdir/$_pkgname-$pkgver-py2"

  python2 setup.py install --root=$pkgdir/ --optimize=1
}
