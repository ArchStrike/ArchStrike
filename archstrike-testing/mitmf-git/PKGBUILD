# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=mitmf
pkgname=$_pkgname-git
pkgver=20151103.r425
pkgrel=4
groups=('archstrike' 'archstrike-proxy' 'archstrike-spoof')
pkgdesc='Framework for Man-In-The-Middle attacks'
arch=('any')
url='https://github.com/byt3bl33d3r/MITMf'
license=('GPL3')
depends=(
  'backdoor-factory-git'
  'capstone'
  'impacket'
  'python2-beautifulsoup4'
  'python2-capstone'
  'python2-chardet'
  'python2-configobj'
  'python2-crypto'
  'python2-cryptography'
  'python2-dnslib'
  'python2-dnspython'
  'python2-flask'
  'python2-ipy'
  'python2-lxml'
  'python2-magic'
  'python2-msgpack'
  'python2-netaddr'
  'python2-netfilterqueue'
  'python2-pefile'
  'python2-pillow'
  'python2-pyasn1'
  'python2-pyinotify'
  'python2-pyopenssl'
  'python2-pypcap'
  'python2-requests'
  'python2-service-identity'
  'python2-twisted'
  'python2-user-agents'
  'python2-watchdog'
  'python2-yaml'
  'scapy'
  'systemd'
)
makedepends=('git')
provides=("$_pkgname")
conflicts=("$_pkgname")
install=$pkgname.install
source=(
  "$pkgname::git+https://github.com/byt3bl33d3r/MITMf.git"
  "$pkgname.tmpfiles.conf"
)
sha512sums=(
  'SKIP'
  'a380c2f2e812efff0ed1f303f91046c4e639e4bb8a8b0f93e17d8e8ac1b7ec1c20c124d2a203d66fc72aaa62f4753de8935e8b365d39a7e2bd90c2c2dfa39376'
)

pkgver() {
  cd $pkgname
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  cd $pkgname
  while read -r; do
      sed -i -re 's|#!\s*(/usr)?(/local)?/bin/.*python.*$|#!/usr/bin/env python2|g' "$REPLY"
  done < <(egrep -rl '^\s*#!\s*(/usr)?(/local)?/bin/.*python')
}

package() {
  # Install tmpfiles script
  install -Dm644 $pkgname.tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$_pkgname.conf"

  # Install package files and create a symlink to backdoor-factory in the package directory
  cd $pkgname
  install -dm755 "$pkgdir/usr/share/$_pkgname"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$_pkgname/"
  rmdir "$pkgdir/usr/share/$_pkgname/libs/bdfactory"
  ln -sf /usr/share/backdoor-factory "$pkgdir/usr/share/$_pkgname/libs/bdfactory"

  # Move license to the package license directory
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  mv "$pkgdir/usr/share/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Create launcher script
  install -dm755 "$pkgdir/usr/bin/"
  printf '%s\n\n%s\n%s\n' \
    '#!/usr/bin/env bash' \
    "cd /usr/share/$_pkgname" \
    "python2 $_pkgname.py \"\$@\"" \
      > "$pkgdir/usr/bin/$_pkgname"

  # Make launcher script executable
  chmod 755 "$pkgdir/usr/bin/$_pkgname"
}
