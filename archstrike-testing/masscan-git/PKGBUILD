# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=masscan-git
pkgver=20160216.r402
pkgrel=1
epoch=1
groups=('archstrike' 'archstrike-scanners' 'archstrike-crackers')
pkgdesc="TCP port scanner, spews SYN packets asynchronously, scanning entire Internet in under 5 minutes"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/robertdavidgraham/masscan'
license=('AGPL')
depends=('libpcap')
makedepends=('git')
conflicts=('masscan')
provides=('masscan')
replaces=('masscan')
backup=('etc/masscan/masscan.conf' 'etc/masscan/exclude.conf')
source=("${pkgname}::git+http://github.com/robertdavidgraham/masscan.git" "masscan.conf")
sha512sums=('SKIP'
            'f9b2f81091f27509c0148bcee13630c26cfd26836c8b29f06afe6c4f83e871976a4e594e6c8499a13224ad44a1b04ed47a303ffec1d8656164804e9527d16e6b')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

build() {
  cd "${pkgname}"
  make
}

package() {
  cd "${pkgname}"
  install -dm755 "${pkgdir}"/usr/share/man/man8
  install -Dm755 bin/masscan "${pkgdir}"/usr/bin/masscan
  gzip -c doc/masscan.8 > "${pkgdir}"/usr/share/man/man8/masscan.8.gz
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
  install -Dm644 data/exclude.conf "${pkgdir}"/etc/masscan/exclude.conf
  install -m644 "${srcdir}"/masscan.conf "${pkgdir}"/etc/masscan/masscan.conf
}
