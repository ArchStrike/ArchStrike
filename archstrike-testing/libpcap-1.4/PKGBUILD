# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=libpcap-1.4
pkgver=1.4.0
pkgrel=1
pkgdesc="A system-independent interface for user-level packet capture"
arch=('armv6h' 'armv7h' 'i686' 'x86_64' 'aarch64')
url="http://www.tcpdump.org/"
license=('BSD')
depends=('glibc' 'libnl' 'sh' 'libusbx')
makedepends=('flex')
source=("http://www.tcpdump.org/release/libpcap-${pkgver}.tar.gz" "config.guess-for-aarch64.patch")
sha512sums=('b7662867c6449c603a65c37e63ce881260dee80989ccb5b71b1e55fa437fb18ac9bb123a7b8ed68302baf4511b97bca40d9f08dea7b0dd0eb6a2ac4a7b73cf15'
            '1a6cf72b42409bcaba94f0886360dcd28a48a7bfd7276c14edf869ce4e134d8fb2f9a3723011d4883114c5f5c6f5fe6691a000719367a865c978d936d6f3bf8b')

prepare() {
  cd ${srcdir}/libpcap-${pkgver}
  patch -Np1 -i $srcdir/config.guess-for-aarch64.patch
}

build() {
  cd ${srcdir}/libpcap-${pkgver}
  ./configure --prefix=/usr --enable-ipv6 --with-libnl
  make
}

package() {
  cd ${srcdir}/libpcap-${pkgver}
  install -d -m755 ${pkgdir}/usr/bin
  make DESTDIR=${pkgdir} install

  # backwards compatibility, programs often look for net/bpf.h
  mkdir -p ${pkgdir}/usr/include/net
  cd ${pkgdir}/usr/include/net
  ln -s ../pcap-bpf.h bpf.h

  # Avoid conflicting with normal libpcap
  rm -rf "$pkgdir/usr/share"
  rm "$pkgdir/usr/lib/libpcap.a"
  mv "$pkgdir/usr/lib/libpcap.so.1.4.0" "$pkgdir/usr/lib/libpcap-1.4.so.1.4.0"
  rm "$pkgdir/usr/lib/libpcap.so"
  rm "$pkgdir/usr/lib/libpcap.so.1"
  ln -s libpcap-1.4.so.1.4.0 "$pkgdir/usr/lib/libpcap-1.4.so"
  ln -s libpcap-1.4.so.1.4.0 "$pkgdir/usr/lib/libpcap-1.4.so.1"
  mv "$pkgdir/usr/bin/pcap-config" "$pkgdir/usr/bin/pcap-config-1.4"
  rm "$pkgdir/usr/include/net/bpf.h"
  ln -s ../pcap-bpf-1.4.h "$pkgdir/usr/include/net/bpf-1.4.h"
  mv "$pkgdir/usr/include/pcap" "$pkgdir/usr/include/pcap-1.4"
  mv "$pkgdir/usr/include/pcap.h" "$pkgdir/usr/include/pcap-1.4.h"
  mv "$pkgdir/usr/include/pcap-bpf.h" "$pkgdir/usr/include/pcap-bpf-1.4.h"
  mv "$pkgdir/usr/include/pcap-namedb.h" "$pkgdir/usr/include/pcap-namedb-1.4.h"
}
