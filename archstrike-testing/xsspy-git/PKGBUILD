# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=xsspy-git
pkgver=20160620.r26
pkgrel=1
groups=('archstrike' 'archstrike-scanners')
arch=('any')
pkgdesc="A python tool for finding Cross Site Scripting vulnerabilities in websites"
url="https://github.com/faizann24/XssPy"
license=('MIT')
depends=('python2' 'python2-mechanize')
makedepends=('git')
source=("$pkgname::git+https://github.com/faizann24/XssPy.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"

}

prepare() {
  cd $pkgname
  sed -i '1i\#!/usr/bin/env python2' XssPy.py
}

package() {
  cd $pkgname
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname"
  install -Dm755 XssPy.py "$pkgdir/usr/bin/xsspy"
}
