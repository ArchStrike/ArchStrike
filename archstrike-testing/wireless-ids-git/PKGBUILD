# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=wireless-ids-git
pkgver=r24.b132071
pkgrel=1
groups=('archstrike' 'archstrike-defense')
pkgdesc="Ability to detect suspicious activity such as (WEP/WPA/WPS) attack by sniffing the air for wireless packets."
arch=('any')
url='https://github.com/SYWorks/wireless-ids'
license=('GPL2')
depends=('python2')
makedepends=('git')
provides=('wireless-ids')
conflicts=('wireless-ids')
replaces=('wireless-ids')
source=("wireless-ids::git+https://github.com/SYWorks/wireless-ids.git")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/wireless-ids"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' "$srcdir/wireless-ids" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/wireless-ids"
  install -dm755 $pkgdir/usr/share/$pkgname
  install -dm755 $pkgdir/usr/bin
  install -Dm755 wids.py $pkgdir/usr/share/$pkgname
  install -Dm644 mac-oui.db $pkgdir/usr/share/$pkgname

cat > $pkgdir/usr/bin/wireless-ids <<EOF
#!/bin/sh
cd /usr/share/wireless-ids
python2 ./wids.py "\$@"
EOF
chmod +x $pkgdir/usr/bin/wireless-ids
}
