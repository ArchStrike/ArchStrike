# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=u3-tool
pkgver=0.3
pkgrel=6
pkgdesc="Tool for controlling USB flash devices that confirm to the U3 specifications"
url="http://u3-tool.sourceforge.net/"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL2')
depends=('glibc')
source=("http://downloads.sourceforge.net/project/u3-tool/u3-tool/$pkgver/u3-tool-$pkgver.tar.gz" "config.guess-for-aarch64.patch")
sha512sums=('b54ac58efa0efce56247cb8c58d60a0598659e9220955aff4edbf2d33589d936077d0068d38eabbbf8192e1905b18cfffc54c1f4927d2b5d3bf252e2639e107c'
            '28dd7a3330cd824e7634362800829fdee1e2bc45412b94e1a9d832da9b189d4acb9e35c0814a43605e450b2d81c049e288412222cdd6b34a8bdc6379ba206c44')

prepare() {
  cd "$srcdir/u3-tool-$pkgver"
  patch -Np1 -i $srcdir/config.guess-for-aarch64.patch
}

build() {
  cd "$srcdir/u3-tool-$pkgver"
  ./configure --prefix="/usr" --sbindir="/usr/bin"
  make
}

package() {
  cd "$srcdir/u3-tool-$pkgver"
  install -dm755 "$pkgdir/usr"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/u3-tool/LICENSE"
  make DESTDIR="$pkgdir" install
}
