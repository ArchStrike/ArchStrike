# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212
pkgname=("python-http-parser" "python2-http-parser")
_module="http-parser"
pkgver=0.8.3
pkgrel=3
pkgdesc="HTTP request/response parser for Python compatible with Python 2, Python 3 and Pypy."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/benoitc/http-parser"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
options=(!emptydirs)
source=("http://pypi.python.org/packages/source/h/http-parser/${_module}-${pkgver}.tar.gz")
sha512sums=('aca583d976dcf172244179cc4199336f296dbb2e2838dded23e64d55f23298fe600f9c24ba6cb6d1d5faad3022710154949fd39adecaef763f7589a58d03fbd1')

prepare() {
        cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
        cd "${srcdir}/${_module}-${pkgver}"
        python setup.py build
        cd "${srcdir}/${_module}-${pkgver}-python2"
        python2 setup.py build
}

package_python-http-parser() {
       depends=("python>=3.3")
       cd "${srcdir}/${_module}-${pkgver}"
       python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
       install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-http-parser() {
        depends=("python2>=2.6")
        cd "${srcdir}/${_module}-${pkgver}-python2"
        python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
        install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}

