# Maintainer: ArchStrike <archstrikelinux@gmail.com>
# Contributor: Srijan Choudhary <srijan4@gmail.com>
# Contributor: Jeffrey Gelens <jgelens@gmail.com>
# Contributor: warheadsse

buildarch=212

pkgname=haproxy
pkgver=1.4.25
pkgrel=1
pkgdesc="The Reliable, High Performance TCP/HTTP Load Balancer"
arch=('i686' 'x86_64' 'armv7h' 'armv6h')
url="http://haproxy.1wt.eu"
license=('GPL')
depends=('pcre')
makedepends=('gcc' 'linux-headers')
backup=('etc/haproxy/haproxy.cfg')
install="${pkgname}.install"
source=(  "http://haproxy.1wt.eu/download/1.4/src/${pkgname}-${pkgver}.tar.gz"
          "haproxy.service" )
sha512sums=('c3424151309be5eea761ff02b29a0e54bd34f5fcb4087e8b99fb2f4cde17bbc3caf07b6cb41ae5a2606325f8a950a06c4683ceb04c05fd6ebf0177326030bca9'
            '09bef475795ece7f5d1b9adbaa0f10df0a488d3835b31a0bc847b3e821cf1e9a28cdae26155fff3f848e6b35c65f982b1f7b0eba0563be9a5b6bf5e248c52847')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make TARGET=linux2628 CPU=native USE_PCRE=1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make PREFIX="${pkgdir}/usr" SBINDIR="${pkgdir}/usr/bin" DOCDIR="${pkgdir}/usr/share/doc/${pkgname}" install

  install -d "${pkgdir}/usr/share/doc/${pkgname}/examples"
  install -m644 examples/*.cfg "${pkgdir}/usr/share/doc/${pkgname}/examples/"
  install -d "${pkgdir}/usr/share/doc/${pkgname}/examples/errorfiles"
  install -m644 examples/errorfiles/*.http "${pkgdir}/usr/share/doc/${pkgname}/examples/errorfiles/"

  install -D -m644 "examples/haproxy.cfg" "${pkgdir}/etc/haproxy/haproxy.cfg"
  install -D -m644 "${startdir}/haproxy.service" "${pkgdir}/usr/lib/systemd/system/haproxy.service"

}

# vim:ts=2:sw=2:et
