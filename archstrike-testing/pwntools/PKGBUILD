# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=pwntools
pkgver=2.2.0
pkgrel=2
pkgdesc='The CTF framework used by #Gallopsled in every CTF'
arch=('any')
url='https://github.com/Gallopsled/pwntools'
license=('MIT')
groups=('archstrike' 'archstrike-misc')
depends=('python2-paramiko' 'python2-argparse' 'python2-mako' 'python2-pyelftools' 'python2-capstone' 'ropgadget' 'python2-pyserial' 'python2-requests')
makedepends=('python2-setuptools')
source=(
  "https://github.com/Gallopsled/$pkgname/archive/$pkgver.tar.gz"
  'capstone-version.patch'
)
sha512sums=(
  '533ebe31c55812cd6a26a5d89b0e431854eff7b6d3df6605b7af77a5ac66774cdcaae69efef2749bb5bce45f8437d88adf5824df5bb41f626daaffdb947983ef'
  '2c1fe0e175aae45cdb0955c8da499dcb6671ca3765a694c23a4eb491faeee9ef8008020035cd9f1431f53d1074082dfd2eb9e8c240a1ca04854b783cb4c3005d'
)

prepare() {
  cd $pkgname-$pkgver
  patch -p1 < ../capstone-version.patch

  while read -r; do
      sed -i -re 's|#!\s*(/usr)?(/local)?/bin/.*python.*$|#!/usr/bin/env python2|g' "$REPLY"
  done < <(egrep -rl '^\s*#!\s*(/usr)?(/local)?/bin/.*python')
}

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  mv "$pkgdir/usr/bin/disasm" "$pkgdir/usr/bin/$pkgname-disasm"
  install -Dm644 LICENSE-$pkgname.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -dm755 "$pkgdir/usr/share/$pkgname"
  cp -r examples "$pkgdir/usr/share/$pkgname/"
}
