# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Mike Sampson <mike at sambodata dot com>
# Contributor: Andrea Benazzo <andy@qitty.net>

buildarch=212

pkgname=cryptmount
pkgver=5.2
pkgrel=1
pkgdesc='Utility which allows an ordinary user to mount an encrypted file system'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
groups=('archstrike' 'archstrike-crypto')
url="http://cryptmount.sourceforge.net/"
license=('GPL')
depends=('cryptsetup')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz")
backup=('etc/cryptmount/cmtab')
sha512sums=('030df15a8c1472fafa61f957d7a0f1af09ed1e958bbc09a714c43bf4e12cbcb3069ce2fc9442586c4b9bab9816d17e0d9bc1a3a968e7d3cc5ff79686e97bc361')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-libgcrypt \
    --with-systemd
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # fixing perms
  chmod 2755 $pkgdir/usr/bin/cryptmount
}

