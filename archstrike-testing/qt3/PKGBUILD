# Maintainer: ArchStrike <archstrikelinux@gmail.com>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

buildarch=212

pkgname=qt3
pkgver=3.3.8b
pkgrel=8
epoch=1
pkgdesc="A cross-platform application and UI framework"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://www.trolltech.com/products/qt/index.html"
license=('GPL')
depends=('libpng' 'libxmu' 'libxcursor' 'libxinerama' 'glu' 'libxft'
         'libxrandr' 'libmng')
makedepends=('mariadb' 'postgresql' 'unixodbc' 'sqlite' 'mesa')
optdepends=('qtchooser: set the default Qt toolkit'
            'libmariadbclient: MariaDB driver'
            'postgresql-libs: PostgreSQL driver'
            'unixodbc: ODBC driver')
options=('!docs')
source=(ftp://ftp.qt-project.org/qt/source/qt-x11-free-${pkgver}.tar.gz
        qt3-png15.patch qt-copy-kde-patches.tar.bz2 qt-patches.tar.bz2
        eastern_asian_languagues.diff qt-odbc.patch gcc-4.6.patch
        qt-x11-free-3.3.5-makelibshared.patch)
sha512sums=('09b5179d6fb6ff1fa63c44150200436c195e45bb9620cd64e9182db79dfdf758ac734c8aac9c668932efb27ecaa9bfbfe11835736335a35c9423eab2ca83efd2'
            'a9ebb35b6ca11d6e8712a33b0a4dc63b1a8038b09ec282f97e909d2a60bc83d93210df8f45c91b504f29eb0b760faa54b2a30e82e96298090033ed8e3882681b'
            '12ef8c5fe27d6010b2910ffc1423af019c09946d7b357497d4defdfa8d795f86b1ef913828f78cf2360bb999fea6723c6769279dc7df4dbae8b64507cac8ade4'
            'a4fb94ce049d8598f1e1548c6f7d3f3a00ab9d9e6defdf196ed02d1351dadb291b26ba8623794527a3efcf470efd12bb9ce27f50a487ec9e235499f5ae87e43b'
            '840f9d24a3ebc8bc179e4679d859cb4219c9059c8664d3b96f2fffb189abdd0872748d6b92d46961ed052bdbadffbc82edb1299901850c1c92c676311f2acf16'
            'c44be35fa91d7eeccd124040d6262ddee62d7b94d0d32066e81aa1218ac6e6bf38b2724443c9481439a085357b4bebe521047d8af95e4b3bec46ca92ebff9df7'
            '7d946a0a5b4e2374c3831668077f0d0f62bd3a3a2cc4db567f365fef72b80a351f3768276b3f51bff88153b6735f25cfb9043a1f3644cf3279be17f2eb14609b'
            '1c549fec1fc1af911dbc72ddd43194606b5d3df8f87f01ade83f2317b15cdb4064f97ca959e8d5107d6d0a26fef96b2266e05e969b66f81e1cfbe7c57be087f2')

# qt-copy-kde-patches come from http://websvn.kde.org/trunk/qt-copy/patches/
# other qt-patches come from fedora and gentoo

prepare() {
  cd qt-x11-free-${pkgver}
  # apply qt patches from kde.org
  for i in ../qt-copy-kde-patches/*; do
    patch -p0 -i $i
  done
  # apply other qt patches and one security fix from debian/gentoo
  for i in ../qt-patches/*; do
    patch -p1 -i $i
  done
  # fix CJK font/chars select error (FS#11245)
  patch -p1 -i "${srcdir}"/eastern_asian_languagues.diff
  # fix build problem against new unixODBC
  patch -p1 -i "${srcdir}"/qt-odbc.patch
  # fix build with gcc 4.6.0
  patch -p1 -i "${srcdir}"/gcc-4.6.patch
  patch -p0 -i "${srcdir}"/qt3-png15.patch

  patch -p1 -i "${srcdir}"/qt-x11-free-3.3.5-makelibshared.patch

  sed -i "s|-O2|$CXXFLAGS|" mkspecs/linux-g++{,-32,-64}/qmake.conf
  sed -i "s|-I. |$CXXFLAGS -I. |" qmake/Makefile.unix
  sed -i "s|read acceptance|acceptance=yes|" configure
}

build() {
  export QTDIR="${srcdir}"/qt-x11-free-${pkgver}
  export PATH=${QTDIR}/bin:${PATH}
  export LD_LIBRARY_PATH=${QTDIR}/lib:${LD_LIBRARY_PATH}
  export QMAKESPEC=$QTDIR/mkspecs/linux-g++

  if [ "$CARCH" = "x86_64" ]; then
      export ARCH="-64"	
    else unset ARCH
  fi

  cd qt-x11-free-${pkgver}
  ./configure \
    -prefix /usr \
    -bindir /usr/lib/qt3/bin \
    -headerdir /usr/include/qt3 \
    -plugindir /usr/lib/qt3/plugins \
    -datadir /usr/share/qt3 \
    -translationdir /usr/share/qt3/translations \
    -I/usr/include/mysql -I/usr/include/postgresql/server \
    -platform linux-g++$ARCH \
    -system-zlib \
    -qt-gif \
    -release \
    -shared \
    -sm \
    -nis \
    -thread \
    -stl \
    -system-lib{png,jpeg,mng} \
    -no-g++-exceptions \
    -plugin-sql-{mysql,psql,sqlite,odbc}

  make
}

package() {
  cd qt-x11-free-${pkgver}
  make INSTALL_ROOT="${pkgdir}" install
  sed -i -e "s|-L${srcdir}/qt-x11-free-${pkgver}/lib ||g" -e "s|${srcdir}/||g" "${pkgdir}"/usr/lib/*.prl
  rm -rf "${pkgdir}"/usr/share/qt3/{phrasebooks,templates,translations}
  rm -rf "${pkgdir}"/usr/share/qt3/mkspecs/{aix*,*bsd*,cygwin*,dgux*,darwin*,hpux*,hurd*,irix*,linux-g++$ARCH/linux-g++$ARCH,lynxos*,macx*,qnx*,reliant*,sco*,solaris*,tru64*,unixware*,win32*}

# install man pages
  install -d -m755 "${pkgdir}"/usr/share/man
  cp -r "${srcdir}"/qt-x11-free-${pkgver}/doc/man/{man1,man3} "${pkgdir}"/usr/share/man/
  for i in "${pkgdir}"/usr/share/man/man1/*; do
    mv $i ${i%.*}-qt3.1
  done

# Useful symlinks for cmake and configure scripts
  install -d "${pkgdir}"/usr/bin
  for b in "${pkgdir}"/usr/lib/qt3/bin/*; do
    ln -s /usr/lib/qt3/bin/$(basename $b) "${pkgdir}"/usr/bin/$(basename $b)-qt3
  done
}
