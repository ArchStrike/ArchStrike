# Maintainer: ArchStrike <team@archstrike.org>

_pkgname=filter_audio

buildarch=220

pkgname=${_pkgname}-git
_pkgver=dd59d99
pkgver=r93.2fc6695
pkgrel=1
pkgdesc='An easy to use audio filtering library made from webrtc code'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/irungentoo/filter_audio'
license=('BSD')
depends=('glibc')
makedepends=('git')
conflicts=("$_pkgname")
provides=("$_pkgname")
source=("$pkgname::git+${url}.git" 'LICENSE')
sha512sums=('SKIP' '2ce9c4c70048c8d09779227381a6303578ed3f31522ba16aa95538967974ba2e484c0a16b25c18ca80048b752a6557fa01e3417363527a80b16d4132b4db7d22')

pkgver() {
    cd $pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd $pkgname
    make
}

package() {
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    cd $pkgname
    make DESTDIR="$pkgdir" PREFIX='/usr' LIBDIR='/lib' install
}
