# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=python2-fuzzywuzzy-git
pkgver=r239.c210da8
pkgrel=1
pkgdesc="Fuzzy string matching in python"
arch=('any')
url="https://pypi.python.org/pypi/fuzzywuzzy/0.2"
license=('custom')
depends=('python2-levenshtein')
makedepends=('git' 'python2-setuptools')
conflicts=('python2-fuzzywuzzy')
provides=('python2-fuzzywuzzy')
replaces=('python2-fuzzywuzzy')
source=(git+https://github.com/seatgeek/fuzzywuzzy.git)
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/fuzzywuzzy"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' "$srcdir/fuzzywuzzy" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

build() {
  cd "${srcdir}/fuzzywuzzy"
  python2 setup.py build
}

package() {
  cd "${srcdir}/fuzzywuzzy"
  python2 setup.py install --root=${pkgdir} --optimize=1
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
