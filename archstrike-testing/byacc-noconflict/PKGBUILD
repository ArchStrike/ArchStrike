# Maintainer: ArchStrike <team@archstrike.org>
_pkgname=byacc

buildarch=212

pkgname=${_pkgname}-noconflict
pkgver=20160324
pkgrel=1
pkgdesc="Non-conflicting version of byacc not providing bison/yacc"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://invisible-island.net/byacc/"
license=('custom')
depends=('glibc')
source=("ftp://invisible-island.net/${_pkgname}/${_pkgname}-${pkgver}.tgz")
sha512sums=('ad2721d1433368560a0d26b8e3a2cabacc62b682f7866ae3be5ac1ef1a0e60438ba84303be070877d6431878753e594d2e136c5beaa04518c1b9b45c09656fad')

build() {
    cd ${_pkgname}-${pkgver}
    ./configure --prefix=/usr --mandir=/usr/share/man
    make
}

package() {
    cd ${_pkgname}-${pkgver}
    make DESTDIR="$pkgdir" install

    # License
    sed -n "/is distributed/,/distributed freely/p" README > COPYING
    install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

    # Rename (to avoid conflicting with yacc)
    cd "$pkgdir"/usr/bin
    mv yacc $_pkgname

    cd "$pkgdir"/usr/share/man/man1
    mv yacc.1 ${_pkgname}.1
}
