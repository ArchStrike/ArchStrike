# Maintainer: ArchStrike <team@archstrike.org>
_pkgname=byacc

buildarch=212

pkgname=${_pkgname}-noconflict
pkgver=20150711
pkgrel=1
pkgdesc="Non-conflicting version of byacc not providing bison/yacc"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://invisible-island.net/byacc/"
license=('custom')
depends=('glibc')
source=("ftp://invisible-island.net/${_pkgname}/${_pkgname}-${pkgver}.tgz")
sha512sums=('348558046acca31a39e8df480ae4c46ad03a1b073a51877a8981914bce65e8922e83e14c45eecd0e122c9d3d018cd4b6ab8c7f29b8dbfa13d648f2f86104a4e9')

build() {
    cd ${_pkgname}-${pkgver}
    ./configure --prefix=/usr --mandir=/usr/share/man
    make
}

package() {
    cd ${_pkgname}-${pkgver}
    make DESTDIR="$pkgdir" install

    # License
    sed -n "/is distributed/,/distributed freely/p" README > COPYING
    install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

    # Rename (to avoid conflicting with yacc)
    cd "$pkgdir"/usr/bin
    mv yacc $_pkgname

    cd "$pkgdir"/usr/share/man/man1
    mv yacc.1 ${_pkgname}.1
}
