# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Tim Jester-Pfadt <t.jp@gmx.de>

buildarch=212

pkgname=squashfuse-git
pkgver=20160327.r303
pkgrel=2
groups=('archstrike' 'archstrike-misc')
pkgdesc="FUSE filesystem to mount squashfs archives"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/vasi/squashfuse"
license=('BSD')
depends=('fuse' 'zlib' 'lzo' 'xz' 'lz4')
makedepends=('git' 'autoconf' 'automake' 'libtool')
source=("${pkgname}::git+https://github.com/vasi/squashfuse.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

build() {
  cd "${pkgname}"
  autoreconf -fi
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

