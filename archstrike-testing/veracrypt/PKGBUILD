# Maintainer: ArchStrike <archstrikelinux@gmail.com>
# Contributor: Kevin MacMartin <prurigro@gmail.com>
# Contributor: Eric Ozwigh <ozwigh at gmail dot com>

buildarch=212

pkgname=veracrypt
_pkgver=1.0f-1
pkgver=${_pkgver/\-/_}
pkgrel=1
pkgdesc='Free disk encryption software based on TrueCrypt'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
groups=('archstrike' 'archstrike-crypto')
url='http://veracrypt.codeplex.com'
license=('custom')
depends=('fuse' 'wxgtk' 'device-mapper')
makedepends=('git' 'nasm')
options=('!makeflags')
source=("https://github.com/veracrypt/VeraCrypt/archive/VeraCrypt_$_pkgver.tar.gz"
        "$pkgname.desktop")
sha512sums=('5d38d2281c04dfe34deb3ff03896315f6410c3f02dabb3366eac42fa8e88ce9977274aad3461fcd1b0184d923f3186f8e2e27e8cc561bc28f0249a1634dc07ee'
            '76e3a277724bab09194e15fabc7a47d4c6c25384926d00dfab06a6a4ed1ae91fbe51d18dd66cfd925290aec8e6eb72397a4aa276d7818c456afd106cd932c09a')

prepare() {
  # Disable the makeself command
  cd VeraCrypt-VeraCrypt_$_pkgver/src
  sed -i 's|makeself|#makeself|' Main/Main.make
}

build() {
  cd VeraCrypt-VeraCrypt_$_pkgver/src
  WX_CONFIG='/usr/bin/wx-config' make LFLAGS+='-ldl'
}

package() {
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  cd VeraCrypt-VeraCrypt_$_pkgver/src
  install -Dm755 Main/$pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 Setup/Linux/usr/share/pixmaps/$pkgname.xpm "$pkgdir/usr/share/pixmaps/$pkgname.xpm"
  install -Dm644 License.txt "$pkgdir/usr/share/licenses/$pkgname/License.txt"
}
