# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=bokken-hg
pkgver=r421.5e63448608c0
pkgrel=1
pkgdesc="GUI for radare2 and pyew"
arch=('any')
url="http://inguma.eu/projects/bokken"
license=('GPL')
depends=('python2'
         'pygtksourceview2'
         'graphviz'
         'r2-bindings')
groups=('archstrike')
optdepends=('pyew: For pyew backend support'
            'radare2-bindings-git: For r2 backend support (devel)')
makedepends=('mercurial')
options=('!strip')
source=('hg+http://inguma.eu/repos/bokken')
sha512sums=('SKIP')
provides=('bokken')
conflicts=('bokken')

pkgver() {
  cd "$srcdir/bokken"
  printf "r%s.%s" "$(hg identify -n)" "$(hg identify -i)"
}

prepare() {
  cd "$srcdir/bokken"
  grep -iRl 'python' * | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/bokken"
  install -dm755 "$pkgdir/usr/share/bokken"
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/licenses/bokken"
  for i in lib  plugins  static  ui; do
        install -dm755 "$pkgdir/usr/share/bokken/$i"
        cp -a --no-preserve=ownership $i/* "$pkgdir/usr/share/bokken/$i/";
  done
  install -Dm755 bokken "$pkgdir/usr/share/bokken/"
  install -Dm755 bokken.py "$pkgdir/usr/share/bokken/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/bokken/"

  cat > "$pkgdir/usr/bin/bokken" << 'EOF'
#!/bin/bash
cd /usr/share/bokken/
exec ./bokken "$@"
EOF
  chmod +x $pkgdir/usr/bin/bokken
}
