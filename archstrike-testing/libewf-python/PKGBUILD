# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: kfgz <kfgz at interia pl>
# Contributor: Adam Wolk <netprobe at gmail dot com>

buildarch=212

_pkgname=libewf
pkgname=$_pkgname-python
pkgver=20160424
pkgrel=2
pkgdesc='A library for support of the Expert Witness Compression Format (with python support)'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/libyal/libewf'
license=('LGPL3')
depends=('util-linux' 'fuse' 'python' 'libbfio')
makedepends=('git')
options=('!libtool')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("https://github.com/libyal/$_pkgname/archive/$pkgver.tar.gz")
sha512sums=('898fd1abc0031a8e1260198d10faf085b3657442322605d47de4f0df7008a1f3c52b09e5634f89631b4cceae13c2835bcb445dbb9a9e22abbdd1afac3e4c57cf')

prepare() {
  cd $_pkgname-$pkgver
  ./synclibs.sh
}

build() {
  cd $_pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --enable-python
  make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
