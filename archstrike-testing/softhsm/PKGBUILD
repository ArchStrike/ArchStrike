# Maintainer: ArchStrike <archstrikelinux@gmail.com>
# Contributor: Javier Torres <javitonino [at] gmail [dot] com>

buildarch=212

pkgname=softhsm
pkgver=2.0.0
pkgrel=1
pkgdesc="Software PKCS11 store"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://www.opendnssec.org/softhsm/"
license=('BSD')
depends=('botan>=1.8.5' 'sqlite3>=3.3.9')
backup=("etc/softhsm2.conf")
options=('!emptydirs')
source=("https://dist.opendnssec.org/source/softhsm-$pkgver.tar.gz")
sha512sums=('1db5b79bc59707d97a3c8ba5306d6778cd02d7049a6b294ef5e60214ef7b1dcc4d51405cc32f3720443d9477377e8b04b68d6cee143731f87b6357e4e5565fe2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # /usr/lib/softhsm -> /usr/bin (no subdirectory)
  sed -i \
  -e "s/full_libdir=\"\$full_libdir\/softhsm\"/#full_libdir=\"\$full_libdir\/softhsm\"/" \
  -e "s/libdir=\$full_libdir/#libdir=\$full_libdir/" \
  configure

  ./configure --prefix=/usr --datarootdir=/usr/share --localstatedir=/var --sysconfdir=/etc --with-botan=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  chmod 755 "$pkgdir/var/lib/$pkgname"
}
