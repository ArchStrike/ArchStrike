# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Javier Torres <javitonino [at] gmail [dot] com>

buildarch=220

pkgname=softhsm
pkgver=2.1.0
pkgrel=2
pkgdesc="Software PKCS11 store"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://www.opendnssec.org/softhsm/"
license=('BSD')
depends=('botan>=1.8.5' 'sqlite3>=3.3.9' 'openssl')
backup=("etc/softhsm2.conf")
options=('!emptydirs')
source=("https://dist.opendnssec.org/source/softhsm-$pkgver.tar.gz")
sha512sums=('b3353cae05be3320d02ad96c75a8ca68f3fa74ee653b33c40189ba683500399e474741b14184774409e3783776267146d0847f2db3337b84b111c94bec5c8185')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # /usr/lib/softhsm -> /usr/bin (no subdirectory)
  sed -i \
  -e "s/full_libdir=\"\$full_libdir\/softhsm\"/#full_libdir=\"\$full_libdir\/softhsm\"/" \
  -e "s/libdir=\$full_libdir/#libdir=\$full_libdir/" \
  configure

  ./configure --prefix=/usr --datarootdir=/usr/share --localstatedir=/var --sysconfdir=/etc --with-botan=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  chmod 755 "$pkgdir/var/lib/$pkgname"
}
