# Maintainer: ArchStrike <team@archstrike.org>
# Contributor drathir <drathir87@gmail.com>

buildarch=212

pkgname=perl-class-load-xs
_lastauthor=E/ET/ETHER
_pkgname=Class-Load-XS
pkgver=0.09
pkgrel=2
pkgdesc="XS implementation of parts of Class::Load"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('custom')
options=('!emptydirs')
depends=('perl-class-load')
checkdepends=('perl-test-requires' 'perl-test-without-module')
url="http://search.cpan.org/dist/${_pkgname}/"
source=(http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz)
sha512sums=('9b24637cf7b035ba369f05e42c0bd5a8590e001f2395b46df078635a50bb3df58ad57850be0010d092ae2530d9a545bbcc5f0055f6ae7f166c93905c022d469d')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/${_pkgname}-${pkgver}"
  perl Makefile.PL
  make
}
check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make test
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
