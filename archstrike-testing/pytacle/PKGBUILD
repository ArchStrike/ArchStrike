# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=pytacle
pkgver=alpha2
pkgrel=3
pkgdesc='Automates the task of sniffing GSM frames'
groups=('archstrike' 'archstrike-sniffers')
arch=('any')
url='https://www.insinuator.net/2013/10/pytacle-alpha2/'
license=('GPL3')
depends=('pygtk')
source=("http://www.ernw.de/download/$pkgname-$pkgver.tar.gz")
sha512sums=('a45b78442e6341cccfd88f243b14628f07f57e0c1ddd7e590701b9bac06f084caad231c825dc62ea68b563733851b457c9638ed3807448289571c32c305e4a66')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|#!\s*(/usr)?(/local)?/bin/.*python.*$|#!/usr/bin/env python2|g' *.py
}

package() {
  cd $pkgname-$pkgver

  # Install package files
  install -Dm644 mcc_mnc.py "$pkgdir/usr/lib/python2.7/$pkgname/mcc_mnc.py"
  install -Dm644 windows.glade "$pkgdir/usr/lib/python2.7/$pkgname/windows.glade"
  install -Dm755 $pkgname.py "$pkgdir/usr/lib/python2.7/$pkgname/$pkgname.py"

  # Create launcher
  install -dm755 "$pkgdir/usr/bin"
  printf '%s\n\n%s\n%s\n' \
    '#!/usr/bin/env bash' \
    "cd /usr/lib/python2.7/$pkgname" \
    "python2 $pkgname.py \"$\@\"" \
      > "$pkgdir/usr/bin/$pkgname"
  chmod 755 "$pkgdir/usr/bin/$pkgname"
}
