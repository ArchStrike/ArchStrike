# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=sipp
pkgver=3.5.1
pkgrel=2
groups=('archstrike' 'archstrike-voip')
pkgdesc="A free Open Source test tool / traffic generator for the SIP protocol."
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://sipp.sourceforge.net/'
license=('GPL2')
depends=('ncurses')
makedepends=('autoconf')
source=(https://github.com/SIPp/sipp/archive/v$pkgver.tar.gz)
sha512sums=('f089ca6c3f04eec0b77fef3a711a289d63513238a3a1ceca3e2aba16bae226a9654b6f894a26f0dd7e918a1035e5a792b4eb72a3a4e4d550cf7f278cf9456967')

prepare() {
 cd $pkgname-$pkgver
 autoreconf -vifs
 # Fixing compile issue
 # make: *** No rule to make target include/version.h', needed bysrc/call.cpp'. Stop.
 echo "#define SIPP_VERSION \"v$pkgver\"" > include/version.h

}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR=$pkgdir install
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
