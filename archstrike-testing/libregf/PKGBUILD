# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname="libregf"
pkgver="20160107"
pkgrel=1
pkgdesc="Library and tools to access the Windows NT Registry File (REGF) format"
arch=("i686" "x86_64" "armv6h" "armv7h")
url='https://github.com/libyal/libregf'
license=('GPL3')
depends=('fuse' 'libcsystem' 'libbfio' 'python2' 'libfwsi')
makedepends=('git')
source=("https://github.com/libyal/libregf/archive/$pkgver.tar.gz")
sha512sums=('522580b1e9a321b835d85cd04452eeedca38144c823f2fc1420f5b878d026028ebb17ebf32bb894bf7ebfad27e321cf25e9f78fe0377c4f3c0e4f6fa950dffea')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./synclibs.sh
  ./autogen.sh
  PYTHON_VERSION="2" ./configure --prefix=/usr --disable-rpath --enable-verbose-output --enable-debug-output --enable-python
  # Fix really weird compile error related to a commented out line libcfile_support.c:742:2: error: #error Missing file remove function
  sed -i '742d' libcfile/libcfile_support.c
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
