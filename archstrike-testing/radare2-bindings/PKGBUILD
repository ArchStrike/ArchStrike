# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Levente Polyak <levente[at]leventepolyak[dot]net>

buildarch=212

pkgname=radare2-bindings
pkgver=0.10.2
pkgrel=2
pkgdesc="Language bindings of the radare2 api for Valabind and friends"
url="http://radare.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
groups=('archstrike')
license=('GPL3')
depends=('radare2' 'gawk' 'ruby' 'lua' 'python2')
makedepends=('valabind' 'swig')
options=(!emptydirs)
provides=('r2-bindings')
source=(http://www.radare.org/get/${pkgname}-${pkgver}.tar.xz)
sha512sums=('f2597fe12cc2ec9c210e15a3673f24c5adae53a90449c801af275b55d7b9b8bc07866b3253dbe582e794386fcb775425499a91ae6b29ebe5de2501649d6d2ca4')

prepare() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable=awk,ctypes,cxx,go,duktape,guile,lua,perl,php5,python,valac,newlisp,dlang,ocaml,r2pipe,vapi
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -dm755 "$pkgdir/usr/lib/perl5/site_perl/r2"
  install -dm755 "$pkgdir/usr/bin"
  make DESTDIR="${pkgdir}" install-plugins
  make DESTDIR="${pkgdir}" install-python
  make DESTDIR="${pkgdir}" install-ctypes
  #GOROOT="${pkgdir}" make DESTDIR="${pkgdir}" install-go
  #make DESTDIR="${pkgdir}" install-cxx # Not configured in makefile
  make DESTDIR="${pkgdir}" install-awk
  # Fixing awkdir in r2awk
  sed -i "s|$pkgdir/usr/lib/radare2/$pkgver/awk|/usr/lib/radare2/$pkgver/awk|" "$pkgdir/usr/bin/r2awk"
  # Installl perl bindings by hand as its messed up in installer
  cp -rf perl/*.so "$pkgdir/usr/lib/perl5/site_perl/r2"
  cp -rf perl/*.pm "$pkgdir/usr/lib/perl5/site_perl/r2"
  make DESTDIR="${pkgdir}" install-vapi
  make DESTDIR="${pkgdir}" install-ruby
  make DESTDIR="${pkgdir}" lua-install
  #make DESTDIR="${pkgdir}" install-php5 # Not configured in makefile
  #make DESTDIR="${pkgdir}" install-gir # Not configured in makefile
  #make EXAMPLEDIR="${pkgdir}" install-examples
  #make EXAMPLEDIR="${pkgdir}" install-python-examples
  #make DESTDIR="${pkgdir}" install-valac # Not configured in makefile
}

# vim: ts=2 sw=2 et:
