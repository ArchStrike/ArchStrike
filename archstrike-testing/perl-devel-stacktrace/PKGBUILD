# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=perl-devel-stacktrace
pkgver=2.00
pkgrel=1
pkgdesc="Stacktrace object w/ info form caller()"
arch=('any')
url="http://search.cpan.org/~DROLSKY/Devel-StackTrace"
license=('PerlArtistic')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/D/DR/DROLSKY/Devel-StackTrace-$pkgver.tar.gz)
md5sums=('826ed2bc7cdd8d852d7d2d8b69aa313c')
sha512sums=('3a91249dd6c6da34ceeb66d947c269e360503f193b57acbed76671b030de4d2f7e9bf2c0cc4104777fbfd1dc610c06cf3c1e21b914334bb068b66875cc4e2af7')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
  cd "${srcdir}/Devel-StackTrace-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
