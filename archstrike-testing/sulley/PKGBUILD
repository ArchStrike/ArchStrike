# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname=sulley
pkgver=1.0.cb5e62c
pkgrel=1
epoch=1
groups=('archstrike' 'archstrike-fuzzers')
pkgdesc="A pure-python fully automated and unattended fuzzing framework"
arch=('any')
url=('https://github.com/OpenRCE/sulley/')
license=('GPL2')
makedepends=('git')
depends=('libpcap' 'python2-pcapy' 'impacket')
source=('git+http://github.com/OpenRCE/sulley.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/sulley"
  # Grabbing major-version from install script
  printf "%s.%s" \
    $(grep -i 'define PRODUCT_VERSION' installer/Nullsoft\ Installer\ Script.nsi | cut -d'"' -f2) \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/sulley"
  grep -iRl '/usr/bin/python' "$srcdir/sulley" | xargs sed -i 's|#!.*/usr/bin/python$|#!/usr/bin/python2|'
}

package() {
  cd "$srcdir/sulley"
  install -dm755 "$pkgdir/usr/share/sulley"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/sulley/"
}
