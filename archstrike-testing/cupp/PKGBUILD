# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname='cupp'
pkgver='3.0'
pkgrel=2
pkgdesc='Common User Password Profiler'
url='http://www.remote-exploit.org/?page_id=418'
groups=('archstrike' 'archstrike-crackers')
license=('GPL3')
depends=('python2')
arch=('any')
md5sums=('d7bea1245095036a368566b9a15ddc86')
source=("http://www.remote-exploit.org/content/cupp-$pkgver.tar.gz")

prepare(){
  grep -iRl 'python' "$srcdir/cupp" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/cupp"

  #Base directories
  install -dm755 "$pkgdir/usr/share/cupp"
  install -dm755 "$pkgdir/usr/bin"

  #Bin
  install -D -m755 "cupp.py" "$pkgdir/usr/share/cupp/cupp.py"
  install -D -m644 "cupp.cfg" "$pkgdir/usr/share/cupp/cupp.cfg"

  # Docs
  install -D -m644 "docs/README" "$pkgdir/usr/share/doc/cupp/README"
  install -D -m644 "docs/LICENSE" "$pkgdir/usr/share/licenses/cupp/LICENSE"

  cat << EOF > "$pkgdir"/usr/bin/cupp
  #!/bin/bash

  cd /usr/share/cupp
  python2 ./cupp.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/cupp"
}
