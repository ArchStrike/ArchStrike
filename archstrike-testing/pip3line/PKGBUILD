# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=pip3line
pkgver=3.0
pkgrel=1
groups=('archstrike' 'archstrike-fuzzers')
pkgdesc="A Raw bytes manipulation & interception framework"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/metrodango/pip3line'
license=('GPL3')
if [[ "$CARCH" == 'x86_64' ]]; then
     depends=('qt5-svg' 'python' 'gcc-libs-multilib' 'distorm' 'qt5-xmlpatterns')
else
     depends=('qt5-svg' 'python' 'gcc-libs' 'distorm' 'qt5-xmlpatterns')
fi
makedepends=('cmake')
source=("https://github.com/metrodango/pip3line/archive/$pkgver.tar.gz" "libdir.patch")
sha512sums=('009bdf50fc26f481f8d442ef45f422bd0451fad2d9ca58a1e3d08d855beb5b206003de9fbacf8fa40229672e98b8e5e78dcc1c04f7959aa80535bb9bd22b8334'
            '450b69f9a8d4be66b694463264f52f45ffa13d8d544dd7e7d4824ba8c5858e4aabf8c70db7f905d9911ab5b53e8f0532023b2c5c19658b17f1abde94f75292ca')

prepare(){
 cd "$srcdir/pip3line-$pkgver"
 patch -p1 -i $srcdir/libdir.patch
}

build(){
  cd "$srcdir/pip3line-$pkgver"
  mkdir build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_SKIP_RPATH=ON -DALL=ON -DWITH_DISTORM=ON -DWITH_OPENSSL=ON -DWITH_PYTHON27=ON  -DWITH_PYTHON3=ON ../
  make
}

package(){
  cd "$srcdir/pip3line-$pkgver/build"
  make DESTDIR=$pkgdir install
}
