# Maintainer: ArchStrike <archstrikelinux@gmail.com>
# Contributor: Kevin MacMartin <prurigro@gmail.com>
# Contributor: PyroPeter <googlemail.com@abi1789>
# Contributor: darkapex <me@jailuthra.in>
# Contributor: tty0 <vt.tty0[d0t]gmail.com>

pkgname=arduino
pkgver=1.0.6
pkgrel=1
groups=('archstrike' 'archstrike-hardware')
pkgdesc="Open-source electronics prototyping platform"
arch=('i686' 'x86_64')
url="http://arduino.cc"
options=('!strip' 'staticlibs')
license=('GPL')
depends=('avr-libc' 'libusb-compat' 'java-runtime' 'java-rxtx' 'desktop-file-utils' 'shared-mime-info')
makedepends=('java-environment' 'avr-gcc' 'apache-ant' 'git' 'zip')
install="${pkgname}.install"

source=("${pkgname}::git+https://github.com/arduino/Arduino.git#tag=tags/${pkgver}"
        "${pkgname}.desktop"
        "${pkgname}.mime.xml")
sha512sums=('SKIP'
            '1f4ce6c4dcd868a805f1270d91ae80e933a7aef576daa4ae14ab21c1249548f36c77ddf5f7fc2faa88517b0503511dbff0c738d2c02f312e2eb77eaa6c5c6109'
            '910f1f5f9c9861a0b010d25e9c3579c78cb2f0b31c94fc79ed4e55b0bc65535d8ce4c54fb5cdf8a90ee589c7f1bb432bb62136ca48be1829fc001fe5acc0f0f3')

prepare(){
    # remove the username/password check for updating reference documentation
    sed -i '/^print "username: "/d;/^print "password: "/d;
            s|--silent --show-error -u \$user:\$pass|--silent --show-error|' \
        ${pkgname}/build/create_reference.pl

    # modify the launcher script to use absolute paths for referenced files
    sed -i 's|#!/bin/sh|#!/usr/bin/env bash|;
            s|processing.app.Base "$@"|processing.app.Base "${args[@]}"|;
            s%cd "$APPDIR"%args=()\nfor arg in "$@"; do\n    [[ "$arg" = -* ]] \\\n        \&\& args+=("$arg") \\\n        || args+=("$(realpath "$arg")")\ndone\n\ncd "$APPDIR"%' \
        ${pkgname}/build/linux/dist/${pkgname}
}

build(){
    cd ${pkgname}/build

    # pull updated reference documentation from the website
    ./fetch.sh

    # build and assemble the distribution package for linux using $pkgver as the version
    unset _JAVA_OPTIONS
    ant dist <<< $pkgver
}

package() {
    # create any directories we might need
    install -d "$pkgdir"/usr/bin "$pkgdir"/usr/share/doc/${pkgname} "$pkgdir"/opt/${pkgname}

    # install the xdg-desktop related files
    install -Dm644 ${pkgname}.desktop "$pkgdir"/usr/share/applications/${pkgname}.desktop
    install -Dm644 ${pkgname}.mime.xml "$pkgdir"/usr/share/mime/packages/${pkgname}.xml
    install -Dm644 ${pkgname}/build/shared/lib/arduino_icon.ico "$pkgdir"/usr/share/pixmaps/${pkgname}.ico

    # extract our distribution package in /opt/arduino
    tar zxf ${pkgname}/build/linux/${pkgname}-${pkgver}-linux.tgz \
        --no-same-owner \
        --preserve-permissions \
        --strip-components=1 \
        -C "$pkgdir"/opt/${pkgname}

    # create a link to the launch script
    ln -s /opt/${pkgname}/${pkgname} "$pkgdir"/usr/bin/${pkgname}

    # use system java-rxtx instead of the included one, which lacks permissions
    ln -sf /usr/lib/librxtxSerial.so "$pkgdir"/opt/arduino/lib/librxtxSerial.so
    ln -sf /usr/lib/librxtxSerial.so "$pkgdir"/opt/arduino/lib/librxtxSerial64.so
    ln -sf /usr/share/java/rxtx/RXTXcomm.jar "$pkgdir"/opt/arduino/lib/RXTXcomm.jar

    # create a link to the documentation in /usr/share/doc/arduino
    ln -s /opt/${pkgname}/reference "$pkgdir"/usr/share/doc/${pkgname}/
}
