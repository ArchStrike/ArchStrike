# Maintainer: ArchStrike <archstrikelinux@gmail.com>
pkgname=pshitt-git
pkgver=20150623.r22
pkgrel=1
groups=('archstrike')
pkgdesc="A lightweight fake SSH server designed to collect authentication data sent by intruders. "
arch=('any')
url='https://github.com/regit/pshitt'
license=('GPL3')
depends=('python2-paramiko')
makedepends=('git')
provides=('pshitt')
source=("${pkgname}::git+https://github.com/regit/pshitt.git")
sha512sums=('SKIP')

prepare(){
  grep -iRl 'python' "$pkgname" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

pkgver() {
    cd "${pkgname}"
    printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

package() {
  cd "${pkgname}"
  install -dm755 "$pkgdir/usr/share/$pkgname"
  install -Dm644 README.rst "$pkgdir/usr/share/$pkgname/README.rst"
  install -Dm644 test_rsa.key "$pkgdir/usr/share/$pkgname/test_rsa.key"
  install -Dm755 pshitt "$pkgdir/usr/bin/pshitt"
}
