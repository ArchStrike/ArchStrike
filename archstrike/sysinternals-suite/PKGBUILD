# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=sysinternals-suite
pkgver=20210323
pkgrel=1
groups=('archstrike')
pkgdesc="Sysinternals tools suite"
url="http://sysinternals.com"
license=('sysinternals')
arch=('any')
makedepends=('unzip')
source=(http://download.sysinternals.com/files/SysinternalsSuite.zip)
sha512sums=('0578c6b1e1ecdf834fb9ddd7796cf296697c06f0e5b3880f6ef3f4435bcc43514e50f61770de7028adfe0aafbbc99b896f719ebbb5999e8098a2c08ec06af827')

package() {
  cd "$srcdir"

  # Base directory.
  install -dm755 "$pkgdir/usr/share/sysinternals-suite"

  find . -type l -exec rm {} \;

  # Transform all files to lowercase
  for f in *; do
    lc=$(echo $f | tr '[A-Z]' '[a-z]')

    if [ ! -f $f -o -f $lc ]; then
      continue
    fi

    mv -i $f $lc
  done

  install -m755 *.exe *.sys "$pkgdir/usr/share/sysinternals-suite"
  install -m644 *.chm *.hlp *.txt "$pkgdir/usr/share/sysinternals-suite"
}
