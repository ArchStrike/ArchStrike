# Maintainer: ArchStrike <team [at] archstrike [dot] org>

buildarch=220

_pkgname=bettercap
pkgname=${_pkgname}-git
pkgver=946.6fcade8
pkgrel=1
groups=('archstrike' 'archstrike-sniffers' 'archstrike-exploit'
        'archstrike-networking')
pkgdesc='A complete, modular, portable and easily extensible MITM framework.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/evilsocket/${_pkgname}"
license=('GPL3')
depends=('ruby' 'libpcap' 'net-tools')
makedepends=('git')
provides=('bettercap')
replaces=('bettercap')
conflicts=('bettercap')
source=("${pkgname}::git+https://github.com/evilsocket/${_pkgname}.git")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "${srcdir}/${pkgname}"

  mkdir -p "${pkgdir}/usr/bin"
  mkdir -p "${pkgdir}/usr/share/${_pkgname}"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --no-user-install -n "${pkgdir}/usr/bin" -i "${pkgdir}/$_gemdir" \
  ${_pkgname}

  install -Dm644 -t "${pkgdir}/usr/share/doc/${_pkgname}/" README.md
  install -Dm644 LICENSE.md "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE.md"

}
