# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=dirb
pkgver=2.22
_pkgver=222
pkgrel=3
groups=('archstrike' 'archstrike-scanners' 'archstrike-webapps')
pkgdesc="A web content scanner, brute forceing for hidden files"
url='http://dirb.sourceforge.net/'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
license=("GPL2")
depends=('curl')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname$_pkgver.tar.gz")
sha512sums=('57305160b11be9d48b44ece5848a102ab7a493a7ac30a44c31339fd7ca659866521ba621dc0639dc28bf21c3b5315390b934441590dac7d5b463e68df4b80b81')

prepare() {
  # fixing stupid shit
  chmod 755 -R "$srcdir/$pkgname$_pkgver"
  # end fixing stupid shit
}

build () {
  cd $srcdir/dirb$_pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/dirb$_pkgver"

  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/usr/share/dirb"

  cp --no-preserve=ownership -r "$srcdir/dirb$_pkgver/wordlists" "$pkgdir/usr/share/dirb/"

  chmod -R 644 "$pkgdir/usr/share/dirb/wordlists"
  chmod -R +X "$pkgdir/usr/share/dirb/wordlists"

  # Fix conflict with extra/icu.
  mv "$pkgdir/usr/bin/gendict" "$pkgdir/usr/bin/dirb-gendict"
}
