#Maintainer: ArchStrike <team@arcstrike.org>

buildarch=1

pkgbase=python2-boto3

pkgname=(python2-boto3 python-boto3)
pkgver=1.4.4
pkgrel=1
pkgdesc="The AWS SDK for Python"
url="https://github.com/boto/boto3"
makedepends=('python-setuptools' 'python2-setuptools')
license=('APACHE')
arch=('any')
source=("https://github.com/boto/boto3/archive/$pkgver.tar.gz")
sha512sums=('948d753809e41bad39fdf3da9646b62ff2fbc9d23966cdd0e46b2df18b07ff8c8af11bb777aa0cd1fea5bcdbc2d43f3f3d06012e30327e3c55b36f7bba815a61')

prepare() {
  cp -a boto3-$pkgver{,-py2}
}

build() {
  cd "$srcdir/boto3-$pkgver"
  python3 setup.py build

  cd "$srcdir/boto3-$pkgver-py2"
  python2 setup.py build
}

package_python2-boto3() {
    depends=('python2')

    cd $srcdir/boto3-$pkgver-py2
    python2 setup.py install --root="$pkgdir" -O1 --skip-build
    install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

package_python-boto3() {
    depends=('python')

    cd $srcdir/boto3-$pkgver
    python setup.py install --root="$pkgdir" -O1 --skip-build
    install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
