# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=o-saft-git
pkgver=r4021.212a16f
pkgrel=1
groups=('archstrike' 'archstrike-scanners')
pkgdesc='Shows informations about SSL certificate and tests the SSL connection according given list of ciphers and various SSL configurations'
arch=('any')
url='https://github.com/OWASP/O-Saft'
license=('GPL')
depends=('ruby' 'perl-net-dns' 'perl-io-socket-ssl' 'perl-net-ssleay' 'perl-io-socket-inet6' 'tk' 'coreutils')
makedepends=('git')
provides=('o-saft')
replaces=('o-saft')
conflicts=('o-saft')
source=("${pkgname}::git+https://github.com/OWASP/O-Saft.git")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "${srcdir}/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  cp -a --no-preserve=ownership ./* "${pkgdir}/usr/share/${pkgname}/"

cat > "${pkgdir}/usr/bin/o-saft" <<EOF
#!/usr/bin/env bash
cd "/usr/share/${pkgname}"
perl o-saft.pl "\$@"
EOF
chmod 755 "${pkgdir}/usr/bin/o-saft"
}
