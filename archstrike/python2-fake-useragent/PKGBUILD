# Maintainer: ArchStrike <team@archstrike.org>
# Maintainer: Daniel M. Capella <polycitizen@gmail.com>

buildarch=1

_pkgname=fake-useragent
pkgbase="python-${_pkgname}"
pkgname=("python-${_pkgname}" "python2-${_pkgname}")
pkgver=0.1.11
pkgrel=2
pkgdesc='Up to date simple useragent faker with real world database'
arch=('any')
url='https://github.com/hellysmile/fake-useragent'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
source=("${url}/archive/${pkgver}.tar.gz")
sha512sums=('f73e1348af2694469a22a4cc91e9a5d57cce33b969d016903fc7fb4f91375ea5b24a5835cff38181f53dd92794d5606f52822e06d43801c0442f2b389378b2d2')

prepare() {
  cp -a "${_pkgname}-${pkgver}"{,-py2}
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}" || exit
  python setup.py build

  cd "${srcdir}/${_pkgname}-${pkgver}-py2" || exit
  python2 setup.py build
}

package_python-fake-useragent() {
  depends=('python')

  cd "${srcdir}/${_pkgname}-${pkgver}" || exit
  python setup.py install --root="${pkgdir}/" -O1 --skip-build
}

package_python2-fake-useragent() {
  depends=('python2')

  cd "${srcdir}/${_pkgname}-${pkgver}-py2" || exit
  python2 setup.py install --root="${pkgdir}/" -O1 --skip-build
}
