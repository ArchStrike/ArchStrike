# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=complemento
pkgver=0.7.7.3
pkgrel=1
pkgdesc="A collection of tools for a pentester: LetDown is a powerful tcp flooder ReverseRaider is a domain scanner that use wordlist scanning or reverse resolution scanning Httsquash is an http server scanner, banner grabber and data retriever"
url="http://complemento.sourceforge.net"
license=("GPL")
groups=('archstrike' 'archstrike-scanners' 'archstrike-fingerprinting')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('c-ares' 'libnet' 'python2' 'libpcap' 'curl')
source=(http://downloads.sourceforge.net/complemento/complemento-$pkgver.tar.gz)
sha512sums=('f26e0c11fd0521fb77a4e64db2e7db87435933443ce0b3b04eafdf2401862c0905b15e21c1a8d93c726f840caeb2867e9a3026ff341ce89d43f4a7f996ffa2a7')

prepare() {
  cd "$pkgname/letdown"
  sed -i 's/python-config/python2-config/g' Makefile

  cd "$srcdir/complemento/httsquash/"
  sed -i 's/python2.5/python2/g' Makefile

  cd "$srcdir/complemento/reverseraider/"
  sed -i 's/python2.5/python2/g' Makefile
}

build() {
  cd "$pkgname"
  export CFLAGS=$CFLAGS
  export LDFLAGS=$LDFLAGS
  make clean
  make
}

package() {
  cd "$pkgname"
  # Base directories
  install -dm755 "$pkgdir/usr/share/complemento/reverseraider/wordlists"
  install -dm755 "$pkgdir/usr/share/complemento/letdown/payloads"
  install -dm755 "$pkgdir/usr/share/doc/complemento"
  install -dm755 "$pkgdir/usr/bin"
  
  # Doc
  install -Dm644 readme "$pkgdir/usr/share/doc/complemento/readme"

  # --- LETDOWN ---

  cd "$srcdir/$pkgname/letdown"

  # Bin
  install -Dm755 letdown "$pkgdir/usr/bin/letdown"

  # Doc
  install -Dm644 README "$pkgdir/usr/share/doc/complemento/readme-letdown"

  # Payloads
  install -Dm644 payloads/* "$pkgdir/usr/share/complemento/letdown/payloads"

  # --- HTTSQUASH ---

  cd "$srcdir/$pkgname/httsquash"

  # Bin
  install -Dm755 httsquash "$pkgdir/usr/bin/httsquash"

  # --- REVERSERAIDER ---

  cd "$srcdir/$pkgname/reverseraider"

  # Bin
  install -Dm755 reverseraider "$pkgdir/usr/bin/reverseraider"

  # Wordlists
  install -m644 wordlists/* "$pkgdir/usr/share/complemento/reverseraider/wordlists"

}
