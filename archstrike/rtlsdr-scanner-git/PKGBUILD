# Maintainer: ArchStrike <team@archstrike.org>

buildarch=204

pkgname=rtlsdr-scanner-git
pkgver=0.r947.a3eec7a
pkgrel=1
groups=('archstrike' 'archstrike-scanners')
pkgdesc="A cross platform Python frequency scanning GUI for the OsmoSDR rtl-sdr library"
url='https://github.com/EarToEarOak/RTLSDR-Scanner'
arch=('armv7h' 'i686' 'x86_64' 'aarch64')
license=('GPL3')
depends=('python2-pyrtlsdr-git' 'wxpython2.8' 'python2-pyserial' 'python2-pillow' 'python2-numpy' 'rtl-sdr' 'python2-matplotlib')
makedepends=('git')
provides=('rtlsdr-scanner')
replaces=('rtlsdr-scanner')
conflicts=('rtlsdr-scanner')
source=("rtlsdr-scanner::git+https://github.com/EarToEarOak/RTLSDR-Scanner.git")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/rtlsdr-scanner"
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' "$srcdir/rtlsdr-scanner" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd $srcdir/rtlsdr-scanner
  install -dm755 "$pkgdir/usr/share/rtlsdr-scanner"
  install -dm755 "$pkgdir/usr/bin"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/rtlsdr-scanner"

cat > "$pkgdir/usr/bin/rtlsdr-scanner" <<EOF
#!/bin/sh
cd /usr/share/rtlsdr-scanner
python2 src/rtlsdr_scan.py "\$@"
EOF

chmod +x "$pkgdir/usr/bin/rtlsdr-scanner"
}
