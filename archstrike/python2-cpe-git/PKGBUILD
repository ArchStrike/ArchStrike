# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname="cpe"
pkgname=python2-${_pkgname}-git
pkgver=1.2.0.r38.gd4f8c94
pkgrel=1
pkgdesc="CPE: Common Platform Enumeration for Python"
arch=('any')
url="https://github.com/nilp0inter/${_pkgname}"
license=('GPL3')
makedepends=("python2-setuptools" "git")
depends=("python2")
source=("${_pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/${_pkgname}"
  grep -ZRil 'python' . | xargs -0 sed -i '1 s|#!.*/usr/bin/python3?|#!/usr/bin/python2|;s|#!.*/usr/bin/env python3?$|#!/usr/bin/env python2|'
}

build() {
  cd "${srcdir}/${_pkgname}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}"
  install -dm755 "${pkgdir}/usr/bin"
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
