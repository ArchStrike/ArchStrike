# Maintainer: ArchStrike <team[at]archstrike[dot]org>
# Contributor: Oliver Mangold omangold at gmail dot com

pkgname=python-xdis
pkgver=5.0.7
pkgrel=1
pkgdesc="Python cross-version byte-code disassembler and marshal routines."
arch=('any')
url="https://github.com/rocky/python-xdis/"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("https://github.com/rocky/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('233a3fc994dc338ebb44bab41da33e80db3b8e034beca7804a97e6dbabe13f8c7b71109dca3c3ce3dcba375356011b36c59e115a0e9e37bd4bf090d85ae534e3')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python setup.py build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}" -O1 --skip-build
    install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
