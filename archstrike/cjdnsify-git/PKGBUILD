# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Kevin MacMartin <prurigro at gmail dot com>

buildarch=1

pkgname=cjdnsify-git
_pkgname=cjdnsify
pkgver=20140717.r6.bc354af
pkgrel=4
pkgdesc='Limit network access of bind-compatible programs to the local cjdns network'
url='https://github.com/prurigro/cjdnsify'
groups=('archstrike' 'archstrike-networking')
license=('GPL')
depends=('bash' 'cjdns' 'force_bind')
makedepends=('git')
arch=('any')
backup=("etc/default/${_pkgname}")
conflicts=('cjdnsify')
replaces=('cjdnsify')
provides=('cjdnsify')
source=("${pkgname}::git://github.com/prurigro/${_pkgname}.git")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd ${pkgname}
  install -Dm755 ${_pkgname} "${pkgdir}/usr/bin/${_pkgname}"
  install -Dm644 ${_pkgname}.conf "${pkgdir}/etc/default/${_pkgname}"
}
