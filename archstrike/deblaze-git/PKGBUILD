# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=deblaze
pkgname=${_pkgname}-git
pkgver=20200807.r3
pkgrel=1
groups=('archstrike' 'archstrike-scanners')
pkgdesc="A remote method enumeration tool for flex servers"
url="https://github.com/SpiderLabs/deblaze"
arch=('any')
license=('GPL3')
depends=('python2-pillow')
conflicts=("deblaze")
makedepends=("git")
source=("${pkgname}::git+https://github.com/SpiderLabs/deblaze.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}" || exit
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  cd "${pkgname}" || exit
  sed -i 's|python|python2|' ${_pkgname}.py
  sed -i 's/import\ Image/from\ PIL\ import\ Image/' ${_pkgname}.py
}

package() {
  cd "${pkgname}" || exit

  install -dm755 "${pkgdir}/usr/share/${_pkgname}"
  install -dm755 "${pkgdir}/usr/bin"

  cp -a ./* "${pkgdir}/usr/share/${_pkgname}"

  cat > "${pkgdir}/usr/bin/${_pkgname}" <<EOF
#!/bin/sh
cd /usr/share/${_pkgname}
python2 ${_pkgname}.py "\$@"
EOF
  chmod +x "${pkgdir}/usr/bin/${_pkgname}"
}
