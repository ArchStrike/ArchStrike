# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=python2-instagram-git
pkgver=20160322.r232
pkgrel=1
pkgdesc="Instagram api library for python."
url='https://github.com/facebookarchive/python-instagram'
license=('custom')
arch=('any')
depends=('python2-httplib2')
makedepends=('git' 'python2-setuptools')
source=("${pkgname}::git+${url}.git")
replaces=('python2-instagram')
provides=('python2-instagram')
conflicts=('python2-instagram')
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

build() {
  cd ${pkgname}
  python2 setup.py build
}

package() {
  cd ${pkgname}
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}
