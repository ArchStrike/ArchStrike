# Maintainer: ArchStrike <team@archstrike.org>

pkgname=binjitsu-git
pkgver=20160621.r2487
pkgrel=1
pkgdesc='binjitsu is a CTF framework and exploit development library. Written in Python, it is designed for rapid prototyping and development, and intended to make exploit writing as simple as possible.'
url='https://github.com/binjitsu/binjitsu/'
depends=(
  'python2-dateutil' 'python2-paramiko' 'python2-mako' 'python2-pyelftools' 'capstone'
  'ropgadget' 'python2-pyserial' 'python2-requests' 'python2-pygments' 'python2-pysocks'
  'python2-tox' 'python2-pip'
)
makedepends=('git' 'python2-setuptools')
license=('custom')
groups=('archstrike' 'archstrike-misc')
arch=('any')
replaces=('pwntools')
provides=('pwntools')
source=("$pkgname::git+https://github.com/binjitsu/binjitsu.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  cd $pkgname
  sed -i 's|python|python2|' pwnlib/elf/datatypes.py
}

build() {
  cd $pkgname
  python2 setup.py build
}

package() {
  cd $pkgname
  python2 setup.py install --root="$pkgdir" --optimize=1

  # install license
  install -Dm644 LICENSE-binjitsu.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE-binjitsu.txt"
  install -Dm644 LICENSE-pwntools.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE-pwntools.txt"

  # fix conflict bins
  mv "$pkgdir/usr/bin/checksec" "$pkgdir/usr/bin/binjitsu-checksec"
  mv "$pkgdir/usr/bin/disasm" "$pkgdir/usr/bin/binjitsu-disasm"
  mv "$pkgdir/usr/bin/errno" "$pkgdir/usr/bin/binjitsu-errno"
}
