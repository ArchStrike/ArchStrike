# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

pkgname=nassl
pkgver=4.0.0
pkgrel=1
groups=('archstrike' 'archstrike-misc' 'archstrike-crypto')
pkgdesc='Experimental Python wrapper for OpenSSL'
arch=('i686' 'x86_64')
url='https://github.com/nabla-c0d3/nassl'
license=('GPL')
depends=('python')
makedepends=('openssl' 'openssl-1.0' 'zlib' 'python-setuptools' 'python-invoke' 'python-pipenv')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/nabla-c0d3/nassl/archive/${pkgver}.tar.gz")

sha512sums=('7653cea596d349267599fd0657ae1c0428a79609405294b02d402cef63d9dd48a3ce587eef84224f638699fb58aa7d8a95fd9a8f8c445dea0541ed740801875e')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|python_version.*$||' Pipfile
  #sed -i 's/3.7/3.8/' Pipfile
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # since archlinux does not include libssl.a files, ArchStrike used to host them at
  #    https://github.com/ArchStrike/nassl-lfs
  # following the compile instructions the static files which will be linked are created. So, nassl-lfs was removed
  # and a hybrid of their compile instructions is used.
  pipenv install --dev
  invoke build.zlib
  invoke build.legacy-openssl
  invoke build.modern-openssl
  python setup.py build_ext -i -I/usr/include/openssl-1.0/ -I/usr/include/openssl/
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" -O1 --skip-build
}
