# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=owasp-zsc-git
pkgver=20200503.r315
pkgrel=2
groups=('archstrike' 'archstrike-exploit')
arch=('any')
pkgdesc="Shellcode/Obfuscate Code Generator"
url="https://github.com/OWASP/zsc"
license=('GPL3')
depends=('python2' 'java-environment')
makedepends=('git')
source=("${pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  grep -iRl 'python' "${srcdir}/${pkgname}" | xargs sed -i 's|python$|python2|'
}

package() {
  cd "${srcdir}/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"

  install -Dm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}"
  cp -a --no-preserve=ownership ./* "${pkgdir}/usr/share/${pkgname}"

cat > "${pkgdir}/usr/bin/zsc" <<EOF
#!/bin/sh
cd "/usr/share/${pkgname}"
python2 zsc.py "\$@"
EOF
chmod 755 "${pkgdir}/usr/bin/zsc"

}
