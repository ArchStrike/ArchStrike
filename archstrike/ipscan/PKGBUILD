# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

pkgname=ipscan
pkgver=3.7.4
pkgrel=1
pkgdesc='Fast and friendly network scanner'
arch=('i686' 'x86_64')
groups=('archstrike' 'archstrike-scanners')
license=('GPL2')
url='https://angryip.org'
depends=('java-runtime')
makedepends=('jre11-openjdk' 'jdk11-openjdk' 'git')

source=(
  "git+https://github.com/angryip/ipscan#tag=$pkgver"
  'build-fix.patch'
  'google-analytics-removal.patch'
  "$pkgname.sh"
  "$pkgname.desktop"
)

sha512sums=(
  'SKIP'
  'f572a1bd593fef2dabb4282241842317c70c7e0a4993967fd4af1681f152aeabfa57c46e47f08bfb81f46d78dbdf6a611903ed0f9673776f0c7f278281cba210'
  '7bbd590567e9e0f42e45031fba7ab6044cf1e9de647fea72ea1cc39c332c1e73cb2e2e01e276aa35e1985f7c15df80f6e3a8c38d8137297e9e434dc596e532cc'
  '6a9545ee20dd64cbb27fe15f3913ad386e12f71b9ea1bac9ed6489ec111ad69d0e84302f2b933089a0ee25fe832f2d320bd406c45d92b2df47448cab13e8572d'
  '0e3e47dc0bc97695221977a077548e8ada397d264581411d9018e562b782113a1dbde9d0b017ae61418d61e0e6e9182feca04e88c335f942926164b0acbc09f8'
)

prepare() {
  cd $pkgname
  git apply ../build-fix.patch
  git apply ../google-analytics-removal.patch
  sed -i "s|^version = .*|version = '$pkgver'|" build.gradle
  sed -i -E "s|^( *'Version':) version,|\1 '$pkgver',|g" build.gradle
}

build() {
  cd $pkgname
  unset _JAVA_OPTIONS
  export PATH=/usr/lib/jvm/java-11-openjdk/bin:$PATH
  GRADLE_USER_HOME="$srcdir" ./gradlew current
}

package() {
  install -Dm755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  cd $pkgname
  install -Dm644 resources/images/icon256.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 build/libs/ipscan-*-$pkgver.jar "$pkgdir/usr/share/java/$pkgname/$pkgname.jar"
}
