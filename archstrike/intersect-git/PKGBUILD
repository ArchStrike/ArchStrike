# Maintainer: ArchStrike <team@archstrike.org>

buildarch=196

_pkgname=intersect
pkgname=$_pkgname-git
_pkgver=2.5
pkgver=2.5.r81.51caa40
pkgrel=1
pkgdesc='Post-exploit framework'
arch=('x86_64' 'i686' 'armv7h')
url='https://github.com/ohdae/Intersect-2.5'
license=('custom')
groups=('archstrike' 'archstrike-autonomous')
depends=('python' 'python2')
makedepends=('git')
replaces=("$_pkgname")
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$pkgname::git+https://github.com/deadbits/Intersect-$_pkgver" 'Intersect.patch')
sha512sums=('SKIP' '280e5a1590f5c02b3b71d0ce66a480742f628b47ac0a884e3d16069db90ece1f8162aca0e37d09fe43a352ae2372d6b79e790e84e48436657c6f53ae763bc80e')

pkgver() {
  cd $pkgname
  printf "%s.r%s.%s" "$_pkgver" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $pkgname
  patch -p1 -i ../Intersect.patch
}

package() {
  cd $pkgname
  install -dm755 "$pkgdir/usr/share/Intersect"
  install -Dm755 Create.py "$pkgdir/usr/bin/$_pkgname-create"

  for dir in Docs Junk Logs Scripts src Storage Tools; do
    cp --no-preserve=ownership -r "$dir" "$pkgdir/usr/share/Intersect/"
  done
}
