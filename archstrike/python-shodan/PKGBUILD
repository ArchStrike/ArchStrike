# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgbase=shodan
pkgname=('python-shodan' 'python2-shodan')
_pkgname=${pkgbase}-python
pkgver=1.21.1
pkgrel=1
groups=('archstrike' 'archstrike-recon')
pkgdesc='A Python2/3 library for Shodan.io'
url='http://github.com/achillean/shodan-python'
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/s/shodan/shodan-${pkgver}.tar.gz")
sha512sums=('56b7163d72cc47a9073ef814b3f0c69bbc456e37967569b75e74e1de935ba70903b2d2bde5e5ac05be042375794896979ea24cddd72cfa22230c2836cd8ee257')

prepare() {
  cp -r shodan-${pkgver} shodan2-${pkgver}

  grep -iRl 'python' shodan2-${pkgver} | xargs sed -i 's|python$|python2|g'
}

build() {
  cd ${srcdir}/shodan-${pkgver}
  python setup.py build

  cd ${srcdir}/shodan2-${pkgver}
  python2 setup.py build

}

package_python-shodan() {
depends=('python-click' 'python-click-plugins' 'python-colorama' 'python-requests' 'python-simplejson' 'python-xlsxwriter')
  cd shodan-${pkgver}
  python setup.py install --root="$pkgdir" -O1 --skip-build
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-shodan() {
conflicts=('shodan')
replaces=('shodan')
provides=('shodan')
depends=('python2-click' 'python2-click-plugins' 'python2-colorama' 'python2-requests' 'python2-simplejson' 'python2-xlsxwriter' 'python2-ipaddress')
  cd shodan2-${pkgver}
  python2 setup.py install --root="$pkgdir" -O1 --skip-build
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # move executable
  mv ${pkgdir}/usr/bin/shodan ${pkgdir}/usr/bin/shodan2
}
