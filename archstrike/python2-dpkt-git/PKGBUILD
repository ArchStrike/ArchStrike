# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=python2-dpkt-git
pkgver=20181205.r465
pkgrel=1
pkgdesc='Python library for interacting with and creating packets.'
arch=('any')
url='https://github.com/kbandla/dpkt'
license=('custom')
depends=('python2' 'python2-mock' 'python2-sphinx')
makedepends=('git' 'python2-setuptools')
replaces=('python2-dpkt')
provides=('python2-dpkt')
source=("$pkgname::git+https://github.com/kbandla/dpkt.git")
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

package() {
  cd "$pkgname"
  python2 setup.py install "--root=$pkgdir" -O1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
