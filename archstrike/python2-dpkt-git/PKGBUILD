# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=python2-dpkt-git
pkgver=20160823.r285
pkgrel=1
pkgdesc='This is a Python library for interacting with and creating packets.'
arch=('any')
url='https://github.com/kbandla/dpkt'
license=('custom')
depends=('python2')
makedepends=('git' 'python2-setuptools')
replaces=('python2-dpkt')
provides=('python2-dpkt')
source=("$pkgname::git+https://github.com/kbandla/dpkt.git")
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  grep -iRl 'python' "$pkgname" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$pkgname"
  python2 setup.py install "--root=$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
