# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Sachin Kamath <mail@skamath.me>

pkgname=ntlmrecon-git
_pkgname=ntlmrecon
pkgver=20200315.r42
pkgrel=1
groups=('archstrike' 'archstrike-recon' 'archstrike-scanner')
pkgdesc='A tool to enumerate information from NTLM authentication enabled web endpoints.'
arch=('any')
url='https://github.com/sachinkamath/ntlmrecon'
license=('MIT')
depends=('python' 'python-iptools' 'python-termcolor' 'python-colorama' 'python-requests')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/sachinkamath/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

build() {
  cd $_pkgname
  python setup.py build
}

package() {
  cd $_pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$_pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}

