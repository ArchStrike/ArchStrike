# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=pysmb
_pkgver="rel-1-1-27"
pkgname=("python-${_pkgname}" "python2-${_pkgname}")
pkgver=1.1.27
pkgrel=2
pkgdesc="An experimental SMB/CIFS library written in Python"
arch=('any')
url="https://miketeo.net/wp/index.php/projects/pysmb"
license=('GPL')
depends=('python' 'python2')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/miketeo/pysmb/archive/${_pkgver}.tar.gz")
sha512sums=('87e07c35b332b493d982e6f3b40e822d64b0ff94ff116371e390bcd804c798c1fc5a29d64c7024b4162df11c0bf1939f2c75b1c01caa1ed354e6d84e8f6b512c')

prepare(){
  cp -a ${_pkgname}-${_pkgver}{,-py2}
}

build() {
  cd "${srcdir}/${_pkgname}-${_pkgver}"
  python setup.py build

  cd "${srcdir}/${_pkgname}-${_pkgver}-py2"
  python2 setup.py build
}

package_python-pysmb() {
  depends=('python')
  makedepends=('python-setuptools')

  cd "${srcdir}/${_pkgname}-${_pkgver}"
  python3 setup.py install --root="${pkgdir}/" -O1 --skip-build
}

package_python2-pysmb() {
  depends=('python2')
  makedepends=('python2-setuptools')

  cd "${srcdir}/${_pkgname}-${_pkgver}-py2"
  python2 setup.py install --root="${pkgdir}/" -O1 --skip-build
}
