# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

_pkgname=wpscan
pkgname=$_pkgname-git
pkgver=20160311.r2017
pkgrel=2
epoch=1
groups=('archstrike' 'archstrike-webapps' 'archstrike-scanners')
pkgdesc='A vulnerability scanner which checks the security of WordPress installations using a black box approach'
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
url='http://wpscan.org'
license=('custom')
depends=('ruby-bundler' 'libxslt' 'pyyaml-libyaml' 'curl')
makedepends=('git' 'unzip')
options=('!strip')
conflicts=("$_pkgname")
provides=("$_pkgname")
source=("$pkgname::git+https://github.com/wpscanteam/wpscan")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  # Use the local share folder for files that need write access
  cd $pkgname/lib/common
  sed -i "s|ROOT_DIR, 'cache'|ENV['HOME'] + '/.local/share/$_pkgname/cache'|" common_helper.rb
  sed -i "s|ROOT_DIR, 'log.txt'|ENV['HOME'] + '/.local/share/$_pkgname/log.txt'|" common_helper.rb
}

package() {
  # Create required directories
  install -dm755 "$pkgdir/usr/share/$_pkgname"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -dm755 "$pkgdir/usr/bin"

  # Move application files to the share folder
  cd $pkgname
  cp -a --no-preserve=owner * "$pkgdir/usr/share/$_pkgname"

  # Build and install locally
  cd "$pkgdir/usr/share/$_pkgname"
  bundle config build.nokogiri --use-system-libraries
  bundle install --path vendor/bundle

  # Extract the data and delete what we don't need
  unzip -o data.zip
  rm data.zip
  rm data/LICENSE
  find . -type d -name ext -exec rm '{}' \;

  # Install the license
  mv LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Create the launcher
  cat > "$pkgdir/usr/bin/$_pkgname" << EOF
#!/usr/bin/env bash
cd /usr/share/$_pkgname
unset GEM_HOME
/usr/bin/ruby ./$_pkgname.rb "\$@"
EOF
  chmod 0755 "$pkgdir/usr/bin/$_pkgname"
}
