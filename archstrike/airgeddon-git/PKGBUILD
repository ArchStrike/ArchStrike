# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=airgeddon-git
pkgver=20170222.r456
pkgrel=2
groups=('archstrike' 'archstrike-networking')
arch=('any')
pkgdesc="Multi-use bash script for Linux systems to audit wireless networks."
url="https://github.com/v1s1t0r1sh3r3/airgeddon"
license=('GPL')
depends=('bash' 'iw' 'gawk' 'aircrack-ng' 'xterm' 'reaver-wps-fork-t6x-git')
optdepends=('wpaclean' 'crunch' 'hashcat' 'mdk3')
makedepends=('git')
source=("$pkgname::git+https://github.com/v1s1t0r1sh3r3/airgeddon.git" "remove-update.patch")
sha512sums=('SKIP'
            '7fa7f37d081c869e72ec07df88d8e89773eac88c76190a90cb3f66bdb78cf32a3762b33391e1f28cfcc503d4b85116c2b678d67dfd3cf6dad6b5bd3da506ae97')

pkgver() {
  cd $pkgname
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  cd $pkgname
  patch -Np1 -i $srcdir/remove-update.patch
}

package() {
  cd $pkgname
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/$pkgname"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname"
  cd "$pkgdir/usr/share/$pkgname"
  chmod 755 airgeddon.sh
  rm LICENSE.md

  ln -s "/usr/share/$pkgname/airgeddon.sh" "$pkgdir/usr/bin/airgeddon"
}
