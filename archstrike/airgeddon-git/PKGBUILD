# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=airgeddon-git
pkgver=20160828.r98
pkgrel=1
groups=('archstrike' 'archstrike-networking')
arch=('any')
pkgdesc="Multi-use bash script for Linux systems to audit wireless networks."
url="https://github.com/v1s1t0r1sh3r3/airgeddon"
license=('GPL')
depends=('bash' 'iw' 'gawk' 'aircrack-ng' 'xterm')
optdepends=('wpaclean' 'crunch' 'hashcat' 'mdk3')
makedepends=('git')
source=("$pkgname::git+https://github.com/v1s1t0r1sh3r3/airgeddon.git" "remove-update.patch")
sha512sums=('SKIP'
            '37dab1d94bef4ba1fee3457bf77ba5882a0d6932b4c8e214526753dea24efeb7caa89e489524a8407c8832e5cdad3b764a58560ac30cc66100d30db9a514d544')

pkgver() {
  cd $pkgname
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  cd $pkgname
  patch -Np1 -i $srcdir/remove-update.patch
}

package() {
  cd $pkgname
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/$pkgname"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname"
  cd "$pkgdir/usr/share/$pkgname"
  chmod 755 airgeddon.sh
  rm LICENSE

  ln -s "/usr/share/$pkgname/airgeddon.sh" "$pkgdir/usr/bin/airgeddon"
}
