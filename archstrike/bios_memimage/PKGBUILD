# Maintainer: ArchStrike <team@archstrike.org>

buildarch=200

pkgname='bios_memimage'
pkgver='1.2'
pkgrel=2
pkgdesc='A tool to dump RAM contents to disk (aka cold boot attack)'
arch=('i686' 'x86_64' 'aarch64')
url='http://citp.princeton.edu/memory/code/'
license=('BSD')
groups=('archstrike' 'archstrike-crackers' 'archstrike-forensics')
depends=('glibc')
options=('!makeflags')
source=("http://citp.princeton.edu/memory-content/src/bios_memimage-$pkgver.tar.gz")
sha512sums=('af358cdb5a2e35cc3434ddfd74d10147c55483951f37c193513de29df50fba8be9817b21b65417dd5124de13007fd5f0e7805406ac3f4e9ac7da21f3864c506e')

build() {
  cd $pkgname

  if [[ "$CARCH" = 'x86_64' ]]; then
    make -f Makefile.64 -C stand
    make -f Makefile.64
  else
    make
  fi
}

package() {
  cd $pkgname

  install -D -m755 'pxedump/pxedump' "$pkgdir/usr/bin/pxedump"
  install -D -m755 'usbdump/usbdump' "$pkgdir/usr/bin/usbdump"
  install -D -m644 'pxe/scraper.bin' "$pkgdir/usr/share/bios_memimage/pxe/scraper.bin"
  install -D -m644 'usb/scraper.bin' "$pkgdir/usr/share/bios_memimage/usb/scraper.bin"
  install -D -m644 'doc/readme.txt' "$pkgdir/usr/share/doc/bios_memimage/readme.txt"
  install -D -m644 'doc/faq.txt' "$pkgdir/usr/share/doc/bios_memimage/faq.txt"
}
