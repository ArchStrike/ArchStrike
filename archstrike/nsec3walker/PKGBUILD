# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=nsec3walker
pkgver=20101223
pkgrel=4
groups=('archstrike' 'archstrike-recon')
pkgdesc="Enumerates domain names using DNSSEC"
url='http://dnscurve.org/nsec3walker.html'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
depends=('python2')
license=('custom')
source=("http://dnscurve.org/nsec3walker-${pkgver}.tar.gz" "gcc6.1.fixes.patch")
sha512sums=('19378a2d37b9f796ee5834d2765e968e2bbf98e110495dda809aa6d7ea610d674332af340b084c947070d969a63ed53ece61825143ecd4571cb7c8f701b79d92'
            'a617db6b61c9d84ce3f275226c47230ec4adb49cedde3417c3694f525129dd629ab0d639ade01c8c9b77ff88032db571599a24dc7d68632c1099093989a0ea42')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/gcc6.1.fixes.patch

  sed -i 's|env python|env python2|' ./*
  sed -i 's|collect.todo|/tmp/collect.todo|' collect
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Base directory.
  install -dm755 "${pkgdir}/usr/bin"

  for bin in collect dicthashes query randomhashes unhash ; do
    install -Dm755 $bin "${pkgdir}/usr/bin/nsec3walker-$bin"; done
}
