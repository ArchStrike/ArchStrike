# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Kyle Keen <keenerd@gmail.com>

buildarch=220

pkgname=multimon-ng
pkgver=1.1.5
pkgrel=2
pkgdesc="An sdr decoder, supports pocsag, ufsk, clipfsk, afsk, hapn, fsk, dtmf, zvei."
url="http://dekar.wc3edit.net/2012/05/24/multimonng/"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('archstrike')
license=("GPL2")
depends=('libpulse')
makedepends=('qt4')
optdepends=('sox: wav conversion')
source=("https://github.com/EliasOenal/multimon-ng/archive/${pkgver}.tar.gz")
sha512sums=('661d5bd43930ccc7b1490de0649f29f1ede181222586a384d71ab4669dc81f671482d9110bbf3e1035962f0bece589ff7f358f6307d0896c129357b5551e9dd0')

build() {
  cd "${pkgname}-${pkgver}"

  qmake-qt4 PREFIX=/usr multimon-ng.pro
  sed -i 's/-lpulse-simple/& -lpulse/' Makefile
  make PREFIX=/usr
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 multimon-ng "${pkgdir}/usr/bin/multimon-ng"
}
