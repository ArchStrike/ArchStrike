# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=hotpatch-git
pkgver=20200110.r90.fd2baf1
pkgrel=1
groups=('archstrike' 'archstrike-backdoors')
pkgdesc="Hot patches executables on Linux using .so file injection"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.selectiveintellect.com/hotpatch.html'
license=('BSD')
depends=('glibc')
makedepends=('cmake' 'git')
source=("${pkgname}::git+https://github.com/vikasnkumar/hotpatch")
sha512sums=('SKIP')

pkgver() {
     cd $pkgname
     printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/${pkgname}"
  export PREFIX=/usr
  export CFLAGS+=" -std=gnu99"
  if [[ ${CARCH} == x86_64 ]]; then
        make
  else
        make ARCH=i386
  fi
}

check() {
  cd "$pkgname"
  if [[ ${CARCH} == "x86_64" ]]; then
        make test
  else
        make test ARCH=i386
  fi
}

package() {
  cd "$srcdir/${pkgname}"
  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
}
