# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

pkgname=rr
pkgver=4.4.0
pkgrel=1
groups=('archstrike' 'archstrike-misc' 'archstrike-crypto' 'archstrike-analysis')
pkgdesc="A Record and Replay Framework"
arch=('i686' 'x86_64')
url='https://github.com/mozilla/rr'
makedepends=("cmake" "python2-pexpect" "gdb")
license=('custom')
depends_x86_64+=('libpfm4' 'lib32-gcc-libs' 'bash' 'lib32-zlib' 'python2' 'gcc-multilib')
depends_i686+=('libpfm4' 'bash' 'zlib' 'python2')
source=("https://github.com/mozilla/rr/archive/$pkgver.tar.gz")
sha512sums=('d2481e4a81407327b052671798235e96ef7b4282ab8dfca0cfb0e192f93df664a109205f6fa4175c30b1569d22e79d8684f35ae108d77889f8783ec8bfd5ff0d')

build(){
  cd "$srcdir/rr-$pkgver"
  export CFLAGS+=" -fPIC"
  mkdir build && cd build
  cmake -DCMAKE_VERBOSE_MAKEFILE=ON -DCMAKE_INSTALL_PREFIX=/usr -DPYTHON_EXECUTABLE=/usr/bin/python2 ..
  make
}

package() {
  cd "$srcdir/rr-$pkgver/build"
  make DESTDIR=$pkgdir install
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
