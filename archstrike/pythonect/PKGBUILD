# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=pythonect
# OLD VERSION NOTE:
#  0.6.0 includes changes to the .parse() that appear to break it, the return values should be
#  a list/graph and instead return 1, this functionality breaks other packages that depend on
#  pythonect (e.g. hackersh) due to this I've rolled the version back to 0.5.0
pkgver=0.5.0
pkgrel=1
epoch=1
pkgdesc="A general-purpose dataflow programming language written in Python"
arch=('any')
url="http://www.pythonect.org/"
license=('BSD')
depends=('python2-ply')
makedepends=('git' 'python2-distribute')
options=('!emptydirs')
source=("https://pypi.python.org/packages/source/P/Pythonect/Pythonect-$pkgver.tar.gz")
sha512sums=('f33ec18f4511140920e11e46a8c5bcc5847da2d8033b1ed71c29a17d999100af204fefbc3434cdd467c17138eb3f8a85b69ab29a4ff15e524422724e79f6774f')

package() {
  cd "$srcdir/Pythonect-$pkgver"

  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -dm0755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  mv "$pkgdir/usr/LICENSE" "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
   -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
   -e "s|#![ ]*/bin/python$|#!/usr/bin/python2|" \
   $(find $pkgdir -name '*.py')
}
