# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

epoch=1
_pkgname=binaryornot
pkgbase=python-${_pkgname}
pkgname=(python-${_pkgname} python2-${_pkgname})
pkgver=0.4.4
pkgrel=1
arch=('any')
url="https://github.com/audreyr/binaryornot"
license=('BSD')
makedepends=('python' 'python2' 'python-setuptools' 'python2-setuptools')
replaces=('python2-${_pkgname}')
source=(${pkgname}::https://files.pythonhosted.org/packages/a7/fe/7ebfec74d49f97fc55cd38240c7a7d08134002b1e14be8c3897c0dd5e49b/${_pkgname}-${pkgver}.tar.gz)
sha512sums=('379e71b41824b9389ea02c64223e26c57694d07b749c197e3a6b9f10558c8d502e9b93a13ae6c3a4bca49064ffc650ce822073dc312fb06e50e06b8a3f04f419')

prepare(){
  cp -a ${_pkgname}-${pkgver}{,-py2}
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build

  cd "${srcdir}/${_pkgname}-${pkgver}-py2"
  python2 setup.py build
}

package_python-binaryornot() {
  pkgdesc="Ultra-lightweight pure Python package to check if a file is binary or text"
  depends=('python' 'python-chardet')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python3 setup.py install --root="${pkgdir}/" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-binaryornot() {
  pkgdesc="Ultra-lightweight pure Python package to check if a file is binary or text"
  depends+=('python2' 'python2-chardet')

  cd "${srcdir}/${_pkgname}-${pkgver}-py2"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
