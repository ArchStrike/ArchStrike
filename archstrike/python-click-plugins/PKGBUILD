# Maintainer: ArchStrike <team[at]archstrike[dot]org

pkgbase=click-plugins
pkgname=("python2-${pkgbase}" "python-${pkgbase}")
pkgver=1.0.4
pkgrel=1
pkgdesc="Extension module for click to enable registering CLI commands."
url="https://github.com/click-contrib/click-plugins"
makedepends=('python2-setuptools' 'python-setuptools')
license=('New BSD')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/c/click-plugins/click-plugins-${pkgver}.tar.gz")
sha512sums=('97e2f0f2b78d31d079fc5f2cc0c526ad68c62f769e8e9892771214678a2879624f0be1635b05e49340888d179f44a45433eb38b68571a714ff5e85e65e7195ce')

prepare() {
    cp -r ${pkgbase}-${pkgver} ${pkgbase}2-${pkgver}
}

build() {
    cd ${srcdir}/${pkgbase}2-${pkgver}
    python2 setup.py build

    cd ${srcdir}/${pkgbase}-${pkgver}
    python setup.py build
}

package_python2-click-plugins() {
depends=('python2-click')
    cd ${srcdir}/${pkgbase}2-${pkgver}
    python2 setup.py install --root="${pkgdir}" -O1 --skip-build
    install -Dm644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt
}

package_python-click-plugins() {
depends=('python-click')
    cd ${srcdir}/${pkgbase}-${pkgver}
    python setup.py install --root="${pkgdir}" -O1 --skip-build
    install -Dm644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt
}
