# Maintainer: ArchStrike <team@archstrike.org>
pkgbase=viper-git

buildarch=1

pkgname=(viper-git viper-git-docs)
pkgver=r1105.56bf88b
pkgrel=1
groups=('archstrike' 'archstrike-malware')
pkgdesc="A Binary analysis framework"
arch=('any')
url='https://github.com/botherder/viper'
license=('custom')
makedepends=('git' 'python2-sphinx')
source=("$pkgname::git+https://github.com/botherder/viper.git")
sha512sums=('SKIP')

prepare(){
  grep -iRl 'python' "$pkgname" | xargs sed -i 's|python$|python2|'
}

pkgver() {
  cd $pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package_viper-git() {
  depends=('python2-magic' 'python2-bottle' 'python2-pefile' 'python2-pyliblzma' 'python2-pydeep' 'python2-requests' 'python2-sqlalchemy' 'python2-crypto' 'python2-beautifulsoup4' 'python2-olefile' 'python2-prettytable' 'python2-pyelftools' 'python2-bitstring' 'python2-dnspython' 'python2-m2crypto' 'python2-pyasn1' 'python2-pyexiftool-git' 'python2-terminaltables' 'python2-future' 'python2-yara' 'python2-pypdns' 'python2-pypssl' 'python2-dateutil' 'python2-r2pipe' 'python2-pype32' 'python2-pbkdf2')
  provides=('viper')
  conflicts=('viper')
  replaces=('viper')
  optdepends=('androguard: APK support'
              'cuckoo: Cuckoo support'
              'python2-beautifulsoup4: html parsing'
              'peepdf: PDF Parsing'
              'yara: Malware parsing'
              'python2-pyclamd: Clamav parsing')
  cd $pkgname
  install -dm755 "$pkgdir/usr/share/$pkgname"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -dm755 "$pkgdir/usr/bin"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname"

cat > "$pkgdir/usr/bin/viper" <<EOF
#!/bin/sh
cd /usr/share/$pkgname
python2 viper-cli "\$@"
EOF
chmod 755 "$pkgdir/usr/bin/viper"
}

package_viper-git-docs() {
  provides=('viper-docs')
  conflicts=('viper-docs')
  replaces=('viper-docs')
  makedepends=('python2-sphinx')
  pkgdesc="Viper documentation"
  cd viper-git/docs
  make SPHINXBUILD=sphinx-build2 html
  install -dm755  "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -a --no-preserve=ownership build/html/* "$pkgdir/usr/share/doc/$pkgname"
}
