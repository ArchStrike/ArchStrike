# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=wifiphisher
pkgname=${_pkgname}-git
pkgver=20161223.r305
pkgrel=1
groups=('archstrike' 'archstrike-wireless')
pkgdesc='A tool for Fast automated phishing attacks against WPA networks'
arch=('any')
url='https://github.com/sophron/wifiphisher'
license=('GPL3')
depends=('scapy' 'hostapd' 'aircrack-ng' 'dnsmasq' 'python2' 'python2-jinja' 'python2-pyric' 'net-tools' 'procps-ng')
makedepends=('git')
provides=("${_pkgname}")
source=("${pkgname}::git+https://github.com/sophron/${_pkgname}.git")
sha512sums=('SKIP')

prepare(){
  grep -iRl python ${pkgname} | xargs sed -i 's|python$|python2|g'
  sed -i 's|/usr/sbin/|/usr/bin/|g' ${pkgname}/${_pkgname}/pywifiphisher.py
}

pkgver() {
  cd ${pkgname}
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

package() {
  cd ${pkgname}

  # Make base directories.
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname"

cat > "${pkgdir}/usr/bin/${_pkgname}" << EOF
#!/usr/bin/env bash
python2 /usr/share/${pkgname}/bin/${_pkgname} "\$@"
EOF
chmod 755 "${pkgdir}/usr/bin/${_pkgname}"
}
