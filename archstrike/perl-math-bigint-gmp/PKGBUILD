# Maintainer: ArchStrike <team@archstrike.org>
_author=PJACKLAM
_perlmod=Math-BigInt-GMP

buildarch=220

pkgname=perl-math-bigint-gmp
pkgver=1.6007
pkgrel=4
pkgdesc="Big integer calculations using the GNU Multiple Precision Arithmetic Library."
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://metacpan.org/pod/Math::BigInt::GMP'
license=('GPL' 'PerlArtistic')
depends=('perl>=5.16.2' 'gmp')
options=(!emptydirs)
source=(http://cpan.perl.org/modules/by-authors/id/P/PJ/${_author}/${_perlmod}-${pkgver}.tar.gz)
sha512sums=('cdeaeec1e450e95d761cefc63c5262c0b43cf5fc5865e62d0463e34821541c2b83d156979c1e7a8157c9747ccf5e3cefa80ac52489a7ce3186425bc27c176512')

build() {
  cd "${srcdir}/${_perlmod}-${pkgver}"
  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "${srcdir}/${_perlmod}-${pkgver}"
  make install DESTDIR="${pkgdir}/"

  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
}
