# Maintainer: ArchStrike <team@archstrike.org>
_author=PJACKLAM
_perlmod=Math-BigInt-GMP

buildarch=220

pkgname=perl-math-bigint-gmp
pkgver=1.6008
pkgrel=1
pkgdesc="Big integer calculations using the GNU Multiple Precision Arithmetic Library."
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://metacpan.org/pod/Math::BigInt::GMP'
license=('GPL' 'PerlArtistic')
depends=('perl>=5.16.2' 'gmp')
options=(!emptydirs)
source=(http://cpan.perl.org/modules/by-authors/id/P/PJ/${_author}/${_perlmod}-${pkgver}.tar.gz)
sha512sums=('12c312a978f603bfa12e94809778e9a65b9c725b60e4d9c449af679a911ec3f72e51a9735af4f5b1c779d49f334284aa9e4adc320dbef97b09c5159bef5781ac')

build() {
  cd "${srcdir}/${_perlmod}-${pkgver}"
  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "${srcdir}/${_perlmod}-${pkgver}"
  make install DESTDIR="${pkgdir}/"

  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
}
