# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=python2-pefile
pkgver=2016.3.28
pkgrel=1
pkgdesc="This is a Python library for PE file support and investigation."
arch=('any')
url='https://github.com/erocarrera/pefile'
license=('MIT')
depends=('python2')
makedepends=('python2-distribute')
source=("https://github.com/erocarrera/pefile/files/192316/pefile-$pkgver.tar.gz"
        "https://reverse-engineering-scripts.googlecode.com/files/UserDB.TXT"
        "http://research.pandasecurity.com/blogs/images/userdb.txt" )
sha512sums=('f27918634507fd839b3bc71677b57676f1b2e46c43970dba158b1b4cb4202344de41fef53fffd294dd70f89b1a52aec0124e835a410ff7f0bec9b11ece425201'
            '3db3041768285f664eb42319e4c7a18e4a5a1c0731d806a8e7a4070d086395727ad5cba292f81db5ba9f6de7f244f34594b7379d32234fa01380083966874809'
            'e23030b87c029911bb1fed9dfd245b645ffcb70781c3a0aa8f1dd0ecc5e3f02e36261ac69d1a25a6eee4622954217b4830a87feb8446483bc394a579d7bbb0ad')

prepare() {
  cd "pefile-${pkgver}"
  sed -i 's|python$|python2|' pefile.py
}

build() {
  cd "pefile-${pkgver}"
  python2 setup.py build
}

package() {
  cd "pefile-${pkgver}"
  python2 setup.py install -O1 --skip-build --root="${pkgdir}" --prefix="/usr"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python2-pefile/COPYING"
  cd "$srcdir"
  cat userdb.txt UserDB.TXT > UserDB-concat.txt
  install -Dm644 UserDB-concat.txt ${pkgdir}/usr/share/pefile/UserDB.TXT
}
