# Maintainer: ArchStrike <team@archstrike.org>

buildarch=28

_pkgname="ultrajson"
pkgname="python2-ujson"
pkgver=2.0.3
pkgrel=1
pkgdesc="Ultra fast JSON encoder and decoder for python2"
url="https://github.com/ultrajson/ultrajson"
arch=('armv6h' 'armv7h' 'aarch64')
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools' 'python2-pip')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('31ccee759847424de1d683bb8b55554898ea2b358a3c48524f4f455aa0c5d732d0edf84bd5fa0c203ca9c58b4fbc6d94130f447c1cbae564dc60e4863b17dad8')
b2sums=('063aa628c9d06f0dcebd73ee044ec3458d0613d2ff7e97b33d7b187ebc9557e9ab53d7db5ba987e2acf374ed3c2a9e7d0d5163a22e2d2bd691789711487ce52a')

prepare() {
  sed '/setup_requires=\["setuptools_scm"\],/d' -i "${_pkgname}-${pkgver}"/setup.py
  sed "s/{version}/${pkgver}/" "${_pkgname}-${pkgver}"/python/version_template.h > "${_pkgname}-${pkgver}"/python/version.h
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py install --skip-build -O1 --root="${pkgdir}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
