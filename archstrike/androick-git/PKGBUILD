# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname='androick-git'
_pkgname='Androick'
pkgver=r5.35048d7
pkgrel=2
groups=('archstrike' 'archstrike-forensics')
pkgdesc="A python tool to help in forensics analysis on android."
arch=('any')
url='https://github.com/Flo354/Androick'
license=('custom')
makedepends=('git')
depends=('python2-magic' 'android-tools' 'sqlite')
install=$pkgname.install
conflicts=('androick')
provices=('androick')
source=("$_pkgname::git+https://github.com/Flo354/$_pkgname.git"
	'license.txt')
sha512sums=('SKIP'
            '2b5a9627000852151717f01d72df9697151b7c2266c5689b9c4f61e513f22f97f86ab86a6c56fb619bdcf3d1e8df22e3a2503a44501712b5807d8b3960379d81')

pkgver() {
  cd "$_pkgname"
    ( set -o pipefail
      git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
      printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

prepare(){
  grep -iRl 'python' "$srcdir/$_pkgname" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/$_pkgname"

  # Make base directories.
  install -dm755 "$pkgdir/usr/share/$pkgname"
  install -dm755 "$pkgdir/usr/bin"

  install -Dm755 *.py "$pkgdir/usr/share/$pkgname/"
  install -Dm644 changelog "$pkgdir/usr/share/$pkgname/"
  install -Dm644 $srcdir/license.txt "$pkgdir/usr/share/licenses/$pkgname/license.txt"

cat > "$pkgdir/usr/bin/androick" << EOF
#!/bin/sh
cd /usr/share/$pkgname
./androick.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/androick"
}
