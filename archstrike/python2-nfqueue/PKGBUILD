# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=python2-nfqueue
pkgver=0.5
pkgrel=4
pkgdesc="Python bindings for libnetfilter_queue by Pierre Chifflier"
url="https://github.com/chifflier/nfqueue-bindings"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL')
depends=('python2' 'libnetfilter_queue')
makedepends=('python2-distribute' 'cmake' 'swig')
source=("https://github.com/chifflier/nfqueue-bindings/archive/nfqueue-bindings-$pkgver.tar.gz"
        python-support.patch)
sha512sums=('c88ef6997b8e4b3ae2c2eb6c1002b74f39979c73b4dc3a4da428aa4eeb6eedeeaff1cbc27c0740e8b29fb8b6a6f98af326be0b4fbc6ed3e432a9884febb3f7a6'
            '354e1c51eca8eff38cde7e13b0ca6a90ea2387bb2b6a678709b4bfcdab54b4ebee4f9a0031ecb769c208186b6bd157780f0e012cbaccc4a16a1a5d6716d1de47')

prepare() {
  cd "$srcdir/nfqueue-bindings-nfqueue-bindings-$pkgver"

  patch -p1 -i ../python-support.patch
  # fixing more python issues
  sed -i 's|dist-packages|site-packages|' python/CMakeLists.txt
}

package() {
  cd "$srcdir/nfqueue-bindings-nfqueue-bindings-$pkgver"

  # Examples.
  install -dm755 -p "$pkgdir/usr/share/doc/nfqueue-bindings/examples"
  install -m644 examples/*.py "$pkgdir/usr/share/doc/nfqueue-bindings/examples"

  # Library.
  make PREFIX="$pkgdir/usr" install

  # Fixing python issues
  sed -i 's|python|python2|' $pkgdir/usr/share/doc/nfqueue-bindings/examples/*.py
}
