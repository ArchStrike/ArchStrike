# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname=wol-e
pkgver=2.0
pkgrel=4
groups=('archstrike' 'archstrike-networking' 'archstrike-misc')
pkgdesc="A suite of tools for the Wake on LAN feature of network attached computers"
arch=('any')
url='http://code.google.com/p/wol-e/'
license=('GPL3')
depends=('python2' 'bash')
source=("http://wol-e.googlecode.com/files/wol-e-$pkgver.tar")
sha512sums=('fae1f4c59145d746548673a69468cc5b950332ee66b60647db9e57807a57b0bdc020f02f1a9cc46a145f447c5bfeddee3c3fa6e00082de6c4d30930e255ce9ee')

prepare() {
  cd "$srcdir/wol-e"
  sed -i 's|bin/python$|bin/python2|' *.py
}

package() {
  cd "$srcdir/wol-e"

  # Base directories.
  install -dm755 "$pkgdir/usr/share/wol-e"
  install -dm755 "$pkgdir/usr/bin"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/wol-e"

  cat > "$pkgdir/usr/bin/wol-e" <<\EOF
#!/bin/sh
cd /usr/share/wol-e
python2 wol-e.py "$@"
EOF
  chmod +x "$pkgdir/usr/bin/wol-e"
}
