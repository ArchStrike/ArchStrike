# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=angr
pkgver=8.20.6.8
pkgrel=1
pkgdesc="The next-generation binary analysis platform from UC Santa Barbara's Seclab!"
url="https://github.com/angr/angr"
depends=('python-ana' 'python-archinfo' 'python-bintrees' 'python-cachetools'
         'python-capstone' 'python-cffi' 'python-claripy' 'python-cle'
         'python-cooldict' 'python-dpkt-git' 'python-mulpyplexer'
         'python-networkx' 'python-progressbar' 'python-pyelftools' 'python-pygit2'
         'python-pyvex' 'python-rpyc' 'python-simuvex' 'python-unicorn'
         'python-sortedcontainers' 'python-ailment')
makedepends=('python-setuptools')
license=('custom')
arch=('any')
install="${pkgname}.install"
source=("https://files.pythonhosted.org/packages/source/a/angr/angr-${pkgver}.tar.gz"
        "${pkgname}.install")
sha512sums=('fbe5a54bac403e353bd52cea92c091cf829a89536d20b341de0c219f844dd82afc76f481e22d128046cfef7bdebfc462beb9a216443786cbeb2fe3fa09027be9'
            '3e5a33f78a27d5abd22eee426e08f9742e706acc3408cc4812d97d2e7fcf968ad59aace8845687cfb5d4c7d3d9a8b24cba40ac6d879266b13308f5f4d06b3aaa')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
