# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=angr
pkgver=8.20.7.27
pkgrel=2
pkgdesc="The next-generation binary analysis platform from UC Santa Barbara's Seclab!"
url="https://github.com/angr/angr"
depends=('python-ailment' 'python-ana' 'python-archinfo' 'python-bintrees' 'python-cachetools'
         'python-capstone' 'python-cffi' 'python-claripy' 'python-cle' 'python-cooldict'
         'python-cppheaderparser' 'python-dpkt-git' 'python-gitpython' 'python-itanium_demangler'
         'python-mulpyplexer' 'python-networkx' 'python-progressbar' 'python-protobuf'
         'python-psutil' 'python-pycparser' 'python-pyelftools' 'python-pygit2' 'python-pyvex'
         'python-rpyc' 'python-simuvex' 'python-sortedcontainers' 'python-unicorn')
makedepends=('python-setuptools')
license=('BSD')
arch=('any')
install="${pkgname}.install"
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.install")
sha512sums=('07fadd2afacd5454c33e271fe59f0eb32a956954d641c6b5e01ae2796e5a33cdbe0ad3cca52b9d1a448bd2070ae8e7851e1e8b3f243abffe88ff8b8c3f9d63a1'
            '3e5a33f78a27d5abd22eee426e08f9742e706acc3408cc4812d97d2e7fcf968ad59aace8845687cfb5d4c7d3d9a8b24cba40ac6d879266b13308f5f4d06b3aaa')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
