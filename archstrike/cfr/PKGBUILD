# Maintainer: ArchStrike <team@archstrike.org>
# Maintainer: lukas <lukas <dot> graetz <at> web <dot>  de>
# Maintainer: Graham Edgecombe <graham@grahamedgecombe.com>

buildarch=1

pkgname=cfr
pkgver=150
pkgrel=1
groups=('archstrike' 'archstrike-decompile')
pkgdesc='Another Java decompiler'
arch=('any')
url="http://www.benf.org/other/cfr/"
license=('MIT')
depends=('java-runtime' 'java-environment' 'bash')
source=("https://github.com/leibnitz27/${pkgname}/releases/download/0.${pkgver}/${pkgname}-0.${pkgver}.jar"
        'LICENSE')
noextract=("cfr-0.${pkgver}.jar")
sha512sums=('f22a4591b695fe297242ef4eb5f27efac880024d8d382de52903a1445183e61620b48481eebdca2c37ba601c679b38305e8484c69b19b9aa4f44d136ca87f039'
            '34f760447784493317b1715d975c649107765325443dd4aa1806cc8278dfb50bb1ced120b467a67438efd0f8db81049f2b1497f9b7752d445be5f53dddfebbaa')

package() {
  cd "${srcdir}"
  install -Dm644 "cfr-0.${pkgver}.jar" "${pkgdir}/usr/share/${pkgname}/cfr.jar"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -dm755 "${pkgdir}/usr/bin"
cat > "${pkgdir}/usr/bin/cfr" << EOF
#!/usr/bin/env bash
java -jar /usr/share/${pkgname}/cfr.jar "\$@"
EOF
chmod 755 "${pkgdir}/usr/bin/cfr"
}
