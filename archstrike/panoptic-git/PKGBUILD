# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=panoptic-git
pkgver=0.r183.bc7236c
pkgrel=1
groups=('archstrike' 'archstrike-autonomous')
pkgdesc="A tool that automates the process of search and retrieval of content for common log and config files through LFI vulnerability"
url="https://github.com/lightos/Panoptic"
arch=('any')
license=('MIT')
depends=('python2')
makedepends=('git')
replaces=('panoptic')
provides=('panoptic')
conflicts=('panoptic')
source=("${pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' ${pkgname} | xargs sed -i 's|python$|python2|g'
}

package() {
  cd ${pkgname}

  # Base directories.
  install -dm755 "${pkgdir}/usr/share/panoptic"
  install -dm755 "${pkgdir}/usr/bin/"

  cp -a --no-preserve=ownership * "${pkgdir}/usr/share/panoptic"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

cat > "${pkgdir}/usr/bin/panoptic" << EOF
#!/usr/bin/env bash
cd /usr/share/panoptic/
python2 panoptic.py "\$@"
EOF
chmod 755 "${pkgdir}/usr/bin/panoptic"
}
