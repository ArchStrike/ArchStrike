# Maintainer: ArchStrike <team@archstrike.org>

buildarch=128

pkgname=dradis
pkgver=3.18.0
pkgrel=1
pkgdesc="An open source framework to enable effective information sharing."
groups=('archstrike' 'archstrike-misc')
url="http://dradisframework.org/"
license=('GPL')
depends=('ruby' 'redis')
arch=('x86_64')
options=(!strip)
source=("https://github.com/dradis/dradis-ce/archive/v${pkgver}.tar.gz")
sha512sums=('dd645c1b1611db295104915e10f5f6f27a23ec35bfe639f067fedf22ade265f84d146608abf6f608110b9208c8f1ac10e4ac11d4e7185cbc33f371598d2070d6')

package() {

  cd "${srcdir}/${pkgname}-ce-${pkgver}"
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"

  cp -a --no-preserve=ownership ./* "${pkgdir}/usr/share/${pkgname}"

  cd "${pkgdir}/usr/bin"

  # Joint scripts.
  cat > dradis-webapp << EOF
#!/bin/sh
cd "/usr/share/${pkgname}"
./dradis-webapp "\$@"
EOF

  cat > dradis-worker << EOF
#!/bin/sh
cd "/usr/share/${pkgname}"
./dradis-worker "\$@"
EOF

  chmod 755 dradis-webapp dradis-worker
}
