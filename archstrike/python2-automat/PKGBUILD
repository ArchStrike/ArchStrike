# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname="Automat"
pkgname="python2-automat"
pkgver=20.2.0
pkgrel=1
pkgdesc='Self-service finite-state machines for the programmer on the go'
arch=('any')
license=('MIT')
url='https://github.com/glyph/automat'
depends=('python2' 'python2-attrs' "python2-six")
makedepends=('python2-setuptools-scm' "m2r")
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('715cb5dc087288492e6465a29e7d8502a84fadf451bc3d29da86335ea1c20f8efd9549f0c1eaac8800559dd8001dd73736c3bfacdc6321c83a35d2288d69632c')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  mv "${pkgdir}/usr/bin/automat-visualize"{,2}
}
