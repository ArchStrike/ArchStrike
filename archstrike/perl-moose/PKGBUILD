# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=perl-moose
_cpanname=Moose
pkgver='2.2014'
pkgrel=1
pkgdesc="A postmodern object system for Perl 5"
url="https://metacpan.org/pod/Moose"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-class-load>=0.09' 'perl-class-load-xs>=0.01'
         'perl-data-optlist>=0.107' 'perl-devel-globaldestruction'
         'perl-dist-checkconflicts>=0.02' 'perl-eval-closure>=0.04'
         'perl-list-moreutils>=0.28' 'perl-mro-compat>=0.05'
         'perl-package-deprecationmanager>=0.11' 'perl-package-stash>=0.32'
         'perl-package-stash-xs>=0.24' 'perl-params-util>=1.00'
         'perl-sub-exporter>=0.980' 'perl-sub-name>=0.05' 'perl-task-weaken'
         'perl-test-checkdeps' 'perl-try-tiny>=0.02')
makedepends=('perl-dist-checkconflicts>=0.02' 'perl-test-fatal>=0.001' 'perl-test-requires>=0.05' 'perl-test-without-module' 'libxcrypt')
source=("http://cpan.metacpan.org/authors/id/E/ET/ETHER/${_cpanname}-${pkgver}.tar.gz")
sha512sums=('ee472cffe928b86732150bd68d1a74f7c2d5769ef3369339e429361a093f3381fdc327910c1a4b9576c01b88ae90f86f1f8979dc0784fcce8ffeb9b42d67aafc')

build() {
  cd "${srcdir}/${_cpanname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "${srcdir}/${_cpanname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  find "${pkgdir}" -name '.packlist' -o -name '*.pod' -delete
}
