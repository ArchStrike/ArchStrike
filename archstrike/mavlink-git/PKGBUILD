# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=mavlink-git
pkgver=20200324.r3008
pkgrel=1
pkgdesc="MAVLink micro air vehicle marshalling / communication libraries"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://mavlink.io/"
groups=('archstrike-drone')
license=('LGPL')
depends=("python-future" "bash")
makedepends=('git' 'cmake' 'python-setuptools')
provides=('python-mavlink')
source=("${pkgname}"::'git://github.com/mavlink/mavlink.git')
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}" || exit
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  cd "${srcdir}/${pkgname}" || exit
  git submodule init
  git submodule update
}

build() {
  mkdir -p "${srcdir}/${pkgname}-build"

  cd "${srcdir}/${pkgname}-build" || exit
  cmake "${srcdir}/${pkgname}" -DCMAKE_INSTALL_PREFIX=/usr -DPYTHON_EXECUTABLE=/usr/bin/python
  make

  cd "${srcdir}/${pkgname}/pymavlink" || exit
  python setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-build" || exit
  make DESTDIR="${pkgdir}/" install

  cd "${srcdir}/${pkgname}/pymavlink" || exit
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1 --skip-build
  install -Dm0644 "${srcdir}/${pkgname}/pymavlink/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  if [[ -d "${pkgdir}"/usr/lib64 ]] ; then
    mv "${pkgdir}"/usr/lib64 "${pkgdir}"/usr/lib
  fi
}

