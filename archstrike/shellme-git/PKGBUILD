# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=shellme-git
pkgver=r3.8c7919d
pkgrel=2
pkgdesc="Because sometimes you just need shellcode and opcodes quickly"
url="https://github.com/hatRiot/shellme"
arch=('any')
license=('MIT')
groups=('archstrike')
depends=('python2')
makedepends=('git')
provides=('shellme')
conflicts=('shellme')
replaces=('shellme')
source=("${pkgname}::git+https://github.com/hatRiot/shellme.git")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd ${pkgname}
  install -dm755 "$pkgdir/usr/bin/"
  install -dm755 "$pkgdir/usr/share/shellme-git"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/${pkgname}/LICENSE.md"
  install -Dm644 README.md "$pkgdir/usr/share/${pkgname}/README.md"
  install -Dm755 shellme.py "$pkgdir/usr/share/${pkgname}/shellme.py"

cat > "$pkgdir/usr/bin/shellme" <<EOF
#!/bin/sh
cd /usr/share/shellme-git
python2 shellme.py "\$@"
EOF
chmod 755 "$pkgdir/usr/bin/shellme"
}

