# Maintainer: ArchStrike <team [at] archstrike [dot] org>

buildarch=212

pkgname=bettercap
pkgver=1.5.7
pkgrel=1
groups=('archstrike' 'archstrike-sniffers' 'archstrike-exploit' 'archstrike-networking')
pkgdesc='A complete, modular, portable and easily extensible MITM framework.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/evilsocket/bettercap'
license=('GPL3')
depends=('ruby-rspec-core' 'libpcap' 'net-tools')
source=("https://github.com/evilsocket/bettercap/archive/v$pkgver.tar.gz")
sha512sums=('93e1b670f5f183309a61be838c6a398defe93b16fb022d9143844808781377d1da281f7cd7212983a44592ecedb8a32f3aecd55562ccd5433b6ed51c32cd94cc')

build() {
  cd "$pkgname-$pkgver"

  gem build bettercap.gemspec
}

package() {
  cd "$pkgname-$pkgver"

  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --no-user-install -n "$pkgdir/usr/bin" -i "$pkgdir/$_gemdir" bettercap*.gem

  # fix $pkgdir reference by deleting gem_make.out files
  find ${pkgdir}/${_gemdir} -name gem_make.out | xargs rm -rf
 
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/"
}
