# Maintainer: ArchStrike <team@archstrike.org>

_pkgname=pulledpork

buildarch=1

pkgname=$_pkgname-git
pkgver=20160613.r283
pkgrel=2
pkgdesc="Snort rule management"
arch=('any')
url="https://github.com/shirkdog/pulledpork"
license=('GPL2')
groups=('archstrike' 'archstrike-misc')
depends=('perl')
makedepends=('git')
source=("$pkgname::git+https://github.com/shirkdog/pulledpork.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

package() {
  cd "$pkgname"

  install -dm755 "$pkgdir/usr/share/$pkgname/"
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/etc/pulledpork"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"
  cd "$pkgdir/usr/share/$pkgname/etc/"
  cp -a --no-preserve=ownership * "$pkgdir/etc/pulledpork"

cat > "$pkgdir/usr/bin/$_pkgname" <<EOF
#!/bin/sh
cd /usr/share/$pkgname
perl pulledpork.pl "\$@"
EOF
chmod 755 "$pkgdir/usr/bin/$_pkgname"
}

