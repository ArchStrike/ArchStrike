# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=recon-ng-git
pkgver=20200623.r1017
pkgrel=1
groups=('archstrike' 'archstrike-recon')
pkgdesc='A full-featured Web Reconnaissance framework written in Python'
arch=('any')
url="https://github.com/lanmaster53/recon-ng"
license=('GPL3')
depends=('python' 'python-pyaml' 'python-dnspython' 'python-lxml'
         'python-mechanize' 'python-requests' 'python-flask'
         'python-flask-restful' 'python-flasgger' 'python-dicttoxml'
         'python-xlsxwriter' 'python-unicodecsv' 'python-rq')
makedepends=('git')
source=("${pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  cd "${srcdir}/${pkgname}"

  while read -r; do
    sed -i -re 's|#!\s*(/usr)?(/local)?/bin/.*python.*$|#!/usr/bin/env python2|g' "$REPLY"
  done < <(egrep -rl '^\s*#!\s*(/usr)?(/local)?/bin/.*python')
}

package() {
  cd "${srcdir}/${pkgname}"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  cp -R * "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"
  ln -sf "/usr/share/${pkgname}/recon-cli" "${pkgdir}/usr/bin/recon-cli"
  ln -sf "/usr/share/${pkgname}/recon-ng" "${pkgdir}/usr/bin/recon-ng"
  ln -sf "/usr/share/${pkgname}/recon-web" "${pkgdir}/usr/bin/recon-web"
}
