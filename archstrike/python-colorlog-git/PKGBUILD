# Maintainer: ArchStrike <team@archstrike org>

buildarch=1

_pkgname="python-colorlog"
pkgname="${_pkgname}-git"
pkgver=20200102.r150
pkgrel=1
pkgdesc="Log formatting with colors!"
url="https://github.com/borntyping/python-colorlog"
license=('MIT')
arch=('any')
depends=('python' 'python-colorama')
makedepends=('python-setuptools' 'git')
source=("git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}" || exit
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

build() {
    cd "${srcdir}/${_pkgname}" || exit
    python setup.py build
}

package() {
    cd "${srcdir}/${_pkgname}" || exit
    python setup.py install --root="${pkgdir}" -O1 --skip-build
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
