# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=commix
pkgname=${_pkgname}-git
pkgver=20200501.r1466
pkgrel=1
groups=('archstrike' 'archstrike-webapps')
pkgdesc='Find and exploit a command injection vulnerability in a certain vulnerable parameter or string'
url='https://github.com/commixproject/commix'
depends=('python-urllib3' 'python-httplib2')
makedepends=('git')
license=('GPL3')
arch=('any')
source=("${pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}" || exit
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

package() {
  cd "${pkgname}" || exit
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  cp -a --no-preserve=ownership ./* "${pkgdir}/usr/share/${pkgname}/"

cat > "${pkgdir}/usr/bin/${_pkgname}" <<EOF
#!/usr/bin/env bash
cd "/usr/share/${pkgname}"
python "${_pkgname}.py" "\$@"
EOF

  chmod 755 "${pkgdir}/usr/bin/${_pkgname}"
}
