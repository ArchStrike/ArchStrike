# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=metasploit-framework
pkgver=5.0.74
pkgrel=2
pkgdesc='Advanced open-source platform for developing, testing, and using exploit code'
groups=('archstrike' 'archstrike-exploits' 'archstrike-fuzzers' 'archstrike-scanners')
url='https://www.metasploit.com/'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('BSD')
depends=('ruby' 'libpcap' 'postgresql-libs' 'ruby-bundler' 'sqlite' 'libxslt' 'libxml2' 'git' 'inetutils')
makedepends=('make')
options=('!strip' '!emptydirs')
conflicts=('metasploit')
source=("${pkgname}"-"${pkgver}".tar.gz::https://github.com/rapid7/metasploit-framework/archive/"${pkgver}".tar.gz)
sha512sums=('7723144525d830ed79d5740bf3c06fc33cb657cadc59f1bfaae9fe3fd164cec909e13a4f1076152bbb25e58557ecb066cbefe1b9771e1a1f7f813a509657ac6b')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Ready for upcoming Bundler changes
  mkdir -p .bundle
cat > ".bundle/config" <<EOF
BUNDLE_DEPLOYMENT: "true"
BUNDLE_CACHE_ALL: "false"
BUNDLE_JOBS: "NPROC"

EOF
  sed -i "s|NPROC|$(nproc)|" .bundle/config


  # https://github.com/bundler/bundler/issues/6882
  sed -e '/BUNDLED WITH/,+1d' -i Gemfile.lock

  bundle config build.nokogiri --use-system-libraries
  sed 's|git ls-files|find -type f|' -i metasploit-framework.gemspec
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  CFLAGS+=" -I/usr/include/libxml2"
  bundle install
  find vendor/bundle/ruby -exec chmod o+r '{}' \;
  find vendor/bundle/ruby \( -name gem_make.out -or -name mkmf.log \) -delete
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -d "${pkgdir}/opt/${pkgname}" "${pkgdir}/usr/bin"
  cp -r . "${pkgdir}/opt/${pkgname}"

  for f in "${pkgdir}"/opt/"${pkgname}"/msf*; do
    _msffile="${pkgdir}"/usr/bin/$(basename "${f}")
    echo -e "#!/bin/sh\nBUNDLE_GEMFILE=/opt/${pkgname}/Gemfile bundle exec ruby /opt/${pkgname}/$(basename "${f}") \"\$@\"" > "${_msffile}"
    chmod 755 "${_msffile}"
  done

  (cd "${pkgdir}/opt/${pkgname}"
    for f in tools/*/*.rb; do
      install -Dm 755 "${f}" ".${f}"
      echo -e "#!/bin/sh\nBUNDLE_GEMFILE=/opt/${pkgname}/Gemfile bundle exec ruby /opt/${pkgname}/.${f} \"\$@\"" > "${f}"
      chmod 755 "${f}"
    done
  )

  install -Dm 644 external/zsh/_* -t "${pkgdir}/usr/share/zsh/site-functions"
  install -Dm 644 LICENSE COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${pkgdir}/usr/share/doc"
  mv "${pkgdir}/opt/${pkgname}/documentation" "${pkgdir}/usr/share/doc/${pkgname}"
  rm "${pkgdir}/usr/bin/msfupdate"
}
