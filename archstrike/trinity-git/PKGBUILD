# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=trinity-git
pkgver=v1.6.641.g8742750
pkgrel=1
epoch=1
pkgdesc="A Linux System call fuzzer"
groups=('archstrike' 'archstrike-fuzzers')
arch=('armv6h' 'armv7h' 'i686' 'x86_64' 'aarch64')
url="http://codemonkey.org.uk/projects/trinity/"
license=('GPL')
depends=('glibc')
makedepends=('git' 'linux-headers')
replaces=('trinity')
conflicts=('trinity')
provides=('trinity')
source=("$pkgname::git+https://github.com/kernelslacker/trinity.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --always | sed 's/-/./g'
}

build() {
  cd $pkgname
  ./configure
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir/usr" install
}
