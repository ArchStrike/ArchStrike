# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Salan54 <salan at fremenil dot com>

buildarch=220

pkgname=zulucrypt
pkgver=6.0.0
pkgrel=1
pkgdesc="a cli and gui frontend to cryptsetup."
arch=('x86_64' 'i686' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/mhogomchungu/zuluCrypt/"
license=('GPL')
depends=('cryptsetup' 'qt5-base' 'libpwquality' 'libsecret' 'tcplay'
         'gtk-update-icon-cache' 'libutil-linux' 'libgcrypt' 'device-mapper')
makedepends=('cmake')
optdepends=('kdeutils-kwallet: retrieve volume keys from kde kwallet')
options=('!strip' '!buildflags' '!distcc')
source=("https://github.com/mhogomchungu/zuluCrypt/releases/download/${pkgver}/zuluCrypt-${pkgver}.tar.xz")
sha512sums=('99a4453150c56413c638fb7a2b6fa9e79542e5e37040f097e2e04329aa3e33a8b6d8fa8634994e2ad8b11267450b730f78c7d89d4a4b356c39ee516d4a6571bf')

build() {
    cd ${srcdir}/zuluCrypt-"${pkgver}"
    mkdir -p "${srcdir}"/zuluCrypt-"${pkgver}"/build && cd build
    cmake -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DNOKDE=true -DUDEVSUPPORT=true \
    -DCMAKE_BUILD_TYPE=release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_LIB_SUFFIX=/usr/lib . ..
    make
}

package() {
  cd "zuluCrypt-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  chown root:root "${pkgdir}/usr/bin/zuluCrypt-cli"
  chown root:root "${pkgdir}/usr/bin/zuluMount-cli"
  chmod 0755 "${pkgdir}/usr/bin/zuluMount-cli"
  chmod 0755 "${pkgdir}/usr/bin/zuluCrypt-cli"
  if [[ -d "${pkgdir}"/usr/lib64 ]] ; then
    mv "${pkgdir}"/usr/lib64 "${pkgdir}"/usr/lib
  fi
}
