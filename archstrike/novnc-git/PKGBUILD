# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=novnc-git
pkgver=r1362.255fbe0
pkgrel=1
groups=('archstrike' 'archstrike-networking')
pkgdesc="VNC client using HTML5 (Web Sockets, Canvas) with encryption (wss://) support."
arch=('any')
url='https://github.com/kanaka/noVNC'
license=('custom')
makedepends=('git')
depends=('bash' 'python2' 'websockify' 'nodejs' 'net-tools')
provides=('novnc')
source=("${pkgname}::git+https://github.com/kanaka/noVNC.git")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' ${pkgname} | xargs sed -i 's|python$|python2|g'
}

package() {
  cd ${pkgname}

  # Make base directories.
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
  cp -a --no-preserve=ownership * "${pkgdir}/usr/share/${pkgname}"

cat > ${pkgdir}/usr/bin/novnc <<EOF
#!/usr/bin/env bash
cd /usr/share/${pkgname}
exec ./utils/launch.sh "\$@"
EOF

chmod 755  ${pkgdir}/usr/bin/novnc

}
