# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=perl-net-cidr
_cpanname="Net-CIDR"
pkgver=0.20
pkgrel=4
pkgdesc="Manipulate IPv4/IPv6 netblocks in CIDR notation"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.5.0')
url='https://metacpan.org/pod/Net::CIDR'
source=("http://search.cpan.org/CPAN/authors/id/M/MR/MRSAM/${_cpanname}-${pkgver}.tar.gz")
sha512sums=('e00fd79bc21e9e56e74cb8de90372daf12e23a2be9a3cbfe328c2d72b7a280faba8368d89f8df11c79e2be47874b24f225504747c468498950ff33c9996cc314')

# Function to change to the working directory and set
# environment variables to override undesired options.
_enviro() {
  cd "${srcdir}/${_cpanname}-${pkgver}" || exit
  export \
    PERL_MM_USE_DEFAULT=1                                  \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR=${pkgdir}"     \
    PERL_MB_OPT="--installdirs vendor --destdir ${pkgdir}" \
    MODULEBUILDRC=/dev/null
}

build() {
  _enviro
  /usr/bin/perl Makefile.PL
  make
}

check() {
  _enviro
  make test
}

package() {
  _enviro
  make install

  # Remove "perllocal.pod" and ".packlist".
  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
