# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=crackmapexec-git
_pkgname=crackmapexec
pkgver=20200626.r569
pkgrel=1
groups=('archstrike' 'archstrike-exploit')
pkgdesc='A swiss army knife for pentesting Windows/Active Directory environments'
arch=('any')
url='https://github.com/byt3bl33d3r/CrackMapExec'
license=('GPL3')
depends=('python-virtualenv')
makedepends=('python-setuptools' 'git')
source=("${pkgname}::git+${url}.git")
provides=('crackmapexec')
replaces=('crackmapexec')
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  cd ${pkgname}
  git submodule init
  git submodule update
}

package() {
  cd ${pkgname}
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  virtualenv --system-site-packages "${pkgdir}/usr/share/${pkgname}/virtualenv"
  source "${pkgdir}/usr/share/${pkgname}/virtualenv/bin/activate"
  pip install -r requirements.txt
  python setup.py install -O1

  # Install the license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cat > "${pkgdir}/usr/bin/${_pkgname}" << EOF
#!/usr/bin/env bash
source /usr/share/${pkgname}/virtualenv/bin/activate
python /usr/share/${pkgname}/virtualenv/bin/${_pkgname} "\$@"
EOF
  chmod 755 "${pkgdir}/usr/bin/${_pkgname}"

  cat > "${pkgdir}/usr/bin/cmedb" << EOF
#!/usr/bin/env bash
source /usr/share/${pkgname}/virtualenv/bin/activate
python /usr/share/${pkgname}/virtualenv/bin/cmedb "\$@"
EOF
  chmod 755 "${pkgdir}/usr/bin/cmedb"

}
