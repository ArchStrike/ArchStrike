# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=httpry
pkgver=0.1.8
pkgrel=3
groups=('archstrike' 'archstrike-sniffers')
pkgdesc="A specialized packet sniffer designed for displaying and logging HTTP traffic."
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://dumpsterventures.com/jason/httpry/"
depends=('libpcap' 'perl')
license=('GPL2')
source=("http://dumpsterventures.com/jason/httpry/httpry-$pkgver.tar.gz")
sha512sums=('28d9fc37229e8b47711227762134e5b6e81bde0a44947f2e527771da2d042ba7e7c38b1ec205808a9e444e1235c91932ba16df4a31cc3d739c6474b25e149636')

prepare(){
  cd $srcdir/httpry-$pkgver
  sed -i '19s|plugins|/usr/share/httpry/plugins|' scripts/parse_log.pl
}

build() {
  cd $srcdir/httpry-$pkgver
  make
}

package() {
  cd $srcdir/httpry-$pkgver
  install -dm755 $pkgdir/usr/share/licenses/$pkgname
  install -dm755 $pkgdir/usr/bin
  install -dm755 $pkgdir/usr/share/$pkgname/plugins
  install -dm755 $pkgdir/usr/share/$pkgname
  install -dm755 $pkgdir/usr/share/man/man1

  #Bins
  install -Dm755 httpry "$pkgdir/usr/bin/"
  install -Dm755 scripts/parse_log.pl "$pkgdir/usr/bin/httpry-log-parse"

  #Docs
  install -Dm644 doc/* "$pkgdir/usr/share/$pkgname/"
  install -Dm644 httpry.1 "$pkgdir/usr/share/man/man1/"
  install -Dm644 doc/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  #Plugins
  install -Dm644 scripts/plugins/* "$pkgdir/usr/share/$pkgname/plugins/"

}
