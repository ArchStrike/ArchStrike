# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Prurigro

buildarch=220

_pkgname="Tomb"
pkgname="tomb"
pkgver=2.7
pkgrel=2
pkgdesc="A system for easy encryption and backup of personal files"
url="http://www.dyne.org/software/${pkgname}"
groups=('archstrike')
license=('GPL3')
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
depends=('cryptsetup' 'pinentry' 'sudo' 'gnupg' 'zsh' 'gtk2' 'libnotify')
optdepends=('dcfldd: Replacement for dd with hashing support'
            'wipe: Secure file wiping utility'
            'e2fsprogs: Create and manipulate ext3 and ext4 filesystems'
            'coreutils: Create temp image files in shared memory'
            'steghide: Hide data in various kinds of image and audio files'
            'swish-e: Support file content indexing'
            'qrencode: Create paper backups of keys as QR codes')

source=("https://files.dyne.org/tomb/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('dbe9d7e2658ca787ba267a1a43065ce5d8af79699e166226b863b0867813a1b6434e08b4a076e9710b20ce52d20e29bc04266dc4ba76a8f5c51a34ac80743240')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  sed -i 's/^#!\/bin\/zsh/#!\/usr\/bin\/env\ zsh/g' "${pkgname}"
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/extras/gtk-tray"
  make

  cd ../kdf-keys
  make
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

  # Install tomb
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  chmod 644 "${pkgdir}/usr/share/man/man1/tomb.1"

  # Install the gtk tray icon
  install -Dm755 extras/gtk-tray/tomb-gtk-tray "${pkgdir}/usr/bin/tomb-gtk-tray"

  # Install kdf-keys
  cd extras/kdf-keys
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
}
