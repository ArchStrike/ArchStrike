# Maintainer: ArchStrike <team[at]archstrike[dot]org>
# Contributor:  Chris Severance aur.severach aATt spamgourmet dott com
# Contributor : xav <xav at ethertricks dot net>

buildarch=220

pkgname=ipsumdump
pkgver=1.86
pkgrel=2
pkgdesc='A tool that summarizes TCP/IP dump files into a self-describing ASCII format easily readable by humans and programs'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.cs.ucla.edu/~kohler/ipsumdump/'
license=('GPL2' 'custom:Click')
depends=('libpcap')
source=("http://www.cs.ucla.edu/~kohler/${pkgname}/${pkgname}-${pkgver}.tar.gz" "LICENSE" "config.guess-for-aarch64.patch")
sha512sums=('38bf55589ab81eb7601283410a9f962508e419c1a0ef24f088a2d1328b29e360a9788a20689ea45724fbc56194ecb5ede1f3b792e5bc7e97a500a7b2e434fe65'
            '664edb8bdfededa74d5675f37ce188b8976d001db0c9d0c2752a735a76d7ee597456e6d963c1da540619da197e84053c2329d6d1f4037ee826c28f200c395a9e'
            '86af1d0ee847f804aa8de07a7d40e4316ec08adab1cd686b4931c5cfd27e3f5095ea87b7adf15c9368a939a45f4253e92f87f5654a7d8628928c18ce0123e113')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i $srcdir/config.guess-for-aarch64.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix='/usr' --mandir='/usr/share/man' --sbindir='/usr/bin'
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dpm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
