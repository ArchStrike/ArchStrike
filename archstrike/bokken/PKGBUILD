# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=bokken
pkgver=1.8
pkgrel=2
pkgdesc="GUI for radare2 and pyew"
arch=('any')
url="http://bokken.re"
license=('GPL')
depends=('pygtksourceview2' 'graphviz' 'r2-bindings')
groups=('archstrike')
options=('!strip')
source=("https://inguma.eu/attachments/download/212/bokken-$pkgver.tar.gz")
sha512sums=('4fc34c054b018bdf2012024827d909420f4384d3de32196b319299794a9e7ccf0d1ce22987bad5526af077fe2486b0b3d89ce62ef762c3c84dee58cb32fa09d0')
provides=('bokken-hg')
replaces=('bokken-hg')


prepare() {
  cd "$pkgname-$pkgver"
  grep -iRl 'python' * | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$pkgname-$pkgver"
  install -dm755 "$pkgdir/usr/share/bokken"
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/licenses/bokken"
  for i in lib static ui; do
        install -dm755 "$pkgdir/usr/share/bokken/$i"
        cp -a --no-preserve=ownership $i/* "$pkgdir/usr/share/bokken/$i/";
  done
  install -Dm755 bokken "$pkgdir/usr/share/bokken/"
  install -Dm755 bokken.py "$pkgdir/usr/share/bokken/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/bokken/"

  cat > "$pkgdir/usr/bin/bokken" << 'EOF'
#!/bin/bash
cd /usr/share/bokken/
exec ./bokken "$@"
EOF
  chmod +x $pkgdir/usr/bin/bokken
}
