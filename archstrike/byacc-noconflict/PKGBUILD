# Maintainer: ArchStrike <team@archstrike.org>
_pkgname=byacc

buildarch=220

pkgname=${_pkgname}-noconflict
pkgver=20160606
pkgrel=1
pkgdesc="Non-conflicting version of byacc not providing bison/yacc"
groups=('archstrike' 'archstrike-misc')
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://invisible-island.net/byacc/"
license=('custom')
depends=('glibc')
source=("ftp://invisible-island.net/${_pkgname}/${_pkgname}-${pkgver}.tgz")
sha512sums=('b8d2e9523aa615399dd6c3ed0efe90b4ac943dc8039b655958db8fd4ebd0bd920af9a7a5a4c8e525a0f04e4bfd76474e39bbfa861068851d230e4b4726ca3fea')

build() {
    cd ${_pkgname}-${pkgver}
    ./configure --prefix=/usr --mandir=/usr/share/man
    make
}

package() {
    cd ${_pkgname}-${pkgver}
    make DESTDIR="$pkgdir" install

    # License
    sed -n "/is distributed/,/distributed freely/p" README > COPYING
    install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

    # Rename (to avoid conflicting with yacc)
    cd "$pkgdir"/usr/bin
    mv yacc $_pkgname

    cd "$pkgdir"/usr/share/man/man1
    mv yacc.1 ${_pkgname}.1
}
