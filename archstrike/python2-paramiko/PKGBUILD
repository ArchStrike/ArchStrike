# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname="paramiko"
pkgname="python2-${_pkgname}"
pkgver=2.7.2
pkgrel=1
pkgdesc="Library for making SSH2 connections"
arch=('any')
url="https://pypi.org/project/${_pkgname}/"
license=('LGPL')
depends=('python2' 'python2-bcrypt' 'python2-cryptography' 'python2-pynacl')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('230522734379b9861da5e9fc4baf5b7386ea35990430ba320c6d55d04beebeaefdd7d000a2be23d1178d7fd96ba400702ffe0ab03e8856c604456108876545bd')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a --no-preserve=ownership demos "${pkgdir}/usr/share/doc/${pkgname}"
  chmod 755 "${pkgdir}/usr/share/doc/${pkgname}/demos"/*.py
  grep -iRl 'python' "${pkgdir}/usr/share/doc/${pkgname}/demos"/*.py | xargs sed -i 's/python$/python2/g'
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
}

