# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname="paramiko"
pkgname="python2-${_pkgname}"
pkgver=2.7.1
pkgrel=1
pkgdesc="Library for making SSH2 connections"
arch=('any')
url="https://pypi.org/project/${_pkgname}/"
license=('LGPL')
depends=('python2' 'python2-bcrypt' 'python2-cryptography' 'python2-pynacl')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('80068c8f72ab8c98d84188caa074deecead9fb459b1a71cedb3cfa627736730928a2b9c6eb2b5a8b60acb9bccb0f68aefbef9d0d55554001c835b5cf897751ab')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a --no-preserve=ownership demos "${pkgdir}/usr/share/doc/${pkgname}"
  chmod 755 "${pkgdir}/usr/share/doc/${pkgname}/demos"/*.py
  grep -iRl 'python' "${pkgdir}/usr/share/doc/${pkgname}/demos"/*.py | xargs sed -i 's/python$/python2/g'
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
}

