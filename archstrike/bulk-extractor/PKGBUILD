# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=bulk-extractor
pkgver=1.5.5
pkgrel=2
groups=('archstrike' 'archstrike-forensics' 'archstrike-misc')
pkgdesc="Bulk Email and URL extraction tool"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/simsong/bulk_extractor'
license=('GPL')
depends=('tre' 'java-environment' 'exiv2' 'afflib' 'libewf' 'sqlite' 'expat')
source=(http://digitalcorpora.org/downloads/bulk_extractor/bulk_extractor-${pkgver}.tar.gz)
sha512sums=('9f1384f9d3b499beecc858f78e0abd4c904dad0c64ea04bf98c1a0195b62b17be19ed2bb7b104a3c65545988c6ec729d83f75673e27690585f18b5eb468a083a')
prepare() {
  cd "${srcdir}/bulk_extractor-${pkgver}"
  sed -i 's|`dirname $0`|/usr/share/bulk-extractor|' java_gui/BEViewer
}

build() {
  cd "${srcdir}/bulk_extractor-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc
  make
  make -C java_gui
}

package() {
  cd "${srcdir}/bulk_extractor-${pkgver}"
  install -dm755 "${pkgdir}/usr/share/bulk-extractor"
  make DESTDIR="${pkgdir}" install
  install -m644 java_gui/BEViewer.jar "${pkgdir}/usr/share/bulk-extractor"
  install -m755 java_gui/BEViewer "${pkgdir}/usr/bin"
}
