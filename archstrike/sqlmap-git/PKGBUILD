# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=sqlmap
pkgname=$_pkgname-git
pkgver=20160225.r7096
pkgrel=1
epoch=1
groups=('archstrike' 'archstrike-webapps' 'archstrike-exploit' 'archstrike-fuzzers')
pkgdesc='An automatic SQL injection tool developed in Python'
url="https://github.com/sqlmapproject/sqlmap"
license=('GPL')
arch=('any')
depends=('python2')
makedepends=('git')
options=('!strip')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$pkgname::git+https://github.com/sqlmapproject/$_pkgname")
sha512sums=('SKIP')
backup=("etc/$_pkgname.conf")

pkgver() {
  cd $pkgname
  printf '%s.r%s' "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  cd $pkgname
  while read -r; do
      sed -i -re 's|#!\s*(/usr)?(/local)?/bin/.*python.*$|#!/usr/bin/env python2|g' "$REPLY"
  done < <(egrep -rl '^\s*#!\s*(/usr)?(/local)?/bin/.*python')
}

package() {
  cd $pkgname

  install -dm755 "$pkgdir/usr/share/$_pkgname"
  install -dm755 "$pkgdir/usr/share/doc"
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/etc"

  cp --no-preserve=ownership -R * "$pkgdir/usr/share/$_pkgname"
  mv "$pkgdir/usr/share/$_pkgname/doc" "$pkgdir/usr/share/doc/$_pkgname"
  mv "$pkgdir/usr/share/$_pkgname/$_pkgname.conf" "$pkgdir/etc/$_pkgname.conf"
  ln -sf /etc/$_pkgname.conf "$pkgdir/usr/share/$_pkgname/$_pkgname.conf"

  if [[ "$CARCH" = 'i686' ]]; then
    rm -rf "$pkgdir/usr/share/$_pkgname/udf/mysql/linux/64/"
  fi

  cat > "$pkgdir/usr/bin/$_pkgname" <<EOF
#!/usr/bin/env bash
cd /usr/share/$_pkgname
python2 ./$_pkgname.py "\$@"
EOF
  chmod 755 "$pkgdir/usr/bin/$_pkgname"
}
