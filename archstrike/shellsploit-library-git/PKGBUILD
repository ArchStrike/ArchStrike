# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=shellsploit-library-git
pkgver=20161001.r6
pkgrel=2
groups=('archstrike' 'archstrike-exploit')
arch=('any')
pkgdesc="Exploit development library for python users"
url="https://github.com/b3mb4m/shellsploit-library"
license=('MIT')
depends=('python2')
makedepends=('git' 'python2-setuptools')
source=("${pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  grep -iRl 'python' ${pkgname} | xargs sed -i 's|python$|python2|g'
}

build() {
  cd ${pkgname}
  python2 setup.py build
}

package() {
  cd ${pkgname}
  python2 setup.py install --root=${pkgdir} --optimize=1
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}"

  # fix conflicting bin with shellsploit
  rm "${pkgdir}/usr/bin/shellsploit"
}
