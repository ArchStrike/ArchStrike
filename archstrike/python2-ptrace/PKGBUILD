# Maintainer: ArchStrike <team@archstrike.org>
pkgbase=python2-ptrace

buildarch=1

pkgname=(python2-ptrace python-ptrace)
_pkgname=python-ptrace
pkgver=0.9.1
pkgrel=2
pkgdesc="A debugging using ptrace (Linux, BSD and Darwin system call to trace processes) written in Python."
url="https://pypi.python.org/pypi/python-ptrace/"
arch=('any')
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://pypi.python.org/packages/f0/72/6b4ddca3e38f1187ed406a6f5b88c1900e0bfbfdfe30b087576a32bb7d92/python-ptrace-$pkgver.tar.gz")
sha512sums=('f92c321fe06313a46b5d2bf750a4fc0f735a6050b97ae9b1a512c8656c7700b3487bc73acd757a23eb76aa704984e60dc64c02061ca0c1dc3d12ce2b4f577326')

prepare() {
  cp -a $_pkgname-$pkgver{,-py2}

  cd $_pkgname-$pkgver-py2
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build

  cd ../$_pkgname-$pkgver-py2
  python2 setup.py build
}

package_python-ptrace() {
  depends=('python')
  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py install --root=$pkgdir/ -O1 --skip-build
  #Fixing for conflicts
  mv $pkgdir/usr/bin/gdb.py $pkgdir/usr/bin/gdb3.py
  mv $pkgdir/usr/bin/strace.py $pkgdir/usr/bin/strace3.py
}

package_python2-ptrace() {
  depends=('python2')
  cd "$srcdir/$_pkgname-$pkgver-py2"

  python2 setup.py install --root=$pkgdir/ -O1 --skip-build
}
