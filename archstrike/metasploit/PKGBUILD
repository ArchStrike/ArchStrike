# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=metasploit
pkgver=4.12.15
pkgrel=1
epoch=1
groups=('archstrike' 'archstrike-exploit' 'archstrike-fuzzers' 'archstrike-scanners')
pkgdesc="An open source platform that supports vulnerability research, exploit development and the creation of custom security tools representing the largest collection of quality-assured exploits"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
makedepends=('ruby-bundler')
depends_x86_64+=('lib32-libxslt')
depends+=('ruby' 'git' 'libxslt' 'postgresql-libs' 'sqlite' 'libpcap' 'postgresql')
optdepends=('java-environment: msfgui'
            'dradis: dradisis database system')
url='http://www.metasploit.com'
license=('BSD')
options=('!emptydirs' '!strip')
source=("https://github.com/rapid7/metasploit-framework/archive/$pkgver.tar.gz")
replaces=('metasploit-git')
conflicts=('metasploit-git')
provides=('metasploit-git')
backup=('usr/share/metasploit/log/production.log')
install="$pkgname.install"
sha512sums=('d95801cfcb3434335d2ff406ff2660ba302d503d4514796dbe37dc18d644e43881ea6dca02a1c904cf16fa917d5ace978e40ede35606282ba8ca075cd4cbcc4c')

build() {
  cd "$pkgname-framework-$pkgver"
  
  # Nokogiri SHOULD be ok with that
  bundle config build.nokogiri --use-system-libraries
  bundle install --deployment
  
  # Locking msfupdate
  rm msfupdate

  chmod 755 vendor -R
}

package() {
  cd "$pkgname-framework-$pkgver"

  # Base directories.
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/etc"
  install -dm755 "${pkgdir}/usr/share/metasploit"
  install -dm755 "${pkgdir}/usr/share/doc/metasploit"

  # Shareable.
  cp --no-preserve=ownership -a documentation/* "${pkgdir}/usr/share/doc/metasploit"
  cp --no-preserve=ownership README.md "${pkgdir}/usr/share/doc/metasploit"
  
  # Source + Bin.
  cp -a --no-preserve=ownership * "${pkgdir}/usr/share/metasploit"
  install -m644 .rspec "${pkgdir}/usr/share/metasploit"
  install -m644 .rubocop.yml "${pkgdir}/usr/share/metasploit"
  install -m644 .ruby-gemset "${pkgdir}/usr/share/metasploit"
  install -m644 .ruby-version "${pkgdir}/usr/share/metasploit"
  install -m644 .simplecov "${pkgdir}/usr/share/metasploit"
  install -m644 .travis.yml "${pkgdir}/usr/share/metasploit"
  install -m644 .yardopts "${pkgdir}/usr/share/metasploit"
  install -m644 .gitmodules "${pkgdir}/usr/share/metasploit"
  install -m644 .mailmap "${pkgdir}"/usr/share/metasploit
  
  # We need to move the .bundle dir which contains bundle's config file
  # or metasploit won't find its gems
  cp -Rf --no-preserve=ownership .bundle "${pkgdir}/usr/share/metasploit/.bundle"

  # Commenting out the .git dir to shrink size. This will cause
  # a warning when starting about not being a git dir.
  #cp -a --no-preserve=ownership .git "${pkgdir}/usr/share/metasploit"
  # Conform to the FHS.
  mv "${pkgdir}/usr/share/metasploit/config" "${pkgdir}/etc/metasploit"
  ln -s /etc/metasploit "${pkgdir}/usr/share/metasploit/config"

  cd ${pkgdir}/usr/share/metasploit
  
  # Set the original mod
  chmod 0644 vendor/bundle/ruby/2.3.0/gems/*/*/*.rb
  
  install -Dm0666 /dev/null "${pkgdir}"/usr/share/metasploit/log/production.log

  cat > "${pkgdir}/usr/bin/msfbinscan" <<EOF
#!/bin/sh
cd /usr/share/metasploit
ruby ./msfbinscan "\$@"
EOF

  cat > "${pkgdir}/usr/bin/msfconsole" <<EOF
#!/bin/sh
if ! [[ -f /usr/share/metasploit/database.yml ]]; then
    cd /usr/share/metasploit
    ruby ./msfconsole -y msfconsole/database.yml "\$@"
else
    cd /usr/share/metasploit
    ruby ./msfconsole "\$@"
fi
EOF

  cat > "${pkgdir}/usr/bin/msfd" <<EOF
#!/bin/sh
cd /usr/share/metasploit
ruby ./msfd "\$@"
EOF

  cat > "${pkgdir}/usr/bin/msfelfscan" <<EOF
#!/bin/sh
cd /usr/share/metasploit
ruby ./msfelfscan "\$@"
EOF

  cat > "${pkgdir}/usr/bin/msfmachscan" <<EOF
#!/bin/sh
cd /usr/share/metasploit
ruby ./msfmachscan "\$@"
EOF

  cat > "${pkgdir}/usr/bin/msfpescan" <<EOF
#!/bin/sh
cd /usr/share/metasploit
ruby ./msfpescan "\$@"
EOF

  cat > "${pkgdir}/usr/bin/msfrop" <<EOF
#!/bin/sh
cd /usr/share/metasploit
ruby ./msfrop "\$@"
EOF

  cat > "${pkgdir}/usr/bin/msfrpc" <<EOF
#!/bin/sh
cd /usr/share/metasploit
ruby ./msfrpc "\$@"
EOF

  cat > "${pkgdir}/usr/bin/msfrpcd" <<EOF
#!/bin/sh
cd /usr/share/metasploit
ruby ./msfrpcd "\$@"
EOF

# Commenting out since we are locking out msfupdate
#  cat > "${pkgdir}/usr/bin/msfupdate" <<EOF
##!/bin/sh
#ruby /usr/share/metasploit/msfupdate "\$@"
#EOF

  cat > "${pkgdir}/usr/bin/msfvenom" <<EOF
#!/bin/sh
cd /usr/share/metasploit
ruby ./msfvenom "\$@"
EOF

  chmod +x "${pkgdir}"/usr/bin/*
}
