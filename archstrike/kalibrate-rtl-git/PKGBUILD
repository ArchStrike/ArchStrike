# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: blackleg <blackleg@openmailbox.org>

buildarch=220

pkgname=kalibrate-rtl-git
pkgver=20131214.r11
pkgrel=3
pkgdesc='Fork of http://thre.at/kalibrate/ for use with rtl-sdr devices'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('archstrike')
license=('custom')
url='https://github.com/steve-m/kalibrate-rtl'
provides=('kalibrate-rtl')
depends=("rtl-sdr" "fftw")
makedepends=('git')
source=("${pkgname}::git+https://github.com/steve-m/kalibrate-rtl")
sha512sums=('SKIP')

pkgver() {
    cd "${pkgname}"
    printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

build() {
  cd "${srcdir}/${pkgname}"
  ./bootstrap
  ./configure --prefix=/usr
  make

}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
        install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
