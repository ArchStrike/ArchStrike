# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=argtable
pkgver=2.13
pkgrel=2
pkgdesc="ANSI C command line parser"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://argtable.sourceforge.net/"
license=('GPL')
groups=('archstrike')
depends=('glibc')
source=("http://downloads.sourceforge.net/argtable/argtable${pkgver/./-}.tar.gz"
        'argtable.desktop'
        'gcc6.1.fixes.patch'
        'config.guess-for-aarch64.patch')
sha512sums=('3d8303f3ba529e3241d918c0127a16402ece951efb964d14a06a3a7d29a252812ad3c44e96da28798871e9923e73a2cfe7ebc84139c1397817d632cae25c4585'
            '895405825b16e8c1314ca7ac29bc97cc50d3fe1ed909f4cbfac54d601ae4ac1624228cd81c7d92edb0dda53981dd4754e0d2e2c1b478bfba706e859bfbb9cb6e'
            'f931e4704cb35f5c161399dd82dcc45beb8701bdc41fb287e61a45137eecfb1a52d5d4aac7f31f9f98c47710f8e0298fd79d49a31f14167a7c94f02d2af767c3'
            'c9742fa2995be7cf2183c2e1725f5b790d3931fb52b7e7139cc603f23f036482880bd70fb6833c69f40f0c95f0a530e29ccff63c645cecc497e6d0f603fe85c9')

prepare() {
  cd "$srcdir/argtable${pkgver/./-}"
  patch -Np1 -i $srcdir/gcc6.1.fixes.patch
  patch -Np1 -i $srcdir/config.guess-for-aarch64.patch
}

build() {
  cd "$srcdir/argtable${pkgver/./-}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/argtable${pkgver/./-}"
  make "DESTDIR=$pkgdir" install
  install -Dm644 "$srcdir/argtable.desktop" "$pkgdir/usr/share/applications/argtable.desktop"
}
