# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=guymager
pkgver=0.8.12
pkgrel=1
groups=('archstrike' 'archstrike-forensics')
pkgdesc='A forensic imager for media acquisition'
url='http://guymager.sourceforge.net'
license=('GPL')
arch=('armv6h' 'armv7h' 'i686' 'x86_64' 'aarch64')
depends=('libguytools' 'libewf' 'libbfio' 'parted')
makedepends=('qt5-tools')
options=('!strip')
backup=("etc/${pkgname}/${pkgname}.cfg")
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/LatestSource/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.desktop")
sha512sums=('79131abc41fae197012fcb4c915ef739b3709e78024f72f68b00d33778bfa4fa36b87d1f64fbde0cb6f9dd992e3852e429031eabe9aa5ec1d9cef541f470ce7f'
            '2f3de7257590ec00d6a5d222813ac9169a38b49832b863bdea7f2177eeda60ff4d98ad6656d15552e59884bfbf73cee960cb69cd1c1412bd633331025ccd9a81')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  qmake
  make
  lrelease guymager.pro
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dm644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 "${pkgname}_128.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "${pkgname}.cfg" "${pkgdir}/etc/${pkgname}/${pkgname}.cfg"
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
}
