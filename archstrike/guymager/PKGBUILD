# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=guymager
pkgver=0.8.1
pkgrel=3
groups=('archstrike' 'archstrike-forensics')
pkgdesc='A forensic imager for media acquisition'
url='http://guymager.sourceforge.net'
license=('GPL')
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
depends=('libguytools' 'libewf' 'libbfio' 'parted')
makedepends=('qtchooser' 'qt5-tools')
options=('!strip')
backup=("etc/$pkgname/$pkgname.cfg")
source=(
  "http://downloads.sourceforge.net/project/$pkgname/$pkgname/LatestSource/$pkgname-$pkgver.tar.gz"
  "$pkgname.desktop"
  'build-fix.patch'
)
sha512sums=(
  '3c17d04efcb13f41f00cb0a6a772c3fe58fe9cee5503c32af18e909be623c7d30359d7b27e8e6ae9a713a6ff97a04061334d591d2d83851ce34e6c98264495cb'
  '2f3de7257590ec00d6a5d222813ac9169a38b49832b863bdea7f2177eeda60ff4d98ad6656d15552e59884bfbf73cee960cb69cd1c1412bd633331025ccd9a81'
  '854f0849ab4c726cc15f0e6c0ff56a9e1e4d8029f774e05c0d85ecfbd8d5f13919c944e4fce38678cd01585968b9c8011ef8e5f7ee729d6f7a0232e55b722558'
)

prepare() {
  cd $pkgname-$pkgver
  patch -p1 < ../build-fix.patch
}

build() {
  cd $pkgname-$pkgver
  qmake
  make
  lrelease guymager.pro
}

package() {
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  cd $pkgname-$pkgver
  install -Dm644 ${pkgname}_128.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 $pkgname.cfg "$pkgdir/etc/$pkgname/$pkgname.cfg"
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
}
