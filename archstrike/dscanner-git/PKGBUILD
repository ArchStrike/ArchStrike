# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

pkgname=dscanner-git
pkgver=20200414.r1405
pkgrel=1
pkgdesc="Swiss-army knife for D source code"
arch=('i686' 'x86_64')
url="https://github.com/Hackerpilot/Dscanner"
license=('custom')
depends=('gcc-libs')
groups=('archstrike' 'archstrike-debugging')
makedepends=('git' 'libphobos-devel' 'dmd')
provides=('dscanner')
conflicts=('dscanner')
options=('!strip')
source=("${pkgname}::git+https://github.com/Hackerpilot/Dscanner.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${pkgname}"
    git submodule init
    git submodule update
}

build(){
  cd "${pkgname}"
  make
}

package() {
  cd "${pkgname}"
  install -Dm755 bin/dscanner "$pkgdir/usr/bin/dscanner"
  install -Dm644 LICENSE_1_0.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README.md "$pkgdir"/usr/share/dscanner-git
}
