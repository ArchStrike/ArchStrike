# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

pkgname=edb
pkgver=1.0.0
pkgrel=3
groups=('archstrike' 'archstrike-reverse' 'archstrike-debugging')
pkgdesc="A QT4-based binary mode debugging with the goal of having usability on par with OllyDbg."
arch=('i686' 'x86_64')
url='https://github.com/eteran/edb-debugger'
license=('GPL2')
depends=('qt5-xmlpatterns' 'capstone' 'graphviz' 'qt5-svg')
makedepends=('boost>=1.35.0' 'cmake')
conflicts=('edb-debugger')
provides=('edb-debugger')
source=("https://github.com/eteran/edb-debugger/releases/download/$pkgver/edb-debugger-$pkgver.tgz")
sha512sums=('d15a3d167b90b70227f273c416eae56f7ad174daae608081364284021c7da7aecb6cb42f9c5bcd85f9df70f07cba213d8345ef9d62e8d13c79e7edede38a04a9')

build() {
  cd $srcdir/edb-debugger-$pkgver
  mkdir build && cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr/ -DCMAKE_BUILD_TYPE="Release" ..
  make
}

package() {
  cd $srcdir/edb-debugger-$pkgver/build

  make DESTDIR=$pkgdir install

  # /usr/lib64 -> /usr/lib on x86_64
  [[ "$CARCH" = "x86_64" ]] && (mv $pkgdir/usr/lib64 $pkgdir/usr/lib)

}
