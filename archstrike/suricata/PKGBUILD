# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=suricata
pkgver=6.0.3
pkgrel=1
pkgdesc="An Open Source Next Generation Intrusion Detection and Prevention Engine"
arch=('armv6h' 'armv7h' 'i686' 'x86_64' 'aarch64')
url="https://suricata-ids.org/"
license=('GPL2')
groups=('archstrike')
depends=('nss' 'jansson' 'libcap-ng' 'libnetfilter_queue' 'pcre' 'libdaq-static'
         'libnet' 'libpcap' 'libyaml' 'file' 'luajit' 'geoip' 'rust' 'hiredis'
         'libnetfilter_log' 'libmaxminddb' 'libnfnetlink' 'python' 'python-distlib')
optdepends=('snort: suricata can use rulesets provided by snort')
backup=('etc/suricata/suricata.yaml')
install="${pkgname}.install"
source=("http://openinfosecfoundation.org/download/${pkgname}-${pkgver}.tar.gz"
        "${install}")
sha512sums=('186b871959988ca7cbd0d69e725aed18af915f93363c7ecc0ffa20d8ad8f50a326be08452d085772b1df84ef25258ef0dd6b35d41b0988cb1c653e60aeb103a2'
            '6c5fb66251e88be0c8feae2bb9b0ed561722b6c3b43df0a407afe302669a70624fec551428b7a4bd0d5a26dad60f006d9018176585dc7ad1ebc395ba6a6ddd0f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
  --localstatedir=/var \
  HAVE_PYTHON_CONFIG=/usr/bin/python \
  --enable-gccprotect \
  --enable-gccmarch-native \
  --enable-hiredis \
  --enable-nfqueue \
  --enable-python \
  --enable-nflog \
  --enable-luajit \
  --enable-geoip \
  --disable-coccinelle \
  --enable-old-barnyard2 \
  --with-pie \
  --enable-rust
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}.yaml "${pkgdir}/etc/${pkgname}/${pkgname}.yaml"
  install -Dm644 ./etc/classification.config "${pkgdir}/etc/${pkgname}/classification.config"
  install -Dm644 ./etc/reference.config "${pkgdir}/etc/${pkgname}/reference.config"
  install -dm755 "${pkgdir}/etc/${pkgname}/rules"
  install -Dm644 ./rules/*.rules "${pkgdir}/etc/${pkgname}/rules/"
  install -dm755 "${pkgdir}/var/log/${pkgname}"
}
