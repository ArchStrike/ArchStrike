# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=suricata
pkgver=4.1.4
pkgrel=1
pkgdesc="An Open Source Next Generation Intrusion Detection and Prevention Engine"
arch=('armv6h' 'armv7h' 'i686' 'x86_64' 'aarch64')
url="http://openinfosecfoundation.org/index.php/download-suricata"
license=('GPL2')
groups=('archstrike')
depends=('nss' 'jansson' 'libcap-ng' 'libnetfilter_queue' 'pcre' 'libdaq-static'
         'libnet' 'libpcap' 'libyaml' 'python2-magic' 'file' 'luajit' 'geoip'
         'libnetfilter_log' 'hiredis')
optdepends=('snort: suricata can use rulesets provided by snort')
replaces=('suricata')
backup=('etc/suricata/suricata.yaml')
install=${pkgname}.install
source=("http://openinfosecfoundation.org/download/${pkgname}-${pkgver}.tar.gz"
        "$install")
sha512sums=('5eca09731cc7446c8d70018f1f16dfbb6a6aaed32b2c4d6a0a6f4f094012af9f3bcdc49a3fe1ff61a24c3f1d363f30b06ca74b3920ffc5452c94677a026a561c'
            '6c5fb66251e88be0c8feae2bb9b0ed561722b6c3b43df0a407afe302669a70624fec551428b7a4bd0d5a26dad60f006d9018176585dc7ad1ebc395ba6a6ddd0f')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}" || exit
  find "${srcdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
  find "${srcdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|'
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}" || exit
  ./configure --prefix=/usr --sysconfdir=/etc \
  --localstatedir=/var \
  HAVE_PYTHON_CONFIG=/usr/bin/python2 \
  --enable-gccprotect \
  --enable-gccmarch-native \
  --enable-hiredis \
  --enable-nfqueue \
  --enable-python \
  --enable-nflog \
  --enable-luajit \
  --enable-geoip \
  --disable-coccinelle \
  --enable-old-barnyard2 \
  --with-pie
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}" || exit
  make DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}.yaml "${pkgdir}/etc/${pkgname}/${pkgname}.yaml"
  install -Dm644 classification.config "${pkgdir}/etc/${pkgname}/classification.config"
  install -Dm644 reference.config "${pkgdir}/etc/${pkgname}/reference.config"
  install -dm755 "${pkgdir}/etc/${pkgname}/rules"
  install -Dm644 rules/*.rules "${pkgdir}/etc/${pkgname}/rules/"
  install -dm755 "${pkgdir}/var/log/${pkgname}"
}
