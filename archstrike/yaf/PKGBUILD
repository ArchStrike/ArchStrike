# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Jipuf <jipuf@gmx.fr>

buildarch=220

pkgname=yaf
pkgver=2.8.4
pkgrel=3
pkgdesc="Yet Another Flowmeter"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://tools.netsa.cert.org/yaf/'
groups=('archstrike' 'archstrike-misc')
license=('GPL')
depends=('libltdl' 'libpcap' 'libfixbuf>=1.7.0')
makedepends=('p0f' 'doxygen' 'graphviz')
install=$pkgname.install
source=("http://tools.netsa.cert.org/releases/yaf-$pkgver.tar.gz")
sha512sums=('c1af7422d26f272dcb1947bd6ee56b56f0311641095aed388a0c93ab0427bf9cb238a668105e74ab4785edda02522a87013fb8b3a23dd66035ce963956a343ee')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export LIBS="-lpthread"
  ./configure --prefix=/usr --enable-applabel --enable-mpls --enable-plugins --enable-entropy --enable-localtime --enable-nonip --with-pic
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # fixing  /etc path
  mv $pkgdir/usr/etc $pkgdir/etc
}
