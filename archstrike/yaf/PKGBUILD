# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Jipuf <jipuf@gmx.fr>

buildarch=220

pkgname=yaf
pkgver=2.11.2
pkgrel=1
pkgdesc="Yet Another Flowmeter"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://tools.netsa.cert.org/yaf/'
groups=('archstrike' 'archstrike-misc')
license=('GPL')
depends=('libltdl' 'libpcap' 'libfixbuf>=1.7.0')
makedepends=('p0f' 'doxygen' 'graphviz')
install=${pkgname}.install
source=("http://tools.netsa.cert.org/releases/yaf-${pkgver}.tar.gz")
sha512sums=('53c2c96f0f5c5af02f124dd2a4b7ad831ea7bfa421a8e97565d7f2240d5c81fb596388c6653b604a91146a562e01657efebf713261724d6914f71ea46f24cb2f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export LIBS="-lpthread"
  ./configure --prefix=/usr --enable-applabel --enable-mpls --enable-plugins --enable-entropy --enable-localtime --enable-nonip --with-pic
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  # fixing  /etc path
  mv ${pkgdir}/usr/etc ${pkgdir}/etc
}
