# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname="twisted"
pkgname="python2-${_pkgname}"
pkgver=20.3.0
pkgrel=1
pkgdesc='An asynchronous networking framework written in Python'
arch=('any')
license=('MIT')
url="https://twistedmatrix.com/"
depends=('python2-zope-interface' 'python2-constantly' 'python2-incremental' 'python2-automat'
         'python2-hyperlink' 'python2-attrs' 'python-twisted')
makedepends=('python2-setuptools')
source=("https://github.com/twisted/twisted/archive/twisted-${pkgver}.tar.gz")
sha512sums=('72307a0b9165d5f6b31f5563f4f76f9c759349d55f6dcaf43358eab3e8f1b2065e0b12c8ac43f14061ee17cb2d9b27f463bcb9e642750fb8e6e510e403bd2b06')

build() {
  cd "${srcdir}/${_pkgname}-${_pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${_pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  for name in "${pkgdir}/usr/bin/"*; do
    mv "${name}"{,2}
  done
}
