# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=cewl
_pkgname=CeWL
pkgver=5.4.9
pkgrel=2
groups=('archstrike' 'archstrike-autonomous' 'archstrike-crackers')
pkgdesc='A custom word list generator'
arch=('any')
url="https://github.com/digininja/CeWL"
license=('CCPL')
depends=('ruby' 'ruby-spider' 'ruby-mini_exiftool' 'ruby-nokogiri'
         'ruby-http_configuration' 'ruby-rubyzip' 'ruby-zip' 'ruby-mime' 'ruby-mime-types')
makedepends=('ruby-bundler')
source=("${url}/archive/${pkgver}.tar.gz" 'bundler.patch')
sha512sums=('7395b13f439eb97cc4f0e31d370c800e793d94ad2ea2f53655ef91eb12065dc5e586746ed63ae9a5e89499abc9e37b691c9a9a73a97862a2273a01e2e2e65f7e'
            'cfe984632dd80009baa117cb75ff6dc50bb4994ac8a03ca8cc39cb72f9984c012199180df12501c4a75c3a66548c3bc32ea8afd561c5557fee1c8ca3baee01e7')

prepare() {
  cd ${_pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/bundler.patch
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  cp -a --no-preserve=ownership ./* "${pkgdir}/usr/share/${pkgname}"
  cd "${pkgdir}/usr/share/${pkgname}"
  bundle install --path vendor/bundle


cat > "${pkgdir}/usr/bin/${pkgname}" << EOF
#!/usr/bin/env bash
cd "/usr/share/${pkgname}"
unset GEM_HOME
/usr/bin/bundler exec ./cewl.rb "\$@"
EOF

chmod 755 "${pkgdir}/usr/bin/${pkgname}"
}
