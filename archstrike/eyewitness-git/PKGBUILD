# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=eyewitness-git
pkgver=r546.8f79bdb
pkgrel=1
pkgdesc="designed to take screenshots of websites, provide some server header info, and identify default credentials is possible"
arch=('any')
url="https://github.com/ChrisTruncer/EyeWitness"
license=('GPL3')
groups=('archstrike')
depends=('python2-pyqt4' 'ghost.py' 'python2-netaddr' 'python2-beautifulsoup4' 'python2-fuzzywuzzy-git' 'python2-selenium' 'python2-levenshtein' 'python2-pyasn1' 'python2-pyvirtualdisplay' 'python2-rdpy')
makedepends=('git')
provides=('eyewitness')
replaces=('eyewitness')
conflicts=('eyewitness')
options=('!emptydirs')
source=("${pkgname}::git+https://github.com/ChrisTruncer/EyeWitness.git")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  grep -iRl 'python' ${pkgname} | xargs sed -i 's|python$|python2|'
}

package() {
  cd ${pkgname}
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  cp -a --no-preserve=ownership * "${pkgdir}/usr/share/${pkgname}"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}"

cat > "$pkgdir/usr/bin/eyewitness" <<EOF
#!/bin/sh
python2 /usr/share/${pkgname}/EyeWitness.py "\$@"
EOF
chmod 755 "$pkgdir/usr/bin/eyewitness"
}

