# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=dbpwaudit
pkgver=0.8
pkgrel=1
groups=('archstrike' 'archstrike-defense' 'archstrike-crackers')
pkgdesc="A Java tool that allows you to perform online audits of password quality for several database engines"
url='http://www.cqure.net/wp/dbpwaudit/'
license=('GPL')
depends=('java-runtime')
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
source=(http://www.cqure.net/tools/dbpwaudit_${pkgver//./_}.zip)
sha512sums=('74b2ab5d0a142f0f03d6c67452dba6b2')

prepare() {
  cd "$srcdir/DBPwAudit"
  sed -i "1a\cd /usr/share/dbpwaudit" *.sh
}

package() {
  cd "$srcdir/DBPwAudit"

  # Make base directories.
  install -dm755 "$pkgdir/usr/share/dbpwaudit"
  install -dm755 "$pkgdir/usr/bin"

  cp --no-preserve=ownership -r * "$pkgdir/usr/share/dbpwaudit"
  install -m755 dbpwaudit.sh "$pkgdir/usr/bin/dbpwaudit"

  # This isn't Windows.
  rm "$pkgdir/usr/share/dbpwaudit"/*.cmd
  chmod -x "$pkgdir/usr/share/dbpwaudit"/*.conf
}
