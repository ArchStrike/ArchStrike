# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname=python2-dartspylru-git
pkgver=20130715.r7
pkgrel=1
pkgdesc="An LRUDict is basically a simple dictionary, which has a defined maximum capacity"
arch=('any')
url='https://github.com/deterministic-arts/DartsPyLRU'
license=('MIT')
makedepends=('git' 'python2-setuptools')
provides=('python2-dartspylru')
source=("${pkgname}::git+https://github.com/deterministic-arts/DartsPyLRU.git" "LICENSE")
sha512sums=('SKIP'
            'ee73bd4a1492d894a6fe55a0d8ec1dc51ce034872e8c59e15d7881b5393a6dd6f6fbbd96d90d639d40338f8119e077bbb1c3a6b9eb6d59571f98f65d68067979')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare(){
  grep -iRl 'python' "${pkgname}" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "${pkgname}"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 $srcdir/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
