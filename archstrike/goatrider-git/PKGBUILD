# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=goatrider-git
pkgver=20181120.r6
pkgrel=2
groups=('archstrike' 'archstrike-analysis' 'archstrike-misc')
arch=('any')
pkgdesc="simple tool for doing a comparison of IP addresses or hostnames to BDS Artillery Feeds, OTX, Alexa Top 1M, and TOR"
url="https://github.com/BinaryDefense/goatrider"
license=('custom')
depends=('python2')
makedepends=('git')
source=("${pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  cd "${srcdir}/${pkgname}"
  sed -i 's|python2.7b|python2|' goatrider.py
}

package() {
  cd "${srcdir}/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"

  install -Dm755 goatrider.py "${pkgdir}/usr/bin/goatrider"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}"

}
