# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

_pkgname="pydeep"
pkgbase="python-${_pkgname}"
pkgname=("python-${_pkgname}" "python2-${_pkgname}")
pkgver=0.4
pkgrel=4
pkgdesc='This is the Python interface to ssdeep'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://pypi.org/project/pydeep/'
license=('custom:PSF')
depends=('python' 'python2' 'ssdeep')
makedepends=('python-setuptools' 'python2-setuptools' 'ssdeep')
source=("https://files.pythonhosted.org/packages/60/7b/7fa895d023dc11385625311f81f1e2da6a9f3b10bcf6c0bca6113710e558/${_pkgname}-${pkgver}.tar.gz"
        'LICENSE')
sha512sums=('f924eb61dfba173704b7d8345bf14a8a01a78229b2733309861869f69cd1807325512ada440c7d7ff247fe2ceeca3ac3df2203cb55a5671b658734cb550146f1'
            '88330e6f805b5e8522f6da8610816c58d8d147d9fe6825819a70147e6ef1966d67439d0f695e3212bdf28d00385755e20184baffd981c2a0ae77feb71c18a6fe')

prepare(){
  cp -a ${_pkgname}-${pkgver}{,-py2}
}

package_python-pydeep() {
  depends=('python' 'ssdeep')

  cd "${srcdir}/${_pkgname}-${pkgver}" || exit
  python setup.py install --root="${pkgdir}" -O1
  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pydeep() {
  depends=('python2' 'ssdeep')

  cd "${srcdir}/${_pkgname}-${pkgver}-py2" || exit
  python2 setup.py install --root="${pkgdir}" -O1
  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
