# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=python2-pypdf2
pkgver=1.25.1
pkgrel=2
pkgdesc='PDF toolkit'
url='http://mstamy2.github.com/PyPDF2'
depends=('python2')
makedepends=('python2-setuptools')
license=('MIT')
arch=('any')
source=("https://pypi.python.org/packages/source/P/PyPDF2/PyPDF2-$pkgver.tar.gz")
sha512sums=('0cb43d4557d17ec82298ceabce5c4731438a0e16ad6c6b2abc54204a3d93373a46b86c995e9b39cec475de1dce325d552da17ce36de3733c7b0471779e3b0899')

prepare() {
  cd PyPDF2-$pkgver
  grep -iRl 'python' | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

build() {
  cd PyPDF2-$pkgver
  python2 setup.py build
}

package() {
  cd PyPDF2-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
