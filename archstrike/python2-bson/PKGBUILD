# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=python2-bson
_pkgname=bson
pkgver=0.4.6
pkgrel=4
pkgdesc="BSON codec for Python"
url="https://github.com/py-bson/bson"
depends=('python2' 'python2-pytz' 'python2-pymongo')
makedepends=('python2-setuptools' 'python2-pip')
license=('BSD')
arch=('any')
source=("${url}/archive/${pkgver}.tar.gz")
sha512sums=('98765170637b40630dd669436a3943061db6fc18bd102645dff9eda7e5fc825469a2b6ea2195c90995b6eb79839e8060cdf31750443341e2f943781f2c74d21a')

prepare() {
  grep -iRl 'python' ${_pkgname}-${pkgver} | xargs sed -i 's|python$|python2|g'
}

build() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py build
}

package() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  #fix conflict with python2-pymongo
  rm -f ${pkgdir}/usr/lib/python2.7/site-packages/bson/__init__*
}
