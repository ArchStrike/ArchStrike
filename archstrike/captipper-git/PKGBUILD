# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=captipper-git
pkgver=20201002.r74
pkgrel=1
pkgdesc="A python-based HTTP traffic explorer tool used to analyze, explore and revive HTTP malicious traffic"
arch=('any')
url="https://github.com/omriher/captipper"
groups=('archstrike' 'archstrike-analysis')
license=('Apache')
depends=('python2')
makedepends=('git')
provides=('captipper')
source=("${pkgname}::git+https://github.com/omriher/captipper.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}" || exit
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare(){
  grep -iRl 'python' "${srcdir}/${pkgname}" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "${pkgname}" || exit

  install -dm755 "${pkgdir}/usr/share/captipper/"
  install -dm755 "${pkgdir}/usr/bin/"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  cp -a --no-preserve=ownership ./* "${pkgdir}/usr/share/captipper/"

cat > "${pkgdir}/usr/bin/captipper" <<EOF
#!/bin/sh
cd /usr/share/captipper
python2 CapTipper.py "\$@"
EOF
chmod +x "${pkgdir}/usr/bin/captipper"
}
