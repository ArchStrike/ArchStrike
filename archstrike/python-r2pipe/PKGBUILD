# Maintainer: ArchStrike <team@archstrike.org>

_pkgname=r2pipe
pkgbase=python-$_pkgname

buildarch=1

pkgname=(python-$_pkgname python2-$_pkgname)
pkgver=0.8.9
pkgrel=1
pkgdesc="A Pipe interface for radare2"
url="https://pypi.python.org/pypi/r2pipe/"
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/ea/33/6564663d3f8af41213033cc760fb3823315c1ec38aa6dab877000ddddeff/r2pipe-${pkgver}.tar.gz")
sha512sums=('5718c2fa55050725d68460a2f2ef801de30112e77d565eceea9ffd73fef7cfe10994bd9be0fe8db7c894f731dbce56613679e9a5bef73b6bdee4f2e4976dbd44')

prepare() {
  cp -a $_pkgname-$pkgver{,-py2}
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  python3 setup.py build

  cd "$srcdir/$_pkgname-$pkgver-py2"
  python2 setup.py build
}

package_python-r2pipe() {
  depends=('python')

  cd "$srcdir/$_pkgname-$pkgver"
  python3 setup.py install --root="$pkgdir/" -O1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-r2pipe() {
  depends=('python2')

  cd "$srcdir/$_pkgname-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" -O1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  sed -i 's|env python|env python2|' $pkgdir/usr/lib/python2.7/site-packages/r2pipe/__init__.py
}

