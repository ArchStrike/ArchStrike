# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=python2-bowcaster-git
pkgver=20150429.r172
pkgrel=1
groups=('archstrike')
pkgdesc="An open-source forensic framework written in Python/GTK that manages cases and case items, providing an abstract interface for developing extensions."
arch=('any')
url='https://github.com/zcutlip/bowcaster'
license=('GPL2')
depends=('python2')
makedepends=('git' 'python2-setuptools')
provides=('python2-bowcaster')
replaces=('python2-bowcaster')
conflicts=('python2-bowcaster')
source=("$pkgname::git+https://github.com/zcutlip/bowcaster.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare(){
  grep -iRl 'python' "$pkgname" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$pkgname"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
