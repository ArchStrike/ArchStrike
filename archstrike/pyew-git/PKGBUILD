# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=pyew-git
pkgver=20190906.r109
pkgrel=1
groups=('archstrike' 'archstrike-malware')
pkgdesc="a (command line) python tool to analyse malware."
arch=('any')
url="https://github.com/joxeankoret/pyew"
license=('GPL2')
depends=('distorm' 'python2' 'java-environment' 'python2-capstone' 'python2-setuptools')
makedepends=('git')
conflicts=('pyew')
provides=('pyew')
source=("${pkgname}::git+https://github.com/joxeankoret/pyew.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci VERSION_3X | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare(){
  grep -iRl 'python' ${pkgname} | xargs sed -i 's|python$|python2|'
  grep -iRl 'python' ${pkgname} | xargs sed -i 's|python2.4|python2|'
}

package(){
  cd $pkgname
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"

  cp -a --no-preserve=ownership * "${pkgdir}/usr/share/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  cd ${pkgdir}/usr/share/${pkgname}
  rm LICENSE COPYING

cat > "${pkgdir}/usr/bin/pyew" <<EOF
#!/usr/bin/env bash
cd /usr/share/${pkgname}
./pyew "\$@"
EOF
chmod 755 "${pkgdir}/usr/bin/pyew"
}
