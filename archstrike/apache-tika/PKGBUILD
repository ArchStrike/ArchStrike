# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Filip Gralinski <filipg@amu.edu.pl>

buildarch=1

pkgname=apache-tika
pkgver=1.23
pkgrel=1
pkgdesc='Toolkit for detecting and extracting metadata and structured text content'
arch=('any')
license=('Apache')
url='https://tika.apache.org'
depends=('java-environment' 'bash')
noextract=("tika-server-${pkgver}.jar")
source=(
  "http://us.mirrors.quenda.co/apache/tika/tika-server-${pkgver}.jar"
  "${pkgname}.service"
)

sha512sums=('4e1b8418aed496eb1ee5bf32921580e855672ab701bffdd642261ac27ae011913f36fdd70b725adef4dec55a53fa04cf0f3b8a3b45e15026dd76c8d312b62291'
            'e46522bfcc86ada8776c4f12cf645a81c34492b1c86e9bd1258d7fa248fb8fed9c2bd57e5971c5c9a4687f35626f872f63920649279cfaba5498a55c3331bcf5')

package() {
  install -dm755 "${pkgdir}/usr/bin"
  install -Dm755 "tika-server-${pkgver}.jar" "${pkgdir}/usr/share/java/${pkgname}/tika-server-${pkgver}.jar"
  install -Dm644 "${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"

cat > "${pkgdir}/usr/bin/tika" <<EOF
#!/usr/bin/env bash
java -jar "/usr/share/java/${pkgname}/tika-server-${pkgver}.jar" "\$@"
EOF

  chmod 755 "${pkgdir}/usr/bin/tika"
}
