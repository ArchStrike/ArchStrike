# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=fpdns
pkgver=20130404
pkgrel=2
pkgdesc="Program that remotely determines DNS server versions"
url="https://github.com/kirei/fpdns"
depends=('perl')
license=('BSD')
options=('!emptydirs')
arch=('any')
groups=('archstrike' 'archstrike-analysis')
source=("https://github.com/kirei/fpdns/archive/$pkgver.tar.gz")
sha512sums=('1a4d9c1cb666efa3f48dcca2b5cd04793e92727359652095572b17479f5343479c57d2937f1fff8070c071a2698eda5c4dff981caf86436f184f0b076b0fa0b3')

build() {
  cd "$pkgname-$pkgver"

  eval $(perl -V:archname)

  /usr/bin/perl Makefile.PL \
    INSTALLARCHLIB=/usr/lib/perl5/current/$archname \
    INSTALLSITELIB=/usr/lib/perl5/site_perl/ \
    INSTALLSITEARCH=/usr/lib/perl5/site_perl/$archname

  /usr/bin/make
}

package() {
  cd "$pkgname-$pkgver"
  make pure_install DESTDIR="$pkgdir"
  mv $pkgdir/usr/bin/site_perl/$pkgname $pkgdir/usr/bin/$pkgname
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete

  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
