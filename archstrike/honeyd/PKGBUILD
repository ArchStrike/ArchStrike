# Maintainer: ArchStrike <team@archstrike.org>
# TODO: check license

buildarch=212

pkgname=honeyd
pkgver=1.6.7
pkgrel=3
groups=('archstrike')
pkgdesc="A small daemon that creates virtual hosts on a network."
url="https://github.com/DataSoft/Honeyd/"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=("GPL")
depends=('libdnet' 'libevent' 'libedit' 'libpcap' 'pcre' 'zlib' 'tcl' 'python2')
options=('!makeflags')
source=("https://github.com/DataSoft/Honeyd/archive/honeyd-$pkgver.tar.gz"
        "python.patch")
sha512sums=('293b306a06203fff37fc4bdd35a070dd04379c6a66ad3a34813b7c8e248ba005d8509c1ae62b96a197ea7d8864bf4d5de09e26465fb299da916d0d1bd421d8ef'
            '8e85f3e386cd52ee70496d828d2b98ce116b2be192090f1c57c5b9c9cba6d97e22979c25a6699d51be507875fc5aa29d5d81197fd6c0bfdb78ee9c9420c8ddb8')

build() {
  cd "$srcdir/Honeyd-honeyd-$pkgver"

  ./autogen.sh
  ./configure --prefix=/usr
  patch -p1 --ignore-whitespace -i ../python.patch
  make

  sed -i 's/python/python2/' Makefile
}

package() {
  cd "$srcdir/Honeyd-honeyd-$pkgver"

  # Base directories
  install -dm755 "$pkgdir/usr/share/honeyd"

  # Bin.
  make "prefix=$pkgdir/usr" install
  find "${pkgdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|'
  # Fix permissions on webserver dirs
  for dir in webserver webserver/htdocs webserver/htdocs/graphs \
             webserver/htdocs/images webserver/htdocs/styles \
             webserver/htdocs/templates webserver/htdocs/templates/inc ; do
      chmod 755 "$pkgdir/usr/share/honeyd/$dir"
  done
}

