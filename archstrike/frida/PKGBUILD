# Maintainer: ArchStrike <team[at]archstrike[dot]org>

buildarch=192

pkgname=frida
pkgver=15.0.17
pkgrel=1
pkgdesc="Inject JavaScript to explore native apps on Windows, Mac, Linux, iOS and Android"
url="http://www.frida.re"
depends=('python-colorama' 'python-pygments' 'python-prompt_toolkit')
makedepends=('python-setuptools')
license=('custom')
arch=('i686' 'x86_64')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "https://raw.githubusercontent.com/frida/frida/master/COPYING")
sha512sums=('338eb2231e19f21d07ddb8ac49029cdcfe897b84e9cea269d32c3eb7925727dd24838891e5936b1b34e9d07678627e106fe5911e2fa79955a816c3b803bbd300'
            'e36c8d5a0538fa4bb3575a60322057f7a687aab04c13deed25399462ba0be74ce6071cc41d967e23b9a96e552462d210280427a945c6cc956919b062878efc9a')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python setup.py build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}" -O1 --skip-build
    install -Dm644 ${srcdir}/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
