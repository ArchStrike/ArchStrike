# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=chisel
pkgver=1.7.1
pkgrel=1
groups=('archstrike' 'archstrike-networking' 'archstrike-tunnel')
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
pkgdesc="A fast TCP tunnel over HTTP"
url="https://github.com/jpillora/chisel"
license=('MIT')
depends+=('glibc')
makedepends=('go' 'git')
source=("${url}/archive/v${pkgver}.tar.gz"
        "LICENSE")
sha512sums=('72a627d038abcc3ca2dee616338ac537a93800e27498c1b2249a07adebc92b7ef7250f1b8cbcf668b8d837d7c4b035c700f3575e72e03f5d5ad08546bc4c9c40'
            '4450bb1b51fc9660d861e959139aced1756a6879a53ed6ed8ac836a57475eb65640498c73a7a9cd76e30f97e66484568713a549de07caac40b111b7c72193280')

prepare() {
  export GOPATH="${srcdir}"
  go get -d -u github.com/jpillora/chisel
}

build() {
  cd "${pkgname}-${pkgver}"
  export GOPATH="${srcdir}"
  go build -ldflags "-X github.com/jpillora/${pkgname}/share.BuildVersion=${pkgver}"
}

package() {
  cd "${pkgname}-${pkgver}"

  # license
  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # binary
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # example
  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 "example/users.json" "${pkgdir}/usr/share/doc/${pkgname}/examples/users.json"
}
