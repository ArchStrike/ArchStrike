# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=golismero-git
pkgver=20160511.r40
pkgrel=1
groups=('archstrike' 'archstrike-webapps')
pkgdesc="Opensource web security testing framework"
url="https://github.com/golismero/golismero"
license=('custom')
arch=('any')
depends=('python2' 'python2-beautifulsoup3' 'python2-colorizer'
         'python2-decorator' 'python2-django>=1.5' 'python2-dnspython'
         'python2-netaddr' 'python2-nltk' 'python2-cjson' 'python2-requests'
         'python2-requests_ntlm' 'python2-texttable' 'python2-tldextract')
makedepends=('git')
optdepends=('sslscan: for scanning ssl certificates'
            'spiderfoot: for fingerprinting'
            'openvas-server: for openvas integration')
options=('!strip')
source=("${pkgname}::git+https://github.com/golismero/golismero.git")
replaces=('golismero')
provides=('golismero')
conflicts=('golismero')
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

package() {
  cd "$pkgname"

  install -dm755 "$pkgdir/usr/share/golismero"
  install -dm755 "$pkgdir/usr/bin"

  cp -a * "$pkgdir/usr/share/golismero"

  cat > "$pkgdir/usr/bin/golismero" <<EOF
#!/bin/sh
cd /usr/share/golismero
python2 golismero.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/golismero"
}
