# Maintainer: ArchStrike <team archstrike org>

buildarch=192

pkgname=pwntools
pkgver=4.7.0
pkgrel=1
pkgdesc='CTF framework and exploit development library'
arch=('i686' 'x86_64')
url='https://github.com/Gallopsled/pwntools'
license=('MIT')
groups=('archstrike' 'archstrike-exploit' 'archstrike-misc')
depends_x86_64+=('lib32-glibc')
depends=('python-dateutil' 'python-paramiko' 'python-mako' 'python-pyelftools' 'capstone' 'ropgadget' 'python-pyserial' 
         'python-requests' 'python-pygments' 'python-pysocks' 'python-tox' 'python-pip' 'python-psutil' 'python-unicorn'
         'python-intervaltree' 'python-pypandoc' 'python-sortedcontainers' 'python-colored-traceback' 'python-rpyc')
makedepends=('python-setuptools')
replaces=('binjitsu-git')
provides=('binjitsu-git')
source=("https://github.com/Gallopsled/pwntools/archive/${pkgver}.tar.gz")
sha512sums=('3ee621eb482cec33d4d633073cd623ba8455fa7a2d31a7b339da403fac22b2210cb8be4803e7b5296f51303bf727d80f692dde5a70ea6fb310fc80153868db7a')

build(){
    cd "${srcdir}/${pkgname}-${pkgver}"
    python setup.py build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}/" -O1 --skip-build

    # install license
    install -Dm644 LICENSE-pwntools.txt "${pkgdir}/usr/share/licenses/${pkgname}/license-pwntools.txt"

    # fix conflict bins
    mv "${pkgdir}/usr/bin/checksec" "${pkgdir}/usr/bin/pwntools-checksec"
    mv "${pkgdir}/usr/bin/disasm" "${pkgdir}/usr/bin/pwntools-disasm"
    mv "${pkgdir}/usr/bin/errno" "${pkgdir}/usr/bin/pwntools-errno"

}
