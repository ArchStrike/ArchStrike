# Maintainer: ArchStrike <team archstrike org>

buildarch=192

pkgname=pwntools
pkgver=4.4.0
pkgrel=1
pkgdesc='CTF framework and exploit development library'
arch=('i686' 'x86_64')
url='https://github.com/Gallopsled/pwntools'
license=('MIT')
groups=('archstrike' 'archstrike-exploit' 'archstrike-misc')
depends_x86_64+=('lib32-glibc')
depends=('python-dateutil' 'python-paramiko' 'python-mako' 'python-pyelftools' 'capstone' 'ropgadget' 'python-pyserial' 
         'python-requests' 'python-pygments' 'python-pysocks' 'python-tox' 'python-pip' 'python-psutil' 'python-unicorn'
         'python-intervaltree' 'python-pypandoc' 'python-sortedcontainers')
makedepends=('python-setuptools')
replaces=('binjitsu-git')
provides=('binjitsu-git')
source=("https://github.com/Gallopsled/pwntools/archive/${pkgver}.tar.gz")
sha512sums=('950b366e3daedb615449a5f48a0246fad2ae582c4c98059447b73c389e1c1bac72eb40388a458034e6ebed851b9c41f64723fd895028bd5b6bcc18e6c639e1c6')

build(){
    cd "${srcdir}/${pkgname}-${pkgver}"
    python setup.py build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}/" -O1 --skip-build

    # install license
    install -Dm644 LICENSE-pwntools.txt "${pkgdir}/usr/share/licenses/${pkgname}/license-pwntools.txt"

    # fix conflict bins
    mv "${pkgdir}/usr/bin/checksec" "${pkgdir}/usr/bin/pwntools-checksec"
    mv "${pkgdir}/usr/bin/disasm" "${pkgdir}/usr/bin/pwntools-disasm"
    mv "${pkgdir}/usr/bin/errno" "${pkgdir}/usr/bin/pwntools-errno"

}
