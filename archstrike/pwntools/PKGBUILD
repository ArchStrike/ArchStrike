# Maintainer: ArchStrike <team archstrike org>

buildarch=1

pkgname=pwntools
pkgver=3.1.0
pkgrel=1
pkgdesc='CTF framework and exploit development library'
arch=('any')
url='https://github.com/Gallopsled/pwntools'
license=('MIT')
groups=('archstrike' 'archstrike-exploits' 'archstrike-misc')
depends_x86_64+=('lib32-glibc')
depends=('python2-dateutil' 'python2-paramiko' 'python2-mako' 'python2-pyelftools' 'capstone' 'ropgadget' 'python2-pyserial' 
         'python2-requests' 'python2-pygments' 'python2-pysocks' 'python2-tox' 'python2-pip' 'python2-psutil')
makedepends=('python2-setuptools')
replaces=('binjitsu-git')
provides=('binjitsu-git')
source=("https://github.com/Gallopsled/pwntools/archive/$pkgver.tar.gz")
sha512sums=('394e6b9b4de05cd77dd98fa28e418789f3a7ba2569ade27ff12f36aff5d7692427dcc085cb1538f8f95f3d6c43cafd50fdba658ae10d5aaee74689b1bef67f5e')

prepare() {
  cd "$pkgname-$pkgver"
  sed -i 's|python|python2|' pwnlib/elf/datatypes.py
}

build(){
    cd "$pkgname-$pkgver"
    python2 setup.py build
}

package() {
    cd "$pkgname-$pkgver"
    python2 setup.py install --root="$pkgdir/" --optimize=1

    # install license
    install -Dm644 LICENSE-pwntools.txt "$pkgdir/usr/share/licenses/$pkgname/license-pwntools.txt"
   
    # fix conflict bins
    mv "$pkgdir/usr/bin/checksec" "$pkgdir/usr/bin/pwntools-checksec"
    mv "$pkgdir/usr/bin/disasm" "$pkgdir/usr/bin/pwntools-disasm"
    mv "$pkgdir/usr/bin/errno" "$pkgdir/usr/bin/pwntools-errno"

} 
