# Maintainer: ArchStrike <team archstrike org>

buildarch=196

pkgname=pwntools
pkgver=4.1.4
pkgrel=1
pkgdesc='CTF framework and exploit development library'
arch=('armv7' 'i686' 'x86_64')
url='https://github.com/Gallopsled/pwntools'
license=('MIT')
groups=('archstrike' 'archstrike-exploit' 'archstrike-misc')
depends_x86_64+=('lib32-glibc')
depends=('python-dateutil' 'python-paramiko' 'python-mako' 'python-pyelftools' 'capstone' 'ropgadget' 'python-pyserial' 
         'python-requests' 'python-pygments' 'python-pysocks' 'python-tox' 'python-pip' 'python-psutil' 'python-unicorn'
         'python-intervaltree' 'python-pypandoc' 'python-sortedcontainers')
makedepends=('python-setuptools')
replaces=('binjitsu-git')
provides=('binjitsu-git')
source=("https://github.com/Gallopsled/pwntools/archive/$pkgver.tar.gz")
sha512sums=('71b6432e9ae4e9b04c946a7d38d13401d6461abae1ac25f06cad21380b68a3acbaa5d95eea809415b1696ae74e75fc320a125f6c7437d39259f7adc76c8bd865')

build(){
    cd "$pkgname-$pkgver"
    python setup.py build
}

package() {
    cd "$pkgname-$pkgver"
    python setup.py install --root="$pkgdir/" -O1 --skip-build

    # install license
    install -Dm644 LICENSE-pwntools.txt "$pkgdir/usr/share/licenses/$pkgname/license-pwntools.txt"
   
    # fix conflict bins
    mv "$pkgdir/usr/bin/checksec" "$pkgdir/usr/bin/pwntools-checksec"
    mv "$pkgdir/usr/bin/disasm" "$pkgdir/usr/bin/pwntools-disasm"
    mv "$pkgdir/usr/bin/errno" "$pkgdir/usr/bin/pwntools-errno"

} 
