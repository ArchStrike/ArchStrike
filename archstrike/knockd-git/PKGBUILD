# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Michael Goehler <somebody.here@gmx.de>
# Contributor: Mike Sampson <mike at sambodata dot com>
# Contriburor: Steven <steven at stebalien dot com>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

buildarch=220

pkgname=knockd-git
_pkgname=knock
pkgver=r92.c54d3cc
pkgrel=1
pkgdesc="A simple port-knocking daemon and client"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://www.zeroflux.org/projects/knock"
license=(GPL)
groups=('archstrike')
depends=('libpcap')
conflicts=('knockd')
makedepends=(git)
backup=('etc/knockd.conf')
source=('knock::git+https://github.com/jvinet/knock.git'
        'knockd.logrotate'
        'knockd.service')
sha512sums=('SKIP'
            '3ea2ae9392d3d79bb5df6b75090f6c1a9548979e01dcd4a75471cfa46dbc70401a166496b5158f90fc61326a554acacc38f8605aab02b5b53b75d60a7d80648a'
            '6e5fec91e07fb74169798f320607bd413258af0af48bd2a9c006a02bc97ce4ea9a5293eb9e166b9870c086bf147fca1462b98b3d914d7a28708e9fa2cc9c85cb')

pkgver() {
    cd "$srcdir/${_pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/${_pkgname}"
    autoreconf -fi
    ./configure --prefix=/usr \
                --sbin=/usr/bin \
                --sysconfdir=/etc
    make
}

package() {
  cd "$srcdir/${_pkgname}"
  make DESTDIR="$pkgdir" MANDIR=/usr/share/man install
  install -Dm755 "$srcdir/knockd.service" "$pkgdir/usr/lib/systemd/system/knockd.service"
  install -Dm644 "$srcdir/knockd.logrotate" "$pkgdir/etc/logrotate.d/knockd"
}

# vim:set ts=4 sw=4 et:
