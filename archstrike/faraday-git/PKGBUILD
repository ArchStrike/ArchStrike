# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=faraday-git
pkgver=20170207.r3520
pkgrel=2
pkgdesc="A new concept (IPE) Integrated Penetration-Test Environment a multiuser Penetration test IDE. Designed for distribution, indexation and analyze of the generated data during the process of a security audit."
groups=('archstrike' 'archstrike-misc')
url="http://www.faradaysec.com/"
depends_x86_64+=('zsh' 'java-environment' 'python2-pip' 'python2-argparse' 'python2-flask' 'python2-tornado' 'python2-dateutil'  'python2-colorama' 'couchdb' 'python2-pysvn' 'python2-ipy' 'libpqxx' 'python2-couchdbkit' 'python2-requests' 'python2-whoosh' 'python2-psycopg2' 'python2-restkit' 'python2-mockito' 'lib32-glibc' 'ruby' 'python2-ipy' 'python2-sqlalchemy' 'python2-service-identity' 'python2-twisted' 'gobject-introspection' 'gobject-introspection-runtime')
depends_i686+=('zsh' 'java-environment' 'python2-pip' 'python2-argparse' 'python2-flask' 'python2-tornado' 'python2-dateutil'  'python2-colorama' 'couchdb' 'python2-pysvn' 'python2-ipy' 'libpqxx' 'python2-couchdbkit' 'python2-requests' 'python2-whoosh' 'python2-psycopg2' 'python2-restkit' 'python2-mockito' 'ruby' 'python2-ipy' 'python2-sqlalchemy' 'python2-service-identity' 'python2-twisted' 'gobject-introspection' 'gobject-introspection-runtime')
depends_armv6h+=('zsh' 'java-environment' 'python2-pip' 'python2-argparse' 'python2-flask' 'python2-tornado' 'python2-dateutil'  'python2-colorama' 'couchdb' 'python2-pysvn' 'python2-ipy' 'libpqxx' 'python2-couchdbkit' 'python2-requests' 'python2-whoosh' 'python2-psycopg2' 'python2-restkit' 'python2-mockito' 'ruby' 'python2-ipy' 'python2-sqlalchemy' 'python2-service-identity' 'python2-twisted' 'gobject-introspection' 'gobject-introspection-runtime')
depends_armv7h+=('zsh' 'java-environment' 'python2-pip' 'python2-argparse' 'python2-flask' 'python2-tornado' 'python2-dateutil'  'python2-colorama' 'couchdb' 'python2-pysvn' 'python2-ipy' 'libpqxx' 'python2-couchdbkit' 'python2-requests' 'python2-whoosh' 'python2-psycopg2' 'python2-restkit' 'python2-mockito' 'ruby' 'python2-ipy' 'python2-sqlalchemy' 'python2-service-identity' 'python2-twisted' 'gobject-introspection' 'gobject-introspection-runtime')
makedepends=('git')
options=('!strip')
license=('GPL')
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
source=("${pkgname}::git+https://github.com/infobyte/faraday.git")
replaces=('faraday')
conflicts=('faraday')
provides=('faraday')
install="${pkgname}.install"
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  grep -iRl 'python' "${pkgname}" | xargs sed -i 's|python$|python2|g'
}

package() {
  cd "${pkgname}"
  install -dm755 "$pkgdir/usr/share/$pkgname"
  install -dm755 "$pkgdir/usr/bin"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname"

cat > "$pkgdir/usr/bin/faraday" << EOF
#!/usr/bin/env bash
cd /usr/share/faraday-git/
python2 faraday.py "\$@"
EOF

cat > "$pkgdir/usr/bin/faraday-server" <<EOF
#!/usr/bin/env bash
cd /usr/share/faraday-git/
python2 faraday-server.py "\$@"
EOF

for i in faraday faraday-server; do chmod 755 $pkgdir/usr/bin/$i; done
}

