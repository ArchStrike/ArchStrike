# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=pdfminer3k
pkgver=1.3.0
pkgrel=1
arch=('any')
pkgdesc='A python3 port of pdfminer'
license=('MIT')
url='https://pypi.python.org/pypi/pdfminer3k'
depends=('python')
makedepends=('python-setuptools')
provides=('python-pdfminer')
replaces=('python-pdfminer')
source=("https://pypi.python.org/packages/d4/80/c0c837cfd85dd0b684d4dd140516fc3b4058b3dad72aa590bc1e42e5d458/pdfminer3k-$pkgver.tar.gz" "LICENSE")
sha512sums=('4a1113d601a321606fb39b3d1a13c6fbac623d2dbf4b47e9cda284622900ce9b0232a5c43d268bba2bfacc4ee7b92566b17268726002b238cb742d9ca30fec8c'
            'f39753423638ab131ac193675e9280163769fe1102315f5c810c5bb58e255fc5673f0c1fbb96f0b153151cb8fc2763d38615a465f4c0e5fc121c0103cf97ab5c')

prepare() {
  grep -iRl 'python' ${pkgname}-${pkgver} | xargs sed -i 's|python2|python|'
}

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  python setup.py install --root="$pkgdir" --optimize=1
  for i in dumppdf latin2ascii pdf2txt; do mv "${pkgdir}/usr/bin/${i}.py" "${pkgdir}/usr/bin/${i}3k.py";done
  install -Dm644 "${srcdir}/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
