# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=gef-git
_pkgname=gef
pkgver=r352.c8de096
pkgrel=1
pkgdesc="Provides additional features to GDB using the Python API to assist during the process of dynamic analysis or exploit development."
url='https://github.com/hugsy/gef'
groups=('archstrike' 'archstrike-exploit')
arch=('any')
license=('MIT')
depends=('python2' 'gdb' 'capstone' 'ropgadget' 'radare2')
makedepends=('git')
install='gef-git.install'
source=("${pkgname}::git+https://github.com/hugsy/gef.git")
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' "$pkgname" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$pkgname"
  install -dm755 "$pkgdir/usr/share/$pkgname"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"
}
