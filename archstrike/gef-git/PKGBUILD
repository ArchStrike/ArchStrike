# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=gef
pkgname=${_pkgname}-git
pkgver=r1746.5502d7b
pkgrel=1
pkgdesc='Provides additional features to GDB using the Python API to assist during the process of dynamic analysis or exploit development'
url='https://github.com/hugsy/gef'
groups=('archstrike' 'archstrike-exploit')
arch=('any')
license=('MIT')
depends=('python' 'gdb' 'python-capstone' 'ropper' 'python-keystone' 'unicorn' 'python-ropper'
         'python-pylint' 'keystone' 'python-unicorn')
makedepends=('git')
install="${pkgname}.install"
source=("${pkgname}::git+https://github.com/hugsy/gef.git")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' ${pkgname} | xargs sed -i 's|python$|python2|g'
}

package() {
  cd ${pkgname}
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/$_pkgname"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  cp -a --no-preserve=ownership * "${pkgdir}/usr/share/$_pkgname/"

cat > "${pkgdir}/usr/bin/${_pkgname}-setup" <<EOF
#!/usr/bin/env bash

install -Dm644 /usr/share/${_pkgname}/${_pkgname}.py "\$HOME/.gdbinit-gef.py"
printf '%s\n' "Installed $_pkgname to \$HOME/.gdbinit-gef.py"

[[ -f "\$HOME/.gdbinit" ]] && {
  mv "\$HOME/.gdbinit" "\$HOME/.gdbinit.old"
  printf '%s\n' "Moved \$HOME/.gdbinit to \$HOME/.gdbinit.old"
}

printf '%s\n' "source \$HOME/.gdbinit-gef.py" > "\$HOME/.gdbinit"
printf '%s\n' "Created \$HOME/.gdbinit"
EOF

  chmod 755 "${pkgdir}/usr/bin/${_pkgname}-setup"
}
