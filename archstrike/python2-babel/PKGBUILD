# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname="python2-babel"
pkgver=2.8.0
_core=36
pkgrel=1
pkgdesc="A collection of tools for internationalizing Python applications"
url="http://babel.pocoo.org/"
license=("BSD")
arch=('any')
makedepends=('python2' 'python2-setuptools' 'python2-pytz')
noextract=("cldr-core-$_core.zip")
source=("https://github.com/python-babel/babel/archive/v${pkgver}.tar.gz"
        "cldr-core-$_core.zip::http://unicode.org/Public/cldr/$_core/core.zip")
sha512sums=('e4ad388dde6e388d8ac0772f1931724fd37a371f440595aeac0a2809f4753387775774ecc7c402fb3636e1765fe1297e3629e7a9162506d8f433ada06795b181'
            '773aa51fe86485a92208ba0ce348761cc31f800f2d5d559799c4a9a2069086ef7796bca3936229d4263e56acee749a29eb6f535e35a68c73436a237c6d1b163e')

prepare() {
  cp "${srcdir}/cldr-core-$_core.zip" "babel-${pkgver}/cldr/cldr-core-$_core.zip"
}

build() {
  cd "${srcdir}/babel-${pkgver}"
  python2 setup.py import_cldr
  python2 setup.py build
}

package() {
  cd "${srcdir}/babel-${pkgver}"
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  mv "${pkgdir}/usr/bin/pybabel" "${pkgdir}/usr/bin/pybabel2"
  install -D -m0644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
