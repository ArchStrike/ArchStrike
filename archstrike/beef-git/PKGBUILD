# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

_pkgname=beef
pkgname=${_pkgname}-git
pkgver=20191218.r3403
pkgrel=1
epoch=1
groups=('archstrike' 'archstrike-exploit')
pkgdesc='Pentesting framework that focuses on web-browser exploitation'
arch=('i686' 'x86_64')
url='http://beefproject.com'
license=('Apache')
options=('!emptydirs')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
depends=('ruby' 'ruby-rdoc' 'ruby-bundler' 'java-environment' 'python' 'imagemagick' 'nodejs' 'sqlite' 'libtool' 'openssl' 'gcc' 'readline' 'zlib' 'libyaml' 'bzip2' 'autoconf' 'automake' 'bison')
makedepends=('git')
source=("${pkgname}::git+http://github.com/beefproject/beef.git")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

package() {
  cd ${pkgname}

  # Install the package files
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/${_pkgname}"
  cp -a --no-preserve=ownership * "${pkgdir}/usr/share/${_pkgname}"
  cd "${pkgdir}/usr/share/${_pkgname}"
  bundle install --path vendor/bundle --without test development

cat > "${pkgdir}/usr/bin/${_pkgname}" << EOF
#!/usr/bin/env bash
cd /usr/share/${_pkgname}
unset GEM_HOME
/usr/bin/ruby ./${_pkgname} "\$@"
EOF
chmod 0755 "${pkgdir}/usr/bin/${_pkgname}"
}
