# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

_pkgname=beef
pkgname=${_pkgname}-git
pkgver=20200803.r3895
pkgrel=1
epoch=1
groups=('archstrike' 'archstrike-exploit')
pkgdesc='Pentesting framework that focuses on web-browser exploitation'
arch=('i686' 'x86_64')
url='https://github.com/beefproject/beef'
license=('Apache')
options=('!emptydirs')
depends=('ruby' 'ruby-rdoc' 'ruby-bundler' 'java-environment' 'python' 'imagemagick'
         'nodejs' 'sqlite' 'libtool' 'openssl' 'gcc' 'readline' 'zlib' 'libyaml' 'bzip2'
         'autoconf' 'automake' 'bison')
makedepends=('git')
source=("${pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

# Ready for upcoming Bundler changes
prepare() {
  cd "${srcdir}/${pkgname}"
  sed -i '3d' .bundle/config
  sed -i '2 a\BUNDLE_PATH: \"vendor\/bundle\"' .bundle/config
  sed -i 's|development|development:test|' .bundle/config
}

package() {
  cd "${srcdir}/${pkgname}"

  # Install the package files
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/${_pkgname}"
  cp -a --no-preserve=ownership ./* "${pkgdir}/usr/share/${_pkgname}"
  cd "${pkgdir}/usr/share/${_pkgname}"
  # --path deprecated. However, libfakeroot errors without it
  bundle install --path vendor/bundle

cat > "${pkgdir}/usr/bin/${_pkgname}" << EOF
#!/usr/bin/env bash
cd "/usr/share/${_pkgname}"
unset GEM_HOME
/usr/bin/ruby ./"${_pkgname}" "\$@"
EOF
chmod 0755 "${pkgdir}/usr/bin/${_pkgname}"
}
