# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=3proxy
pkgver=0.9.0
pkgrel=1
groups=('archstrike' 'archstrike-misc')
pkgdesc='Tiny free proxy server'
url='http://3proxy.ru'
license=('GPL')
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
depends=('glibc' 'openssl')
makedepends=('make')
backup=('etc/3proxy/3proxy.cfg')
source=("https://github.com/z3APA3A/3proxy/archive/${pkgver}.tar.gz"
        "3proxy.service" "tmpfiles.conf" "sysusers.conf")
sha512sums=('7429389bf70c38d57e7a6f34e649a04aaad4f81555b7ef131cae026123ff7877d3f1aef8d10f84b9fc2c58e9ad5ab3d55e4af7326dbce14c05f3cd9973fafd83'
            '3010f5626148459ba7cecc0929fb47b801a1aa70d3df02a7ddd46a147a7a5e811330d9bcaf5ea092c2f0ef7663c8d4d44d67236e9224362850f72a95817f5936'
            '5ef573d9e0e763bab5b67e9735843e2c7d931000b967aeac332f3cbb2e079c9ba282b1aa6d1adf5e23af6e97675ea83192d0c81734aa23743638e556f52e006f'
            '0b98afd23e04f3a8fd249821b9a45bc8b6ad7f62d8b0119ed0cc7e8135675d33d39be7d18474330bf4e898b6d27a6e810f78ddf00163007e45bfa30c4bffbe2a')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed --follow-symlinks -i -e 's| -O2||g;s|CFLAGS = -g|CFLAGS =|;s|CC = gcc|CC ?= gcc|;' \
                           -e 's|LN = gcc|LN ?= gcc|;137,$d' Makefile.Linux
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix=/usr DESTDIR="$pkgdir" ETCDIR=/etc/3proxy INITDIR=/etc/init.d BINDIR=/usr/bin -f Makefile.Linux 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix=/usr install DESTDIR="$pkgdir" ETCDIR="$pkgdir/etc/3proxy" \
       INITDIR="$pkgdir/etc/init.d" RUNBASE="$pkgdir/run" LOGBASE="$pkgdir/var/log" \
       -f Makefile.Linux

  mkdir "$pkgdir/usr/lib"
  mv "$pkgdir/usr/local/3proxy/libexec/"*.so "$pkgdir/usr/lib/"
  mv "$pkgdir/usr/bin/"{proxy,3proxy-proxy}
  rm -rf "$pkgdir/etc/3proxy/counters" "$pkgdir/etc/3proxy/passwd" \
         "$pkgdir/etc/3proxy/bandlimiters" "$pkgdir/etc/3proxy/3proxy.cfg" "$pkgdir/etc/3proxy/conf" "$pkgdir/usr/local"
  install -D -m644 copying "$pkgdir/usr/share/licenses/$pkgname/copying"
  install -D -m644 cfg/3proxy.cfg.sample $pkgdir/etc/3proxy/3proxy.cfg.sample
  install -D -m644 cfg/counters.sample $pkgdir/etc/3proxy/counters.sample
  mkdir -p "$pkgdir/usr/lib/systemd/system/"
  install -Dm644 "$srcdir/sysusers.conf" "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -Dm644 "$srcdir/tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  install -D -m644 "$srcdir/3proxy.service" "$pkgdir/usr/lib/systemd/system/3proxy.service"
}
