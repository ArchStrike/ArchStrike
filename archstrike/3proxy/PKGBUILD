# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=3proxy
pkgver=0.8.7
pkgrel=1
groups=('archstrike' 'archstrike-misc')
pkgdesc='Tiny free proxy server'
url='http://3proxy.ru'
license=('GPL')
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
depends=('glibc' 'openssl')
options=('!makeflags')
source=("https://github.com/z3APA3A/3proxy/archive/3proxy-$pkgver.tar.gz")
sha512sums=('3794392667ce2231ac9b8fa4fb81fabb99b894d637c1630e22bc195234b5442412eb9e6255d7e32319ed2be1abca1d35f320ad7ee688b696596ffbb1c14d9bf7')

prepare() {
  cd $pkgname-$pkgname-$pkgver
  sed -i 's|/usr/bin/install|/bin/install|g' Makefile.Linux
  sed -i 's|/usr/local|/usr|g' Makefile.Linux
  sed -i 's|-s ||g' Makefile.Linux
  sed -i "s|-Wall -g -O2|$CFLAGS|g" Makefile.Linux
  sed -i 's|\$(prefix)/etc/|/etc/|g' Makefile.Linux
}

build() {
  cd $pkgname-$pkgname-$pkgver
  make -f Makefile.Linux prefix=/usr
}

package() {
  cd $pkgname-$pkgname-$pkgver
  make -f Makefile.Linux install DESTDIR="$pkgdir"
  find "$pkgdir/etc/$pkgname" -type f | xargs chmod 644
  mv "$pkgdir/usr/bin/proxy" "$pkgdir/usr/bin/$pkgname"
}
