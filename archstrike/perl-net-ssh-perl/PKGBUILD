# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=perl-net-ssh-perl
pkgver=2.01
pkgrel=3
pkgdesc="Perl client interface to SSH"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL' 'custom')
options=('!emptydirs')
depends=('perl-convert-pem>=0.05' 'perl-crypt-dh>=0.01' 'perl-crypt-dsa>=0.11'
         'perl-crypt-idea' 'perl-crypt-rsa>=1.37'
         'perl-digest-bubblebabble>=0.01' 'perl-digest-hmac'
         'perl-digest-sha1>=2.10' 'perl-math-gmp>=1.04'
         'perl-string-crc32>=1.2')
url='http://search.cpan.org/dist/Net-SSH-Perl'
source=("http://search.cpan.org/CPAN/authors/id/S/SC/SCHWIGON/Net-SSH-Perl-$pkgver.tar.gz")
sha512sums=('4d32711323df5e0098decbb42f134a9a79e7fc216508aa23ae59a2807d2fbe983bfe0a9e50cfc128ef22adc6bd878a71f419f2546419acf9e15cf037b8984cf4')
_distdir="Net-SSH-Perl-$pkgver"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    /usr/bin/perl Makefile.PL
    make )
}

package() {
  cd "$srcdir/$_distdir"
  make install
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
