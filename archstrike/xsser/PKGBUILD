# Maintainer: ArchStrike <team@archstrike.org>

buildarch=196

pkgname=xsser
pkgver=1.8.2
pkgrel=3
groups=('archstrike' 'archstrike-webapps' 'archstrike-fuzzers' 'archstrike-exploit')
pkgdesc='An automatic -framework- to detect, exploit and report XSS vulnerabilities in web-based applications'
arch=('armv7h' 'i686' 'x86_64')
url='https://xsser.03c8.net'
license=('GPL3')
depends=('python-pycurl' 'python-beautifulsoup4' 'python-geoip' 'python-geoip2' 'python-gobject' 'python-cairocffi')
makedepends=('python-setuptools')
replaces=("${pkgname}-git")
conflicts=("${pkgname}-git")
source=("https://github.com/epsylon/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('f870d799b5220b2e694e16973db6252864b88867243748b4bf3e7148d13cfd402bc0b7eb2189c3f4e1e7c913312f783f4308ec1de1af6975796af2cd7b9f0c02')

prepare() {
  cd "${pkgname}-${pkgver}" || exit

  # Expect the user-agents.txt file in the system-installed location since we're not running this locally
  sed -i "s|core/fuzzing/user-agents.txt|/usr/share/${pkgname}/user-agents.txt|g" core/*.py
}

package() {
  cd "${pkgname}-${pkgver}" || exit
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1
  install -Dm644 core/fuzzing/user-agents.txt "${pkgdir}/usr/share/${pkgname}/user-agents.txt"
}
