# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=packetsender
pkgver=7.2.3
pkgrel=1
groups=('archstrike' 'archstrike-networking')
pkgdesc="A utility to allow sending and receiving TCP and UDP packets."
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://packetsender.com/'
license=('GPL2')
depends=('qt5-base')
source=("https://github.com/dannagle/PacketSender/archive/v${pkgver}.tar.gz")
sha512sums=('dc42b90696d78abb1bc51a3bb38e5193ce9721e97edb4d69c01cfdf392476659c82a0c8a526a8cd02bca6a2638f0667eecd4799541da4f0c24704750626d660b')

build(){
  cd "${srcdir}/PacketSender-${pkgver}/src"
  qmake PacketSender.pro
  make
}

package() {
  cd "${srcdir}/PacketSender-${pkgver}/"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/share/${pkgname}/screenshots"
  install -Dm644 screenshots/* "${pkgdir}/usr/share/${pkgname}/screenshots/"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm755 src/packetsender "${pkgdir}/usr/bin/${pkgname}"
}
