# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=archstrike-menu
pkgver=1.2
pkgrel=1
arch=('any')
pkgdesc="ArchStrike menu for XFCE"
url="https://github.com/ArchStrike/archstrike-menu"
license=('GPL')
source=("${url}/archive/${pkgver}.tar.gz")
sha512sums=('b65ced72c295585b13de0d0b01742d794ba01ee6c77ef6ee9eb8cf6de4a209cac18792337727d27fa97622b48a7f2ff09afa4b2eb1b31ed24691058442c19588')

package() {
  cd ${pkgname}-${pkgver}
  for i in icons/hicolor applications desktop-directories; do
    install -dm755 "${pkgdir}/usr/share/${i}"
  done
  install -dm755 "${pkgdir}/etc/xdg/menus/applications-merged"

  cp -a --no-preserve=ownership menus/* "${pkgdir}/etc/xdg/menus/applications-merged/"
  cp -a --no-preserve=ownership desktop-directories/* "${pkgdir}/usr/share/desktop-directories/"
  cp -a --no-preserve=ownership desktop-files/* "${pkgdir}/usr/share/applications/"
  cp -a --no-preserve=ownership menu-icons/* "${pkgdir}/usr/share/icons/hicolor/"

  ln -sf /etc/xdg/menus/applications-merged "${pkgdir}/etc/xdg/menus/gnome-applications-merged"
  ln -sf /etc/xdg/menus/applications-merged "${pkgdir}/etc/xdg/menus/kf5-applications-merged"
}
