# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=fakedns-git
pkgver=20200803.r109.2123879
pkgrel=1
groups=('archstrike' 'archstrike-spoof' 'archstrike-proxy')
pkgdesc="A regular-expression based python MITM DNS server with correct DNS request passthrough and \"Not Found\" responses."
arch=('any')
url="https://github.com/Crypt0s/FakeDns"
license=('MIT')
depends=('python2')
makedepends=('git')
replaces=('fakedns')
conflicts=('fakedns')
provides=('fakedns')
source=("$pkgname::git+https://github.com/Crypt0s/FakeDns.git")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${pkgname}"
  grep -iRl 'python' . | xargs sed -i 's|python$|python2|g'
}

package() {
  cd "${srcdir}/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin/"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cp -a --no-preserve=ownership ./* "${pkgdir}/usr/share/${pkgname}"

cat > "${pkgdir}"/usr/bin/fakedns << EOF
#!/usr/bin/env bash
cd "/usr/share/${pkgname}"
python2 fakedns.py "\$@"
EOF
  chmod 755 "${pkgdir}/usr/bin/fakedns"
}
