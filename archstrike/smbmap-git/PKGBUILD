# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=smbmap-git
pkgver=20210113.r145
pkgrel=1
groups=('archstrike' 'archstrike-enumeration')
pkgdesc="A handy SMB enumeration tool"
url='https://github.com/ShawnDEvans/smbmap'
license=("GPL3")
makedepends=('git')
depends=('python' 'impacket' 'python-pycryptodomex' 'python-termcolor')
arch=('any')
source=("${pkgname}::git+${url}")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed "s/\ .*//g;s/-//g")" "$(git rev-list --count HEAD)"
}

package() {
  cd "${srcdir}/${pkgname}"
  install -Dm755 smbmap.py "${pkgdir}/usr/bin/smbmap"
  install -Dm644 README.md "${pkgdir}/usr/share/doc/smbmap/README.md"
}
