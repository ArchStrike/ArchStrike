# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=bed
pkgver=0.5
pkgrel=6
groups=('archstrike' 'archstrike-exploit')
pkgdesc="Collection of scripts to test for buffer overflows, format string vulnerabilities."
arch=('any')
url='http://www.aldeid.com/wiki/Bed'
license=('GPL')
depends=('perl')
source=("http://web.archive.org/web/20101229024258/http://www.remote-exploit.org/wp-content/uploads/2010/01/${pkgname}-${pkgver}.tar.gz")
sha512sums=('17a2debe4516c13dd677529b8ebe34bee777ce554668c1b5b43f94b05688611423392e04bbb5b4b52218ec18bf3bc77857ce493683384f73206b93a8b0c97aef')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}" || exit

  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"

  install -m755 ${pkgname}.pl  "${pkgdir}/usr/share/${pkgname}"

  cp --no-preserve=ownership -a ${pkgname}mod "${pkgdir}/usr/share/${pkgname}"
  cp --no-preserve=ownership -a docs/* "${pkgdir}/usr/share/doc/${pkgname}"

  # Joint script.
  cat >> "${pkgdir}/usr/bin/${pkgname}" <<EOF
#!/bin/sh
cd /usr/share/${pkgname}
./${pkgname}.pl "\$@"
EOF
  chmod +x "${pkgdir}/usr/bin/${pkgname}"
}
