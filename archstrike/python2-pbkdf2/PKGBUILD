# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgbase=python-pbkdf2
pkgname=python2-pbkdf2
pkgver=1.3
pkgrel=2
pkgdesc='Implements the password-based key derivation function PBKDF2'
url='https://www.dlitz.net/software/python-pbkdf2/'
arch=('any')
license=('MIT')
depends=('python2')
makedepends=('python2' 'python2-setuptools' 'python2-crypto')
optdepends=('python2-crypto: to make use of PyCryptos HMAC and SHA')
checkdepends=('python2-pytest')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/dlitz/python-pbkdf2/archive/v${pkgver}.tar.gz)
sha512sums=('642372615a2ffc9cf352805789f836c42763c49cbf113d64524a55c6ff8a09f0fda36c92713387f27c2f6a2950ce89ae04dc9f92495cba904400685b148f947e')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  find . -type f -name "*.py" -exec sed -i 's#/usr/bin/python#/usr/bin/env python2#g' {} +
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  py.test2
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
}
