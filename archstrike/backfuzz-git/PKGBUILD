# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=backfuzz-git
pkgver=20130611.r36.8e54ed6
pkgrel=1
epoch=1
groups=('archstrike' 'archstrike-fuzzers')
pkgdesc="A protocol fuzzing toolkit."
url="https://github.com/localh0t/backfuzz"
arch=('any')
license=('GPL3')
depends=('python2')
makedepends=('git')
replaces=('backfuzz')
provides=('backfuzz')
conflicts=('backfuzz')
source=("${pkgname}::git+https://github.com/localh0t/backfuzz.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//  g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd ${pkgname}

  # Base directories.
  install -dm755 "$pkgdir/usr/bin/"
  install -dm755 "$pkgdir/usr/share/$pkgname"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname"

cat > "$pkgdir/usr/bin/backfuzz" << EOF
#!/bin/sh
cd /usr/share/${pkgname}
python2 backfuzz.py "\$@"
EOF
chmod 755 "$pkgdir/usr/bin/backfuzz"
}
