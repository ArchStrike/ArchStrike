# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=dvcs-ripper-git
pkgver=20151124.r44
pkgrel=3
groups=('archstrike' 'archstrike-misc')
pkgdesc="Rip web accessible (distributed) version control systems: SVN/GIT/..."
arch=('any')
url='https://github.com/kost/dvcs-ripper'
license=('GPL2')
depends=('perl' 'perl-dbd-sqlite' 'perl-dbi' 'perl-parallel-forkmanager' 'perl-io-socket-ssl')
optdepends=('git: for git repos'
            'cvs: for cvs repos'
            'subversion: for svn repos'
            'bzr: for bzr repos'
            'mercurial: for hg repos')
makedepends=('git')
provides=('dvcs-ripper')
source=("${pkgname}::git+https://github.com/kost/dvcs-ripper.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

package() {
  cd "${pkgname}"

  # Make base directories.
  install -dm755 "$pkgdir/usr/share/${pkgname}"
  install -dm755 "$pkgdir/usr/bin"

  install -Dm644 README.md "$pkgdir/usr/share/${pkgname}"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm755 rip-cvs.pl "$pkgdir/usr/bin/rip-cvs.pl"
  install -Dm755 rip-git.pl "$pkgdir/usr/bin/rip-git.pl"
  install -Dm755 rip-svn.pl "$pkgdir/usr/bin/rip-svn.pl"
}
