# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=firmware-mod-kit
pkgver=099
pkgrel=3
groups=('archstrike' 'archstrike-misc')
pkgdesc=" Modify firmware images without recompiling!"
url="http://code.google.com/p/firmware-mod-kit"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('MIT')
depends=('python-magic')
source=("http://firmware-mod-kit.googlecode.com/files/fmk_${pkgver}.tar.gz")
sha512sums=('a5261935e42446568c0c0d4c4fc1beff18c5aa7b85b1aa3398dfeaf80c8bdbb4fb2a6fb4945defc29599d6e58f1553e80e8bb755da4d98f79e99447f0c3414b1')

build(){
 cd "$srcdir/fmk/src"
 ./configure
 make
}

package() {
  cd "$srcdir/fmk"
  install -dm0755 "$pkgdir/usr/share/$pkgname"
  install -dm0755 "$pkgdir/usr/share/doc/$pkgname"
  install -m0755 *.sh "$pkgdir/usr/share/firmware-mod-kit/"
  install -m0644 *.inc "$pkgdir/usr/share/firmware-mod-kit/"
  install -m0644 *.htm "$pkgdir/usr/share/doc/firmware-mod-kit/"
  install -m0644 firmware_mod_kit_version.txt  "$pkgdir/usr/share/firmware-mod-kit/"
  for i in src ipk_template; do cp -a --no-preserve=ownership $i "$pkgdir/usr/share/firmware-mod-kit/"; done
}

