# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=thezoo
pkgname=${_pkgname}-git
pkgver=20200912.r205
pkgrel=1
pkgdesc="A project created to make the possibility of malware analysis open and available to the public"
url="https://github.com/ytisf/theZoo"
arch=('any')
license=('GPL3')
groups=('archstrike' 'archstrike-malware')
depends=('python2')
makedepends=('git')
source=("${pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${pkgname}" || exit
    printf "%s.r%s" \
        "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" \
        "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${pkgname}" || exit
    find . -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
    find . -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|'
}

package() {
    cd "${pkgname}" || exit
    install -dm0755 "${pkgdir}"/usr/{bin,share/"${_pkgname}"}
    cp -a --no-preserve=ownership ./* "${pkgdir}/usr/share/${_pkgname}"

cat > "${pkgdir}/usr/bin/${_pkgname}" << EOF
#!/usr/bin/env bash
cd /usr/share/thezoo
/usr/bin/python2 ./theZoo.py "\$@"
EOF
chmod 755 "${pkgdir}/usr/bin/${_pkgname}"
}
