# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: William Robertson <wkr@wkr.name>

buildarch=1

pkgname='androguard'
pkgver='2.0'
pkgrel='2'
pkgdesc='Python framework for Android application analysis.'
arch=('any')
url='https://code.google.com/p/androguard/'
license=('Apache')
groups=('archstrike')
depends=('python2')
makedepends=('python2-setuptools' 'muparser' 'sparsehash' 'snappy')
optdepends=(
    'ipython2: interactive analysis support'
    'python2-pygments: color support'
    'pydot: CFG support'
    'python2-magic: file identification support'
    'psyco2-svn: acceleration support'
)
source=("https://github.com/androguard/androguard/archive/v$pkgver.tar.gz")
sha512sums=('8ff72bc564da7d583add4b8de917f7c413d53874fe7a3f154029c06b72ac250de52fe1747f1389c777f80bd5b94667d75635c68631e80bf177ac9cf1d1fb49ae')

build() {
   cd "$srcdir/androguard-$pkgver"
   make
   python2 setup.py build
}

package() {
    cd "$srcdir/androguard-$pkgver"
    python2 setup.py install --root="$pkgdir" --optimize=1
    grep -iRl 'python' "$pkgdir/usr/lib/python2.7/site-packages/androguard/core/api_specific_resources" | xargs sed -i 's|python$|python2|'
}
