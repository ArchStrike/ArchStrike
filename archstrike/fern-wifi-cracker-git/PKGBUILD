# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=fern-wifi-cracker
pkgname=$_pkgname-git
pkgver=20160920.r239.6415b67
pkgrel=1
groups=('archstrike' 'archstrike-wireless' 'archstrike-crackers')
pkgdesc='WEP, WPA wifi cracker for wireless penetration testing'
arch=('any')
url='https://github.com/savio-code/fern-wifi-cracker'
license=('GPL')
depends=('python2-pyqt4' 'macchanger' 'xterm' 'aircrack-ng' 'scapy' 'gksu')
makedepends=('git')
provides=("$_pkgname")
conflicts=("$_pkgname")
replaces=("$_pkgname")
source=(
  "git+https://github.com/savio-code/$_pkgname.git"
  'remove-update.patch'
  'remove-pro-tip.patch'
  "$_pkgname.desktop"
)
sha512sums=(
  'SKIP'
  '4bfbf15c3287562ff437c984d1a5e4fa7f809d014c5459b02ed4c3c1c848564737e148c9a98f6020954acb8a11e4f97c57be7815bbe2b64c7c2c67dff8eee0ca'
  'af0575b24e2b84b79a5b25ca6c702cae6af3668653f4cbb7c0ae36df446d68e402508148d8a57bae45a909151ad5b20694536d20d8d22b1beafeab4f991997bc'
  '352dcf1a1dfd798ca8e7d0c50b3b9bb7912109fc3b0d9d82323810552fcb725e95ed92e244fd548f46c438014aeccca6c91fb0f27f197736484893638b1015e4'
)

pkgver() {
  cd $_pkgname
  printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $_pkgname
  patch -p1 < ../remove-update.patch
  patch -p1 < ../remove-pro-tip.patch
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' Fern-Wifi-Cracker/execute.py
}

package() {
  install -Dm644 "$_pkgname.desktop" "$pkgdir/usr/share/applications/$_pkgname.desktop"

  install -d "$pkgdir/usr/bin"
  echo -e '#!/usr/bin/env bash\n\ncd /usr/share/fern-wifi-cracker\npython2 ./execute.py "\$@"' > "$pkgdir/usr/bin/$_pkgname"
  chmod 755 "$pkgdir/usr/bin/$_pkgname"

  install -d "$pkgdir/usr/share/$_pkgname"
  cp -R --no-preserve=ownership $_pkgname/Fern-Wifi-Cracker/* "$pkgdir/usr/share/$_pkgname/"
}
