# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

_pkgname="Amass"
pkgname="amass"
pkgver=3.21.2
pkgrel=1
pkgdesc=" tool to help information security professionals perform network mapping of attack surfaces and perform external asset discovery"
arch=('i686' 'x86_64')
url="https://github.com/OWASP/Amass"
license=('Apache')
groups=('archstrike' 'archstrike-enumeration' 'archstrike-recon')
makedepends=('go' 'git')
source=("${url}/archive/v${pkgver}.tar.gz")
sha512sums=('0b3aa2822c19598c8ff0826ff98a1915d260f5f5d7bd7310fd57e35dea2424ebad0b76e1ec0895f2e95fff67f6fcdc9de3ec93961d3e7b402b5db33a6a2efd36')

prepare() {
  mkdir -vp "${srcdir}/github.com/OWASP"
  mv "${srcdir}/${_pkgname}-${pkgver}" "${srcdir}/github.com/OWASP/${_pkgname}"

  export GOPATH="${srcdir}"
  cd "${GOPATH}/github.com/OWASP/${_pkgname}"
  go get -d ./...
}

check() {
  cd "${srcdir}/github.com/OWASP/${_pkgname}"
  export GOPATH="${srcdir}"
  go test ./...
}

build() {
  cd "${srcdir}/github.com/OWASP/${_pkgname}"
  export GOPATH="${srcdir}"
  go install -v ./...
}

package() {
  export GOPATH="${srcdir}"
  cd "${GOPATH}/bin"
  install -dm 755 "${pkgdir}/usr/share/${pkgname}"
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  cp -a --no-preserve=ownership "${GOPATH}/github.com/OWASP/${_pkgname}/examples/" "${pkgdir}/usr/share/${pkgname}"
  chmod 644 "${pkgdir}/usr/share/${pkgname}"/examples/*
}
