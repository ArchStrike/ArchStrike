# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname="Amass"
pkgname="amass"
pkgver=3.11.7
pkgrel=1
pkgdesc=" tool to help information security professionals perform network mapping of attack surfaces and perform external asset discovery"
arch=('any')
url="https://github.com/OWASP/Amass"
license=('Apache')
groups=('archstrike' 'archstrike-enumeration' 'archstrike-recon')
makedepends=('go' 'git')
source=("${url}/archive/v${pkgver}.tar.gz")
sha512sums=('87b0e47fb9a94aa76e82b6f28fa4dcc3efb8c260dc26719c3ea365ec61e6027963b17d7ba9cc5ad6414483cfd5a45ba32fed78591068aecaf4a01e525ea67b39')

prepare() {
  mkdir -vp "${srcdir}/github.com/OWASP"
  mv "${srcdir}/${_pkgname}-${pkgver}" "${srcdir}/github.com/OWASP/${_pkgname}"

  export GOPATH="${srcdir}"
  cd "${GOPATH}/github.com/OWASP/${_pkgname}"
  go get -d ./...
}

#check() {
#  cd "${srcdir}/github.com/OWASP/${_pkgname}"
#  export GOPATH="${srcdir}"
#  go test ./...
#}

build() {
  cd "${srcdir}/github.com/OWASP/${_pkgname}"
  export GOPATH="${srcdir}"
  go install -v ./...
}

package() {
  export GOPATH="${srcdir}"
  cd "${GOPATH}/bin"
  install -dm 755 "${pkgdir}/usr/share/${pkgname}"
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  cp -a --no-preserve=ownership "${GOPATH}/github.com/OWASP/${_pkgname}/"{wordlist/,examples/} "${pkgdir}/usr/share/${pkgname}"
  chmod 644 "${pkgdir}/usr/share/${pkgname}/"{wordlist/*,examples/*}
}
