# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

_pkgname="Amass"
pkgname="amass"
pkgver=3.19.3
pkgrel=1
pkgdesc=" tool to help information security professionals perform network mapping of attack surfaces and perform external asset discovery"
arch=('i686' 'x86_64')
url="https://github.com/OWASP/Amass"
license=('Apache')
groups=('archstrike' 'archstrike-enumeration' 'archstrike-recon')
makedepends=('go' 'git')
source=("${url}/archive/v${pkgver}.tar.gz")
sha512sums=('1f2935a8923445ae2b17706097c3f5ddd62a6f1b657eba8405c9a90f37b153959f8b2f5a11fc4e296fae255cbf783079b0d32e868b371399db3d68d5bcee1ae8')

prepare() {
  mkdir -vp "${srcdir}/github.com/OWASP"
  mv "${srcdir}/${_pkgname}-${pkgver}" "${srcdir}/github.com/OWASP/${_pkgname}"

  export GOPATH="${srcdir}"
  cd "${GOPATH}/github.com/OWASP/${_pkgname}"
  go get -d ./...
}

check() {
  cd "${srcdir}/github.com/OWASP/${_pkgname}"
  export GOPATH="${srcdir}"
  go test ./...
}

build() {
  cd "${srcdir}/github.com/OWASP/${_pkgname}"
  export GOPATH="${srcdir}"
  go install -v ./...
}

package() {
  export GOPATH="${srcdir}"
  cd "${GOPATH}/bin"
  install -dm 755 "${pkgdir}/usr/share/${pkgname}"
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  cp -a --no-preserve=ownership "${GOPATH}/github.com/OWASP/${_pkgname}/examples/" "${pkgdir}/usr/share/${pkgname}"
  chmod 644 "${pkgdir}/usr/share/${pkgname}"/examples/*
}
