# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname="Amass"
pkgname="amass"
pkgver=3.11.9
pkgrel=1
pkgdesc=" tool to help information security professionals perform network mapping of attack surfaces and perform external asset discovery"
arch=('any')
url="https://github.com/OWASP/Amass"
license=('Apache')
groups=('archstrike' 'archstrike-enumeration' 'archstrike-recon')
makedepends=('go' 'git')
source=("${url}/archive/v${pkgver}.tar.gz")
sha512sums=('0332c98c06bae5cdd82e134403ee9e32ea286f49358457673debe6f7bc2202bb255342f237038c668843a46fcfc5aa88f7a8d670e6874300facbb6534d98dce5')

prepare() {
  mkdir -vp "${srcdir}/github.com/OWASP"
  mv "${srcdir}/${_pkgname}-${pkgver}" "${srcdir}/github.com/OWASP/${_pkgname}"

  export GOPATH="${srcdir}"
  cd "${GOPATH}/github.com/OWASP/${_pkgname}"
  go get -d ./...
}

#check() {
#  cd "${srcdir}/github.com/OWASP/${_pkgname}"
#  export GOPATH="${srcdir}"
#  go test ./...
#}

build() {
  cd "${srcdir}/github.com/OWASP/${_pkgname}"
  export GOPATH="${srcdir}"
  go install -v ./...
}

package() {
  export GOPATH="${srcdir}"
  cd "${GOPATH}/bin"
  install -dm 755 "${pkgdir}/usr/share/${pkgname}"
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  cp -a --no-preserve=ownership "${GOPATH}/github.com/OWASP/${_pkgname}/"{wordlist/,examples/} "${pkgdir}/usr/share/${pkgname}"
  chmod 644 "${pkgdir}/usr/share/${pkgname}/"{wordlist/*,examples/*}
}
