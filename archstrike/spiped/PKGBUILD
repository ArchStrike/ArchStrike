# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Simon Perry <aur [at] sanxion [dot] net>
# Contributor: Nicolas Pouillard http://nicolaspouillard.fr

buildarch=220

pkgname=spiped
pkgver=1.6.1.99
pkgrel=1
pkgdesc="A utility for creating symmetrically encrypted and authenticated pipes between socket addresses"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
source=("http://www.tarsnap.com/${pkgname}/${pkgname}-${pkgver}.tgz")
url="http://www.tarsnap.com/spiped.html"
groups=('archstrike' 'archstrike-networking')
license=('BSD')
depends=('openssl')
sha512sums=('bd9b0b12db5f154ef29d495d6e93fd94ee571fe025d0ce8f50f1e47be48b8367cd885c8d78a926bfc76e6c5aa46b1de915502468dac704a2b664d9d9da777103')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -dm755 "${pkgdir}/usr/share/man/man1"
  MAN1DIR="${pkgdir}/usr/share/man/man1" BINDIR="${pkgdir}/usr/bin" make install
  install -Dm644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

