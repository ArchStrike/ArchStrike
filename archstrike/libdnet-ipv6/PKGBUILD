# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: James Fryman <jfryman@gmail.com>

buildarch=220

_pkgname=libdnet
pkgname=$_pkgname-ipv6
pkgver=1.12
pkgrel=11
pkgdesc='A simplified, portable interface to several low-level networking routines (with ipv6 support)'
url='https://github.com/dugsong/libdnet'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
depends=('python2' 'bash')
makedepends=('python2' 'pyrex')
provides=("$_pkgname")
conflicts=("$_pkgname")
license=('BSD')
source=(
  "https://github.com/dugsong/libdnet/archive/$_pkgname-$pkgver.tar.gz"
  "$_pkgname-1.12.pyrex2.7.patch"
  "$_pkgname-ipv6.patch"
)
sha512sums=(
  'a2410a98b76d4bccecf7bcb82ff0b6e9fb809b389f7fea938cc42d94e18622cc5f353d2de737121a1990d9e16e9db8cc2f82677c94f78cd03c9227e815eab9e0'
  'ac5fecf1c7028b469965cddc92ed8e8b87605282ae14417274fcf685e57485f89d05583e2573d86611043f5bbfdc924fc5f5bb761a43cc18a2bcd6b5c10f0821'
  '3f9eb49a54172bae25ab6f137020c9babcb6fdb888d8b27050bfbdb87986de83df7fe81d783068d61fa33cd803fecd74d0f9acaf26c4991585609787e783b3be'
)

prepare() {
  cd $_pkgname-$_pkgname-$pkgver
  patch -p0 < ../$_pkgname-1.12.pyrex2.7.patch
  patch -p1 < ../$_pkgname-ipv6.patch
  rm -f python/dnet.c
}

build() {
  cd $_pkgname-$_pkgname-$pkgver
  autoreconf -I config --force --install
  ./configure --prefix=/usr --sbindir=/usr/bin
  pyrexc python/dnet.pyx
  make
}

package() {
  cd $_pkgname-$_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/license"

  cd python
  python2 setup.py install --root="$pkgdir"
}
