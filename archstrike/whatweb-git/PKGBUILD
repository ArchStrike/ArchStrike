# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname="WhatWeb"
pkgname="whatweb-git"
pkgver=r4771.a214dc80
pkgrel=1
groups=('archstrike' 'archstrike-webapps' 'archstrike-recon')
pkgdesc="Next generation web scanner that identifies what websites are running."
arch=('any')
url='https://github.com/urbanadventurer/WhatWeb'
license=('GPL')
depends=('ruby' 'ruby-bundler')
makedepends=('git' 'rubygems')
source=("${pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

package() {
  cd "${srcdir}/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/whatweb"
  cp -a --no-preserve=ownership ./* "${pkgdir}/usr/share/whatweb"
  cd "${pkgdir}/usr/share/whatweb"
  bundle install --path vendor/bundle

cat > "${pkgdir}/usr/bin/whatweb" << EOF
#!/usr/bin/env bash
cd /usr/share/whatweb
unset GEM_HOME
/usr/bin/bundler exec ./whatweb "\$@"
EOF

chmod 0755 "${pkgdir}/usr/bin/whatweb"
}
