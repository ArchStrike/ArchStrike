# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=perl-algorithm-permute
pkgver=0.16
pkgrel=7
pkgdesc="Perl module for handy and fast permutations with object oriented interface"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
license=("GPL")
url="http://search.cpan.org/dist/Algorithm-Permute/"
depends=("perl")
makedepends=('perl-test-leaktrace')
options=(!emptydirs)
source=("https://cpan.metacpan.org/authors/id/S/SL/SLOYD/Algorithm-Permute-${pkgver}.tar.gz")
sha512sums=('1d9748903593d065cc8bc7358a5984c0f6c7e8539a175a87e39f30831103f0be8eb00a1a2ce3a8c83660a19f2a082749d23072d79543cae44601898b263aff14')

build() {
  cd "${srcdir}/Algorithm-Permute-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/Algorithm-Permute-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/Algorithm-Permute-${pkgver}"
  make install DESTDIR="${pkgdir}"
}
