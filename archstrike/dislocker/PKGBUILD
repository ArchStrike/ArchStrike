# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=dislocker
pkgver=0.7.1
pkgrel=4
pkgdesc="Read BitLocker encrypted volumes under Linux"
arch=('armv6h' 'i686' 'armv7h' 'x86_64' 'aarch64')
url="https://github.com/Aorimn/dislocker"
license=('GPL2')
groups=('archstrike')
depends=('fuse' 'mbedtls' 'ruby')
makedepends=('cmake')
source=("https://github.com/Aorimn/dislocker/archive/v${pkgver}.tar.gz")
sha512sums=('8852ba3e363fdea992eebecfe1e4dad2b85404f57c57ce6b2937a9859d03cfa88d969926c9e11e6d22596c6d96805b7d1737893ae2c5a957448ab26708bce226')

build() {
  cd "${pkgname}-${pkgver}/"
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  install -dm755 "${pkgdir}"/usr/bin
  install -dm755 "${pkgdir}"/usr/lib
  install -dm755 "${pkgdir}"/usr/share/man/man1
  install -Dm644 README.md "${pkgdir}"/usr/share/dislocker/README.md
  install -Dm644 CHANGELOG.md "${pkgdir}"/usr/share/dislocker/CHANGELOG.md
  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/dislocker/LICENSE
  cd src
  make DESTDIR="${pkgdir}" install
  ## Fixing installer issues
  if [[ $CARCH == x86_64 ]]; then
        mv ${pkgdir}/usr/lib64/libdislocker* ${pkgdir}/usr/lib/
        rm -rf ${pkgdir}/usr/lib64
  fi
}
