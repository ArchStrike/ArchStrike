# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

pkgname="meltdown-git"
pkgver=20180118.r73
pkgrel=1
groups=('archstrike' 'archstrike-exploit')
arch=('i686' 'x86_64')
pkgdesc="Meltdown Proof-of-Concept"
url="https://github.com/IAIK/meltdown"
license=('custom')
makedepends=('git')
source=("${pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed "s/\ .*//g;s/-//g")" "$(git rev-list --count HEAD)"
}

build() {
  cd "${srcdir}/${pkgname}"
  make

  cd "${srcdir}/${pkgname}/libkdump"
  make
}

package() {
  cd "${pkgname}"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm644 libkdump/README.md "${pkgdir}/usr/share/doc/${pkgname}/libkdump/README.md"

  install -Dm755 libkdump/libkdump.so "${pkgdir}/usr/lib/libkdump.so"
  install -Dm644 libkdump/libkdump.h "${pkgdir}/usr/include/libkdump.h"

  for i in test kaslr reliability secret physical_reader memdump; do
    install -Dm755 "${i}" "${pkgdir}/usr/bin/meltdown_${i}"
  done
}
