# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>

buildarch=192

_pkgname=unicorn
pkgname='unicorn-engine'
pkgver=0.9
pkgrel=3
pkgdesc='Lightweight, multi-platform, multi-architecture CPU emulator framework based on QEMU'
url='http://www.unicorn-engine.org'
arch=('i686' 'x86_64')
groups=('archstrike' 'archstrike-debugging' 'archstrike-decompile')
license=('GPL2')
depends=('glib2')
makedepends=('pkg-config' 'python2-setuptools' 'python-setuptools')
checkdepends=('cmocka')
source=(${_pkgname}-${pkgver}.tar.gz::https://github.com/unicorn-engine/${_pkgname}/archive/${pkgver}.tar.gz)
sha512sums=('c69bb03b7ba4ebec9aef23b80d1ffe33a30de958a74136ef5933b5fa10a2b2ffddd716fdb1881b78dfdcc5a53fece15c368c661b12d20f80dbf955a66930eb0b')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed 's|libdir=$(LIBDIR)|libdir=$(PREFIX)/$(LIBDIRARCH)|g' -i Makefile
  sed 's|includedir=$(INCDIR)|includedir=$(PREFIX)/include|g' -i Makefile
  sed 's|ifeq ($(UNICORN_DEBUG),yes)|ifneq ($(UNICORN_DEBUG),yes)|g' -i Makefile
  sed 's|-O3|-O2|g' -i Makefile qemu/configure
  sed 's|-g ||g' -i qemu/configure
  sed 's|UNICORN_DEBUG ?= yes|UNICORN_DEBUG ?= no|g' -i config.mk
}

build() {
  cd ${_pkgname}-${pkgver}
  make UNICORN_QEMU_FLAGS="--python=/usr/bin/python2"
  # python bindings
  cd bindings/python
  # python2 bindings
  python2 setup.py build
  #python3 bindings
  python3 setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  make test
}

package() {
  cd ${_pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 samples/*.c -t "${pkgdir}/usr/share/doc/${pkgname}/samples"
  # install python bindings
  cd bindings/python
  # install python2 bindings
  python2 setup.py install --root="${pkgdir}"
  # installl python2 bindings
  python3 setup.py install --root="${pkgdir}"
}

# vim: ts=2 sw=2 et:
