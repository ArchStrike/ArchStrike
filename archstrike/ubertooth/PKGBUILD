# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Ari Mizrahi <codemunchies@debugsecurity.com>

buildarch=212

pkgname=ubertooth
pkgver=2015.10.R1
pkgrel=2
groups=('archstrike' 'archstrike-bluetooth')
pkgdesc='A 2.4 GHz wireless development board suitable for Bluetooth experimentation. Open source hardware and software. Tools only'
url='https://greatscottgadgets.com/ubertoothone'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
makedepends=('cmake' 'libpcap')
depends=('libusbx' 'libbtbb' 'bluez-libs' 'python-numpy' 'python-pyusb' 'python-pyside' 'libpcap')
source=(
  "https://github.com/greatscottgadgets/$pkgname/archive/${pkgver//./-}.tar.gz"
  'specan_ui.patch'
)
sha512sums=(
  'f0d8512136e79ee56010d17af4b0d227a7585822c08896fbaa22ba8e756d6cc561d9c45a54f84b964f4df98d3ce47f0136c6cdc19d077dcca807e189c1cfc05a'
  'c89cf605005d1fe5030d7e05f3cc9899294065699907b93d64c1adfa3b21d6ef337baa153f01b7ed4e208f3fd767554fe294610d36562e46bb7434f87c4ef47d'
)

prepare(){
  cd $pkgname-${pkgver//./-}/host/python/specan_ui
  patch CMakeLists.txt < "$srcdir/specan_ui.patch"
}

build() {
  cd $pkgname-${pkgver//./-}/host
  install -dm755 build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX="$pkgdir/usr" -DPYTHON_EXECUTABLE=/usr/bin/python3 ..
  make
}

package() {
  cd $pkgname-${pkgver//./-}/host/build
  make clock_debug=true install
}
