# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=wmat
pkgver=20160717
pkgrel=1
groups=('archstrike' 'archstrike-webapps' 'archstrike-crackers')
pkgdesc="Automatic tool for testing webmail accounts"
arch=('any')
url="http://security-net.biz"
license=('GPL3')
depends=('python2' 'python2-pycurl')
makedepends=('hd2u')
source=("http://security-net.biz/wmat/wmat.zip")
sha512sums=('11814dcedd5375aa319d5223a3b03ee3fb9d6eecca4896cc0cfee7b566d9f9c18127348abef2b22d1a99df51e6f358fc82828d5f781b5f0c475ac47efa411750')

pkgver() {
  date +%Y%m%d
}

prepare() {
  cd "$srcdir/wmat"

  sed -i 's/env python/env python2/' wmat.py
  sed -i '2i# -*- coding: utf-8 -*-' wmat.py
  sed -i 's/”/"/gi' wmat.py
  dos2unix *.py
}

package() {
  cd "$srcdir/wmat"

  # Base directories.
  install -dm755 "$pkgdir/usr/share/wmat"
  install -dm755 "$pkgdir/usr/bin"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -r * "$pkgdir/usr/share/wmat"
  chmod +x "$pkgdir/usr/share/wmat/wmat.py"

  cat > "$pkgdir/usr/bin/wmat" <<EOF
#!/bin/sh
python2 /usr/share/wmat/wmat.py "\$@"
EOF
  chmod 755 "$pkgdir/usr/bin/wmat"
}
