# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=dnsbrute-git
pkgver=0.r2.b1dc84a
pkgrel=3
groups=('archstrike' 'archstrike-dns')
pkgdesc="Multi theaded DNS bruteforcing"
arch=('any')
url='https://github.com/d4rkcat/dnsbrute'
license=('GPL2')
depends=('python2' 'python2-dnspython')
makedepends=('git')
provides=('dnsbrute')
conflicts=('dnsbrute')
replaces=('dnsbrute')
source=("git+https://github.com/d4rkcat/dnsbrute.git")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/dnsbrute"
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' "$srcdir/dnsbrute" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/dnsbrute"

  # Base directories.
  install -dm755 "$pkgdir/usr/share/${pkgname}"
  install -dm755 "$pkgdir/usr/bin/"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/${pkgname}/"
  ln -s "/usr/share/${pkgname}/dnsbrute.py" "${pkgdir}/usr/bin/dnsbrute"

}

