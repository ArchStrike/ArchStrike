# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Gergely Imreh <imrehg(at)gmail(dot)com>

buildarch=220

pkgname=python2-pyrtlsdr-git
pkgver=r368.85f27ab
pkgrel=1
pkgdesc="A Python wrapper for librtlsdr (a driver for Realtek RTL2832U based SDR's)"
depends=('python2' 'rtl-sdr')
makedepends=('git' 'python2-setuptools')
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/roger-/pyrtlsdr"
license=('GPL3')
source=("${pkgname}::git+${url}")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"
  grep -iRl 'python' "${srcdir}/${pkgname}" | xargs sed -i 's|python$|python2|'
}

package(){
  cd "${srcdir}/${pkgname}"
  python2 setup.py install --root=${pkgdir} -O1
}
