# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

_pkgname=pygraphviz
pkgname=python2-${_pkgname}
pkgver=1.5
pkgrel=2
pkgdesc='PyGraphviz is a Python interface to the Graphviz graph layout and visualization package.'
arch=('armv6h' 'armv7h' 'i686' 'x86_64' 'aarch64')
license=('LGPL')
url="https://pygraphviz.github.io/"
depends=('python2' 'graphviz')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/7e/b1/d6d849ddaf6f11036f9980d433f383d4c13d1ebcfc3cd09bc845bda7e433/${_pkgname}-${pkgver}.zip")
sha512sums=('6d3df6642a7e23ecb6b687761480b80a3ee5886508ef50b81697041425be7d09d623bf46990c5cea3ef36817a28d9e5c2905eb32267296f55524fdedb2199ea2')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  python2 setup.py install --prefix=/usr --root="${pkgdir}" -O1 --skip-build
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt

  # Patch to use python2
  find "${pkgdir}" -type f \( -name '*.py' -or -executable \) -exec \
    sed -i -e 's|#![ ]*/usr/bin/python$|#!/usr/bin/python2|' \
           -e 's|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|' \
    {} \;
}
