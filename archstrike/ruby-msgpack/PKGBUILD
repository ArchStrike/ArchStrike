# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

_pkgname=msgpack
pkgname=ruby-$_pkgname
pkgver=1.0.2
pkgrel=2
pkgdesc='MessagePack, a binary-based efficient data interchange format.'
arch=('armv6h' 'armv7h' 'i686' 'x86_64' 'aarch64')
url='https://github.com/msgpack/msgpack-ruby'
license=('Apache')
depends=('ruby')
options=(!emptydirs)
source=("https://rubygems.org/downloads/$_pkgname-$pkgver.gem")
noextract=("$_pkgname-$pkgver.gem")
sha512sums=('e821c7057ee3497c833be522c4baa8133ae029ff582bd9892ab20b8981d26c757d6348e8ae9ea4288ef3d4fccc44c5d7304473a9e4c7475a78727811630ccfe9')

package() {
  cd "$srcdir"
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  if [[ $CARCH == arm* ]] ; then
      gem install --no-rdoc --no-ri --no-user-install --ignore-dependencies -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_pkgname-$pkgver.gem
      find $pkgdir -type f -iname gem_make.out |xargs rm -rf
  else
      gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_pkgname-$pkgver.gem
      find $pkgdir -type f -iname gem_make.out |xargs rm -rf
  fi

  rm "$pkgdir/$_gemdir/cache/$_pkgname-$pkgver.gem"
}
