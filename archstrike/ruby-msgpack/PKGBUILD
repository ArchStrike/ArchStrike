# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

_pkgname=msgpack
pkgname=ruby-$_pkgname
pkgver=1.0.0
pkgrel=1
pkgdesc='MessagePack, a binary-based efficient data interchange format.'
arch=('armv6h' 'armv7h' 'i686' 'x86_64' 'aarch64')
url='https://github.com/msgpack/msgpack-ruby'
license=('Apache')
depends=('ruby')
options=(!emptydirs)
source=("https://rubygems.org/downloads/$_pkgname-$pkgver.gem")
noextract=("$_pkgname-$pkgver.gem")
sha512sums=('e87c267a67cc8d89dcc499570bf3316a84643acfbd045ec7a66990b3388e9305c068b7d5c759d35e52c9acb1172794399d2df4a3db4b7685978f3488ff1c3eca')

package() {
  cd "$srcdir"
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  if [[ $CARCH == arm* ]] ; then
      gem install --no-rdoc --no-ri --no-user-install --ignore-dependencies -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_pkgname-$pkgver.gem
      find $pkgdir -type f -iname gem_make.out |xargs rm -rf
  else
      gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_pkgname-$pkgver.gem
      find $pkgdir -type f -iname gem_make.out |xargs rm -rf
  fi

  rm "$pkgdir/$_gemdir/cache/$_pkgname-$pkgver.gem"
}
