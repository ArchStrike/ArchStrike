# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=l0l-git
pkgver=20200731.r12
pkgrel=1
groups=('archstrike' 'archstrike-exploit')
arch=('i686' 'x86_64' 'armv6h' 'arm7h' 'aarch64')
pkgdesc="The Exploit Development Kit"
url="https://github.com/xorond/l0l"
license=('MIT')
depends=('python2')
makedepends=('git')
source=("${pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  grep -iRl 'python' ${pkgname} | xargs sed -i 's|python$|python2|g'
}

build() {
  cd ${pkgname}
  make
}

package() {
  cd ${pkgname}
  make install DESTDIR=${pkgdir}
  # move license dir to ${pkgname}
  mv ${pkgdir}/usr/share/licenses/l0l ${pkgdir}/usr/share/licenses/${pkgname}
}
