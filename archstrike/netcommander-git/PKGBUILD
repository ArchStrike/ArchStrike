# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=netcommander-git
pkgver=20111208.r21
pkgrel=5
groups=('archstrike' 'archstrike-spoof')
pkgdesc="An easy-to-use arp spoofing tool"
arch=('any')
url='https://github.com/sin5678/NetCommander'
license=('GPL3')
depends=('python2' 'scapy')
makedepends=('git')
source=("$pkgname::git+${url}.git")
sha512sums=('SKIP')
pkgver() {
  cd "$pkgname" || exit
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  cd "$pkgname" || exit

  # Fix file location.
  sed -i 's|mac-prefixes|/usr/share/netcommander/mac-prefixes|' netcmd.py

  # Fix shebang.
  sed -i 's/python$/python2/' netcmd.py
}

package() {
  cd "$pkgname" || exit

  install -Dm755 "netcmd.py" "$pkgdir/usr/bin/netcmd"
  install -Dm644 "mac-prefixes" "$pkgdir/usr/share/mac-prefixes"
}
