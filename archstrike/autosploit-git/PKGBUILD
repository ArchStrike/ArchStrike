# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=autosploit
pkgname=${_pkgname}-git
pkgver=r281.9a6a5ef
pkgrel=1
groups=('archstrike' 'archstrike-exploit')
pkgdesc="Automate the exploitation of remote hosts"
arch=('any')
url='https://github.com/NullArray/AutoSploit'
license=('GPL3')
depends=('python2' 'python2-psutil' 'metasploit-framework' 'sudo' 'python2-beautifulsoup4')
makedepends=('git')
source=("${pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' "${srcdir}/${pkgname}" | xargs sed -i 's|python$|python2|g'

  # Fix services
  cd "${srcdir}/${pkgname}"
  sed -i "s|service postgresql status|systemctl status postgresql|" autosploit.py
  sed -i "s|service apache2 status|systemctl status httpd|" autosploit.py
  sed -i "s|service postgresql start|systemctl start postgresql|g" autosploit.py
  sed -i "s|service apache2 start|systemctl start httpd|g" autosploit.py
}

package() {
  cd "${srcdir}/${pkgname}"

  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"

  cp -a ./* "${pkgdir}/usr/share/${pkgname}"

cat > "${pkgdir}/usr/bin/${_pkgname}" << EOF
#!/usr/bin/env bash
cd /usr/share/${pkgname}
sudo python2 ${_pkgname}.py "\$@"
EOF

chmod 755 "${pkgdir}/usr/bin/${_pkgname}"
}
