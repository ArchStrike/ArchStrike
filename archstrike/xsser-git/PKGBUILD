# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=xsser
pkgname=${_pkgname}-git
pkgver=r36.9f196b7
pkgrel=1
groups=('archstrike' 'archstrike-webapps' 'archstrike-fuzzers' 'archstrike-exploit')
pkgdesc="A penetration testing tool for detecting and exploiting XSS vulnerabilites."
arch=('any')
url='https://xsser.03c8.net/'
license=('GPL3')
depends=('orbited' 'python2-beautifulsoup4' 'python2-pycurl' 'python2-twisted'
         'pygtk')
makedepends=('git' 'python2-setuptools')
optdepends=('python2-extras')
replaces=('xsser')
source=("${pkgname}::git+https://github.com/epsylon/xsser.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}/${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "${pkgname}/${_pkgname}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  find "${pkgdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/python2|'
  find "${pkgdir}" -type f -name 'crawler.py' | xargs sed -i 's|from BeautifulSoup|from bs4|'
}
