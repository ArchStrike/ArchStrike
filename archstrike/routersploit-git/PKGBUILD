# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=routersploit-git
_pkgname=routersploit
pkgver=20191215.r784
pkgrel=1
groups=('archstrike' 'archstrike-exploit')
pkgdesc='The Router Exploitation Framework'
arch=('any')
url='https://github.com/threat9/routersploit'
license=('GPL3')
depends=('python-requests' 'python-paramiko' 'python-future' 'python-pysnmp' 'python-pycryptodome')
makedepends=('git')
source=("${pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}" || exit
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

package() {
  cd "${pkgname}" || exit
  install -dm755 "${pkgdir}/usr/share/${_pkgname}"
  install -dm755 "${pkgdir}/usr/bin"
  cp -a --no-preserve=ownership ./* "${pkgdir}/usr/share/${_pkgname}/"
  # Move license to the package license directory
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Create launcher script
cat > "${pkgdir}/usr/bin/routersploit" << EOF
#!/usr/bin/env bash
cd /usr/share/routersploit
./rsf.py "\$@"
EOF
chmod 755 "${pkgdir}/usr/bin/${_pkgname}"
}
