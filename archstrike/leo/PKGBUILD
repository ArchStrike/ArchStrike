# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=leo
pkgver=6.3
pkgrel=1
pkgdesc="Literate programmer's editor, outliner, and project manager"
arch=('any')
url='http://leoeditor.com'
license=('custom')
depends=('python-pyqt5')
optdepends=('python-pyenchant: For Spelling support')
makedepends=('unzip' 'python-setuptools')
groups=('archstrike' 'archstrike-misc')
source=("https://github.com/leo-editor/leo-editor/archive/v${pkgver}.tar.gz")
sha512sums=('92f78741f81a208c6df429b4b2351f91dd6da663df6cfe6174e0749a15119fdf9af9be0e110108933e675c9bba1423c36aaeeb89cab523f52023cf6cc7a018e3')

package() {
  cd "${pkgname}-editor-${pkgver}"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  cp -a --no-preserve=ownership ./* "${pkgdir}/usr/share/${pkgname}/"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -dm755 "${pkgdir}/usr/bin"
  printf '%s\n%s\n' '#!/usr/bin/env bash' 'python /usr/share/leo/launchLeo.py "$1"' > "${pkgdir}/usr/bin/${pkgname}"
  chmod 755 "${pkgdir}/usr/bin/${pkgname}"
}
