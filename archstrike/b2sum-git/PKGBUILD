# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Michael DÃ¼ll <michael.duell@rub.de>
# Contributor: Dalton Miller

buildarch=128

pkgname=b2sum-git
pkgver=20161109.r99
pkgrel=1
pkgdesc="BLAKE2 file hash sum check. Computes the BLAKE2 (BLAKE2b or -s, -bp, -sp) cryptographic hash of a given file"
arch=('x86_64')
url="https://blake2.net/"
groups=('archstrike')
license=('custom')
depends=('gcc-libs-multilib')
makedepends=('unzip' 'gnuplot' 'git')
provides=("b2sum")
replaces=("b2sum")
conflicts=("b2sum")
source=("$pkgname::git+https://github.com/BLAKE2/BLAKE2.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  cd "$pkgname/b2sum"
  sed -i "s# -fopenmp##" makefile
  make clean
}

build() {
  cd "$pkgname/b2sum"
  make all
}

package() {
  cd "$pkgname/b2sum"
  install -Dm755 b2sum "${pkgdir}"/usr/bin/b2sum
  install -Dm644 ../COPYING "${pkgdir}"/usr/share/licenses/$pkgname/COPYING
  install -Dm644 ../README.md "${pkgdir}"/usr/share/$pkgname/README.md
}
