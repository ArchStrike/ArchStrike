# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=hasher-git
pkgver=r48.40173c5
pkgrel=5
groups=('archstrike' 'archstrike-crackers')
pkgdesc="A tool that allows you to quickly hash plaintext strings, or compare hashed values with a plaintext locally."
arch=('any')
url='https://github.com/FortyNorthSecurity/Hasher'
license=('GPL2')
depends=('python2-passlib' 'python2-bcrypt' 'python2-wsgiref')
makedepends=('git' 'python2-setuptools')
provides=('hasher')
source=("git+https://github.com/FortyNorthSecurity/Hasher.git")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/Hasher"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  cd "$srcdir/Hasher"
  sed -i 's|env python$|env python2|' *.py
  sed -i 's|py-bcrypt>=0.4|bcrypt>=3.0.0|' *.py
}

package() {
  cd "$srcdir/Hasher"

  python2 setup.py install --root=$pkgdir --optimize=1

  #License
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  sed -i 's|env python$|env python2|' $pkgdir/usr/lib/python2.7/site-packages/hashes/__main__.py

}
