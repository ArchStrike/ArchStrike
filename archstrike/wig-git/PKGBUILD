# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=wig
pkgname=$_pkgname-git
pkgver=r562.a8b237a
pkgrel=1
groups=('archstrike' 'archstrike-webapps' 'archstrike-scanners')
pkgdesc='WebApp Information Gatherer'
arch=('any')
url='https://github.com/jekyc/wig'
license=('custom')
provides=('wig')
depends=('python-requests')
makedepends=('git')
options=('!emptydirs')
source=("$_pkgname::git+https://github.com/jekyc/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd $_pkgname

  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/$_pkgname"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -dm755 "$pkgdir/usr/share/$_pkgname/cache"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$_pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname"

  cat > "$pkgdir/usr/bin/$_pkgname" <<EOF
#!/usr/bin/env bash
cd /usr/share/$_pkgname

if [[ "\$1" = "" ]]; then
    ./$_pkgname.py -h
else
    ./$_pkgname.py "\$@"
fi
EOF

  chmod 755 "$pkgdir/usr/bin/$_pkgname"
}
