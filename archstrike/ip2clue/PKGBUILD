# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Prurigro

buildarch=220

pkgname=ip2clue
pkgver=0.0.94
pkgrel=1
pkgdesc="A mall memory/CPU footprint daemon to lookup country (and other info) based on IP (v4 and v6)"
url="http://kernel.embedromix.ro/us/"
license=('GPL3')
groups=('archstrike' 'archstrike-networking')
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
depends=('bash' 'conn')
options=('!emptydirs')
source=("http://kernel.embedromix.ro/us/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}d.service")
sha512sums=('d33f9fab32d7539ccf180c5be9320c03ced76db1aac76899f0f29fe78655ca225699f9da78c30b0b678261b95c2a3bedfd72a621aaea8428699ba6103b48c766'
            '90e43e1a1482c2ab2234a63156a03bc728132abeccb7330d6326dbd93140b8a9bd109e92006c6fb9f9d4de553b4663452173953ae334ca06bd7864a75e4dc947')

build() {
    cd "${pkgname}-${pkgver}"
    ./configure --prefix=/usr
    make
}

package() {
    install -Dm644 "${pkgname}d.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}d.service"
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    mv "${pkgdir}"/usr/sbin/* "${pkgdir}"/usr/bin
    rm -rf "${pkgdir}/etc/rc.d" # We're using systemd instead
}
