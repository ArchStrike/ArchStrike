# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=dirsearch
pkgver=0.4.1.alpha2
pkgrel=1
groups=('archstrike' 'archstrike-webapps')
pkgdesc='A HTTP(S) directory/file brute forcer'
arch=('any')
url='https://github.com/maurosoria/dirsearch'
license=('custom')
depends=('python' 'python-urllib3' 'python-chardet' 'python-certifi')
source=("https://github.com/maurosoria/${pkgname}/archive/v${pkgver/.a/-a}.tar.gz")
sha512sums=('192aa5c011e2a3e8ece6d1dca4f7a5d560486c628262107adbd944957e09a08498e37f5472ed3e4178c5e95643a45e8ae96f66ff18fa5946b862e68dc88b46e1')

package() {
  cd "${pkgname}-${pkgver/.a/-a}"

  # Make base directories.
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"

  for dir in db logs lib thirdparty reports; do
    install -dm755 "${pkgdir}/usr/share/${pkgname}/${dir}"
    cp -a --no-preserve=ownership "${dir}"/* "${pkgdir}/usr/share/${pkgname}/${dir}/"
  done

  install -Dm755 ${pkgname}.py "${pkgdir}/usr/share/${pkgname}"
  install -Dm755 default.conf "${pkgdir}/usr/share/${pkgname}"
  install -Dm644 README.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  ln -s "/usr/share/${pkgname}/${pkgname}.py" "${pkgdir}/usr/bin/${pkgname}"
}
