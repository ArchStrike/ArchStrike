# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=dirsearch
pkgver=0.4.0
pkgrel=1
groups=('archstrike' 'archstrike-webapps')
pkgdesc='A HTTP(S) directory/file brute forcer'
arch=('any')
url='https://github.com/maurosoria/dirsearch'
license=('custom')
depends=('python')
source=("https://github.com/maurosoria/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('155108d5b78dd2e889b5a39957c79b61e92470c29c97c9a236f706e022ec3c315ebcc955e1cbbb059ba7a7042bb907b28b53403d9ba72edfdb504344613bd80f')

package() {
  cd "${pkgname}-${pkgver}"

  # Make base directories.
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"

  for dir in db logs lib thirdparty reports; do
    install -dm755 "${pkgdir}/usr/share/${pkgname}/${dir}"
    cp -a --no-preserve=ownership "${dir}"/* "${pkgdir}/usr/share/${pkgname}/${dir}/"
  done

  install -Dm755 ${pkgname}.py "${pkgdir}/usr/share/${pkgname}"
  install -Dm755 default.conf "${pkgdir}/usr/share/${pkgname}"
  install -Dm644 README.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  ln -s "/usr/share/${pkgname}/${pkgname}.py" "${pkgdir}/usr/bin/${pkgname}"
}
