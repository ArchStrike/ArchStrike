# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: xorond <xorond@gmail.com>
_author='Karel Miko'
_perlmod='CryptX'

buildarch=220

pkgname=perl-crypt-x
pkgver=0.071
pkgrel=1
pkgdesc="Cryptographically secure random number generator"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="https://metacpan.org/pod/CryptX"
license=('custom')
depends=('perl>=5.10.0')
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/M/MI/MIK/CryptX-${pkgver}.tar.gz")
sha512sums=('e2bb2cc33290e44d1896d069207e854c82a93430ee6b8abf10aeb0138d1209d8bb8e973bcac1674fae7a3500b54fe935fdbb1f1b172a9e3ec9f779dcaddfb1ff')

build() {
  cd "${srcdir}/${_perlmod}-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_perlmod}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_perlmod}-${pkgver}"

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  make install DESTDIR="${pkgdir}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
