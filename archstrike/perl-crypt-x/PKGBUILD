# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: xorond <xorond@gmail.com>
_author='Karel Miko'
_perlmod='CryptX'

buildarch=220

pkgname=perl-crypt-x
pkgver=0.068
pkgrel=2
pkgdesc="Cryptographically secure random number generator"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="https://metacpan.org/pod/CryptX"
license=('custom')
depends=('perl>=5.10.0')
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/M/MI/MIK/CryptX-${pkgver}.tar.gz")
sha512sums=('f6e792a94dc33f391c40d24ed525d1c13443c88e7c3b29d46a2510f9a4bb9afddcc62e8ab19fdb20e15d8a3ab17897f9aff8d79b5b51ce1ef1b25d14f49e5227')

build() {
  cd "${srcdir}/${_perlmod}-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_perlmod}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_perlmod}-${pkgver}"

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  make install DESTDIR="${pkgdir}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
