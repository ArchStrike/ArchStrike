# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Claire Farron <diesal3@googlemail.com>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

buildarch=220

_pkgname=cryptsetup
pkgname=$_pkgname-nuke-keys
pkgver=1.7.2
pkgrel=1
pkgdesc='A patched cryptsetup which adds the option to nuke all keyslots given a certain passphrase'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL')
url='https://github.com/offensive-security/cryptsetup-nuke-keys'
groups=('archstrike')
depends=('device-mapper' 'libgcrypt' 'popt' 'libutil-linux')
options=('!emptydirs')
provides=("$_pkgname")
conflicts=("$_pkgname")

source=(
  "https://www.kernel.org/pub/linux/utils/cryptsetup/v1.7/$_pkgname-$pkgver.tar.xz"
  'encrypt_hook'
  'encrypt_install'
  'sd-encrypt'
  'cryptsetup.c.patch'
  'keymanage.c.patch'
  'libcryptsetup.h.patch'
  'setup.c.patch'
)

sha512sums=(
  'ff761bd0c5e9a7941fd27d55839804f2ce96145a45a9689d234954fc43c6172c913c59c83b37ef6cc5459ccecff63212c369077fdea70c14326372076eae7f86'
  '27fe07cb8098534a1666f579920d4ac49c3b688e6a50320e98e3f6520e641a1c965541a19292e5872858d86c0a9df052496c3d81637bf6e1da3c1b43884a401d'
  '9a98b696de6d0ea10018dd0aabbc04af3f75407261d91a0299a339715b0278638868d44e4ca6d35bb9ce9575285491ae78f932f8468a34084b185483ddab3fd0'
  '76892c8f6fb2143c4964cb1233bb4dda44de0a26909e4da65be6f702ebe4b242a74c93f7910f57d1ca8f1ac322dbf926e215ad42181632f6ef535cf92c6e7a91'
  '7b43e60e1e905bbce31b01f3a3f7a9458e5f60b7e97c5554d5dee415f5619d3d2633708acc03e66a64e44a22a6cedb5739100fda97ec8126e0e632897fcc39d4'
  '73376be26b6240a500a277a66eff23f3dcea8240f4ee7185f3c1f11811b89b300a92621ec3913a9cca81be39e9d1cfcba72e716d4a360e0eacd0c9f1c60c0333'
  '3d9f5599b531d3f4506143d2982cb2fe6b75fdbfbd4d73b9e09ee354b6a409d48fc16af8ba46d218a9d04149c72a436dfc8251f2a2516fa55a2befbfc3872b9f'
  '964bd334ad37ea4a8502125673a2d062ee36b886fc2738455a43ce8a76f026a34eaba434a8a36f8236c633a6cb8bdf8eda15b1b0338d505d651f1eb0970ddc6e'
)

prepare() {
  cd $_pkgname-$pkgver

  # Patch in nuke keys support
  patch -p1 < ../cryptsetup.c.patch
  patch -p1 < ../keymanage.c.patch
  patch -p1 < ../libcryptsetup.h.patch
  patch -p1 < ../setup.c.patch
}

build() {
  cd $_pkgname-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin --disable-static --enable-cryptsetup-reencrypt
  make
}

package() {
  # install hook
  install -Dm644 encrypt_hook "$pkgdir/usr/lib/initcpio/hooks/encrypt"
  install -Dm644 encrypt_install "$pkgdir/usr/lib/initcpio/install/encrypt"
  install -Dm644 sd-encrypt "$pkgdir/usr/lib/initcpio/install/sd-encrypt"

  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
