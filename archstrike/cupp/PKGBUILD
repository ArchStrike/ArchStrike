# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname='cupp'
pkgver='3.0'
pkgrel=3
pkgdesc='Common User Password Profiler'
url='http://www.remote-exploit.org/?page_id=418'
groups=('archstrike' 'archstrike-crackers')
license=('GPL3')
depends=('python2')
arch=('any')
sha512sums=('54a40f7c3a373104a702b62f9c90f941261f0e0b1f21eb4edaca4c638c5f8db78fce4fe301c8ada61a0293cf5bbe103719b9ea95133f1544d7b27726e38b72c5')
source=("http://www.remote-exploit.org/content/cupp-$pkgver.tar.gz")

prepare(){
  grep -iRl 'python' "$srcdir/cupp" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/cupp"

  # Base directories
  install -dm755 "$pkgdir/usr/share/cupp"
  install -dm755 "$pkgdir/usr/bin"

  # Bin
  install -D -m755 "cupp.py" "$pkgdir/usr/share/cupp/cupp.py"
  install -D -m644 "cupp.cfg" "$pkgdir/usr/share/cupp/cupp.cfg"

  # Docs
  install -D -m644 "docs/README" "$pkgdir/usr/share/doc/cupp/README"
  install -D -m644 "docs/LICENSE" "$pkgdir/usr/share/licenses/cupp/LICENSE"

  # Wrapper script
cat > "$pkgdir/usr/bin/cupp" << EOF
#!/bin/bash
cd /usr/share/cupp
python2 ./cupp.py "\$@"
EOF

chmod +x "$pkgdir/usr/bin/cupp"
}
