# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=XlsxWriter
pkgname=python2-${_pkgname,,}
pkgver=1.3.7
pkgrel=1
pkgdesc="A Python module for creating Excel XLSX files"
url="https://pypi.org/project/XlsxWriter"
depends=('python2')
makedepends=('python2-setuptools')
license=('MIT')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('6ad8bd7fe308a15056e3b2572763c7db8509dcd9828351a0122d700312468abf90d5e634c1aea4fdb0c7900de271d92eb39603ddc285ec8be010f4537b2ccf4d')

build() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py build
}

package() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py install --root="$pkgdir" -O1 --skip-build
  
  # fix conflict with py3 version
  mv ${pkgdir}/usr/bin/vba_extract.py ${pkgdir}/usr/bin/vba_extract2.py
}
