# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=pygitlab
pkgver=3.8.1
pkgrel=1
pkgdesc="A Python wrapper for the GitLab API"
url="https://github.com/gpocentek/python-gitlab"
arch=('any')
license=('GPL3')
depends=('python' 'python-requests' 'python-requests-toolbelt')
makedepends=('python-setuptools')
source=("https://github.com/gpocentek/python-gitlab/archive/v${pkgver}.tar.gz")
sha512sums=('245918a3d2888ea80753c86acc363aab48af25ebbc951aecee6171fc7b2994761bc358d28bea562349e2825ad5b3279f1074b805f36fca80ff76680bebffba5b')

package(){
  cd "${srcdir}/python-gitlab-${pkgver}"
  python setup.py install --root="${pkgdir}" -O1
  install -Dm644 README.rst "${pkgdir}/usr/share/${pkgname}/README.rst"
  # Rename bin to not conflict with gitlab
  mv "${pkgdir}/usr/bin/gitlab" "${pkgdir}/usr/bin/pygitlab"
}
