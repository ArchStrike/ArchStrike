# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_gitname=RFIDIOt
pkgname=rfidiot-git
pkgver=20200701.r107
pkgrel=1
epoch=1
groups=('archstrike' 'archstrike-wireless')
pkgdesc="An open source python library for exploring RFID devices."
url='http://rfidiot.org'
license=('GPL')
depends=('python2' 'openssl' 'python2-pyscard')
makedepends=('git' 'python2-setuptools' 'python2-crypto')
arch=('any')
source=("${pkgname}::git+https://github.com/AdamLaurie/RFIDIOt.git")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  cd "${srcdir}/${pkgname}"
  # Change shebang lines.
  find . -type f -name '*.py' -exec sed -i '1s/python\r\?$/python2/' '{}' \;
}

package() {
  cd "${srcdir}/${pkgname}"
  python2 setup.py install --root="${pkgdir}/" -O1

  cd "${pkgdir}/usr/bin"

  # Remove test scripts.
  rm -f *.sh

  # Strip extensions and add the 'rfidiot-' prefix.
  install -Dm755 rfidiot-cli.py "${pkgdir}/usr/bin/rfidiot-cli"
  rm rfidiot-cli.py
  for file in *.py ; do
    # Remove extension. Add prefix.
    new_file=rfidiot-$(basename "${file%.*}")
    # '_' -> '-'
    new_file="${new_file/_/-}"
    install -Dm755 "$file" "$new_file"
    rm -f "$file"
  done
}
