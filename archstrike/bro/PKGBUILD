# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=bro
pkgver=2.6.4
pkgrel=1
pkgdesc="A highly configurable IDS"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://bro-ids.org/"
backup=(etc/bro/{broctl.cfg,networks.cfg,node.cfg})
license=('BSD')
depends=('curl' 'file' 'geoip' 'gperftools' 'ipsumdump' 'postfix'
         'bind-tools' 'openssl' 'libpcap' 'bash' 'python2' 'zlib')
makedepends=('cmake' 'swig' 'bison' 'flex')
install=${pkgname}.install
groups=('archstrike' 'archstrike-networking')
options=('!emptydirs')
source=("https://github.com/zeek/zeek/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "$install")
sha512sums=('9a40149807eded32a9862aa1f51a21ffd72232d961023465bf380333cfbc1aecc18c219baca92c1883565e608dfd1f5e1a6ec448b3fbe8b7ca62ce06ff277528'
            '4b8cc874148b9a640809420950d07b3f9ffe1c16a5fe145c85c17fa2f2a8f023a96370e810c895a5fab173cc9d32908ad0b8e603ae8a3206d52ed65f179bec00')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  if [[ ${CARCH} == i686 ]] || [[ ${CARCH} == armv6h ]] || [[ ${CARCH} == armv7h ]]; then
    LDFLAGS="${LDFLAGS} -latomic"
    ./configure --prefix="/usr" --conf-files-dir="/etc/bro" --spooldir="/usr/share/bro" \
    --logdir="/usr/share/bro/logs" --localstatedir="/var/spool" --with-python=/usr/bin/python2 \
    --enable-mobile-ipv6
  else
    ./configure --prefix="/usr" --conf-files-dir="/etc/bro" --spooldir="/usr/share/bro" \
    --logdir="/usr/share/bro/logs" --localstatedir="/var/spool" --with-python=/usr/bin/python2 \
    --enable-mobile-ipv6
  fi

  make DESTDIR="${pkgdir}"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p "${pkgdir}/usr/share/bro/logs"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  find "${pkgdir}" -type f -print0 | xargs -0 sed -i "/# This file was automatically generated by bifcl/d"
  find "${pkgdir}" -type f -print0 | xargs -0 sed -i 's|/usr/bin/env python|/usr/bin/env python2|'
  find "${pkgdir}" -type f -print0 | xargs -0 sed -i 's|/usr/bin/python2|/usr/bin/env python2|'
}
