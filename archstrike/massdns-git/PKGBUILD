# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=massdns-git
pkgver=20210125.r219
pkgrel=1
groups=('archstrike' 'archstrike-dns' 'archstrike-networking')
pkgdesc='A high-performance DNS stub resolver in C'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/blechschmidt/massdns'
license=('GPL3')
depends=('ldns' 'python2')
makedepends=('git')
source=("${pkgname}::git+${url}")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

build() {
  cd "${srcdir}/${pkgname}"
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -Dm755 bin/massdns "${pkgdir}/usr/bin/massdns"
  cp -a --no-preserve=ownership lists scripts tests src "${pkgdir}/usr/share/${pkgname}"
  install -Dm644 README.md "${pkgdir}/usr/share/${pkgname}/README.md"
  grep -iRl python "${pkgdir}/usr/share/${pkgname}" | xargs sed -i 's|python$|python2|g'
}
