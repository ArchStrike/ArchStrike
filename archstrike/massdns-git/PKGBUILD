# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=massdns-git
pkgver=20200322.r182
pkgrel=1
groups=('archstrike' 'archstrike-dns' 'archstrike-networking')
pkgdesc='A high-performance DNS stub resolver in C'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/blechschmidt/massdns'
license=('GPL3')
depends=('ldns' 'python2')
makedepends=('git')
provides=("massdns")
replaces=("massdns")
source=("$pkgname::git+https://github.com/blechschmidt/massdns.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  install -dm755 $pkgdir/usr/share/$pkgname
  install -Dm755 bin/massdns $pkgdir/usr/bin/massdns
  cp -a --no-preserve=ownership lists scripts tests modules "${pkgdir}/usr/share/${pkgname}"
  install -Dm644 README.md $pkgdir/usr/share/$pkgname/README.md

  # fix python issues
  grep -iRl python ${pkgdir}/usr/share/${pkgname} | xargs sed -i 's|python$|python2|g'
}
