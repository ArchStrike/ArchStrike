# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

_pkgname="regex"
pkgname="python2-${_pkgname}"
pkgver=2020.11.13
pkgrel=1
pkgdesc="Alternative regex module for python, with additional functionality"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="https://bitbucket.org/mrabarnett/mrab-regex"
license=('MIT')
depends=('python2')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('9cbb1f654d4e0bd4493204086bb13fd80506e5ee0cbcb38e8595e634141485462535b5799887abc2a627e9a3eded0ed942c2c5b3ea935fa8a6ddcf33c8302da9')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py install --root "${pkgdir}" --skip-build
  install -Dm644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
