# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=w3af
pkgver=1.6.54
pkgrel=4
groups=('archstrike' 'archstrike-webapps' 'archstrike-fuzzers' 'archstrike-scanners')
pkgdesc="Web Application Attack and Audit Framework"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://w3af.sourceforge.net'
license=('GPL')
depends=(
  'graphviz' 'gtk2' 'halberd' 'libxml2' 'libxslt' 'mitmproxy' 'pdfminer'
  'pygtk' 'pygtksourceview2' 'python2-pyasn1' 'python2-beautifulsoup3'
  'python2-chardet' 'python2-clamd' 'python2-cluster' 'python2-dartspylru-git'
  'python2-esmre' 'python2-flask' 'python2-futures' 'python2-gitpython'
  'python2-gobject' 'python2-guess-language' 'python2-jinja' 'python2-lxml'
  'python2-markdown' 'python2-msgpack' 'python2-ndg-httpsclient' 'python2-nltk'
  'python2-ntlm' 'python2-phply' 'python2-pip' 'python2-psutil'
  'python2-pybloomfiltermmap' 'python2-pygithub' 'python2-pyopenssl'
  'python2-pysqlite' 'python2-ruamel.ordereddict' 'python2-stopit'
  'python2-tblib' 'python2-vulndb-git' 'python2-xdot' 'pywebkitgtk' 'scapy')
depends_x86_64+=('lib32-glibc')
makedepends=('git')
options=('!strip')
source=(
  "https://github.com/andresriancho/w3af/archive/$pkgver.tar.gz"
  "$pkgname.desktop"
  "$pkgname-no-update.patch"
)
sha512sums=('8132bb3ee0433739b9a3bd4a6b25d90e9f71d915eb1f7a63c382199bfc8b9dc5abe6bdd73c7715ab5804e6cf22a99f8ba98ac39cee92a5f3e547f7cb939f8ac6'
            'f4f2f5b115c4c42457fe0a4700b3a4b06eedd25bd19d7e31a642d5217c28a79af4aef61aa644bb9096faac1bb75919458beb69f16d8cce8fd8a4b7ce11d421e6'
            '1cf6d9322bb21a3ca04641eb80ed38a5acae640bda6b28975667351e7debfa939ff51563e66e0508a33c5f9ac41f0bebb9944fef81886ed4f7f2cbf9d5d2d710')

prepare() {
  # Delete some of the stuff from w3af-extlib (stuff that needs to be installed) -> dependencies
  cd $pkgname-$pkgver
  rm -rf $pkgname/extlib/fpconst-0.7.2
  rm -rf $pkgname/extlib/SOAPpy

  # Patchto remove the update and set the version
  patch -Np1 -i ../$pkgname-no-update.patch

  # Remove dependency checks.
  sed -i 's/dependency_check()/#&/' ${pkgname}_{console,gui}

  # Change the default metasploit location.
  sed -i 's|/opt/metasploit3/bin/|/usr/share/metasploit/|' $pkgname/core/controllers/misc_settings.py

  # Fix python2 hash-bang
  sed -i 's|#!\s*/usr/bin.*python.*$|#!/usr/bin/env python2|g' $(egrep -rl '\s*#!\s*/usr/bin.*python')
}

package() {
  # Install the XDG desktop file
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"

  # Install w3af
  cd $pkgname-$pkgver
  install -dm755 "$pkgdir/usr/share/$pkgname"
  cp --no-preserve=ownership -a * "$pkgdir/usr/share/$pkgname"

  # Install launch scripts for w3af_console and w3af_gui
  install -dm755 "$pkgdir/usr/bin"
  printf '%s\n\n%s\n' '#!/usr/bin/env bash' 'python2 /usr/share/w3af/w3af_console "$@"' \
    > "$pkgdir/usr/bin/$pkgname"
  printf '%s\n\n%s\n' '#!/usr/bin/env bash' 'python2 /usr/share/w3af/w3af_gui "$@"' \
    > "$pkgdir/usr/bin/$pkgname-gui"
  chmod 755 "$pkgdir"/usr/bin/$pkgname{,-gui}
}
