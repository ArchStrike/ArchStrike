# Maintainer: ArchStrike <team@archstrike.org>
# Contributor drathir <drathir87@gmail.com>

buildarch=220

pkgname=hashdeep
pkgver=4.4
pkgrel=2
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('archstrike' 'archstrike-forensics')
pkgdesc="Advanced checksum hashing tool"
url="https://github.com/jessek/hashdeep"
license=('Public Domain' 'GPL')
provides=('md5deep')
replaces=('md5deep')
conflicts=('md5deep')
depends_x86_64=('gcc-libs-multilib')
source=("https://github.com/jessek/hashdeep/archive/release-$pkgver.tar.gz" "gcc6.1-fixes.patch")
sha512sums=('c37ec85ed04d4452f9038f43434c02e064cf9e17c4eaa233867dba0236443922a14ccb8ec76c68820087751c2ca3db014d3f17dd8fcd2c2bde84d620aae50de1'
            'dd0ec1fde21d37dd0a0dc95a7198564c2f07cf13f224eff4157d9c0438717f66d07173a13c65eecbfbb2eb3f17f6359ecc0f8e15ae4765de44957de168eafd7b')

prepare() {
  cd "$srcdir/hashdeep-release-$pkgver"
  patch -Np1 -i $srcdir/gcc6.1-fixes.patch
}

build() {
  cd "$srcdir/hashdeep-release-$pkgver"

  sh ./bootstrap.sh

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/hashdeep-release-$pkgver"

  make DESTDIR=$pkgdir install
  install -D -m644 "$srcdir/hashdeep-release-$pkgver/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
