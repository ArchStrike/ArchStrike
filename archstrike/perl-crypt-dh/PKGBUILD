# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=perl-crypt-dh
pkgver=0.07
pkgrel=13
pkgdesc="Perl/CPAN Module Crypt::DH: Diffie-Hellman key exchange system"
_cpan_name=Crypt-DH-${pkgver}
url='https://metacpan.org/pod/Crypt::DH'
license=(GPL PerlArtistic)
arch=('any')
makedepends=(make)
depends=('perl' 'perl-math-bigint-gmp' 'perl-module-install')
source=("http://search.cpan.org/CPAN/authors/id/M/MI/MITHALDU/${_cpan_name}.tar.gz")
sha512sums=('60e58403e513535463290255989e9f2263107aeae6831d51d761e18527b1745baa45c16f655452fd618a0fb0e98413d5ed68f3926247ab2ad2da4032863b8ee7')

build() {
  cd "${srcdir}/${_cpan_name}"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  /usr/bin/perl Makefile.PL
  make
}

package() {
  cd "${srcdir}/${_cpan_name}"
  make pure_install DESTDIR="${pkgdir}"

  cd "${pkgdir}"
  find . -name '.packlist' -delete
  find . -name '*.pod'     -delete
}
