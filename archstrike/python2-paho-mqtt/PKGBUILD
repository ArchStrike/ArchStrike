# Maintainer: ArchStrike <team archstrike org>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>

buildarch=1

pkgbase=paho-mqtt
_pkgname=paho.mqtt.python
pkgname=(python-$pkgbase python2-$pkgbase)
pkgver=1.2
pkgrel=1
url="http://www.eclipse.org/paho/clients/python"
license=('EPL')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/eclipse/paho.mqtt.python/archive/v${pkgver}.tar.gz")
sha512sums=('c1db1ac287ca15e9e4af03c09e73c1abc502204641ac1be72d3e1c0ef0fbcb4bc4f97afd2b17410e3d196f0441daaa3c4ffda644d33e92ec014f94848e2e7b81')


prepare() {
   cp -r $_pkgname-${pkgver} python2-$_pkgname-${pkgver}
}

build() {
   cd $srcdir/$_pkgname-${pkgver}
   python setup.py build

   cd $srcdir/python2-$_pkgname-${pkgver}
   python2 setup.py build
}

package_python-paho-mqtt() {
    pkgdesc="Python client library for MQTT v3.1 and v3.1.1"
    depends=('python')

    cd $srcdir/$_pkgname-$pkgver
    python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-paho-mqtt() {
    pkgdesc="Python 2 client library for MQTT v3.1 and v3.1.1"
    depends=('python2')

    cd $srcdir/python2-$_pkgname-$pkgver
    python2 setup.py install --root="$pkgdir" --optimize=1
}
