# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname="lem"
pkgname=python2-${_pkgname}-git
pkgver=0.3.1.r3.g8587ff33
pkgrel=1
pkgdesc="Linux Exploit Mapper correlates CVEs local to a Linux system with known exploits"
arch=('any')
url="https://github.com/redteam-project/${_pkgname}"
license=('GPL3')
makedepends=("python2" "python2-setuptools" "git" "python2-requests" "python2-dateutil" "python2-argparse"
             "python2-redteamcore-git" "python2-cpe-git" "exploit-curation-git")
source=("${_pkgname}::git+${url}.git"
        "lem.patch")
sha512sums=('SKIP'
            'bef8e4659fb3cbafd1e133c833fa88200929fbee99f0ad7dc6cd90214a23f908fee82806b06061734375c2fc2c0886f6c8dc077082699898de053547b593dde2')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/${_pkgname}"
  grep -ZRil 'python' . | xargs -0 sed -i '1 s|#!.*/usr/bin/python3?|#!/usr/bin/python2|;s|#!.*/usr/bin/env python3?$|#!/usr/bin/env python2|'
}

build() {
  cd "${srcdir}/${_pkgname}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}"
  install -dm755 "${pkgdir}/usr/bin"
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:se ts=2 sw=2 et:
