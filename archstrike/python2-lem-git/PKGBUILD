# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname="lem"
pkgname=python2-${_pkgname}-git
pkgver=0.3.2.r6.g69ef2ccc
pkgrel=1
pkgdesc="Linux Exploit Mapper correlates CVEs local to a Linux system with known exploits"
arch=('any')
url="https://github.com/redteam-project/${_pkgname}"
license=('GPL3')
makedepends=("python2" "python2-setuptools" "git" "python2-requests" "python2-dateutil" "python2-argparse"
             "python2-redteamcore-git" "python2-cpe-git" "exploit-curation-git" "arch-audit")
source=("${_pkgname}::git+${url}")
options=('!emptydirs')
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/${_pkgname}"
  grep -ZRil 'python' . | xargs -0 sed -i '1 s|#!.*/usr/bin/python3?|#!/usr/bin/python2|;s|#!.*/usr/bin/env python3?$|#!/usr/bin/env python2|'
}

build() {
  cd "${srcdir}/${_pkgname}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}"
  install -dm755 "${pkgdir}/usr/bin"
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  rm -rf "${pkgdir}/build"
}
