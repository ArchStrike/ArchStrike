# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=pev
pkgver=0.80
pkgrel=2
groups=('archstrike' 'archstrike-forensics' 'archstrike-reverse')
pkgdesc="Command line based tool for PE32/PE32+ file analyze"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://pev.sourceforge.net'
license=('GPL')
depends=('openssl' 'pcre')
makedepends=('unzip')
install=${pkgname}.install
source=("http://sourceforge.net/projects/pev/files/pev-${pkgver}/pev-${pkgver}.tar.gz" "destdir.patch")
sha512sums=('d0c8ff8fcd60470899d5dbccfd7a7e4d960da007f42be9c7eab6b22a3d7cf4dde3c3d5cd9c7d70e369164503fb124640cf508ceda3d4792244cf33d11ab83321'
            '5cc0f64476b72a803f434f3253895b3626a6d715ab165ebca4e3e003c967cc5beca5ca05e49013ef91fe9c90ee5fae6667085113cf3c6d88891079353026153c')

prepare() {
    cd ${pkgname}
    sed -i 's|prefix = /usr/local|prefix = /usr|' src/Makefile lib/libpe/Makefile
    patch -Np1 -i ${srcdir}/destdir.patch
}

build() {
    cd ${pkgname}
    make
}

package() {
    cd ${pkgname}
    make DESTDIR="${pkgdir}" install
}
