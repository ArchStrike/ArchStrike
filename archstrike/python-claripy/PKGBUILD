# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=claripy
pkgname=python-${_pkgname}
pkgver=9.1.11752
pkgrel=1
pkgdesc="An abstraction layer for constraint solvers"
url="https://pypi.org/project/claripy/"
depends=('python' 'python-ana' 'python-z3' 'python-future' 'python-cachetools')
makedepends=('python-setuptools')
license=('BSD')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('268fadb45f09b15bef25d99027246e13a924d26c0d5505203af250489bc9ee36c9510e9f12b2858f4fdebe95af653300726475af31ad826cfbe848bd07fcb429')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py build
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}" -O1 --skip-build
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
