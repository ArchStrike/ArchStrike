# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=claripy
pkgname=python-${_pkgname}
pkgver=8.20.7.27
pkgrel=1
pkgdesc="An abstraction layer for constraint solvers"
url="https://pypi.org/project/claripy/"
depends=('python' 'python-ana' 'python-z3' 'python-future' 'python-cachetools')
makedepends=('python-setuptools')
license=('BSD')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('e851f243a7d1d7eb5ec81f8321f124104c8220223548d8921fcb582706678e7f3c8edca9697c388c4a5c9082163904b5dd4d63c0652df11720aebd28d6515f5d')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py build
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}" -O1 --skip-build
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
