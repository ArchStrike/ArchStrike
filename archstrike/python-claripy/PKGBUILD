# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=claripy
pkgname=python-${_pkgname}
pkgver=9.0.4495
pkgrel=1
pkgdesc="An abstraction layer for constraint solvers"
url="https://pypi.org/project/claripy/"
depends=('python' 'python-ana' 'python-z3' 'python-future' 'python-cachetools')
makedepends=('python-setuptools')
license=('BSD')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('2afbf2906b00830be42cd5290c481175de8b32d42d981f968ba1e5d70ff7f85141efa1138fb6c7b04a092af178f76995efe225d1daea88f423c930aca75dce52')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py build
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}" -O1 --skip-build
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
