# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=fuzzap-git
pkgver=r17.057002b
pkgrel=4
groups=('archstrike' 'archstrike-wireless')
pkgdesc="A python script for obfuscating wireless networks"
arch=('any')
url='https://github.com/PlantDaddy/FuzzAP'
license=('custom')
depends=('python2' 'python2-scapy')
makedepends=('git')
provides=('fuzzap')
conflicts=('fuzzap')
replaces=('fuzzap')
source=("fuzzap::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/fuzzap"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  cd "$srcdir/fuzzap"
  sed -i 's|python$|python2|' *.py
}

package() {
  cd "$srcdir/fuzzap"

  # Make base directories.
  install -dm755 "$pkgdir/usr/share/fuzzap"
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/licenses/fuzzap-git"

  install -Dm755 fuzzap.py "$pkgdir/usr/share/fuzzap/fuzzap.py"
  install -Dm644 common.txt "$pkgdir/usr/share/fuzzap/"
  install -Dm644 ssid.txt "$pkgdir/usr/share/fuzzap/"
  install -Dm644 README.md "$pkgdir/usr/share/licenses/fuzzap-git/"

cat > "$pkgdir/usr/bin/fuzzap" << EOF
#!/usr/bin/env bash
cd /usr/share/fuzzap
python2 fuzzap.py "\$@"
EOF

chmod 755 "$pkgdir/usr/bin/fuzzap"

}
