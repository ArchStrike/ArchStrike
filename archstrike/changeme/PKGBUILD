# Maintainer: ArchStrike <team archstrike org>

pkgname=changeme
pkgver=0.3.1
pkgrel=2
groups=('archstrike' 'archstrike-scanners')
pkgdesc="A default credential scanner"
arch=('any')
url='https://github.com/ztgrace/changeme'
license=('GPL3')
depends=('python2-requests' 'python2-logutils' 'python2-yaml' 'python2-netaddr' 'python2-responses' 'python2-nose' 'python2-lxml' 'python2-cerberus' 'shodan')
source=("https://github.com/ztgrace/changeme/archive/$pkgver.tar.gz")
sha512sums=('186ef97cf9aee58a30a89255238a545117d23c893f1c67be784cd81d91d26da9716e6152f58f42b02c880e2ff10b750fc0ef38fcf115c5ae128ec18e15edbc89')


prepare(){
  grep -iRl 'python' "$pkgname-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}


package() {
  cd "$pkgname-$pkgver"
  install -dm755 $pkgdir/usr/share/$pkgname
  install -dm755 $pkgdir/usr/bin
  for py in *.py; do install -Dm755 $py $pkgdir/usr/share/$pkgname;done
  for i in creds tests; do install -dm755 $i $pkgdir/usr/share/$pkgname/$i; cp -a --no-preserve=ownership $i/* "$pkgdir/usr/share/$pkgname/$i/";done

cat > $pkgdir/usr/bin/$pkgname <<EOF
#!/bin/sh
cd /usr/share/$pkgname
python2 $pkgname.py "\$@"
EOF
chmod 755 $pkgdir/usr/bin/$pkgname
}
