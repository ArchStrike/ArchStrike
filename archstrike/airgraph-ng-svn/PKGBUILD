# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=airgraph-ng
pkgname=${_pkgname}-svn
pkgver=3023
pkgrel=1
pkgdesc='Graphing tool for the aircrack suite'
arch=('any')
url='http://www.aircrack-ng.org'
license=('GPL2')
depends=('aircrack-ng' 'graphviz' 'python2')
makedepends=('subversion')
groups=('archstrike' 'archstrike-misc')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=('svn+http://svn.aircrack-ng.org/trunk/scripts/airgraph-ng')
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname
  svnversion | tr -d [A-z]
}

prepare() {
  cd $_pkgname

  while read -r; do
      sed -i -re 's|#!\s*(/usr)?(/local)?/bin/.*python.*$|#!/usr/bin/env python2|g' "$REPLY"
  done < <(egrep -rl '^\s*#!\s*(/usr)?(/local)?/bin/.*python')
}

build() {
  cd $_pkgname
  python2 setup.py build
}

package() {
  cd $_pkgname
  install -Dm644 man/${_pkgname}.1 "$pkgdir/usr/share/man/man1/${_pkgname}.1"
  install -Dm644 man/dump-join.1 "$pkgdir/usr/share/man/man1/dump-join.1"
  install -Dm644 test/test-1.txt "$pkgdir/usr/share/$_pkgname/test/test-1.txt"
  python2 setup.py install --root="$pkgdir" --record files.txt
}
