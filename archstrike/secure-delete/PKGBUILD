# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

DLAGENTS=('https::/usr/bin/curl -k -o %o %u')

pkgname=secure-delete
pkgver=3.1
pkgrel=3
depends=('bash')
groups=('archstrike' 'archstrike-defense' 'archstrike-forensics')
pkgdesc='Secure file, disk, swap, memory erasure toolkit'
url='http://www.thc.org'
license=('GPL')
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
source=("https://www.thc.org/releases/secure_delete-$pkgver.tar.gz")
sha512sums=('9e7ae1458ffcce53fac23cf9b8df673d0754e6c3ff7791c445f2290dfa04f01fd2eda8b7c5d0045883159a32438783c380f770d7277ca77c9642c68e8f42dd07')

prepare() {
  cd secure_delete-$pkgver

  sed -i -e 's/mktemp/mkstemp/g' sfill.c
  sed -i -e "s/sswap smem sdel-mod.o/sswap smem/" -e '/test.*sdel-mod/d' \
         -e "s/^srm: /srm: sdel-lib.o /" -e "s/^sfill: /sfill: sdel-lib.o /" \
         -e "s/^sswap: /sswap: sdel-lib.o /" -e "s/^smem: /smem: sdel-lib.o /" \
         Makefile
}

build() {
  cd secure_delete-$pkgver

  make
}

package() {
  cd secure_delete-$pkgver

  make \
    INSTALL_DIR="$pkgdir/usr/bin" \
    MAN_DIR="$pkgdir/usr/share/man" \
    DOC_DIR="$pkgdir/usr/share/doc/secure_delete" \
      install
  chmod 755 "$pkgdir"/usr/bin/*

  # patching the_cleaner.sh
  sed -i 's|smem|sdmem|' $pkgdir/usr/bin/the_cleaner.sh
  sed -i 's|srm|sdrm|' $pkgdir/usr/bin/the_cleaner.sh

  # Fix a conflict with community/smem.
  mv "$pkgdir/usr/bin/srm" "$pkgdir/usr/bin/sdrm"
  mv "$pkgdir/usr/bin/smem" "$pkgdir/usr/bin/sdmem"

  # Fix a conflict with community/srm.
  mv "$pkgdir/usr/share/man/man1/srm.1" "$pkgdir/usr/share/man/man1/sdrm.1"
  mv "$pkgdir/usr/share/man/man1/smem.1" "$pkgdir/usr/share/man/man1/sdmem.1"
}
