# Maintainer: ArchStrike <team@archstrike.org>
# Contributer: fnord0 <fnord0 AT riseup DOT net>

buildarch=220

pkgname=ghostpdl
pkgver=9.23
pkgrel=2
pkgdesc='An interpreter for the Postscript language and for PDF - GhostPCL (PCL-5, and PCL-XL), GhostXPS (XPS), GhostPDF and ghostscript'
url='http://www.artifex.com'
license=('GPL')
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('archstrike')
depends=('libxt' 'libxext' 'python' 'tcl')
source=("https://github.com/ArtifexSoftware/ghostpdl-downloads/releases/download/gs923/ghostpdl-$pkgver.tar.gz"
        "config.guess-for-aarch64.patch")
sha512sums=('477862b41c6546540d048ae0af078b4ade65509ad0533f21b79f9692d6b818bfb58fb2a29dd3f3dba5b722ac4c4273a4f6dc7935f46bbfbb336e41bf21deed26'
            '1216f676bfe4d9c007e9f615181602cf11a461ec7ee07da039cf2472a919339d75077c3463834a8c5818b46f442f907c716d0965816d859f6d6aef20f97a45f4')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i $srcdir/config.guess-for-aarch64.patch
}


build() {
  cd $pkgname-$pkgver
  OPTVALUE=0
  case $(uname -m) in 
    armv*|aarch65)
        OPTVALUE=2
        ;;
    *)
        OPTVALUE=0
        ;;
  esac
  CPPFLAGS="-DPNG_ARM_NEON_OPT=$OPTVALUE"
  ./configure --prefix=/usr --build=$(uname -m)
  make -j4
}

package() {
  cd $pkgname-$pkgver
  install -dm755 "$pkgdir/usr/share/$pkgname"
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm755 bin/gpcl6 "$pkgdir/usr/bin/gpcl6"
  install -Dm755 bin/gxps "$pkgdir/usr/bin/gxps"
  install -Dm755 pcl/tools/pcl2pdf "$pkgdir/usr/bin/pcl2pdf"
  install -Dm755 pcl/tools/pcl2pdfwr "$pkgdir/usr/bin/pcl2pdfwr"
  cp -r --no-preserve=ownership doc/* "$pkgdir/usr/share/doc/$pkgname/"
  cp -r --no-preserve=ownership pcl/tools "$pkgdir/usr/share/$pkgname/"
  cp -r --no-preserve=ownership pcl/urwfonts "$pkgdir/usr/share/$pkgname/"
}
