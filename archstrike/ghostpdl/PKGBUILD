# Maintainer: ArchStrike <team@archstrike.org>
# Contributer: fnord0 <fnord0 AT riseup DOT net>

buildarch=212

pkgname=ghostpdl
pkgver=9.19
pkgrel=1
pkgdesc='An interpreter for the Postscript language and for PDF - GhostPCL (PCL-5, and PCL-XL), GhostXPS (XPS), GhostPDF and ghostscript'
url='http://www.artifex.com'
license=('GPL')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
groups=('archstrike')
depends=('libxt' 'libxext' 'python' 'tcl')
source=("https://github.com/ArtifexSoftware/ghostpdl-downloads/releases/download/gs919/ghostpdl-$pkgver.tar.gz")
sha512sums=('44c7de7230355b8c9010bf2153e7fd3086ba6a18878a8174de9c11e1b6fb6b8171c41325b650cece69352c51448f242803bb240368cbcdabaf800ae50bc2486b')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  install -dm755 "$pkgdir/usr/share/$pkgname"
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm755 bin/gpcl6 "$pkgdir/usr/bin/gpcl6"
  install -Dm755 bin/gxps "$pkgdir/usr/bin/gxps"
  install -Dm755 pcl/tools/pcl2pdf "$pkgdir/usr/bin/pcl2pdf"
  install -Dm755 pcl/tools/pcl2pdfwr "$pkgdir/usr/bin/pcl2pdfwr"
  cp -r --no-preserve=ownership doc/* "$pkgdir/usr/share/doc/$pkgname/"
  cp -r --no-preserve=ownership pcl/tools "$pkgdir/usr/share/$pkgname/"
  cp -r --no-preserve=ownership pcl/urwfonts "$pkgdir/usr/share/$pkgname/"
}
