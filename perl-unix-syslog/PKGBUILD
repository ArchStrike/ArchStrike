# Maintainer
# Contributor drathir <drathir87@gmail.com>
pkgname='perl-unix-syslog'
_pkgname='Unix-Syslog'
pkgver='1.1'
pkgrel='2'
pkgdesc="Interface to syslog functions in a C-library"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url='http://search.cpan.org/dist/Unix-Syslog'
source=("http://search.cpan.org/CPAN/authors/id/M/MH/MHARNISCH/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('cb1ea3846844b7c82e081f3bb778c698b681d1875b83201b1beef2267a288890c6fc62af8edbfbc04553a9caa1ebafa429130e784f31c9531719b11a4c47c3ba')
_distdir="${_pkgname}-${pkgver}"

build() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/$_distdir"
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd "$srcdir/$_distdir"
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  make test
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
