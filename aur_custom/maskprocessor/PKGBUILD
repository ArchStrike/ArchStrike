# Maintainer: ArchStrike <archstrikelinux@gmail.com>
pkgname=maskprocessor
pkgver=0.73
pkgrel=1
groups=('archstrike' 'archstrike-cracker')
pkgdesc="A High-Performance word generator with a per-position configurable charset"
arch=('i686' 'x86_64')
url='http://hashcat.net/wiki/doku.php?id=maskprocessor'
license=('custom')
makedepends=('p7zip')
source=("http://hashcat.net/tools/maskprocessor/maskprocessor-$pkgver.7z")
sha512sums=('79da3253b8608df498ae420d09aa5ec9f5a7dff5071eeef70ebb2deb1a24ba9b903fca1bb03655b6a85d4c6fdd853bebe9fe55cec56447b6d5774e15ff83c434')

prepare() {
  cd "$srcdir/maskprocessor-$pkgver"

  sed -i 's|dicts/|"$1"/|' generate.sh
  sed -i 's|./mp64.bin|/usr/bin/maskprocessor|' generate.sh
}

package() {
  cd "$srcdir/maskprocessor-$pkgver"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/maskprocessor"

  # Bin.
  if [[ "$CARCH" == 'x86_64' ]]; then
    install -m755 mp64.bin "$pkgdir/usr/bin/mp64"
    ln -s mp64 "$pkgdir/usr/bin/maskprocessor"
  elif [[ "$CARCH" == 'i686' ]]; then
    install -m755 mp32.bin "$pkgdir/usr/bin/mp32"
    ln -s mp32 "$pkgdir/usr/bin/maskprocessor"
  fi
  install -m755 generate.sh "$pkgdir/usr/share/maskprocessor"
}
