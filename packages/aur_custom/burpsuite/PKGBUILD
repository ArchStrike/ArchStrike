# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname=burpsuite
pkgver=1.6
pkgrel=1
groups=('archstrike' 'archstrike-webapp' 'archstrike-scanners' 'archstrike-fuzzer' 'archstrike-proxy')
pkgdesc="An integrated platform for attacking web applications (free edition)."
url='http://portswigger.net/burp/'
depends=('java-environment' 'giflib' 'bash')
arch=('any')
license=('custom')
noextract=("burpsuite.jar")
source=("burpsuite.jar::http://portswigger.net/burp/burpsuite_free_v${pkgver}.jar" LICENSE)
sha512sums=('d453c40680ac4f9bfd5443139f2e046e156285987bc2ee0daa65c3377cecbc63d902f660a79ab3c7b1e6a0deb3d8128efd3ff547eb3222522c2e468cc9ff2fcd'
            '37abc24b19571bfd2941e3dd7aa8a8a330bd3bbbafb3d1f813035ab0313e189c1f4a43a68f820a32656b86dd29cca94afcaefc6a4bfb4219ef18dc0fc4923b86')

package() {
  cd ${srcdir}

  # Make base directories.
  install -dm755 $pkgdir/usr/share/licenses/burpsuite
  install -dm755 $pkgdir/usr/share/burpsuite
  install -dm755 $pkgdir/usr/bin

  install -m644 burpsuite.jar $pkgdir/usr/share/burpsuite/burpsuite.jar
  install -m644 LICENSE $pkgdir/usr/share/licenses/burpsuite

  # Make joint script.
  cat > ${pkgdir}/usr/bin/burpsuite <<EOF
#!/bin/sh
exec \$JAVA_HOME/bin/java -jar /usr/share/burpsuite/burpsuite.jar "\$@"
EOF
  chmod +x ${pkgdir}/usr/bin/burpsuite
}
