# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname=ruby1.9-bundler
_gemname=${pkgname#ruby1.9-}
pkgver=1.7.12
pkgrel=1
pkgdesc="Manages an application's dependencies through its entire life, across many machines, systematically and repeatably."
arch=('any')
url="http://gembundler.com"
license=('MIT')
depends=('ruby1.9')
source=("http://rubygems.org/downloads/${_gemname}-${pkgver}.gem" "https://raw.githubusercontent.com/bundler/bundler/master/LICENSE.md")
noextract=(${_gemname}-${pkgver}.gem)
sha512sums=('1ed8d4c587aeb6720034957326f2ac04484704e5fd5435cb8d8a9474563f78b6e0e0794a8eff1ce86e8946aebc9a175b192ec40d7c3c140d149cfe8f435edc6c'
            'fa43908bb0e9af7b5f2efd785e052749276671c1af46df762df337b24312f0b157d291484a5a0299a644a3d1455e6465a57d120defb859aaa26b158a4a02677a')

package() {
  cd "${srcdir}"

  local _gemdir="$(ruby-1.9 -rubygems -e'puts Gem.default_dir')"
  if [[ $CARCH == arm* ]] ; then
    gem-1.9 install --ignore-dependencies --no-user-install --no-rdoc --no-ri -i "$pkgdir$_gemdir" $_gemname-$pkgver.gem
  else
    gem-1.9 install --ignore-dependencies --no-user-install -i "$pkgdir$_gemdir" $_gemname-$pkgver.gem
  fi
  install -dm755 "$pkgdir/usr/bin/"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
  ln -s $_gemdir/bin/bundle $pkgdir/usr/bin/bundle-1.9
}
