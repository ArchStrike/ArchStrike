# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=212

pkgname=cowpatty
pkgver=4.6
pkgrel=2
groups=('archstrike' 'archstrike-wifi' 'archstrike-crackers')
pkgdesc="Wireless WPA/WPA2 PSK handshake cracking utility"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www.willhackforsushi.com/?page_id=50'
license=('GPL')
depends=('openssl' 'libpcap')
options=(docs !makeflags)
source=("http://www.willhackforsushi.com/code/cowpatty/4.6/cowpatty-$pkgver.tgz")
sha512sums=('87c55f3f43c0342cbcc46227a1e4cfe3f23e215adc68e376991b85344b00b56e7b57e3cf0f1e3d18378cd830a247b2e36644b41b5c0f00a2a382b410f4ab35af')

build() {
  cd "$srcdir/cowpatty-$pkgver"
  # _FORTIFY_SOURCE causes issues.
  CPPFLAGS= make
}

package() {
  cd "$srcdir/cowpatty-$pkgver"

  make DESTDIR="$pkgdir/" "BINDIR=/usr/bin/" install

  install -Dm644 dict "$pkgdir/usr/share/cowpatty/dict"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/cowpatty/COPYING"
}
