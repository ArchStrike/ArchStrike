# Maintainer: ArchStrike <archstrikelinux@gmail.com>
pkgname=perl-netpacket
pkgver=1.5.0
pkgrel=1
pkgdesc="Assemble/disassemble network packets at the protocol level"
arch=('any')
url='http://search.cpan.org/dist/NetPacket/'
license=('custom')
makedepends=('perl-module-build>=0.360.100')
provides=('perl-netpacket-arp=1.5.0' 'perl-netpacket-ethernet=1.5.0'
          'perl-netpacket-icmp=1.5.0' 'perl-netpacket-icmpv6=1.5.0'
          'perl-netpacket-igmp=1.5.0' 'perl-netpacket-ip=1.5.0'
          'perl-netpacket-ipv6=1.5.0' 'perl-netpacket-pflog=1.5.0'
          'perl-netpacket-tcp=1.5.0' 'perl-netpacket-udp=1.5.0')
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/Y/YA/YANICK/NetPacket-$pkgver.tar.gz")
sha512sums=('9c25022ae497c493005ab68d6ca47d679d60caf42de548f0920c25f3154c3264886165430120e09c6b3fed66630387b4de0a88eec41e3b6a51a14b9c5f97255b')

build() {
  cd "$srcdir/NetPacket-$pkgver"

  PERL_MM_USE_DEFAULT=1 perl Build.PL INSTALLDIRS=vendor
  ./Build
}

package() {
  cd "$srcdir/NetPacket-$pkgver"

  ./Build install destdir=${pkgdir}
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Remove perllocal.pod and .packlist.
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete
}
