# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=212

pkgname=scanmem
pkgver=0.15.2
pkgrel=1
groups=('archstrike' 'archstrike-reverse')
pkgdesc="A utility used to locate the address of a variable in an executing process."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/scanmem'
license=('GPL3')
depends=('pygtk' 'desktop-file-utils')
makedepends=('intltool')
optdepends=('gksu: for the GameConqueror GUI.')
install=${pkgname}.install
source=("https://github.com/scanmem/scanmem/archive/v${pkgver}.tar.gz")
sha512sums=('9d1211b10b30962e35821895d7dd7d5a179e64b3f539b6dd2585a3f6b0ade7cd092fe377454b2582ec66e2c7a7676f8c7f04e8aff5c9e6e449a9eb6fb0498501')

build() {
  cd "$srcdir/scanmem-$pkgver"
  ./autogen.sh
  chmod 755 configure
  ./configure --enable-gui --prefix=/usr
  make
}

package() {
  cd "$srcdir/scanmem-$pkgver"

  make -s DESTDIR="$pkgdir" install

  # Fix Python shebang lines.
  sed 's|python|python2|g' -i "$pkgdir/usr/bin/gameconqueror"
  sed 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' -i \
      "$pkgdir/usr/share/gameconqueror/"{backend.py,GameConqueror.py,hexview.py}
}

