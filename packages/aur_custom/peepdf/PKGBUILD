# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname=peepdf
pkgver=0.3
pkgrel=1
groups=('archstrike' 'archstrike-forensic' 'archstrike-malware')
pkgdesc="A Python tool to explore PDF files in order to find out if the file can be harmful or not"
arch=('any')
url='http://code.google.com/p/peepdf/'
license=('GPL3')
depends=('python2' 'python2-simplejson')
source=("http://eternal-todo.com/files/pdf/peepdf/peepdf_$pkgver.tar.gz")
sha512sums=('71039e7c06e7d0a85dd31a96cd7f6c37edb38246fc7201b57c8db66168b6be90fcd81a405f70417a50ab431b678d11efecf083a18455a51aea9af4f9f7e7b1f0')

prepare() {
  cd "$srcdir/${pkgname}_${pkgver}"

  find -name '*.py' -exec sed -i 's/env python$/env python2/' '{}' \;
}

package() {
  cd "$srcdir/${pkgname}_${pkgver}"

  # Base directories.
  install -dm755 "$pkgdir/usr/share/peepdf"
  install -dm755 "$pkgdir/usr/bin"

  # Entire program.
  cp --no-preserve=ownership -R * "$pkgdir/usr/share/peepdf"

  # Joint script.
  cat > "$pkgdir/usr/bin/peepdf" <<EOF
#!/bin/sh
cd /usr/share/peepdf
python2 peepdf.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/peepdf"
}
