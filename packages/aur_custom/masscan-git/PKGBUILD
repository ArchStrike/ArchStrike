# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=212

pkgname=masscan-git
pkgver=20141103.r391
pkgrel=1
epoch=1
groups=('archstrike' 'archstrike-scanners' 'archstrike-crackers')
pkgdesc="TCP port scanner, spews SYN packets asynchronously, scanning entire Internet in under 5 minutes"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/robertdavidgraham/masscan'
license=('AGPL')
depends=('libpcap')
makedepends=('git')
conflicts=('masscan')
provides=('masscan')
replaces=('masscan')
backup=('etc/masscan/masscan.conf' 'etc/masscan/exclude.conf')
source=("${pkgname}::git+http://github.com/robertdavidgraham/masscan.git"
	'masscan.desktop' 'masscan.conf')
sha512sums=('SKIP'
            '634257335408b2ad1f3135660fa51b72d4a8363d53db36778a23157e06ac793dc5ef52aeae04d9d4925d9e2de24f0a61be7e888cd96d2a7aba70c9d081e5908a'
            'f9b2f81091f27509c0148bcee13630c26cfd26836c8b29f06afe6c4f83e871976a4e594e6c8499a13224ad44a1b04ed47a303ffec1d8656164804e9527d16e6b')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

build() {
  cd "${pkgname}"
  make
}

package() {
  cd "${pkgname}"
  install -dm755 "${pkgdir}"/usr/share/man/man8
  install -Dm755 bin/masscan "${pkgdir}"/usr/bin/masscan
  gzip -c doc/masscan.8 > "${pkgdir}"/usr/share/man/man8/masscan.8.gz
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
  install -Dm644 "${srcdir}"/masscan.desktop "${pkgdir}/usr/share/applications/masscan.desktop"
  install -Dm644 data/exclude.conf "${pkgdir}"/etc/masscan/exclude.conf
  install -m644 "${srcdir}"/masscan.conf "${pkgdir}"/etc/masscan/masscan.conf
}
