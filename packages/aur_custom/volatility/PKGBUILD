# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname=volatility
pkgver=2.4.1
pkgrel=2
groups=('archstrike' 'archstrike-forensics')
pkgdesc="A memory forensics toolkit"
arch=('any')
url='https://www.volatilesystems.com/default/volatility'
license=('GPL')
depends=('python2-pillow' 'python2-openpyxl' 'yara' 'distorm' 'python2-crypto')
optdepends=('libforensic1394: for live analysis over firewire')
source=("https://github.com/volatilityfoundation/volatility/archive/$pkgver.tar.gz")
sha512sums=('cc2db2a17b071a21852bab967c9615df0fce369dac35ffbca8754bc789e218a86fc88bd2042203174308e620e281a24debc7b0a8bacd516a404a9170ea522596')

prepare() {
  find "${srcdir}" -type f -name *.py | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/volatility-${pkgver}"
  install -dm755 "$pkgdir/usr/share/volatility"
  python2 setup.py build install --root="${pkgdir}"
  ln -s vol.py "$pkgdir/usr/bin/volatility"
  mv "$pkgdir/usr/tools" "$pkgdir/usr/share/volatility"
  mv "$pkgdir/usr/contrib" "$pkgdir/usr/share/volatility"
}
