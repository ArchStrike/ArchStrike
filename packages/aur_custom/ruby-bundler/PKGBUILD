# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname=ruby-bundler
_gemname=bundler
pkgver=1.7.12
pkgrel=1
pkgdesc="The best way to manage your application's dependencies"
arch=('any')
license=('MIT')
makedepends=('ruby')
url='http://gembundler.com'
source=("http://rubygems.org/downloads/bundler-$pkgver.gem" "https://raw.githubusercontent.com/bundler/bundler/master/LICENSE.md")
sha512sums=('1ed8d4c587aeb6720034957326f2ac04484704e5fd5435cb8d8a9474563f78b6e0e0794a8eff1ce86e8946aebc9a175b192ec40d7c3c140d149cfe8f435edc6c'
            'fa43908bb0e9af7b5f2efd785e052749276671c1af46df762df337b24312f0b157d291484a5a0299a644a3d1455e6465a57d120defb859aaa26b158a4a02677a')
noextract=("bundler-$pkgver.gem")

package() {
  cd "$srcdir"
  local _gemdir=$(ruby -e 'puts Gem.default_dir')
  if [[ $CARCH == arm* ]] ; then
    gem install --ignore-dependencies --no-user-install --no-rdoc --no-ri -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" $_gemname-$pkgver.gem
  else
    gem install --ignore-dependencies --no-user-install -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" $_gemname-$pkgver.gem
  fi
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
