# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=212

pkgname=pev
pkgver=0.70
pkgrel=2
groups=('archstrike' 'archstrike-forensic' 'archstrike-reversing')
pkgdesc="Command line based tool for PE32/PE32+ file analyze"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://pev.sourceforge.net'
license=('GPL')
depends=('openssl' 'pcre' 'archstrike-artwork')
source=("http://sourceforge.net/projects/pev/files/pev-${pkgver}/pev-${pkgver}.tar.gz"
        "${pkgname}.desktop")
sha512sums=('c98ea778b05b514ef26d87627484b5942247fb4190ad6a166ee2a01fc185dd4663848ca9b5ba3a26be5c8a40eaf42f476bec58ddea0565ac4c6fe0710c9575aa'
            '39b5cb750f5f1efbf24d2d9a08fa0a85ef8b4f1b8ecc8d8f82a14c597e4b4db703d9456107737835e6d8c75056e078959c3c183c4bbbbceae4077b9c4194a8f6')

prepare() {
    cd pev
    find . -type f -name "Makefile" -exec sed -i 's|^datarootdir = |datarootdir = \$(DESTDIR)/|' '{}' \;
}

build() {
    cd pev
    make
}

package() {
    install -Dm644 ${pkgname}.desktop "$pkgdir"/usr/share/applications/${pkgname}.desktop
    cd pev
    make DESTDIR="$pkgdir" install
}
