# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=212

pkgname=edb
pkgver=0.9.20
pkgrel=2
groups=('archstrike' 'archstrike-reversing' 'archstrike-debugging')
pkgdesc="A QT4-based binary mode debugging with the goal of having usability on par with OllyDbg."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www.codef00.com/projects.php#Debugger'
license=('GPL2')
depends=('qt4>=4.5')
makedepends=('boost>=1.35.0')
install=edb.install
conflicts=('edb-debugger')
provides=('edb-debugger')
source=(http://www.codef00.com/projects/debugger-$pkgver.tgz
        edb.desktop)
md5sums=('5916d415f598fae8632dd763f58e11d1'
         'a08fb481bc46f47c6c004f7b8bd928e4')

build() {
  cd $srcdir/debugger

  qmake-qt4 -makefile DEFAULT_PLUGIN_PATH="/usr/lib/edb/"
  make
}

package() {
  cd $srcdir/debugger

  make INSTALL_ROOT=$pkgdir/usr install

  # /usr/lib64 -> /usr/lib on x86_64
  [[ "$CARCH" = "x86_64" ]] && (mv $pkgdir/usr/lib64 $pkgdir/usr/lib)

  # Install icons.
  install -D -m 644 src/images/edb48-logo.png $pkgdir/usr/share/pixmaps/edb.png
  install -D -m 644 src/images/edb48-logo.png $pkgdir/usr/share/icons/hicolor/48x48/apps/edb.png

  # Install desktop file.
  cd ..
  install -D -m 644 edb.desktop $pkgdir/usr/share/applications/edb.desktop
}
