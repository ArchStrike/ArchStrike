# Maintainer: ArchStrike <archstrikelinux@gmail.com>

_pkgname=pwnat

buildarch=212

pkgname=$_pkgname-git
pkgver=20140907.r9.1d07c2e
pkgrel=2
groups=('archstrike' 'archstrike-networking')
pkgdesc='Punches holes in firewalls and NATs allowing any numbers of clients behind NATs to directly connect to a server behind a different NAT with no 3rd party, port forwarding, DMZ or spoofing involved'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://samy.pl/pwnat/'
license=('GPL3')
depends=('glibc')
makedepends=('git')
source=("git+https://github.com/samyk/pwnat"
        "http://samy.pl/pwnat/pwnat.pdf")
sha512sums=('SKIP' 'e8ffb3829d23fc25032b93ac02e93a57e89449dbcf41c00a00c98abac9be373dd0c42f8c997db591b3746d3fabfec37a2c851b158710346cb86653d39111af35')
options=('!buildflags')
provides=("$_pkgname")
conflicts=("$_pkgname")
replaces=("$_pkgname")

pkgver() {
  cd $_pkgname
  printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd $_pkgname
  make
}

package() {
  install -Dm644 ${_pkgname}.pdf "$pkgdir/usr/share/doc/$_pkgname/${_pkgname}.pdf"
  cd $_pkgname
  install -Dm644 README-udptunnel "$pkgdir/usr/share/doc/$_pkgname/README-udptunnel"
  install -Dm644 README "$pkgdir/usr/share/doc/$_pkgname/README"
  install -Dm644 Changes "$pkgdir/usr/share/doc/$_pkgname/Changes"
  install -Dm755 $_pkgname "$pkgdir/usr/bin/$_pkgname"
}
