# Maintainer: ArchStrike <archstrikelinux@gmail.com>
pkgname=binwally
pkgver=0.r4.0aabd8b
pkgrel=2
groups=('archstrike' 'archstrike-analysis' 'archstrike-forensics' 'archstrike-source-audit')
pkgdesc="Binary and Directory tree comparison tool using the Fuzzy Hashing concept (pydeep)"
url="https://github.com/bmaia/binwally"
arch=(any)
license=('MIT')
depends=('python2-pydeep')
makedepends=('git')
source=("git+https://github.com/bmaia/binwally.git")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "${srcdir}/${pkgname}"

  grep -iRl 'python' "${srcdir}/${pkgname}" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
  grep -iRl 'ssdeep' "${srcdir}/${pkgname}" | xargs sed -i 's|ssdeep|pydeep|g'
  grep -iRl 'hash_from_file' "${srcdir}/${pkgname}" | xargs sed -i 's|hash_from_file|hash_file|g'

  install -dm755 "${pkgdir}/usr/bin/"
  install -m755 ${pkgname}.py "${pkgdir}/usr/bin/${pkgname}"

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m755 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"
}

