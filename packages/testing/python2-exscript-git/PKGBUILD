# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname=python2-exscript-git
pkgver=0.r1465.4b61cce
pkgrel=1
pkgdesc="A Python module making Telnet and SSH easy"
arch=('any')
url="https://github.com/knipknap/exscript"
license=('GPL2')
depends=('python2-crypto')
makedepends=('git' 'python2-setuptools')
provides=('python2-exscript')
conflicts=('python2-exscript')
replaces=('python2-exscript')
source=("exscript::git+https://github.com/knipknap/exscript.git")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/exscript"
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' "$srcdir/exscript" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/exscript"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  find "${pkgdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|'
}
