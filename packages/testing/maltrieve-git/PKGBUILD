# Maintainer: ArchStrike <archstrikelinux@gmail.com>
_pkgname=maltrieve

buildarch=1

pkgname=$_pkgname-git
pkgver=r342.b9e7560
pkgrel=1
groups=('archstrike' 'archstrike-malware')
pkgdesc='A tool to retrieve malware directly from the source for security researchers.'
arch=('any')
url='https://github.com/technoskald/maltrieve'
license=('GPL3')
depends=('python2-beautifulsoup4' 'python2-lxml' 'python2-feedparser' 'python2-gevent' 'python2-greenlet' 'python2-grequests' 'python2-requests' 'python2-wsgiref' 'python2-magic')
makedepends=('git')
provides=("$_pkgname")
source=("git+$url.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  grep -iRl 'python' $_pkgname | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
  sed -i 's|^dumpdir = |dumpdir = /tmp/|' $_pkgname/$_pkgname.cfg
  sed -i 's|^logfile = |logfile = /tmp/|' $_pkgname/$_pkgname.cfg
}

package() {
  cd $_pkgname

  # Make base directories.
  install -dm755 "$pkgdir/usr/share/$_pkgname"
  install -dm755 "$pkgdir/usr/bin"

  #Bins
  install -Dm644 grequests.py "$pkgdir/usr/share/$_pkgname/"
  install -Dm755 $_pkgname.py "$pkgdir/usr/share/$_pkgname/"
  install -Dm755 ${_pkgname}categorizer.sh "$pkgdir/usr/share/$_pkgname/"
  install -Dm644 $_pkgname.cfg "$pkgdir/usr/share/$_pkgname/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"

  cat > "$pkgdir/usr/bin/$_pkgname" << EOF
#!/bin/sh
cd /usr/share/maltrieve
python2 maltrieve.py "\$@"
EOF

  cat > "$pkgdir/usr/bin/${_pkgname}categorizer" << EOF
#!/usr/bin/env bash
cd /usr/share/maltrieve
bash maltrievecategorizer.sh "\$@"
EOF

  chmod 755 "$pkgdir/usr/bin/${_pkgname}categorizer"
  chmod 755 "$pkgdir/usr/bin/$_pkgname"
}
