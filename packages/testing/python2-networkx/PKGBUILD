# Maintainer: ArchStrike <archstrikelinux@gmail.com>
pkgname=python2-networkx
_pythonname=networkx
pkgver=1.8.1
pkgrel=2
pkgdesc='Python package for the creation, manipulation, and study of the structure, dynamics, and functions of complex networks.'
arch=('any')
license=('BSD')
url="https://networkx.lanl.gov/"
depends=('python2')
optdepends=('python2-numpy: Provides sparse matrix representation of graphs and many numerical scientific tools.',
            'python2-scipy: Provides flexible drawing of graphs.',
            'python2-pygraphviz: Provides graph drawing and graph layout algorithms.',
            'pydot: Provides graph drawing and graph layout algorithms.',
            'python2-pyparsing: Required for pydot, GML file reading.',
            'python2-yaml: Required for YAML format reading and writing.')

source=(https://pypi.python.org/packages/source/n/${_pythonname}/${_pythonname}-${pkgver}.zip)
md5sums=('4a90328898d9db29bbb12ef3e6223bab')

build() {
  cd "$srcdir/${_pythonname}-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/${_pythonname}-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
  find "$pkgdir" -name '*.py' -exec sed -i -e 's/^#!\/usr\/bin\/env python$/#!\/usr\/bin\/env python2/' -e 's/^#!\/usr\/bin\/python$/#!\/usr\/bin\/python2/' {} \;
  install -D -m 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
  mv "$pkgdir/usr/share/doc/${_pythonname}-$pkgver" "$pkgdir/usr/share/doc/$pkgname"
}
