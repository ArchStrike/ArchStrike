# Maintainer: ArchStrike <archstrikelinux@gmail.com>
pkgname=waidps-git
pkgver=20141225.r16
pkgrel=1
groups=('archstrike' 'archstrike-defensive')
pkgdesc="Wireless Auditing, Intrusion Detection & Prevention System"
arch=('any')
url='https://github.com/SYWorks/waidps'
license=('GPL2')
depends=('mdk3' 'python2-crypto')
makedepends=('git')
provides=('waidps')
conflicts=('waidps')
source=("${pkgname}::git+https://github.com/SYWorks/waidps.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}



prepare(){
  grep -iRl 'python' "${pkgname}" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}


package() {
  cd "${pkgname}"
  install -dm755 "$pkgdir/usr/bin"
  install -Dm644 README.md "$pkgdir/usr/share/$pkgname/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm755 waidps.py "$pkgdir/usr/share/$pkgname/waidps.py"

cat >"$pkgdir/usr/bin/waidps" <<EOF
#!/bin/sh
cd /usr/share/$pkgname
python2 ./waidps.py "\$@"
EOF

chmod +x "$pkgdir/usr/bin/waidps"
}
