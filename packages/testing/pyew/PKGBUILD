# Maintainer: ArchStrike <archstrikelinux@gmail.com>
pkgname=pyew
pkgver=2.3.0.0.17bb704fc6d5a.
pkgrel=1
groups=('archstrike' 'archstrike-malware')
pkgdesc="a (command line) python tool to analyse malware."
arch=('any')
url="https://code.google.com/p/${pkgname}"
license=('GPL2')
depends=('distorm')
makedepends=('mercurial')
source=("pyew::hg+https://code.google.com/p/pyew/")
sha512sums=('SKIP')

pkgver(){
    cd $pkgname
    printf %s.%s $(grep -e ^HUMAN_VERSION= pyew.py | sed 's|HUMAN_VERSION="||;s|"$||') $(hg identify | sed 's|\ .*||')
}

prepare(){
    cd $pkgname

    # Fix python invocation
    find . -iname "*.py" -exec \
        sed -i -E 's|\ */usr/bin/env python|/usr/bin/env python2|g;s|\ */usr/(local/)?bin/python(2\.4)?|/usr/bin/env python2|g' '{}' \;
}

package(){
    cd $pkgname

    install -Dm755 ${pkgname} "${pkgdir}"/usr/share/${pkgname}/${pkgname}
    for i in *.py; do
        install -Dm755 $i "${pkgdir}"/usr/share/${pkgname}/${i}
    done
    for i in Elf anal contrib logo plugins scripts; do
        install -dm755 "${pkgdir}"/usr/share/${pkgname}/${i}
        cp -a --no-preserve=ownership ${i}/* "${pkgdir}"/usr/share/${pkgname}/${i}/
    done

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

    install -dm755 "${pkgdir}"/usr/bin
    echo -e '#!/usr/bin/env bash\n\ncd /usr/share/pyew/\n./pyew.py "$@"' > "${pkgdir}/usr/bin/${pkgname}"
    chmod 755 "${pkgdir}/usr/bin/${pkgname}"
}
