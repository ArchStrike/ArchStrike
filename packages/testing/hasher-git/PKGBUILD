# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname=hasher-git
pkgver=r42.0de1936
pkgrel=1
groups=('archstrike' 'archstrike-cracker')
pkgdesc="A tool that allows you to quickly hash plaintext strings, or compare hashed values with a plaintext locally."
arch=('any')
url='https://github.com/ChrisTruncer/Hasher'
license=('GPL2')
depends=('python2-passlib' 'python2-py-bcrypt' 'python2-wsgiref')
makedepends=('git')
provides=('hasher')
source=("git+https://github.com/ChrisTruncer/Hasher.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/Hasher"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  cd "$srcdir/Hasher"
  sed -i 's|env python$|env python2|' *.py
}

package() {
  cd "$srcdir/Hasher"

  # Make base directories.
  install -dm755 "$pkgdir/usr/bin"

  #Bin
  install -Dm755 Hasher.py "$pkgdir/usr/bin/hasher"

  #License
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

}
