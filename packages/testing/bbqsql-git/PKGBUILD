# Maintainer: ArchStrike <archstrikelinux@gmail.com>

_pkgname=bbqsql

buildarch=1

pkgname=$_pkgname-git
pkgver=v1.1.r15.g4f7c086
pkgrel=1
epoch=1
groups=('archstrike' 'archstrike-webapp' 'archstrike-exploit')
pkgdesc="SQL injection exploit tool"
arch=('any')
url="https://github.com/neohapsis/bbqsql"
license=('BSD')
depends=('python2-requests' 'python2-gevent')
makedepends=('git' 'python2-distribute')
conflicts=('bbqsql')
provides=('bbqsql')
replaces=('bbqsql')
source=("git+$url")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_pkgname
  sed -i 's|import os|import os, pwd\nos.getlogin = lambda: pwd.getpwuid(os.getuid())[0]|' bbqsql/menu/bbq_core.py
}

package() {
  cd $_pkgname
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
