# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname=vivisect
pkgver=20140803
pkgrel=1
groups=('archstrike' 'archstrike-debugging' 'archstrike-reversing' 'archstrike-disassembler')
pkgdesc="Python based static analyze and reverse engineering framework, Vdb is a Python based research/reversing focused debugging and programatic debugging API by invisigoth of kenshoto"
arch=('any')
url="http://visi.kenshoto.com/"
license=('BSD')
depends=('java-environment' 'python2-pyqt4' 'pyqt4-common' 'python2-msgpack')
source=("http://visi.kenshoto.com/static/releases/vivisect_$pkgver.zip")
options=('!emptydirs')
sha512sums=('d8a4c9e60df7a54f7ac1053cafd07f09563db75d5207c6beec3c563ee7c6d5760b70aca6d41e0d3cdd0817aa152bc2ef1ab2ae879583a0cb06b09818a25932f8')

prepare() {
  cd "$srcdir/vivisect_$pkgver"

  sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' "$srcdir/vivisect_$pkgver/vdbbin"
  sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' "$srcdir/vivisect_$pkgver/vivbin"
}

package() {
  cd "$srcdir/vivisect_$pkgver"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/vivisect"

  cp -a * "$pkgdir/usr/share/vivisect"

  cat > "$pkgdir/usr/bin/vdbbin" <<EOF
#!/bin/sh
cd /usr/share/vivisect
python2 /usr/share/vivisect/vdbbin "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/vdbbin"

  cat > "$pkgdir/usr/bin/vivbin" <<EOF
#!/bin/sh
cd /usr/share/vivisect
python2 /usr/share/vivisect/vivbin "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/vivbin"
}

