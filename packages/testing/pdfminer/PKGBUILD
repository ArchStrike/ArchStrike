# Maintainer: ArchStrike <archstrikelinux@gmail.com>

pkgname=pdfminer
pkgver=20140328
pkgrel=1
epoch=1
arch=('any')
pkgdesc='PDF parser and analyzer'
license=('MIT')
url='http://euske.github.io/pdfminer/index.html'
depends=('python2')
makedepends=('python2-setuptools')
provides=('python2-pdfminer')
replaces=('python2-pdfminer')
conflicts=('python2-pdfminer')
source=("https://pypi.python.org/packages/source/p/$pkgname/$pkgname-$pkgver.tar.gz" 'LICENSE')
md5sums=('dfe3eb1b7b7017ab514aad6751a7c2ea' '7d5b03b556811d959a94d9336ba04fe5')
sha512sums=('8abb2328f7824ee980df0ce42d758c15534e97379ea9213b4aa1d29864d24d73c9e2d6020964e9ab734c0c6f7d1ae70bccbd6605168eaadf83923718a1f4849a' '21de58a9ffd486ad1b4616b6c0674d04f05c4f2173ed4050da134a721b8fc23022a2254494d4e9e892691f5e6e1f3bfbdf8fed57ed9a4f2e19349041a2e53974')

prepare() {
  cd $pkgname-$pkgver
  sed -i -re 's|#!\s*(/usr)?(/local)?/bin/.*python.*$|#!/usr/bin/env python2|g' $(egrep -rl '^\s*#!\s*(/usr)?(/local)?/bin/.*python')
}

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
  make cmap
}

package() {
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  cd $pkgname-$pkgver
  export PYTHONPATH="$pkgdir/usr/lib/python2.7/site-packages/"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
