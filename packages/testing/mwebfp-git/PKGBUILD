# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname=mwebfp-git
pkgver=20140928.r16
pkgrel=1
groups=('archstrike' 'archstrike-scanners')
pkgdesc="A LNHG - Mass Web Fingerprinter"
arch=('any')
url='https://github.com/falcon-lnhg/mwebfp'
license=('GPL3')
depends=('cutycapt' 'python2-requests' 'python2-nmap')
makedepends=('git')
provides=('mwebfp')
source=("${pkgname}::git+https://github.com/falcon-lnhg/mwebfp.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare(){
  grep -iRl 'python' "${pkgname}" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$pkgname"
  install -dm755 "$pkgdir/usr/bin/"
  install -dm755 "$pkgdir/usr/share/mwebfp"
  install -Dm755 mwebfp.py "$pkgdir/usr/bin/mwebfp"
  install -Dm644 README.md "$pkgdir/usr/share/mwebfp/README.md"
}
