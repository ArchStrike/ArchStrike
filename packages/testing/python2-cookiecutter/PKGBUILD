# Maintainer: ArchStrike <archstrikelinux@gmail.com>
# Contributor: Meir Kriheli <mkriheli@gmail.com>

buildarch=1

pkgname=python2-cookiecutter
_pkgname=cookiecutter
pkgver=1.0.0
pkgrel=1
pkgdesc="A command-line utility that creates projects from project templates"
arch=('any')
url="https://github.com/audreyr/cookiecutter"
license=('BSD')
depends=('python2-jinja' 'python2-yaml' 'python2-binaryornot')
makedepends=('python2-setuptools')
source=("https://github.com/audreyr/${_pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('be36d89d85761d11f64fb670c0542eb40b7cc6b0feda6453656bf9bfd8f16d332fea779c3d477a0e5422532698be29bc5e8facd2c328cc0a425e2b6e7d95e593')

prepare(){
  grep -iRl 'python' "$srcdir/$_pkgname-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
      cd "$srcdir/${_pkgname}-${pkgver}"
      python2 setup.py install --root="$pkgdir/" --optimize=1
      install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
