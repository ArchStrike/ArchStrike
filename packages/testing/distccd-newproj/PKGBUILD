# Maintainer: ArchStrike <archstrikelinux@gmail.com>
# Contributor: Jason Plum <jplum@archlinuxarm.org>
# Contributor: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Josh Cartwright <joshc@eso.teric.us>
_subarchs=(armv6h armv7h)
pkgbase='distccd-projectname'
pkgname=("${_subarchs[@]/#/$pkgbase-}")
pkgver=4.9.2
pkgrel=1
pkgdesc="An x-tools & distcc services package for ArchStrike ARM"
arch=('x86_64')
license=('GPL' )
url="http://archlinuxarm.org/developers/distcc-cross-compiling"
depends=('distcc')
backup=('etc/conf.d/distccd-armv7h.conf' 'etc/conf.d/distccd-armv7h.service' 'etc/conf.d/distccd-armv6h.conf' 'etc/conf.d/distccd-armv6h.service')
options=('libtool' 'emptydirs' '!strip')
source=(http://archlinuxarm.org/builder/xtools/x-tools6h.tar.xz
	http://archlinuxarm.org/builder/xtools/x-tools7h.tar.xz
	distccd-armv7h.conf
	distccd-armv7h.service
	distccd-armv6h.conf
	distccd-armv6h.service
        )
sha512sums=('5eb52553e68769f453b79979082b89187413240c83dc57768c57eafc3d8054b046caa33fd3063d5cb852c024af455ee96609fec67a96eee9afd580c7aa324aeb'
            '5bb56dcd5a96b663499ad0a8b315ee35779a907cc5a13a3b71a14d11c832ff02d598ac777da88e362af95ecd2ed235e6802d40fc6b9e4ec8382a71b140333693'
            '708c370335ce75ecccdd2f52bd324cea3c75c09798007ddec5aa66147a2bbaa9f4280c3697abc5870362e38a50dcc358a3c1eb18d1740fc13313a6f3402e2e8c'
            '74099c2bc620c7a7e191407957f070676e05f9fad5a9f6b3bbef76adf7da3ea32d4b611882eb635dd5d6f7785bc072ca50bf6efd8193ad4ed027b0168221224f'
            '3570f157af8e0cd81d8aa72bf030334c2fa36132863c5ec4b32b6f2a926257b8be311d1459254fbd811c588df28ec3f4d931c35099d65513455779d3bacd9b0c'
            '51b884dcce3a109707f7a9b93874ebeb18a84966189a6c180fb091cc4380305f52221c48d0b1b014904af5de17fd537cbc8ce303384856419a36ea4fcc56b3b8')

_package_subarch() {
    # backup configs
    backup=("etc/conf.d/distccd-$1")
    # install symlink to distccd
    install -d "${pkgdir}/usr/bin"
    ln -sf /usr/bin/distccd "${pkgdir}/usr/bin/distccd-$1"
    # copy in toolchain
    install -d "${pkgdir}/usr/local/x-tools-$1"
    cp -ar "${srcdir}/$2" "${pkgdir}/usr/local/x-tools-$1"
    # install services
    install -Dm0644 "${srcdir}/distccd-$1.service" \
       "${pkgdir}/usr/lib/systemd/system/distccd-$1.service"
    # install config
    install -Dm0644 "${srcdir}/distccd-$1.conf" \
       "${pkgdir}/etc/conf.d/distccd-$1"

}

for i in "${!_subarchs[@]}"; do
    _xtoolsdir="${source[i]##*/}"
    _xtoolsdir="${_xtoolsdir%%.*}"
    eval 'package_distccd-projectname-'${_subarchs[i]}'() {
        _package_subarch '${_subarchs[i]}' '${_xtoolsdir}'
    }'
done
