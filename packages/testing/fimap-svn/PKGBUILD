# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname=fimap-svn
pkgver=20150325.r334
pkgrel=1
groups=('archstrike' 'archstrike-fuzzer' 'archstrike-exploit')
pkgdesc="A little tool for local and remote file inclusion auditing and exploitation"
arch=('any')
url='http://code.google.com/p/fimap/'
license=('GPL2')
depends=('bash' 'python2' 'python-urllib3' 'python2-httplib2')
makedepends=('subversion')
replaces=('fimap')
conflicts=('fimap')
provides=('fimap')
source=("${pkgname}::svn+http://fimap.googlecode.com/svn/trunk/")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" $(svn info | grep "Last Changed Date" | sed 's|Last Changed Date: ||;s|\ .*||;s|-||g') $(svn info | grep Revision: | cut -c11-)
}

package() {
  cd "${pkgname}"
  find . -type f -exec install -D '{}' "${pkgdir}"/usr/share/"${pkgname}"/'{}' \;
  install -d "${pkgdir}"/usr/bin
  echo -e '#!/usr/bin/env bash\n\npython2 /usr/share/fimap-svn/src/fimap.py "$@"' \
    > "${pkgdir}"/usr/bin/fimap
  chmod a+x "$pkgdir"/usr/bin/fimap
}
