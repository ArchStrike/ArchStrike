# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname=mitmf-git
pkgver=20150528.r291
pkgrel=1
groups=('archstrike' 'archstrike-proxy' 'archstrike-spoof')
pkgdesc="A Framework for Man-In-The-Middle attacks written in Python."
arch=('any')
url='https://github.com/byt3bl33d3r/MITMf'
license=('GPL3')
depends=('python2-configobj' 'python2-pillow' 'python2-user-agents' 'python2-service-identity' 'python2-ipy'
         'python2-twisted' 'python2-netfilterqueue' 'python2-msgpack' 'python2-requests' 'python2-pyopenssl'
         'scapy' 'capstone' 'python2-pefile' 'python2-dnspython' 'systemd' 'backdoor-factory-git' 'python2-pypcap'
         'python2-yaml' 'python2-watchdog' 'python2-dnslib' 'impacket')
makedepends=('git')
provides=('mitmf')
replaces=('mitmf')
conflicts=('mitmf')
install=${pkgname}.install
source=("${pkgname}::git+https://github.com/byt3bl33d3r/MITMf.git" "${pkgname}.tmpfiles.conf")
sha512sums=('SKIP'
            'a380c2f2e812efff0ed1f303f91046c4e639e4bb8a8b0f93e17d8e8ac1b7ec1c20c124d2a203d66fc72aaa62f4753de8935e8b365d39a7e2bd90c2c2dfa39376')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare(){
  cd "${pkgname}"
  #git submodule
  grep -iRl 'python' "$srcdir/${pkgname}" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "${pkgname}"

  install -dm755 "$pkgdir/usr/share/mitmf/"
  install -dm755 "$pkgdir/usr/bin/"
  install -Dm644 $srcdir/${pkgname}.tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/${pkgname}.conf"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/mitmf/"

  cd ${pkgdir}
  find ./ -type d -exec chmod 755 {} \+
  find ./ -type f -exec chmod 644 {} \+

cat > "${pkgdir}/usr/bin/mitmf" <<EOF
#!/bin/sh
cd /usr/share/mitmf
python2 mitmf.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/mitmf"
}
