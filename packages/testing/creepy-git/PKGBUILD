# Maintainer: ArchStrike <archstrikelinux@gmail.com>
pkgname=creepy-git
pkgver=r137.9f60449
pkgrel=4
groups=('archstrike' 'archstrike-scanners' 'archstrike-socialengineering')
pkgdesc="A geolocation information gatherer. Offers geolocation information gathering through social networking platforms."
arch=('any')
url='http://github.com/ilektrojohn/creepy'
license=('GPL3')
provides=('creepy')
replaces=('creepy')
conflicts=('creepy')
makedepends=('git')
depends=('python2' 'python2-tweepy' 'python2-flickrapi'
         'python2-configobj' 'python2-pyqt4' 'python2-instagram'
         'python2-yapsy')
source=('creepy::git+https://github.com/ilektrojohn/creepy.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/creepy"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' "$srcdir/creepy" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}


package() {
  cd "$srcdir/creepy"

  # Make base directories.
  install -d -m755 "$pkgdir/usr/share/creepy"
  install -d -m755 "$pkgdir/usr/bin"
  cp -aR --no-preserve=ownership creepy gui "$pkgdir/usr/share/creepy"
  # Make joint script.
  cat > "$pkgdir/usr/bin/creepy" <<EOF
#!/bin/sh
python2 /usr/share/creepy/creepy/CreepyMain.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/creepy"
}
