# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=212

pkgname=imagejs-git
pkgver=20141209.r48
pkgrel=1
groups=('archstrike' 'archstrike-miscellaneous')
pkgdesc="A Small tool to package javascript into a valid image file."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/jklmnn/imagejs'
license=('GPL3')
depends=('glibc')
makedepends=('git')
provides=('imagejs')
source=("${pkgname}::git+https://github.com/jklmnn/imagejs.git")
sha512sums=('SKIP')

pkgver() {
    cd "${pkgname}"
    printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

build(){
  cd "$pkgname"
  make
}

package() {
  cd "$pkgname"
  install -Dm755 imagejs "$pkgdir/usr/bin/imagejs"

}
