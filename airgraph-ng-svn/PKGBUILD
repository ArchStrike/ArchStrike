# Maintainer
pkgname=airgraph-ng-svn
pkgver=2562
pkgrel=1
pkgdesc="Graphing tool for the aircrack suite"
arch=('any')
url="http://www.aircrack-ng.org"
license=('GPL2')
depends=('graphviz' 'python2')
makedepends=("subversion")
groups=('projectname' 'projectname-*miscellaneous')
source=('svn+http://svn.aircrack-ng.org/trunk/scripts/airgraph-ng' 'airgraph-ng.desktop')
provides=('airgraph-ng')
conflicts=('airgraph-ng')
replaces=('airgraph-ng')
sha512sums=('SKIP'
            '9415be15bdcaa9b3644838929e88e19745392c2eeaab132db81b15d8b74389bbb0552c8b540f02706fc063a06bf24a9ac4545fd667e870e031326d1b2fd1631d')

pkgver() {
  cd airgraph-ng
  svnversion | tr -d [A-z]
}

prepare(){
  grep -iRl 'python' "$srcdir/airgraph-ng" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}


build() {
  cd "${srcdir}/airgraph-ng"
  python2 setup.py build
}

package() {
  cd "${srcdir}/airgraph-ng"
  install -Dm644 "$srcdir/airgraph-ng.desktop" "$pkgdir/usr/share/applications/airgraph-ng.desktop"
  python2 setup.py install --root="${pkgdir}/" --prefix=/usr
}
