# Maintainer: ArchStrike <archstrikelinux@gmail.com>
# Contributor: Ari Mizrahi <codemunchies@debugsecurity.com>

pkgname=python-pyside-common
pkgver=1.2.2
pkgrel=1
pkgdesc="Provides LGPL Qt bindings for Python and related tools for binding generation. Will build for both Python 2 and 3 versions."
arch=('any')
license=('LGPL')
url="http://qt-project.org/wiki/PySide"
makedepends=('cmake' 'automoc4' 'python')
depends=('python' 'python2' 'qt4>=4.8' 'phonon' 'shiboken>=1.2.1')
source=("http://download.qt-project.org/official_releases/pyside/pyside-qt4.8+$pkgver.tar.bz2")
sha512sums=('3a1d46c48ac71c3b138e1dc398d1d2a87aff558eace275303e233f78d03e32c6d53cf390db5e7112f44da9b17aa88ed16a30f6f83b2073c1f494e61b962e4c46')

build(){
    cd $srcdir/pyside-qt4.8+$pkgver
    mkdir -p build_py3 && cd build_py3

    _pyverflags=$(python -c 'import sysconfig; print(sysconfig.get_config_var("SOABI"))')
    
    cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DBUILD_TESTS=OFF \
      -DQT_PHONON_INCLUDE_DIR=/usr/include/qt4/phonon -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
      -DPYTHON_SUFFIX=.${_pyverflags}
    make
}

package_python-pyside-common(){
    cd $srcdir/pyside-qt4.8+$pkgver/build_py3
    make DESTDIR=$pkgdir install

    rm -rf "$pkgdir"/usr/lib/python* "$pkgdir"/usr/lib/libpyside.* \
      "$pkgdir"/usr/lib/cmake/PySide-$pkgver/PySideConfig*python*.cmake \
      "$pkgdir"/usr/lib/pkgconfig/pyside.pc \
      "$pkgdir"/usr/lib/pkgconfig
}
